@component-name = "portal-solutions"
definition {

	property osgi.modules.includes = "osb-site-initializer-customer-portal";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "OSB Site Initializer Customer Portal";

	var testSiteName = "Customer Portal Site";
	var testSiteURL = "customer-portal-site";
	var testSiteTamplate = "Liferay OSB Site Initializer Customer Portal";
	var testAccount = "Test Account Portal";
	var testUser = "Test Test";
	var testUserFirstName = "Test";
	var testProduct = "Portal";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Customer Portal Site") {
			CustomerPortal.addCustomerPortalSite(siteName = "${testSiteName}");
		}

		task ("Add test user access to all Accounts") {
			CustomerPortal.assignUserToOrganization();

			CustomerPortal.addUserToOneOrMoreAccounts(accountsList = "${testAccount}");
		}

		task ("Login on OKTA") {
			CustomerPortal.loginOktaToUpdateCP();
		}

		task ("Go to Customer Portal Site") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.waitCP20PageToLoad(userFirstName = "${testUserFirstName}");
		}

		task ("I go to the test account") {
			CustomerPortal.search(searchItem = "${testAccount}");

			CustomerPortal.enterOnProject(title = "${testAccount}");
		}

		task ("I click on DXP product") {
			CP2NavigationMenu.goToProductActivation();

			CP2NavigationMenu.clickOnProductButton(product = "${testProduct}");
		}

		task ("I generate a new key") {
			CP2ActivationKeys.generateNewKey();
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		JSONGroup.deleteGroupByName(groupName = "${testSiteName}");

		CustomerPortal.customerPortalApisTearDown();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Automation Ticket: LPS-151334 | Test Summary: Verify that the Key Type field displays a drop-down list where you can select a key."
	@ignore = "true"
	@priority = "3"
	test CanViewADropdownListInTheKeyTypeField {
		var portalKeyTypeList = "Backup,Flex,Limited,Non-Production,OEM,Production,Unlimited Enterprise-Wide";
		var fieldLabel = "Key Type";

		task ("Given I should be able to view all Portal versions options") {
			for (var keyType : list "${portalKeyTypeList}") {
				CP2ActivationKeys.selectGenerateActivationKeyOptionByField(
					field = "${fieldLabel}",
					option = "${keyType}");

				CP2ActivationKeys.assertSelectedFieldText(
					field = "${fieldLabel}",
					option = "${keyType}");
			}
		}
	}

	@description = "Automation Ticket: LPS-151333 | Test Summary: Verify that the version field displays a drop-down list where you can select a version."
	@ignore = "true"
	@priority = "3"
	test CanViewADropdownListInTheVersionField {
		var portalVersionList = "6.1 GA1,6.1 GA2,6.1 GA3,6.2 EE";
		var fieldLabel = "Version";

		task ("Given I should be able to view all Portal versions options") {
			for (var version : list "${portalVersionList}") {
				CP2ActivationKeys.selectGenerateActivationKeyOptionByField(
					field = "${fieldLabel}",
					option = "${version}");

				CP2ActivationKeys.assertSelectedFieldText(
					field = "${fieldLabel}",
					option = "${version}");
			}
		}
	}

	@description = "Automation Ticket: LPS-151336 | Test Summary: Verify that after selecting a subscription, you receive an informational message."
	@ignore = "true"
	@priority = "3"
	test CanViewAnInformationalMessageInTheSubscription {
		task ("Given I select the first available signature") {
			CP2ActivationKeys.selectFistActiveSubscriptionInput();
		}

		task ("Then I assert the Alert Info message content") {
			var startDate = CP2ActivationKeys.getStartDateTextFromSignature();

			CustomerPortal.assertAlertInfoMessage(messageText = "Activation Keys will be valid indefinitely starting ${startDate} or until manually deactivated.");
		}
	}

	@description = "Automation Ticket: LPS-151337 | Test Summary: Verify that after filling out all the fields the Next button is enabled."
	@ignore = "true"
	@priority = "3"
	test CanViewTheNextButtonEnabled {
		task ("Given I assert that the Next button is desabled") {
			CustomerPortal.assertButtonIsDisabled(text = "Next");
		}

		task ("Given I fulfill first phase form for Generate New License") {
			CP2ActivationKeys.selectGenerateActivationKeyOptionByField(
				field = "Product",
				option = "Portal");

			CP2ActivationKeys.selectGenerateActivationKeyOptionByField(
				field = "Version",
				option = "6.1 GA1");

			CP2ActivationKeys.selectGenerateActivationKeyOptionByField(
				field = "Key Type",
				option = "Production");

			CP2ActivationKeys.selectFistActiveSubscriptionInput();
		}

		task ("Then I should be able to see that the Next button is enabled") {
			CustomerPortal.assertButtonIsActive(text = "Next");
		}
	}

	@description = "Automation Ticket: LPS-151335 | Test Summary: Verify that signatures without available keys are displayed as disabled options."
	@ignore = "true"
	@priority = "3"
	test SignaturesWithoutAvailableKeysWillBeDisabled {
		task ("Given I should be able to view a signature with no keys availables") {
			CP2ActivationKeys.assertSignatureWithNoKeysIsVisible();
		}
	}

	@description = "Automation Ticket: LPS-151332 | Test Summary: Verify if the product field is already selected by default."
	@ignore = "true"
	@priority = "2"
	test TheProductFieldIsSelectedByDefault {
		task ("Then I should be able to see that 'Portal' product is selected by default") {
			CP2ActivationKeys.assertSelectedFieldText(
				field = "Product",
				option = "${testProduct}");
		}
	}

}