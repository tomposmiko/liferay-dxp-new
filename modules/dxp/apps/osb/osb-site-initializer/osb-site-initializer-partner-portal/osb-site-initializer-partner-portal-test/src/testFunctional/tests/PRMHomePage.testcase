@component-name = "OSB Site Initializer Partner Portal"
definition {

	var baseURL = PropsUtil.get("portal.url");

	property custom.properties = "feature.flag.LPS-135430=true";
	property osgi.modules.includes = "osb-site-initializer-partner-portal";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "OSB Site Initializer Partner Portal";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Partner Portal Site") {
			PRMUtils.addPRMSite();
		}

		PRMUtils.gotoPRM();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Partner Portal");
		}
	}

	@description = "LPS-172079 - Verify that the admin user can add a new announcement."
	@priority = "5"
	test AdminCanAddAnnouncement {
		task ("Given: The user goes to Web Content and enters on 'Announcements' folder") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "partner-portal");

			PRMUtils.gotoFolder(folderName = "Announcements");
		}

		task ("When: User add a new announcement and publish it") {
			PRMAnnouncements.addAndPublishAnnouncement(
				announcementBody = "Announcement Body",
				announcementTitle = "Announcement Title");
		}

		task ("Then: The announcement is visible on PRM Home Page") {
			PRMUtils.gotoPRM();

			PRMHomePage.viewAnnouncementOnList(
				announcementBody = "Announcement Body",
				announcementTitle = "Announcement Title");
		}
	}

	@description = "This is a test for LPS-172458. User can access Deal Registration Listing through Quick Links"
	@priority = "4"
	test CanAccessDealListingViaQuickLinks {
		task ("When I click on 'Deal Registration' button, on Quick Links section") {
			PRMHomePage.gotoQuickLinksOption(
				buttonLink = "deal-registrations",
				buttonText = "Deal Registration");
		}

		task ("Then I'm redirected to the 'Deal Registration Listing' page") {
			PRMPages.assertPageTitle(pageTitle = "Partner Deal Registration");
		}
	}

	@description = "This is a test for LPS-171642. User can access MDF Request Listing through Quick Links"
	@priority = "4"
	test CanAccessMDFRequestListingViaQuickLinks {
		task ("When I click on 'MDF' button, on Quick Links section") {
			PRMHomePage.gotoQuickLinksOption(
				buttonLink = "mdf-requests",
				buttonText = "MDF");
		}

		task ("Then I'm redirected to the 'MDF Request Listing' page") {
			PRMPages.assertPageTitle(pageTitle = "MDF Requests");
		}
	}

}