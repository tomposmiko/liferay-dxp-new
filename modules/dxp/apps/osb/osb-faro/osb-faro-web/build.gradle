afterEvaluate {
	Task configJSModulesTask = tasks.findByName("configJSModules")

	if (configJSModulesTask != null) {
		configJSModulesTask.dependsOn = []
		configJSModulesTask.enabled = false
	}
}

dependencies {
	compileInclude group: "com.fasterxml.jackson.jaxrs", name: "jackson-jaxrs-base", version: "2.11.4"
	compileInclude group: "com.fasterxml.jackson.jaxrs", name: "jackson-jaxrs-json-provider", version: "2.11.4"
	compileInclude group: "com.github.albfernandez", name: "juniversalchardet", version: "2.4.0"
	compileInclude group: "com.github.scribejava", name: "scribejava-core", version: "6.6.3"
	compileInclude group: "com.univocity", name: "univocity-parsers", version: "2.9.1"
	compileInclude group: "commons-codec", name: "commons-codec", version: "1.15"
	compileInclude group: "fi.solita.clamav", name: "clamav-client", version: "1.0.1"
	compileInclude group: "javax.annotation", name: "javax.annotation-api", version: "1.3.2"
	compileInclude group: "org.apache.commons", name: "commons-collections4", version: "4.4"
	compileInclude group: "org.apache.commons", name: "commons-lang3", version: "3.12.0"
	compileInclude group: "org.apache.httpcomponents", name: "httpclient", version: "4.5.13"
	compileInclude group: "org.apache.httpcomponents", name: "httpcore", version: "4.4.15"
	compileOnly group: "com.liferay.portal", name: "release.dxp.api"
	compileOnly group: "com.sun.mail", name: "jakarta.mail", version: "1.6.6"
	compileOnly group: "org.apache.cxf", name: "cxf-core", version: "3.2.14"
	compileOnly group: "org.apache.cxf", name: "cxf-rt-frontend-jaxrs", version: "3.2.14"
	compileOnly project(":apps:ip-geocoder:ip-geocoder-api")
	compileOnly project(":dxp:apps:osb:osb-faro:osb-faro-api")
	compileOnly project(":dxp:apps:osb:osb-faro:osb-faro-contacts-api")
	compileOnly project(":dxp:apps:osb:osb-faro:osb-faro-engine-client")
	compileOnly project(":dxp:apps:osb:osb-faro:osb-faro-provisioning-client")
}

generateJSPJava {
	enabled = false
}

packageRunWebpack {
	if (project.hasProperty("faroEnvironmentName")) {
		environment "FARO_ENVIRONMENT_NAME", faroEnvironmentName
	}

	enabled = ((System.getProperty("user.name") != "root") || (System.getProperty("liferay.ci") == "true"))

	inputs.dir "src/main/css"
	inputs.dir "src/main/images"
	inputs.dir "src/main/js"

	outputs.dir "src/main/resources/META-INF/resources/dist"
}

processResources {
	dependsOn packageRunWebpack
}