definition {

	macro _curlDeleteByErc {
		Variables.assertDefined(parameterList = "${groupName},${wikiPageAttachmentErc},${wikiPageErc}");

		var siteId = JSONGroupAPI._getGroupIdByNameNoSelenium(
			groupName = ${groupName},
			site = "true");
		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/o/headless-delivery/v1.0/sites/${siteId}/wiki-pages/by-external-reference-code/${wikiPageErc}/wiki-page-attachments/by-external-reference-code/${wikiPageAttachmentErc} \
				-u test@liferay.com:test \
				-H Content-Type: application/json \
				-H accept: application/json \
		''';

		return ${curl};
	}

	macro _curlWikiPageAttachments {
		var portalURL = JSONCompany.getPortalURL();

		if (isSet(wikiPageId)) {
			var api = "wiki-pages/${wikiPageId}/wiki-page-attachments";
		}
		else if (isSet(wikiPageAttachementId)) {
			var api = "/wiki-page-attachments/${wikiPageAttachementId}";
		}
		else {
			Variables.assertDefined(parameterList = "${groupName},${wikiPageErc},${wikiPageAttachementErc}");

			var siteId = JSONGroupAPI._getGroupIdByNameNoSelenium(
				groupName = ${groupName},
				site = "true");

			var api = "sites/${siteId}/wiki-pages/by-external-reference-code/${wikiPageErc}/wiki-page-attachments/by-external-reference-code/${wikiPageAttachementErc}";
		}

		var curl = '''
			${portalURL}/o/headless-delivery/v1.0/${api} \
				-u test@liferay.com:test \
				-H Content-Type: application/json
		''';

		return ${curl};
	}

	macro _getAttachmentById {
		Variables.assertDefined(parameterList = ${wikiPageAttachmentId});

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/o/headless-delivery/v1.0/wiki-page-attachments/${wikiPageAttachmentId} \
				-u test@liferay.com:test \
				-H Content-Type: application/json \
				-H accept: application/json \
		''';

		var curl = JSONCurlUtil.get(${curl});

		return ${curl};
	}

	macro AssertCorrectErcInResponse {
		Variables.assertDefined(parameterList = "${responseToParse},${expectedErc}");

		var actualErc = JSONUtil.getWithJSONPath(${responseToParse}, "$..externalReferenceCode");

		TestUtils.assertEquals(
			actual = ${actualErc},
			expected = ${expectedErc});
	}

	macro createWikiPageAttachment {
		Variables.assertDefined(parameterList = "${filePath},${wikiPageId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/o/headless-delivery/v1.0/wiki-pages/${wikiPageId}/wiki-page-attachments \
				-u test@liferay.com:test \
				-H Content-Type: multipart/form-data \
				-F file=@${filePath}
		''';

		var curl = JSONCurlUtil.post(${curl});

		return ${curl};
	}

	macro deleteByErc {
		Variables.assertDefined(parameterList = "${groupName},${wikiPageAttachmentErc},${wikiPageErc}");

		var curl = WikiPageAttachmentAPI._curlDeleteByErc(
			groupName = ${groupName},
			wikiPageAttachmentErc = ${wikiPageAttachmentErc},
			wikiPageErc = ${wikiPageErc});

		var curl = JSONCurlUtil.delete(${curl});

		return ${curl};
	}

	macro getAttachmentById {
		Variables.assertDefined(parameterList = ${wikiPageAttachmentId});

		var response = WikiPageAttachmentAPI._getAttachmentById(wikiPageAttachmentId = ${wikiPageAttachmentId});

		return ${response};
	}

	macro getWikiPageAttachments {
		var curl = WikiPageAttachmentAPI._curlWikiPageAttachments(
			groupName = ${groupName},
			wikiPageAttachementErc = ${wikiPageAttachementErc},
			wikiPageAttachementId = ${wikiPageAttachementId},
			wikiPageErc = ${wikiPageErc},
			wikiPageId = ${wikiPageId});

		var response = JSONCurlUtil.get(${curl});

		return ${response};
	}

	macro setupGlobalWikiPageWithAttachment {
		var wikiPage = JSONWiki.addWikiPage(
			groupName = "Guest",
			noSelenium = "true",
			wikiNodeName = "Main",
			wikiPageContent = "Wiki Page Content",
			wikiPageName = "Wiki Page Title");

		var wikiPageId = JSONPathUtil.getIdValue(response = ${wikiPage});
		var wikiPageErc = JSONPathUtil.getErcValue(response = ${wikiPage});
		var filePath = TestCase.getDependenciesDirPath(fileName = "Document_1.txt");

		var wikiPageAttachment = WikiPageAttachmentAPI.createWikiPageAttachment(
			filePath = ${filePath},
			wikiPageId = ${wikiPageId});

		var wikiPageAttachmentErc = JSONPathUtil.getErcValue(response = ${wikiPageAttachment});
		var wikiPageAttachmentId = JSONPathUtil.getIdValue(response = ${wikiPageAttachment});
		static var staticWikiPageId = ${wikiPageId};
		static var staticWikiPageErc = ${wikiPageErc};
		static var staticWikiPageAttachmentId = ${wikiPageAttachmentId};
		static var staticWikiPageAttachmentErc = ${wikiPageAttachmentErc};

		return "${staticWikiPageId }";

		return ${staticWikiPageErc};

		return ${staticWikiPageAttachmentId};

		return ${staticWikiPageAttachmentErc};
	}

}