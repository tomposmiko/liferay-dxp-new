definition {

	macro _createTaxonomyCategory {
		var portalURL = JSONCompany.getPortalURL();

		if (isSet(parentTaxonomyCategoryId)) {
			var api = "taxonomy-categories/${parentTaxonomyCategoryId}/taxonomy-categories";
		}
		else {
			var api = "taxonomy-vocabularies/${taxonomyVocabularyId}/taxonomy-categories";
		}

		var curl = '''
			${portalURL}/o/headless-admin-taxonomy/v1.0/${api} \
				-u test@liferay.com:test \
				-H Content-Type: application/json \
				-d {
					"externalReferenceCode": "${externalReferenceCode}",
					"name": "${name}"
				}
		''';

		var curl = JSONCurlUtil.post(${curl});

		return ${curl};
	}

	macro _curlTaxonomyCategoryByErc {
		Variables.assertDefined(parameterList = "${externalReferenceCode},${taxonomyVocabularyId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/o/headless-admin-taxonomy/v1.0/taxonomy-vocabularies/${taxonomyVocabularyId}/taxonomy-categories/by-external-reference-code/${externalReferenceCode} \
				-u test@liferay.com:test \
				-H Content-Type: application/json \
				-H accept: application/json \
		''';

		return ${curl};
	}

	macro _getTaxonomyCategory {
		Variables.assertDefined(parameterList = ${taxonomyCategoryId});

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/o/headless-admin-taxonomy/v1.0/taxonomy-categories/${taxonomyCategoryId} \
				-u test@liferay.com:test \
				-H Content-Type: application/json \
		''';

		var curl = JSONCurlUtil.get(${curl});

		return ${curl};
	}

	macro createTaxonomyCategory {
		Variables.assertDefined(parameterList = ${name});

		if (!(isSet(externalReferenceCode))) {
			var externalReferenceCode = "";
		}

		var response = TaxonomyCategoryAPI._createTaxonomyCategory(
			externalReferenceCode = ${externalReferenceCode},
			name = ${name},
			parentTaxonomyCategoryId = ${parentTaxonomyCategoryId},
			taxonomyVocabularyId = ${taxonomyVocabularyId});

		return ${response};
	}

	macro deleteTaxonomyCategoryByErc {
		var curl = TaxonomyCategoryAPI._curlTaxonomyCategoryByErc(
			externalReferenceCode = ${externalReferenceCode},
			taxonomyVocabularyId = ${taxonomyVocabularyId});

		var response = JSONCurlUtil.delete(${curl});

		return ${response};
	}

	macro getTaxonomyCategory {
		var getTaxonomyCategoryDetails = TaxonomyCategoryAPI._getTaxonomyCategory(taxonomyCategoryId = ${taxonomyCategoryId});

		return ${getTaxonomyCategoryDetails};
	}

	macro getTaxonomyCategoryByErc {
		var curl = TaxonomyCategoryAPI._curlTaxonomyCategoryByErc(
			externalReferenceCode = ${externalReferenceCode},
			taxonomyVocabularyId = ${taxonomyVocabularyId});

		var response = JSONCurlUtil.get(${curl});

		return ${response};
	}

	macro setUpGlobalResponse {
		var assetLibraryId = JSONGroupAPI._getDepotIdByName(depotName = "Test Depot Name");

		var response = TaxonomyVocabularyAPI.createTaxonomyVocabulary(
			assetLibraryId = ${assetLibraryId},
			groupName = "Guest",
			name = "Vocabulary Name");

		static var responseOfCreated = ${response};

		return ${responseOfCreated};
	}

	macro staticTaxonomyCategoryId {
		Variables.assertDefined(parameterList = ${name});

		var response = TaxonomyCategoryAPI.createTaxonomyCategory(
			externalReferenceCode = ${externalReferenceCode},
			name = ${name},
			parentTaxonomyCategoryId = ${parentTaxonomyCategoryId},
			taxonomyVocabularyId = ${taxonomyVocabularyId});

		var taxonomyCategoryId = JSONPathUtil.getIdValue(response = ${response});

		if (isSet(parentTaxonomyCategoryId)) {
			static var staticChildTaxonomyCategoryId = ${taxonomyCategoryId};

			return ${staticChildTaxonomyCategoryId};
		}
		else {
			static var staticTaxonomyCategoryId = ${taxonomyCategoryId};

			return ${staticTaxonomyCategoryId};
		}
	}

	macro updateTaxonomyCategoryByErc {
		var curl = TaxonomyCategoryAPI._curlTaxonomyCategoryByErc(
			externalReferenceCode = ${externalReferenceCode},
			taxonomyVocabularyId = ${taxonomyVocabularyId});
		var body = '''-d {"name": "${name}"}''';

		var curl = StringUtil.add(${curl}, ${body}, " ");

		var response = JSONCurlUtil.put(${curl});

		return ${response};
	}

}