definition {

	macro _editWalkthroughJSON {
		var jsonSnippet = Walkthrough.getWalkthroughSteps(fileName = "${fileName}");

		SystemSettings.editTextAreaSetting(
			settingName = "Steps",
			settingValue = "${jsonSnippet}");
	}

	macro _enableWalkthrough {
		SystemSettings.configureSystemSetting(
			enableSetting = "true",
			settingFieldName = "Enable Walkthrough");
	}

	macro addWalkthrough {
		Walkthrough.gotoSiteConfigWalkthrough();

		Walkthrough._enableWalkthrough();

		Walkthrough._editWalkthroughJSON(fileName = "${fileName}");

		SystemSettings.saveConfiguration();
	}

	macro enablePopoverMode {
		if (IsElementNotPresent(locator1 = "Walkthrough#POPOVER")) {
			if (IsElementPresent(key_portletName = "JS Walkthrough Sample", locator1 = "Portlet#TITLE")) {
				Click(locator1 = "Walkthrough#HOTSPOT");
			}
			else {
				Click(locator1 = "Walkthrough#SAMPLE_PORTLET_HOTSPOT");
			}
		}
	}

	macro getWalkthroughSteps {
		var dependencyFilePath = TestCase.getDependenciesDirPath(fileName = "${fileName}");

		var fileContent = FileUtil.read("${dependencyFilePath}");

		return "${fileContent}";
	}

	macro goToNextStep {
		Click(locator1 = "Button#OK");

		VerifyElementPresent(
			key_title = "Step ${key_step}",
			locator1 = "Walkthrough#POPOVER_TITLE");
	}

	macro goToPreviousStep {
		Click(locator1 = "Button#PREVIOUS");

		VerifyElementPresent(
			key_title = "Step ${key_step}",
			locator1 = "Walkthrough#POPOVER_TITLE");
	}

	macro gotoSiteConfigWalkthrough {
		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Site Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Walkthrough",
			configurationName = "Walkthrough",
			configurationScope = "Site Scope");
	}

	macro goToSpecificStep {
		while (IsElementNotPresent(key_title = "Step ${key_step}", locator1 = "Walkthrough#POPOVER_TITLE")) {
			Click(locator1 = "Button#OK");
		}
	}

	macro setupWalkthroughPage {
		task ("Given there is a site-level JSON defined for current site") {
			JSONGroup.addGroup(groupName = "Site Name");

			ApplicationsMenu.gotoSite(site = "Site Name");

			Walkthrough.addWalkthrough(fileName = "${filename}");

			JSONLayout.addPublicLayout(
				groupName = "Site Name",
				layoutName = "Walkthrough Page");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Site Name",
				layoutName = "Walkthrough Page",
				widgetName = "JS Walkthrough Sample");
		}

		task ("When instantiate the walkthrough") {
			Navigator.openSitePage(
				pageName = "Walkthrough Page",
				siteName = "Site Name");

			Walkthrough.enablePopoverMode();
		}
	}

}