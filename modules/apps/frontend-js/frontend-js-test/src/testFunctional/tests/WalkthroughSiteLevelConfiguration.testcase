@component-name = "portal-frontend-infrastructure"
definition {

	property osgi.modules.includes = "frontend-js-walkthrough-sample-web";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Walkthrough";
	property testray.main.component.name = "User Interface";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		task ("Given a new Site") {
			JSONGroup.addGroup(groupName = "Site Name");

			ApplicationsMenu.gotoSite(site = "Site Name");
		}

		task ("And Given a walkthrough sample portlet") {
			JSONLayout.addPublicLayout(
				groupName = "Site Name",
				layoutName = "Walkthrough Page 1");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Site Name",
				layoutName = "Walkthrough Page 1",
				widgetName = "JS Walkthrough Sample");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Site Name");
		}
	}

	@description = "LPS-159841. Walkthrough does not show when disabled."
	@priority = "5"
	test CanBeDisabled {
		property portal.acceptance = "true";

		var portalURL = PropsUtil.get("portal.url");

		task ("Given walkthrough enabled site-level") {
			Walkthrough.addWalkthrough(fileName = "walkthrough_configuration.json");
		}

		task ("When walkthrough is disabled") {
			SystemSettings.configureSystemSetting(
				enableSetting = "false",
				settingFieldName = "Enable Walkthrough");

			SystemSettings.saveConfiguration();
		}

		task ("Then hotspot element is not present on site") {
			Navigator.openSitePage(
				pageName = "Walkthrough Page 1",
				siteName = "Site Name");

			AssertElementNotPresent(locator1 = "Walkthrough#HOTSPOT");
		}
	}

	@description = "LPS-159838. Can enable walkthrough in System Settings."
	@priority = "5"
	test CanBeEnabled {
		property portal.acceptance = "true";

		var portalURL = PropsUtil.get("portal.url");

		task ("Given Walkthrough in System Settings") {
			Walkthrough.gotoSiteConfigWalkthrough();
		}

		task ("When walkthrough is enabled") {
			Walkthrough._enableWalkthrough();
		}

		task ("Then success message appears") {
			Alert.viewSuccessMessage();
		}
	}

	@description = "LPS-159839. JSON can be configured on System Settings."
	@priority = "5"
	test JSONCanBeConfigured {
		property portal.acceptance = "true";

		var portalURL = PropsUtil.get("portal.url");

		task ("Given Walkthrough in System Settings") {
			Walkthrough.gotoSiteConfigWalkthrough();
		}

		task ("When a JSON description is saved") {
			Walkthrough._editWalkthroughJSON(fileName = "walkthrough_configuration.json");

			SystemSettings.saveConfiguration();
		}

		task ("Then success message appears") {
			Alert.viewSuccessMessage();
		}
	}

	@description = "LPS-159840. JSON can be updated."
	@priority = "5"
	test JSONCanBeUpdated {
		property portal.acceptance = "true";

		var portalURL = PropsUtil.get("portal.url");

		task ("Given walkthrough enabled site-level") {
			Walkthrough.addWalkthrough(fileName = "walkthrough_configuration.json");
		}

		task ("When step 1 title is modified") {
			Walkthrough._editWalkthroughJSON(fileName = "edit_walkthrough_configuration.json");

			SystemSettings.saveConfiguration();
		}

		task ("Then step 1 title is updated") {
			Navigator.openSitePage(
				pageName = "Walkthrough Page 1",
				siteName = "Site Name");

			Walkthrough.enablePopoverMode();

			AssertElementPresent(
				key_title = "Step 1 of 2: EDIT: Click the Button",
				locator1 = "Walkthrough#POPOVER_TITLE");
		}
	}

	@description = "LPS-159836. Walkthrough displays when defined for a site."
	@priority = "5"
	test WillDisplayWhenDefinedForSite {
		property portal.acceptance = "true";

		var portalURL = PropsUtil.get("portal.url");

		task ("Given there is a site-level JSON defined for current site") {
			Walkthrough.addWalkthrough(fileName = "walkthrough_configuration.json");

			JSONLayout.addPublicLayout(
				groupName = "Site Name",
				layoutName = "Walkthrough Page 1");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Site Name",
				layoutName = "Walkthrough Page 1",
				widgetName = "JS Walkthrough Sample");
		}

		task ("When instantiate the walkthrough") {
			Navigator.openSitePage(
				pageName = "Walkthrough Page 1",
				siteName = "Site Name");

			Walkthrough.enablePopoverMode();
		}

		task ("Then walkthrough is instantiated on the site") {
			AssertElementPresent(
				key_title = "Step 1 of 2: Click the Button",
				locator1 = "Walkthrough#POPOVER_TITLE");
		}
	}

	@description = "LPS-159837. Walkthrough will not display when not defined for a site."
	@priority = "5"
	test WillNotDisplayWhenNotDefinedForSite {
		property portal.acceptance = "true";

		var portalURL = PropsUtil.get("portal.url");

		task ("Given there is no site-level JSON defined for current site") {
			JSONLayout.addPublicLayout(
				groupName = "Site Name",
				layoutName = "Walkthrough Page 1");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Site Name",
				layoutName = "Walkthrough Page 1",
				widgetName = "JS Walkthrough Sample");
		}

		task ("When instantiate the walkthrough") {
			Navigator.openSitePage(
				pageName = "Walkthrough Page 1",
				siteName = "Site Name");
		}

		task ("Then walkthrough is not instantiated on the site") {
			AssertElementNotPresent(locator1 = "Walkthrough#POPOVER");
		}
	}

}