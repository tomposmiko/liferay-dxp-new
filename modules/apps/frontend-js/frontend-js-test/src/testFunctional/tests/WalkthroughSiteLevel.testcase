@component-name = "portal-frontend-infrastructure"
definition {

	property osgi.modules.includes = "frontend-js-walkthrough-sample-web";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Walkthrough";
	property testray.main.component.name = "User Interface";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Site Name");

		ApplicationsMenu.gotoSite(site = "Site Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Site Name");
		}
	}

	@description = "LPS-159823. When skippable is set to true, checked and ends all steps the hotspot element is not present."
	@priority = "3"
	test CanEnableSkippable {
		property portal.acceptance = "true";

		task ("Given walkthrough enabled site-level") {
			Walkthrough.setupWalkthroughPage(
				filename = "walkthrough_configuration_single_page.json",
				siteName = "Site Name");
		}

		task ("And When navigate to second step") {
			Walkthrough.goToSpecificStep(key_step = "2");
		}

		task ("And When enable Do not show me this again") {
			Check.checkNotVisible(
				checkboxName = "Do not show me this again.",
				locator1 = "Checkbox#ANY_CHECKBOX");
		}

		task ("And When navigate to next step") {
			Walkthrough.goToNextStep(key_step = "3");
		}

		task ("And When click ok to finish the steps") {
			Click(
				key_text = "Close",
				locator1 = "Button#ANY");
		}

		task ("Then popover mode is not present") {
			AssertElementNotPresent(
				key_title = "",
				locator1 = "Walkthrough#HOTSPOT");
		}
	}

	@description = "LPS-159844. When a walkthrough step is defined and a step is below the fold, user can see popover is."
	@priority = "3"
	test CanRenderWhenStepIsBelowFold {
		property portal.acceptance = "true";

		task ("Given walkthrough enabled site-level") {
			Walkthrough.addWalkthrough(fileName = "walkthrough_configuration_multiple_page.json");

			JSONLayout.addPublicLayout(
				groupName = "Site Name",
				layoutName = "Walkthrough Page 1");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Site Name",
				layoutName = "Walkthrough Page 1",
				widgetName = "JS Walkthrough Sample");
		}

		task ("Given a page width larger than the preview window.") {
			JSONLayout.addPublicLayout(
				groupName = "Site Name",
				layoutName = "Walkthrough Page 2");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Site Name",
				layoutName = "Walkthrough Page 2",
				widgetName = "Clay Sample");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Site Name",
				layoutName = "Walkthrough Page 2",
				widgetName = "JS Walkthrough Sample");
		}

		task ("When enable popover mode") {
			Navigator.openSitePage(
				pageName = "Walkthrough Page 1",
				siteName = "Site Name");

			Walkthrough.enablePopoverMode();
		}

		task ("And When navigate to step that is below the fold") {
			Walkthrough.goToNextStep(key_step = "2");
		}

		task ("Then popover element is visible when rendered") {
			AssertVisible(
				key_title = "Step 2 of 2: Enter Your Name",
				locator1 = "Walkthrough#POPOVER_TITLE");
		}
	}

	@description = "LPS-167997. It is possible use same JSON on different sites as long as the pages have same name. "
	@priority = "4"
	test SameJSONCanBeEnabledOnMultipleSites {
		property portal.acceptance = "true";

		task ("When site 1 has walkthrough enabled site-level with JSON") {
			Walkthrough.setupWalkthroughPage(
				filename = "walkthrough_configuration_single_page.json",
				siteName = "Site Name");
		}

		task ("And When site 2 has walkthrough enabled site-level with JSON") {
			task ("Create site 2") {
				JSONGroup.addGroup(groupName = "Test Site Name");

				ApplicationsMenu.gotoSite(site = "Test Site Name");
			}

			Walkthrough.setupWalkthroughPage(
				filename = "walkthrough_configuration_single_page.json",
				siteName = "Test Site Name");
		}

		task ("Then hotspot element is present on first site") {
			Navigator.openSitePage(
				pageName = "Walkthrough Page 1",
				siteName = "Site Name");

			AssertElementPresent(
				key_title = "",
				locator1 = "Walkthrough#POPOVER_TITLE");
		}

		task ("And Then hotspot element is present on second site") {
			Navigator.openSitePage(
				pageName = "Walkthrough Page 1",
				siteName = "Test Site Name");

			AssertElementPresent(
				key_title = "",
				locator1 = "Walkthrough#POPOVER_TITLE");
		}
	}

	@description = "LPS-159826. Checked that each step corresponds to the expected Step Id."
	@priority = "5"
	test StepCanCorrespondToCorrectStepId {
		property portal.acceptance = "true";

		var portalURL = PropsUtil.get("portal.url");

		task ("Given walkthrough enabled site-level") {
			Walkthrough.setupWalkthroughPage(
				filename = "walkthrough_configuration_multiple_page.json",
				siteName = "Site Name");
		}

		task ("When on step 1") {
			AssertLocation(value1 = "${portalURL}/web/site-name/walkthrough-page-1");
		}

		task ("Then correspond to the correct step ID on correct page") {
			Walkthrough.assertHighlightedSpecificObject(key_stepId = "step-1");
		}

		task ("When on step 2 correspond to the correct step ID on correct page") {
			Walkthrough.goToNextStep(key_step = "2");

			AssertLocation(value1 = "${portalURL}/web/site-name/walkthrough-page-2");

			Walkthrough.assertHighlightedSpecificObject(key_stepId = "step-2");
		}
	}

}