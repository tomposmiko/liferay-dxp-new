import com.liferay.gradle.util.copy.StripPathSegmentsAction

task buildJQuery(type: Copy)
task buildJQueryPluginsForm

File jsDestinationDir = file("tmp/META-INF/resources")

buildJQuery {
	File jqueryDestinationDir = new File(jsDestinationDir, "jquery")

	dependsOn npmInstall

	doFirst {
		delete jqueryDestinationDir
	}

	eachFile new StripPathSegmentsAction(2)

	from npmInstall.nodeModulesDir
	include "jquery/dist/jquery.js"
	includeEmptyDirs = false
	into jqueryDestinationDir
}

buildJQueryPluginsForm {
	File jQueryPluginsFormJsFile = file("src/main/resources/META-INF/resources/jquery/form.js")

	dependsOn npmInstall

	doLast {
		String text = file(npmInstall.nodeModulesDir + "/jquery-form/jquery.form.js").text

		text = text.replace("typeof define === 'function'", "false && typeof define === 'function'")

		text = ";(function(jQuery){" + System.getProperty("line.separator") + text + '})(AUI.$);'

		jQueryPluginsFormJsFile.text = text
	}

	onlyIf {
		!jQueryPluginsFormJsFile.exists()
	}
}

classes {
	dependsOn buildJQuery
	dependsOn buildJQueryPluginsForm
}

clean {
	delete "tmp"
}

configJSModules {
	enabled = false;
}

dependencies {
	compileOnly group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.taglib", version: "default"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "3.0.0"
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compileOnly group: "javax.servlet.jsp", name: "javax.servlet.jsp-api", version: "2.3.1"
	compileOnly group: "org.osgi", name: "org.osgi.core", version: "6.0.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.component", version: "1.3.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly project(":apps:portal-url-builder:portal-url-builder-api")
	compileOnly project(":apps:static:osgi:osgi-util")
	compileOnly project(":apps:static:portal-configuration:portal-configuration-metatype-api")
	compileOnly project(":core:osgi-felix-util")
	compileOnly project(":core:osgi-service-tracker-collections")
	compileOnly project(":core:petra:petra-lang")
	compileOnly project(":core:petra:petra-string")
}