definition {

	macro addRaylifeAPSite {
		Site.openSitesAdmin();

		Site.addSiteTemplateCP(
			siteName = ${siteName},
			siteTemplateName = "Raylife AP");
	}

	macro assertApplicationNumber {
		var applicationvalue = selenium.getText("RaylifeAP#APPLICATIONS_NUMBER");

		AssertElementNotPresent(locator1 = ${applicationvalue});
	}

	macro assertFieldRequiredMessage {
		AssertElementPresent(
			key_field = ${field},
			locator1 = "RaylifeAP#FIELD_REQUIRED_MESSAGE");
	}

	@summary = "This macro states that the filter options are hidden"
	macro assertNotViewFilterOptions {
		for (var optionList : list ${options}) {
			AssertNotVisible(
				key_option = ${optionList},
				locator1 = "RaylifeAP#RESPONSIVE_FILTER_OPTIONS");
		}
	}

	macro assertPageHeader {
		AssertElementPresent(
			key_header = ${key_header},
			locator1 = "RaylifeAP#COMPONENT_HEADER");
	}

	@summary = "This macro asserts the number of entries shown in the table"
	macro assertQuantity {
		var key_quantity = ${text};

		AssertVisible(locator1 = "RaylifeAP#ASSERT_QUANTITY");
	}

	@summary = "Assert element present in the table"
	macro assertTableElement {
		AssertElementPresent(
			key_colunmPosition = ${column},
			key_linePosition = ${line},
			key_text = ${text},
			locator1 = "RaylifeAP#TABLE_ELEMENT_BY_POSITION");
	}

	macro assertTableValue {
		AssertElementPresent(
			key_product = ${product},
			key_status = ${status},
			locator1 = "RaylifeAP#APPLICATIONS_TABLE_KEBAB");
	}

	macro assertTextAtTable {
		var key_linePosition = ${linePosition};
		var key_colunmPosition = ${lineColunm};
		var key_text = ${text};

		AssertTextEquals(
			locator1 = "RaylifeAP#TABLE_ELEMENT_BY_POSITION",
			value1 = ${text});
	}

	@summary = "This macro states that the filter options are being displayed on the page"
	macro assertViewFilterOptions {
		for (var optionList : list ${options}) {
			AssertVisible(
				key_option = ${optionList},
				locator1 = "RaylifeAP#RESPONSIVE_FILTER_OPTIONS");
		}
	}

	macro assertVisibleSidebar {
		AssertVisible(
			key_menuSidebar = ${key_menuSidebar},
			locator1 = "RaylifeAP#MENU_SIDEBAR");
	}

	macro clickInApplicationKebabOption {
		Click(
			key_product = ${product},
			key_status = ${status},
			locator1 = "RaylifeAP#APPLICATIONS_TABLE_KEBAB");

		Click(
			key_option = ${option},
			locator1 = "RaylifeAP#KEBAB_OPTION");
	}

	macro clickOnAllAplications {
		Button.click(button = "All Applications");
	}

	@summary = "This macro enables and disables the filter"
	macro clickOnFilter {
		Click(locator1 = "RaylifeAP#ICON_FILTER");
	}

	macro clickOnSubsectionTasks {
		Click(
			key_subsection = ${key_subsection},
			locator1 = "RaylifeAP#TASKS_SECTION");
	}

	@summary = "This macro edits the deletion type to cascade and deletes all entries created when adding an application"
	macro deleteAllCreatedEntries {
		var entry = ${searchTerm};

		ObjectAdmin.editObjectDeletionType(
			deletionType = "Cascade",
			field = "Deletion",
			label = "Raylife Application",
			labelName = "RaylifeQuote");

		ObjectAdmin.editObjectDeletionType(
			deletionType = "Cascade",
			field = "Deletion",
			label = "Raylife Quote",
			labelName = "RaylifePolicy");

		ObjectAdmin.editObjectDeletionType(
			deletionType = "Cascade",
			field = "Deletion",
			label = "Raylife Policy",
			labelName = "RaylifeClaim");

		ApplicationsMenu.gotoPortlet(
			category = "Object",
			panel = "Control Panel",
			portlet = "Raylife Applications");

		Search.searchCP(searchTerm = ${searchTerm});

		if (IsElementPresent(key_entry = ${entry}, locator1 = "ObjectPortlet#ENTRY_KEBAB_MENU")) {
			ObjectPortlet.deleteEntry(entry = ${searchTerm});
		}
	}

	macro expandedSidebar {
		MouseOver(
			key_menuSidebar = ${key_menuSidebar},
			locator1 = "RaylifeAP#MENU_SIDEBAR");
	}

	@summary = "This macro is for any ellipses button on the Raylife AP site"
	macro gotoEllipsisMenuItem {
		Click.waitForMenuToggleJSClick(locator1 = "RaylifeAP#ANY_VERTICAL_ELLIPSIS");

		MenuItem.clickNoError(menuItem = ${menuItem});
	}

	macro goToRaylifeAPSiteWithRefresh {
		ApplicationsMenu.gotoSite(site = "Raylife AP");

		Refresh();
	}

	macro goToRaylifeUserProfileMenu {
		Click(locator1 = "UserBar#USER_AVATAR_ICON");

		ScrollWebElementIntoView(
			key_dropdownItem = "My Sites",
			locator1 = "UserBar#USER_AVATAR_DROPDOWN_ITEM");

		Click(
			key_dropdownItem = "My Sites",
			locator1 = "UserBar#USER_AVATAR_DROPDOWN_ITEM");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Navigator.gotoNavItem(navItem = "My Sites");

		SiteSelector.selectSite(site = ${raylifeSite});

		Refresh();
	}

	macro mouseOverSidebar {
		var key_menu = ${menu};

		MouseOver(locator1 = "RaylifeAP#MENU_SIDEBAR");
	}

	@summary = "This macro is for any search box on the Raylife AP site"
	macro search {
		WaitForElementPresent(locator1 = "RaylifeAP#SEARCH");

		AssertElementPresent(locator1 = "RaylifeAP#SEARCH");

		Type(
			locator1 = "RaylifeAP#SEARCH",
			value1 = ${searchTerm});

		KeyPress(
			locator1 = "RaylifeAP#SEARCH",
			value1 = "\RETURN");

		if (!(isSet(modal))) {
			WaitForLiferayEvent.initializeLiferayEventLog();
		}
	}

	macro signIn {
		if (isSet(emailAddress)) {
			Type(
				labelTitle = "Email Address",
				locator1 = "RaylifeAP#SIGN_IN_FORM",
				value1 = ${emailAddress});
		}
		else {
			Type(
				labelTitle = "Email Address",
				locator1 = "RaylifeAP#SIGN_IN_FORM",
				value1 = "test@liferay.com");
		}

		if (isSet(password)) {
			Type(
				labelTitle = "Password",
				locator1 = "RaylifeAP#SIGN_IN_FORM",
				value1 = ${password});
		}
		else {
			Type(
				labelTitle = "Password",
				locator1 = "RaylifeAP#SIGN_IN_FORM",
				value1 = "test");
		}

		Check(
			inputFieldId = "rememberMe",
			locator1 = "TextInput#INPUT_ID");

		Click(locator1 = "Button#SIGN_IN");
	}

	macro tearDownCP {
		Site.openSitesAdmin();

		PortletEntry.selectAll();

		Click(locator1 = "Icon#SITES_VERTICAL_ELLIPSIS");

		MenuItem.clickNoError(menuItem = "Delete");

		Click(locator1 = "Button#MODAL_DELETE");

		Alert.viewSuccessMessage();
	}

	macro userLoginInRaylife {
		var passwordRaylife = "portal4all";
		var email = "${email}@mailinator.com";

		Navigator.openURL();

		SignIn._signIn(
			licenseExpired = "true",
			userEmailAddress = ${email},
			userPassword = ${passwordRaylife});

		Button.click(button = "I Agree");

		User.answerPasswordReminder();
	}

}