@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var buildName = "Build 1";
	var caseName = "Case 1";
	var caseType = "Case Type 1";
	var componentName = "Component 1";
	var environmentFactorsName = "Environment Factors 1";
	var productVersion = "1.2";
	var projectName = "Project 1";
	var runName = "Run 1";
	var routineName = "Routine 1";
	var teamName = "Team 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Login and create a Testray Site") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();

			Testray.addTestraySite(siteName = ${testSiteName});
		}

		task ("Given that a Testray project, team, component, routine, case type, case, environment factors, product version and build are created via API") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addRoutine(
				projectName = ${projectName},
				routineName = ${routineName});

			JSONTestray.addCases(
				caseName = ${caseName},
				caseType = ${caseType},
				componentName = ${componentName},
				projectName = ${projectName});

			JSONTestray.addEnvironmentFactors(environmentFactorsName = ${environmentFactorsName});

			JSONTestray.addProductVersion(
				productVersion = ${productVersion},
				projectName = ${projectName});

			JSONTestray.addBuild(
				buildName = ${buildName},
				productVersion = ${productVersion},
				projectName = ${projectName},
				routineName = ${routineName});
		}

		task ("Then I go to Site Testray 2") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(
				numberItems = 20,
				valuePagination = 150);
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "casetypes",
			objectName = ${caseType});

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "factorcategories",
			objectName = "Category 1");

		JSONTestrayAPI.deleteProject(projectName = ${projectName});

		Testray.testrayApisTearDown();

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutPG();
		}
	}

	@description = "Story ID: LPS-162434 | Test Summary: Verify if as an Administrator I should be able to add and delete a Run"
	@priority = 5
	test AdminCanAddAndDeleteRun {
		task ("Given that I add a Testray run via API") {
			JSONTestray.addRun(
				buildName = ${buildName},
				runName = ${runName});
		}

		task ("And I go to the run manage page") {
			Testray.goToProjectPage(projectName = ${projectName});

			TestrayRoutines.goToRoutine(routineName = ${routineName});

			TestrayBuild.goToBuild(buildName = ${buildName});

			Testray.projectTabNavigator(navTabName = "Runs");
		}

		task ("When I delete a run") {
			TestrayRun.deleteRun(runName = 00);
		}

		task ("Then I should be assert to say that the run no longer appears in the list") {
			Testray.assertItemNotPresent(text = 00);
		}
	}

	@description = "Story ID: LPS-162434 | Test Summary: Verify if as an Administrator I should be able to edit a Run"
	@priority = 5
	test AdminCanEditRun {
		task ("Given that a Testray categories, options and build is created") {
			TestrayLayout.clickManageOnSidebar();

			TestrayManage.selectOptionsManage(itemValue = "Categories");

			TestrayManage.addNewCategory(category = "Category 2");

			TestrayLayout.clickManageOnSidebar();

			TestrayManage.selectOptionsManage(itemValue = "Options");

			TestrayManage.createNewOption(
				category = "Category 1",
				option = "Option 2");

			TestrayManage.createNewOption(
				category = "Category 2",
				option = "Option 3");

			TestrayManage.closeModalInOptions();

			Testray.goToProjectPage(projectName = ${projectName});

			TestrayRoutines.goToRoutine(routineName = ${routineName});

			TestrayRoutines.selectDefaultEnvironmentFactors(
				categoryField = "Category 1",
				categoryField2 = "Category 2",
				listCategory = "Category 2",
				listOption = "Option 1",
				listOption2 = "Option 3",
				routineName = ${routineName});

			TestrayBuild.addBuildViaUI(
				buildFieldName = "Build 2",
				caseName = ${caseName},
				dropdownItem = "New Build",
				productVersionOption = ${productVersion},
				routine = ${routineName});

			TestrayBuild.goToBuild(buildName = "Build 2");
		}

		task ("And I go to the run page ") {
			Testray.projectTabNavigator(navTabName = "Runs");

			TestrayRun.assertValueRun(valueRun = "Option 1");
		}

		task ("When I edit a run") {
			TestrayRun.editRun(
				label = "Category 1",
				listOption = "Option 2",
				runName = 01);
		}

		task ("Then I should be assert to say that the option has been edited") {
			TestrayRun.assertValueRun(valueRun = "Option 2");
		}

		task ("Delete object") {
			JSONTestrayAPI.deleteObjectViaAPI(
				object = "factorcategories",
				objectName = "Category 2");
		}
	}

	@description = "Story ID: LPS-162434 | Test Summary: Verify if as an Administrator I should not be able to add a duplicate run"
	@ignore = "true"
	@priority = 5
	test AdminCanNotAddDuplicateRun {

		// TODO LPS-160591 AdminCanNotAddDuplicateRun pending implementation

	}

	@description = "Story ID: LPS-162434 | Test Summary: Verify if as an administrator I should not be allowed to add runs with identical environments"
	@ignore = "true"
	@priority = 4
	test AdminCanNotAddRunWithIdenticalEnvironments {

		// TODO LPS-160592 AdminCanNotAddRunWithIdenticalEnvironments pending implementation

	}

}