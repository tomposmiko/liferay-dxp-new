@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var buildName = "Build 1";
	var caseName = "Case 1";
	var caseType = "Case Type 1";
	var componentName = "Component 1";
	var environmentFactorsName = "Environment Factors 1";
	var productVersion = "1.2";
	var projectName = "Project 1";
	var teamName = "Team 1";
	var routineName = "Routine 1";
	var runName = "Option 1";
	var caseResultName = "case result 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Login and create a Testray Site") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();

			Testray.addTR2Site(siteName = ${testSiteName});
		}

		task ("create all objects: projects, routine, teams, components, cases, case type, enviroment factors, product version, build, category, options, run and case results") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = "Team 1");

			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = "Team 2");

			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addRoutine(
				projectName = ${projectName},
				routineName = "Routine 1");

			JSONTestray.addRoutine(
				projectName = ${projectName},
				routineName = "Routine 2");

			JSONTestray.addCaseType(caseType = "Case Type 2");

			JSONTestray.addCases(
				caseName = "Case 1",
				caseType = "Case Type 1",
				componentName = ${componentName},
				projectName = ${projectName});

			JSONTestray.addCases(
				caseName = "Case 2",
				caseType = "Case Type 2",
				componentName = ${componentName},
				projectName = ${projectName});

			JSONTestray.addEnvironmentFactors(environmentFactorsName = ${environmentFactorsName});

			JSONTestray.addProductVersion(
				productVersion = "7.0",
				projectName = ${projectName});

			JSONTestray.addProductVersion(
				productVersion = "1.2",
				projectName = ${projectName});

			JSONTestray.addBuild(
				buildName = "Build 1",
				productVersion = "7.0",
				projectName = ${projectName},
				routineName = "Routine 1");

			JSONTestray.addBuild(
				buildName = "Build 2",
				productVersion = "1.2",
				projectName = ${projectName},
				routineName = "Routine 2");

			JSONTestray.addRun(
				buildName = "Build 1",
				runName = "Run 1");

			JSONTestray.addRun(
				buildName = "Build 2",
				runName = "Run 2");

			JSONTestray.addCaseResult(
				buildName = "Build 1",
				caseName = "Case 1",
				caseResultName = "case result 1",
				componentName = ${componentName},
				runName = "Run 1");

			JSONTestray.addCaseResult(
				buildName = "Build 2",
				caseName = "Case 1",
				caseResultName = "case result 2",
				componentName = ${componentName},
				runName = "Run 2");
		}

		task ("Go to the case history page") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(
				numberItems = 20,
				valuePagination = 150);

			Testray.goToProjectPage(projectName = ${projectName});

			TestrayRoutines.goToRoutine(routineName = ${routineName});

			TestrayBuild.goToBuild(buildName = ${buildName});
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "productversions",
			objectName = "1.2");

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "productversions",
			objectName = "7.0");

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "routines",
			objectName = "Routine 1");

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "routines",
			objectName = "Routine 2");

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "teams",
			objectName = "Team 1");

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "teams",
			objectName = "Team 2");

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "casetypes",
			objectName = "Case Type 1");

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "casetypes",
			objectName = "Case Type 2");

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "factorcategories",
			objectName = "Category 1");

		JSONTestrayAPI.deleteProject(projectName = ${projectName});

		Testray.testrayApisTearDown();

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-173525 | Test Summary: Verify if as an Administrator I should be able to filter case results by assignee"
	@priority = 5
	test AdminCanFilterByAssignee {
		task ("Given that the user assign to me a case result") {
			TestrayResults.goToCaseResults(caseResultsName = "Case 1");

			TestrayTestflow.userAvatarNotPresent(
				label = "Dates",
				status = "Test Test");

			Button.click(button = "Assign to Me");
		}

		task ("When the user filter a case result by assign") {
			Testray.projectTabNavigator(navTabName = "History");

			TestrayFilter.clickFilter();

			Select(
				locator1 = "Select#SELECT_FIELD_LABEL",
				selectFieldLabel = "Assignee",
				value1 = "Test");

			Button.click(button = "Apply");
		}

		task ("Then the user sees routine 1 that has been assigned to the user") {
			AssertElementPresent(
				key_projectName = "Routine 1",
				locator1 = "Testray#ITEM_NAME");
		}

		task ("And the user can see user avatar in the table") {
			Testray.projectTabNavigator(navTabName = "Result");

			TestrayTestflow.userAvatarPresent(
				label = "Dates",
				status = "Test Test");
		}
	}

	@description = "Story ID: LPS-173525 | Test Summary: Verify if as an Administrator I should be able to filter case results by product version name"
	@priority = 5
	test AdminCanFilterByProductVersionName {
		task ("Given that the user has two product version different in the table") {
			TestrayResults.goToCaseResults(caseResultsName = "Case 1");

			Testray.projectTabNavigator(navTabName = "History");

			AssertElementPresent(
				key_projectName = "7.0",
				locator1 = "Testray#ITEM_NAME");

			AssertElementPresent(
				key_projectName = "1.2",
				locator1 = "Testray#ITEM_NAME");
		}

		task ("When the user filter a product version by name") {
			TestrayFilter.clickFilter();

			TestrayFilter.multiSelect(
				header = "Product Version Name",
				label = "Product Version Name",
				listMultiSelect = "7.0");

			TestrayFilter.assertMultiSelect(
				label = "Product Version Name",
				listMultiSelect = "7.0");

			Button.click(button = "Apply");
		}

		task ("Then the user see only product version 7.0 in the table") {
			AssertElementPresent(
				key_projectName = "7.0",
				locator1 = "Testray#ITEM_NAME");
		}

		task ("And do not see the product version 1.2 in the table") {
			AssertElementNotPresent(
				key_projectName = "1.2",
				locator1 = "Testray#ITEM_NAME");
		}
	}

	@description = "Story ID: LPS-173525 | Test Summary: Verify if as an Administrator I should be able to filter case results by routine"
	@priority = 5
	test AdminCanFilterByRoutine {
		task ("Given that the user has two routine different in the table") {
			TestrayResults.goToCaseResults(caseResultsName = "Case 1");

			Testray.projectTabNavigator(navTabName = "History");

			AssertElementPresent(
				key_projectName = "Routine 1",
				locator1 = "Testray#ITEM_NAME");

			AssertElementPresent(
				key_projectName = "Routine 2",
				locator1 = "Testray#ITEM_NAME");
		}

		task ("When the user select a routine called routine 1 in the filter") {
			TestrayFilter.clickFilter();

			Select(
				locator1 = "Select#SELECT_FIELD_LABEL",
				selectFieldLabel = "Routines",
				value1 = "Routine 1");

			Button.click(button = "Apply");
		}

		task ("Then the user see only the routine 1 in the table") {
			AssertElementPresent(
				key_projectName = "Routine 1",
				locator1 = "Testray#ITEM_NAME");
		}

		task ("And do not see Routine 2 in the table") {
			AssertElementNotPresent(
				key_projectName = "Routine 2",
				locator1 = "Testray#ITEM_NAME");
		}
	}

	@description = "Story ID: LPS-173525 | Test Summary: Verify if as an Administrator I should be able to filter case results by status"
	@priority = 5
	test AdminCanFilterByStatus {
		task ("Given that the user complete a test with passed status") {
			TestrayResults.goToCaseResults(caseResultsName = "Case 1");

			Button.click(button = "Assign to Me");

			Button.click(button = "Complete Test");

			TestrayTestflow.editStatus(selectStatus = "Passed");

			WaitForPageLoad();
		}

		task ("When the user filter a case result with passed status") {
			Testray.projectTabNavigator(navTabName = "History");

			TestrayFilter.clickFilter();

			Click.clickAtNotVisible(
				key_label = "Status",
				key_status = "Passed",
				locator1 = "TestrayFilter#STATUS_IN_FILTER");

			Button.click(button = "Apply");
		}

		task ("Then the user see passed status in the table") {
			AssertElementPresent(
				key_projectName = "Passed",
				locator1 = "Testray#ITEM_NAME");
		}
	}

	@description = "Story ID: LPS-173525 | Test Summary: Verify if as an Administrator I should be able to filter the filter options"
	@priority = 3
	test AdminCanFilterOptionsToFilter {
		task ("When the user searches for a field called routine") {
			TestrayResults.goToCaseResults(caseResultsName = "Case 1");

			Testray.projectTabNavigator(navTabName = "History");

			TestrayFilter.clickFilter();

			TestrayFilter.searchFilter(value = "Routines");
		}

		task ("Then the user can see the field label called routine") {
			AssertElementPresent(
				locator1 = "Select#SELECT_FIELD_LABEL",
				selectFieldLabel = "Routines");
		}

		task ("And the user no more see the field called assigne in the filter") {
			AssertElementNotPresent(
				locator1 = "Select#SELECT_FIELD_LABEL",
				selectFieldLabel = "Assignee");
		}
	}

}