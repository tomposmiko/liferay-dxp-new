@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var caseName = "Case 1";
	var caseType = "Case Type 1";
	var componentName = "Component 1";
	var environmentFactorsName = "Environment Factors 1";
	var productVersion = "1.2";
	var projectName = "Project 1";
	var routineName = "Routine 1";
	var suiteName = "Suite 1";
	var teamName = "Team 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Login and create a Testray Site") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();

			Testray.addTestraySite(siteName = ${testSiteName});
		}

		task ("Setup for template") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addRoutine(
				projectName = ${projectName},
				routineName = ${routineName});

			JSONTestray.addCaseType(caseType = ${caseType});

			JSONTestray.addCases(
				caseName = ${caseName},
				caseType = ${caseType},
				componentName = ${componentName},
				projectName = ${projectName});

			JSONTestray.addEnvironmentFactors(environmentFactorsName = ${environmentFactorsName});

			JSONTestray.addProductVersion(
				productVersion = ${productVersion},
				projectName = ${projectName});
		}

		task ("Go to Testray Site") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(
				numberItems = 20,
				valuePagination = 150);
		}

		task ("Go to project and routine") {
			Testray.goToProjectPage(projectName = ${projectName});

			TestrayRoutines.goToRoutine(routineName = ${routineName});
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "factorcategories",
			objectName = "Category 1");

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "routines",
			objectName = ${routineName});

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "teams",
			objectName = ${teamName});

		JSONTestrayAPI.deleteProject(projectName = ${projectName});

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "casetypes",
			objectName = ${caseType});

		Testray.testrayApisTearDown();

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-162435 | Test Summary: Verify if as an Administrator I should not be able to add a duplicate template"
	@priority = 5
	test AdminCanNotAddDuplicateTemplate {
		task ("Given that the user adds a Template") {
			TestrayTemplate.addTemplate(
				description = "Template Description",
				dropdownItem = "New Template",
				fieldName = "Build_Template",
				gitHash = "ae4a18721aa0a0831e0dca25e175b1408fa9ac2sa",
				routineName = ${routineName});
		}

		task ("When the user tries add a duplicate template") {
			TestrayTemplate.addTemplate(
				description = "Template Description",
				dropdownItem = "New Template",
				fieldName = "Build_Template",
				gitHash = "aeda18721a90a0831e0dca25e175b1408fa9ac2sa",
				routineName = ${routineName});
		}

		task ("Then the error message should appear") {
			Alert.viewErrorMessage(errorMessage = "The Build name already exists.");
		}
	}

	@description = "Story ID: LPS-162439 | Test Summary: Verify if that can Activate Disabled Template"
	@priority = 5
	test CanActivateDisabledTemplate {
		task ("Given that the user adds a Template with status active") {
			TestrayTemplate.addTemplate(
				description = "Template Description",
				dropdownItem = "New Template",
				fieldName = "Build_Template",
				gitHash = "aeda18721a90a0831e0dca25e175b1408fa9ac2sa",
				routine = ${routineName});

			Testray.kebabMenuOption(kebabOption = "Manage Templates");

			TestrayTemplate.viewTemplateDetails(
				templateName = "Build_Template",
				templateStatus = "Active");
		}

		task ("When the user changed the status of deactivate for Active") {
			TestrayContextMenu.clickContextMenuProjects(name = "Build_Template");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Deactivate");

			Refresh();

			TestrayTemplate.viewTemplateDetails(
				templateName = "Build_Template",
				templateStatus = "Deactive");

			TestrayContextMenu.clickContextMenuProjects(name = "Deactive");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Active");

			Refresh();
		}

		task ("Then the Active status should appear in the list") {
			Testray.assertItemName(text = "Active");
		}
	}

	@description = "Story ID: LPS-162439 | Test Summary: Verify that when creating a build with template, it comes with 'Active' status by default"
	@priority = 4
	test CanCreateTemplateWithActiveDefault {
		task ("Given that the user adds a Template") {
			TestrayTemplate.addTemplate(
				description = "Template Description",
				dropdownItem = "New Template",
				fieldName = "Build_Template",
				gitHash = "aeda18721a90a0831e0dca25e175b1408fa9ac2sa",
				routineName = ${routineName});
		}

		task ("Then the status active should appear") {
			Testray.kebabMenuOption(kebabOption = "Manage Templates");

			TestrayTemplate.viewTemplateDetails(
				templateName = "Build_Template",
				templateStatus = "Active");
		}
	}

	@description = "Story ID: LPS-162439 | Test Summary: Verify if can delete an deactived template"
	@priority = 4
	test CanDeleteDeactiveTemplate {
		task ("Given that the user adds a Template with status deactive") {
			TestrayTemplate.addTemplate(
				description = "Template Description",
				dropdownItem = "New Template",
				fieldName = "Build_Template",
				gitHash = "aeda18721a90a0831e0dca25e175b1408fa9ac2sa",
				routineName = ${routineName});

			Testray.kebabMenuOption(kebabOption = "Manage Templates");

			TestrayTemplate.viewTemplateDetails(
				templateName = "Build_Template",
				templateStatus = "Active");

			TestrayContextMenu.clickContextMenuProjects(name = "Build_Template");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Deactivate");

			Refresh();

			TestrayTemplate.viewTemplateDetails(
				templateName = "Build_Template",
				templateStatus = "Deactive");
		}

		task ("When the template with deactive status is deleted") {
			TestrayContextMenu.clickContextMenuProjects(name = "Deactive");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Delete");
		}

		task ("Then the template with deactive status should not appear in the list") {
			Testray.assertItemNotPresent(text = "Deactive");
		}
	}

	@description = "Story ID: LPS-162435 | Test Summary: Verify that can filter out activated models"
	@ignore = "Test Stub"
	@priority = 5
	test CanFilterActiveTemplate {

		// TODO LPS-163906 CanFilterActiveTemplate pending implementation

	}

	@description = "Story ID: LPS-162435 | Test Summary: Verify if As an administrator can search for the new template created"
	@priority = 3
	test CanSeachTemplate {
		task ("Given the user adds a template") {
			TestrayTemplate.addTemplate(
				description = "Template Description",
				dropdownItem = "New Template",
				fieldName = "Build_Template",
				gitHash = "aeda18721a90a0831e0dca25e175b1408fa9ac2sa",
				routineName = ${routineName});
		}

		task ("Then can search for the template created in the filter") {
			TestrayTemplate.assertTemplateName(templateName = "Build_Template");
		}
	}

	@description = "Story ID: LPS-162435 | Test Summary: Verify that can see the activated templates in the filter"
	@ignore = "Test Stub"
	@priority = 5
	test CanViewActiveInFilter {

		// TODO LPS-163907 CanViewActiveInFilter pending implementation

	}

	@description = "Story ID: LPS-162435 | Test Summary: Verify if, as an Administrator, can see the required field message"
	@priority = 3
	test CanViewRequiredField {
		task ("When the user tries add a new template without filling out the required fields") {
			TestrayTemplate.addTemplate(dropdownItem = "New Template");
		}

		task ("Then a required field warning message appears") {
			Testray.viewErrorMessage(
				fieldLabel = "Name",
				warningMessage = "Name is a required field.");
		}
	}

}