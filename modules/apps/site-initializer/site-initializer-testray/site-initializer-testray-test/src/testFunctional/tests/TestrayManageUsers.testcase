@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var testSiteName = "Testray 2";

	setUp {
		task ("Login and create a Testray Site") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();

			Testray.addTR2Site(siteName = ${testSiteName});
		}

		task ("Add a user") {
			JSONUser.addUser(
				userEmailAddress = "user@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");
		}

		task ("Go to Testray") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		Testray.tearDownCP();

		Testray.testrayApisTearDown();

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-173531 | Test Summary: Verify if as an Administrator I should be able to filter manage users by name"
	@priority = 5
	test AdminCanFilterUsersByName {
		task ("Given that there are two users present in the table") {
			TestrayLayout.clickManageOnSidebar();

			TestrayManage.selectOptionsManage(itemValue = "Manage Users");

			AssertElementPresent(
				key_projectName = "userfn userln",
				locator1 = "Testray#ITEM_NAME");

			AssertElementPresent(
				key_projectName = "Test Test",
				locator1 = "Testray#ITEM_NAME");
		}

		task ("When the user filter a user by name") {
			TestrayFilter.clickFilter();

			TestrayFilter.typeFilter(
				label = "Name",
				value = "userfn userln");
		}

		task ("Then can see the user named test test in the table and no longer see the userfn userln") {
			AssertElementPresent(
				key_projectName = "userfn userln",
				locator1 = "Testray#ITEM_NAME");

			AssertElementNotPresent(
				key_projectName = "Test Test",
				locator1 = "Testray#ITEM_NAME");
		}
	}

	@description = "Story ID: LPS-173531 | Test Summary: Verify if as an Administrator I should not see manage users that don't exist when I try filtering for them"
	@priority = 4
	test AdminCanNotFilterForNonExistentUsers {
		task ("When filtering a user by his non-existent name") {
			TestrayLayout.clickManageOnSidebar();

			TestrayManage.selectOptionsManage(itemValue = "Manage Users");

			AssertElementPresent(
				key_projectName = "Test Test",
				locator1 = "Testray#ITEM_NAME");

			TestrayFilter.clickFilter();

			TestrayFilter.typeFilter(
				label = "Name",
				value = "Test 2");
		}

		task ("Then the user is not present in the User table") {
			AssertElementNotPresent(
				key_projectName = "Test Test",
				locator1 = "Testray#ITEM_NAME");
		}
	}

	@description = "This is a test for LPS-162987 - Verify if as an Administrador can change his Role"
	@ignore = "true"
	@priority = 5
	test CanChangeRole {

		// TODO LPS-162987 CanChangeRole pending implementation

	}

	@description = "This is a test for LPS-162986 - Verify if as an User can edit account information"
	@priority = 4
	test CanEditAccountInformation {
		task ("Given that the user change the account informations") {
			TestrayLayout.manageAccountSidebar(valueName = "Manage Account");

			TestrayManageUser.editUserInformation(
				userEmailAddressEdit = "test@liferay.com",
				userFirstNameEdit = "changedfirstname",
				userLastNameEdit = "Updateln",
				userScreenNameEdit = "testscreen");

			Testray.checkSuccessAlertAppearsAndDisappears();

			TestrayLayout.clickManageOnSidebar();

			TestrayManage.selectOptionsManage(itemValue = "Manage Users");

			TestrayContextMenu.clickContextMenuProjects(name = "changedfirstname Updateln");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Edit");
		}

		task ("Then the account information is displayed in Manage Users and Manage Account") {
			TestrayManageUser.viewUserInfomation(
				emailAddress = "test@liferay.com",
				firstName = "changedfirstname",
				lastName = "Updateln",
				screenName = "testscreen");

			TestrayLayout.manageAccountSidebar(valueName = "Manage Account");

			TestrayManageUser.viewUserInfomation(
				emailAddress = "test@liferay.com",
				firstName = "changedfirstname",
				lastName = "Updateln",
				screenName = "testscreen");
		}
	}

	@description = "This is a test for LPS-165997 - Verify if as an administrator I cannot save roles without the select options"
	@priority = 4
	test CanNotSaveRolesWithoutOptions {
		task ("Given that the user tries to save roles without options selected") {
			TestrayLayout.manageAccountSidebar(valueName = "Manage Account");

			TestrayManageUser.changeRole(roleName = "Administrator");

			TestrayManageUser.changeRole(roleName = "Power User");

			TestrayManageUser.changeRole(roleName = "User");

			Button.clickSave();
		}

		task ("Then the error message should appear") {
			Alert.viewErrorMessage(errorMessage = "Please, select one or more roles.");
		}
	}

	@description = "This is a test for LPS-168192 - Verify that the user can not edit informations of user if all the required fields are not filled in"
	@priority = 3
	test CanViewFieldRequired {
		task ("Given that the user tries to created account without informations") {
			TestrayLayout.manageAccountSidebar(valueName = "Manage Account");

			TestrayManageUser.editUserInformation(
				userEmailAddressEdit = "",
				userFirstNameEdit = "",
				userLastNameEdit = "",
				userScreenNameEdit = "");
		}

		task ("Then a warning message is displayed") {
			Testray.viewErrorMessage(
				fieldLabel = "First Name",
				warningMessage = "This field is required.");

			Testray.viewErrorMessage(
				fieldLabel = "Last Name",
				warningMessage = "This field is required.");

			Testray.viewErrorMessage(
				fieldLabel = "Email Address",
				warningMessage = "This field is required.");

			Testray.viewErrorMessage(
				fieldLabel = "Screen Name",
				warningMessage = "This field is required.");
		}
	}

}