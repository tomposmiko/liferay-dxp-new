@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var buildName = "Build 1";
	var caseName = "Case 1";
	var caseType = "Case Type 1";
	var componentName = "Component 1";
	var environmentFactorsName = "Environment Factors 1";
	var productVersion = "1.2";
	var projectName = "Project 1";
	var routineName = "Routine 1";
	var suiteName = "Suite 1";
	var teamName = "Team 1";
	var runName = "Option 1";
	var taskName = "Task_1";
	var caseResultName = "case result 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Login and create a Testray Site") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();

			Testray.addTestraySite(siteName = ${testSiteName});

			JSONUser.addUser(
				userEmailAddress = "user@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");
		}

		task ("Setup for SubtaskAPI") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addRoutine(
				projectName = ${projectName},
				routineName = ${routineName});

			JSONTestray.addCaseType(caseType = ${caseType});

			JSONTestray.addCases(
				caseName = ${caseName},
				caseType = ${caseType},
				componentName = ${componentName},
				projectName = ${projectName});

			JSONTestray.addEnvironmentFactors(environmentFactorsName = ${environmentFactorsName});

			JSONTestray.addProductVersion(
				productVersion = ${productVersion},
				projectName = ${projectName});

			JSONTestray.addBuild(
				buildName = ${buildName},
				productVersion = ${productVersion},
				projectName = ${projectName},
				routineName = ${routineName});

			JSONTestray.addRun(
				buildName = ${buildName},
				runName = ${runName});

			JSONTestray.addCaseResult(
				buildName = ${buildName},
				caseName = ${caseName},
				caseResultName = ${caseResultName},
				componentName = ${componentName},
				runName = ${runName});

			JSONTestray.addTask(
				buildName = ${buildName},
				key = "INANALYSIS",
				statusTask = "In Analysis",
				taskName = ${taskName});

			JSONTestray.addSubtask(
				key = "OPEN",
				messageError = "Failed prior to running test",
				score = 3,
				statusSubtask = "Open",
				subtaskName = "Subtask_1",
				subtaskNumber = 1,
				taskName = ${taskName});

			JSONTestray.addSubtask(
				key = "OPEN",
				score = 3,
				statusSubtask = "Open",
				subtaskName = "Subtask_2",
				subtaskNumber = 2,
				taskName = ${taskName});
		}

		task ("Go to Testray Site") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});
		}

		task ("Go to Task") {
			TestrayTestflow.goToTestflow();

			TestrayTestflow.goToTask(taskName = "Task_1");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		JSONUser.deleteUserByEmailAddress(userEmailAddress = "user@liferay.com");

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "tasks",
			objectName = ${taskName});

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "routines",
			objectName = ${routineName});

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "components",
			objectName = ${componentName});

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "casetypes",
			objectName = ${caseType});

		JSONTestrayAPI.deleteObjectViaAPI(
			object = "factorcategories",
			objectName = "Category 1");

		JSONTestrayAPI.deleteProject(projectName = ${projectName});

		Testray.testrayApisTearDown();

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-173522 | Test Summary: Verify if as an Administrator I should be able to filter subtasks by assignee"
	@priority = 4
	test AdminCanFilterSubtasksByAssignee {
		task ("Given that subtask 1 is assigned to user userfn userln") {
			TestrayContextMenu.clickContextMenuProjects(name = "Subtask_1");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Assign and Begin Analysis");

			TestrayTestflow.assignUsers(userName = "userfn userln");
		}

		task ("And that subtask 2 is assigned to user Test") {
			TestrayContextMenu.clickContextMenuProjects(name = "Subtask_2");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Assign and Begin Analysis");

			TestrayTestflow.assignUsers(userName = "Test Test");
		}

		task ("When the user select the user userfn userln in the filter") {
			TestrayFilter.clickFilter();

			ScrollWebElementIntoView(
				key_projectName = "Subtask_2",
				locator1 = "Testray#ITEM_NAME");

			Select(
				locator1 = "Select#SELECT_FIELD_LABEL",
				selectFieldLabel = "Assignee",
				value1 = "userfn");

			Button.click(button = "Apply");
		}

		task ("Then can see only user userfn userln in the table") {
			AssertElementPresent(
				key_projectName = "userfn userln",
				locator1 = "Testray#ITEM_NAME");
		}

		task ("And do not see the user Test in the table") {
			AssertElementNotPresent(
				key_projectName = "Test Test",
				locator1 = "Testray#ITEM_NAME");
		}
	}

	@description = "Story ID: LPS-173522 | Test Summary: Verify if as an Administrator I should be able to filter subtasks by errors"
	@priority = 3
	test AdminCanFilterSubtasksByError {
		task ("Given that the user has two subtasks different in the table") {
			AssertElementPresent(
				key_projectName = "Subtask_1",
				locator1 = "Testray#ITEM_NAME");

			AssertElementPresent(
				key_projectName = "Subtask_2",
				locator1 = "Testray#ITEM_NAME");
		}

		task ("When the user filter the subtask in the filter by error") {
			TestrayFilter.clickFilter();

			ScrollWebElementIntoView(
				key_projectName = "Subtask_1",
				locator1 = "Testray#ITEM_NAME");

			TestrayFilter.typeFilter(
				label = "Errors",
				value = "Failed prior to running test");
		}

		task ("Then the user see only subtask 1 with error in the table") {
			AssertElementPresent(
				key_projectName = "Failed prior to running test",
				locator1 = "Testray#ITEM_NAME");
		}

		task ("And do not see the subtask 2 without error in the table") {
			AssertElementNotPresent(
				key_projectName = "Subtask_2",
				locator1 = "Testray#ITEM_NAME");
		}
	}

	@description = "Story ID: LPS-173522 | Test Summary: Verify if as an Administrator I should be able to filter subtasks by name"
	@priority = 3
	test AdminCanFilterSubtasksByName {
		task ("Given that the user has two subtasks different in the table") {
			AssertElementPresent(
				key_projectName = "Subtask_1",
				locator1 = "Testray#ITEM_NAME");

			AssertElementPresent(
				key_projectName = "Subtask_2",
				locator1 = "Testray#ITEM_NAME");
		}

		task ("When the user select the subtask 2 in the filter by name") {
			TestrayFilter.clickFilter();

			ScrollWebElementIntoView(
				key_projectName = "Subtask_2",
				locator1 = "Testray#ITEM_NAME");

			TestrayFilter.typeFilter(
				label = "Subtask Name",
				value = "Subtask_2");
		}

		task ("Then the user see only subtask 2 in the table") {
			AssertElementPresent(
				key_projectName = "Subtask_2",
				locator1 = "Testray#ITEM_NAME");
		}

		task ("And do not see the subtask 1 in the table") {
			AssertElementNotPresent(
				key_projectName = "Subtask_1",
				locator1 = "Testray#ITEM_NAME");
		}
	}

	@description = "Story ID: LPS-173522 | Test Summary: Verify if as an Administrator I should be able to filter subtask using multiple filtering options"
	@priority = 4
	test AdminCanFilterSubtaskWithMultipleOptions {
		task ("Given that subtask 1 is assigned to user userfn userln") {
			TestrayContextMenu.clickContextMenuProjects(name = "Subtask_1");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Assign and Begin Analysis");

			TestrayTestflow.assignUsers(userName = "userfn userln");
		}

		task ("And that subtask 2 is assigned to user Test Test") {
			TestrayContextMenu.clickContextMenuProjects(name = "Subtask_2");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Assign and Begin Analysis");

			TestrayTestflow.assignUsers(userName = "Test Test");
		}

		task ("And that subtask 2 is completed with blocked") {
			TestrayContextMenu.clickContextMenuProjects(name = "Subtask_2");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Complete");

			TestrayTestflow.editStatus(
				comment = "button does not work",
				issues = "LPS-166191",
				selectStatus = "Blocked");

			WaitForPageLoad();
		}

		task ("When the user filter multiple options by: Assignee, user and status in the filter") {
			TestrayFilter.clickFilter();

			ScrollWebElementIntoView(
				key_projectName = "Subtask_2",
				locator1 = "Testray#ITEM_NAME");

			Select(
				locator1 = "Select#SELECT_FIELD_LABEL",
				selectFieldLabel = "Assignee",
				value1 = "Test");

			Click.clickAtNotVisible(
				key_label = "Status",
				key_status = "Complete",
				locator1 = "TestrayFilter#STATUS_IN_FILTER");

			TestrayFilter.typeFilter(
				label = "Subtask Name",
				value = "Subtask_2");
		}

		task ("Then can see only user userfn userln and complete status in the table") {
			AssertElementPresent(
				key_projectName = "Test Test",
				locator1 = "Testray#ITEM_NAME");

			TestrayTestflow.assertStatus(
				label = "Subtasks",
				status = "Complete");
		}

		task ("And do not see the Subtask_2 and user test test in the table") {
			AssertElementNotPresent(
				key_projectName = "userfn userln",
				locator1 = "Testray#ITEM_NAME");

			AssertElementNotPresent(
				key_projectName = "Subtask_1",
				locator1 = "Testray#ITEM_NAME");
		}
	}

	@description = "Story ID: LPS-173522 | Test Summary: Verify if as an Administrator I should not see subtask that don't exist when I try filtering for them"
	@priority = 3
	test AdminCanNotFilterForNonExistentSubtask {
		task ("Given that the user has two subtasks that existent in the table") {
			AssertElementPresent(
				key_projectName = "Subtask_1",
				locator1 = "Testray#ITEM_NAME");

			AssertElementPresent(
				key_projectName = "Subtask_2",
				locator1 = "Testray#ITEM_NAME");
		}

		task ("When filtering a subtask by his non-existent name") {
			TestrayFilter.clickFilter();

			ScrollWebElementIntoView(
				key_projectName = "Subtask_2",
				locator1 = "Testray#ITEM_NAME");

			TestrayFilter.typeFilter(
				label = "Subtask Name",
				value = "Subtask_3");
		}

		task ("Then the user is not present in the subtask table") {
			AssertElementNotPresent(
				key_projectName = "Subtask_3",
				locator1 = "Testray#ITEM_NAME");
		}
	}

	@description = "Story ID: LPS-173522 | Test Summary: Verify that can search for a field"
	@priority = 2
	test AdminCanSearchForField {
		task ("When the user searches for a field called Assignee") {
			TestrayFilter.clickFilter();

			ScrollWebElementIntoView(
				key_projectName = "Subtask_1",
				locator1 = "Testray#ITEM_NAME");

			TestrayFilter.searchFilter(value = "Assignee");
		}

		task ("Then the user can see the field label called Assignee") {
			AssertElementPresent(
				locator1 = "Select#SELECT_FIELD_LABEL",
				selectFieldLabel = "Assignee");
		}

		task ("When the user searches for a field that does not exist") {
			TestrayFilter.searchFilter(value = "Assignee 2");
		}

		task ("Then the user can no more see the label called Assignee in the filter") {
			AssertElementNotPresent(
				locator1 = "Select#SELECT_FIELD_LABEL",
				selectFieldLabel = "Assignee 2");
		}
	}

}