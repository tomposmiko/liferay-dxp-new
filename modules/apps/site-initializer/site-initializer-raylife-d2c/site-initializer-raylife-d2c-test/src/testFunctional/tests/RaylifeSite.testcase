@component-name = "portal-solutions"
definition {

	property osgi.modules.includes = "site-initializer-raylife-d2c";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Raylife D2C";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		Raylife.tearDownCP();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutPG();
		}
	}

	@description = "Verify if Raylife can be created"
	@priority = "5"
	test CanRaylifeBeCreated {
		Site.openSitesAdmin();

		LexiconEntry.gotoAdd();

		LexiconCard.clickCard(card = "Raylife D2C");

		SelectFrame(
			key_title = "Add Site",
			locator1 = "IFrame#MODAL_ANY");

		PortletEntry.inputName(name = "Raylife");

		Click(locator1 = "Button#ADD");

		Site.openSitesAdmin();

		while (IsElementNotPresent(key_siteName = "Raylife", locator1 = "Sites#SITE_TABLE_NAME")) {
			Refresh();

			WaitForPageLoad();
		}
	}

	@description = "LPS-142178 - Verify if when Raylife is created, a Catalog in Commerce will be created for it as well."
	@priority = "5"
	test CatalogForRaylifeIsCreated {
		Raylife.createRaylifeSite(siteName = "Raylife");

		CommerceNavigator.gotoPortlet(
			category = "Product Management",
			portlet = "Catalogs");

		AssertElementPresent(
			locator1 = "Raylife#TABLE_CATALOG_NAME",
			name = "Raylife");

		Click(
			locator1 = "Raylife#TABLE_CATALOG_NAME",
			name = "Raylife");

		AssertElementPresent(
			locator1 = "Raylife#CATALOG_TITLE",
			title = "Raylife");
	}

	@description = "LPS-142180 - Verify if, when Raylife is created, Products related to Raylife are also created."
	@priority = "5"
	test ProductsForRaylifeIsCreated {
		Raylife.createRaylifeSite(siteName = "Raylife");

		CommerceNavigator.gotoPortlet(
			category = "Product Management",
			portlet = "Products");

		while (IsElementNotPresent(locator1 = "Raylife#PRODUCTS_NAME", productList = "General Liability,Professional Liability,Workers Compensation,Business Owners Policy")) {
			Refresh();

			WaitForPageLoad();
		}

		Raylife.viewProductsList(productList = "General Liability,Professional Liability,Workers Compensation,Business Owners Policy");
	}

}