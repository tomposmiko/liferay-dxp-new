definition {

	macro addPriceToSKU {
		Type(
			locator1 = "CommerceEntry#PRICE_FIELD",
			value1 = ${price});
	}

	macro addSKU {
		for (var optionValues : list ${optionValuesList}) {
			var optionValue = StringUtil.extractFirst(${optionValues}, ":");
			var price = StringUtil.extractLast(${optionValues}, ":");
			var optionValueUppercase = StringUtil.upperCase(${optionValue});

			Refresh();

			Click(locator1 = "Button#PLUS");

			if (IsElementPresent(key_menuItem = "Add SKU", locator1 = "MenuItem#DROPDOWN_MENU_ITEM")) {
				AssertClick(
					key_menuItem = "Add SKU",
					locator1 = "MenuItem#DROPDOWN_MENU_ITEM",
					value1 = ${key_menuItem});
			}

			SelectFrame.selectFrameNoLoading(locator1 = "CommerceEntry#IFRAME_SIDE_PANEL");

			CommerceEntry.addSKUsEntry(
				noOption = ${noOption},
				optionValue = ${optionValue},
				productSku = "${optionValueUppercase}SKU");

			WaitForElementNotPresent(locator1 = "TextInput#REQUIRED_ALERT");

			Check.checkToggleSwitch(
				key_toggleSwitchLabel = "Purchasable",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

			CommerceSKUs.addPriceToSKU(price = ${price});

			Button.clickPublish();

			AssertVisible(locator1 = "Message#SUCCESS");

			Click(locator1 = "Icon#CLOSE");
		}

		Refresh();
	}

	macro editSkuERC {
		SelectFrame.selectFrameNoLoading(locator1 = "CommerceEntry#IFRAME_SIDE_PANEL");

		Type(
			locator1 = "CommerceEntry#SKU_ERC_FIELD",
			value1 = ${skuERC});

		if (isSet(publish)) {
			Button.clickPublish();
		}

		if (isSet(closeFrame)) {
			Click(locator1 = "Icon#CLOSE");

			SelectFrame.selectFrameTop();
		}
	}

	macro editSkuPricing {
		SelectFrame.selectFrameNoLoading(locator1 = "CommerceEntry#IFRAME_SIDE_PANEL");

		CommerceSKUs.setSkuPricing(
			basePrice = ${basePrice},
			cost = ${cost},
			promoPrice = ${promoPrice});

		Button.clickPublish();

		Click(locator1 = "Icon#CLOSE");

		SelectFrame.selectFrameTop();
	}

	macro generateAllSkuCombinations {
		Click(locator1 = "Button#PLUS");

		Click(
			key_fieldName = "Generate All SKU Combinations",
			locator1 = "FormViewBuilder#OBJECT_FIELD_BUTTON_DROPDOWN");
	}

	macro setSkuInventory {
		if (isSet(italyQuantity)) {
			Type(
				key_value1 = 0,
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_ITEM_QUANTITY",
				value1 = ${italyQuantity});

			Click(
				key_value1 = 0,
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_SAVE_BUTTON");
		}

		if (isSet(northeastQuantity)) {
			Type(
				key_value1 = 1,
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_ITEM_QUANTITY",
				value1 = ${northeastQuantity});

			Click(
				key_value1 = 1,
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_SAVE_BUTTON");
		}

		if (isSet(southwestQuantity)) {
			Type(
				key_value1 = 2,
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_ITEM_QUANTITY",
				value1 = ${southwestQuantity});

			Click(
				key_value1 = 2,
				locator1 = "CommerceEntry#WAREHOUSE_INVENTORY_SAVE_BUTTON");
		}
	}

	macro setSkuPricing {
		if (isSet(basePrice)) {
			Type(
				key_text = "Base Price",
				locator1 = "TextInput#ANY",
				value1 = ${basePrice});
		}

		if (isSet(promoPrice)) {
			Type(
				key_text = "Promotion Price",
				locator1 = "TextInput#ANY",
				value1 = ${promoPrice});
		}

		if (isSet(cost)) {
			Type(
				key_text = "Cost",
				locator1 = "TextInput#ANY",
				value1 = ${cost});
		}
	}

}