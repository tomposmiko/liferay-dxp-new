definition {

	macro _addDiscount {
		Variables.assertDefined(parameterList = "${active},${level},${target},${title},${usePercentage}");

		var baseURL = "${baseURL}";

		if (!(isSet(baseURL))) {
			var baseURL = PropsUtil.get("portal.url");
		}

		if (!(isSet(limitationType))) {
			var limitationType = "unlimited";
		}

		if (!(isSet(maximumDiscountAmount))) {
			var maximumDiscountAmount = "0";
		}

		if (!(isSet(percentageLevel1))) {
			var percentageLevel1 = "0";
		}

		if (!(isSet(percentageLevel2))) {
			var percentageLevel2 = "0";
		}

		if (!(isSet(percentageLevel3))) {
			var percentageLevel3 = "0";
		}

		if (!(isSet(percentageLevel4))) {
			var percentageLevel4 = "0";
		}

		if (!(isSet(useCouponCode))) {
			var useCouponCode = "false";
		}

		if ("${useCouponCode}" == "false") {
			var couponCode = "";
		}
		else {
			if (!(isSet(couponCode))) {
				var couponCode = "TEST";
			}
		}

		var curl = '''
			${baseURL}/o/headless-commerce-admin-pricing/v2.0/discounts \
				-u test@liferay.com:test \
				-H 'accept: application/json' \
				-H 'Content-Type: application/json' \
				-d '{
					"active": ${active},
					"couponCode": "${couponCode}",
					"level": "${level}",
					"limitationType": "${limitationType}",
					"maximumDiscountAmount": ${maximumDiscountAmount},
					"neverExpire": true,
					"percentageLevel1": ${percentageLevel1},
					"percentageLevel2": ${percentageLevel2},
					"percentageLevel3": ${percentageLevel3},
					"percentageLevel4": ${percentageLevel4},
					"target": "${target}",
					"title": "${title}",
					"useCouponCode": ${useCouponCode},
					"usePercentage": ${usePercentage}
				}'
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	macro _addDiscountAccountGroup {
		Variables.assertDefined(parameterList = "${accountGroupName},${discountTitle}");

		var baseURL = "${baseURL}";

		if (!(isSet(baseURL))) {
			var baseURL = PropsUtil.get("portal.url");
		}

		var discountId = CommerceJSONDiscountsAPI._getDiscountId(discountTitle = "${discountTitle}");
		var accountGroupId = CommerceJSONAccountGroupsAPI._getAccountGroupId(accountGroupName = "${accountGroupName}");

		var curl = '''
			${baseURL}/o/headless-commerce-admin-pricing/v2.0/discounts/${discountId}/discount-account-groups \
				-u test@liferay.com:test \
				-H 'accept: application/json' \
				-H 'Content-Type: application/json' \
				-d '{
					"accountGroupId": ${accountGroupId}
				}'
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	macro _addDiscountProduct {
		Variables.assertDefined(parameterList = "${discountTitle},${productName}");

		var baseURL = "${baseURL}";

		if (!(isSet(baseURL))) {
			var baseURL = PropsUtil.get("portal.url");
		}

		var discountId = CommerceJSONDiscountsAPI._getDiscountId(discountTitle = "${discountTitle}");
		var productId = CommerceJSONProductsAPI._getProductId(productName = "${productName}");

		var curl = '''
			${baseURL}/o/headless-commerce-admin-pricing/v2.0/discounts/${discountId}/discount-products \
				-u test@liferay.com:test \
				-H 'accept: application/json' \
				-H 'Content-Type: application/json' \
				-d '{
					"productId": ${productId}
				}'
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	macro _deleteAllCommerceDiscounts {
		var baseURL = "${baseURL}";

		if (!(isSet(baseURL))) {
			var baseURL = PropsUtil.get("portal.url");
		}

		var discountsCount = CommerceJSONDiscountsAPI._getCommerceDiscountsCount();

		if ("${discountsCount}" != "0") {
			var discountIds = CommerceJSONDiscountsAPI._getCommerceDiscountIds(discountCount = "${discountsCount}");

			for (var discountId : list "${discountIds}") {
				echo("Deleting Commerce discount with id: ${discountId}");

				var curl = '''
					${baseURL}/o/headless-commerce-admin-pricing/v2.0/discounts/${discountId} \
						-u test@liferay.com:test \
						-H 'accept: application/json' \
				''';

				JSONCurlUtil.delete("${curl}");
			}
		}
		else {
			echo("No Commerce Discounts to be deleted");
		}
	}

	macro _getCommerceDiscountIds {
		var baseURL = "${baseURL}";

		if (!(isSet(baseURL))) {
			var baseURL = PropsUtil.get("portal.url");
		}

		if (isSet(discountCount)) {
			var discountsCount = "${discountCount}";
		}
		else {
			var discountsCount = "100";
		}

		var curl = '''
			${baseURL}/o/headless-commerce-admin-pricing/v2.0/discounts?pageSize=${discountsCount} \
				-u test@liferay.com:test \
				-H 'accept: application/json' \
		''';

		var discountIds = JSONCurlUtil.get("${curl}", "$..['id']");

		return "${discountIds}";
	}

	macro _getCommerceDiscountsCount {
		var baseURL = "${baseURL}";

		if (!(isSet(baseURL))) {
			var baseURL = PropsUtil.get("portal.url");
		}

		var curl = '''
			${baseURL}/o/headless-commerce-admin-pricing/v2.0/discounts \
				-u test@liferay.com:test \
				-H 'accept: application/json' \
		''';

		var discountsCount = JSONCurlUtil.get("${curl}", "$['totalCount']");

		echo("The Commerce discounts count is: ${discountsCount}");

		return "${discountsCount}";
	}

	macro _getDiscountId {
		Variables.assertDefined(parameterList = "${discountTitle}");

		var baseURL = "${baseURL}";

		if (!(isSet(baseURL))) {
			var baseURL = PropsUtil.get("portal.url");
		}

		if (isSet(discountCount)) {
			var discountCount = "${discountCount}";
		}
		else {
			var discountCount = "100";
		}

		var curl = '''
			${baseURL}/o/headless-commerce-admin-pricing/v2.0/discounts?pageSize=${discountCount} \
				-u test@liferay.com:test \
				-H 'accept: application/json' \
		''';

		var discountId = JSONCurlUtil.get("${curl}", "$.items[?(@['title'] == '${discountTitle}')].['id']");

		return "${discountId}";
	}

}