@component-name = "portal-commerce"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Order Management";

	setUp {
		CommerceConfiguration.commerceSetUp();
	}

	tearDown {
		CommerceConfiguration.commerceTearDown();
	}

	@description = "Verify users can add new payment terms."
	@priority = 3
	test AddANewPaymentTerm {
		property portal.acceptance = "false";

		task ("Given the Sales Manager goes into the Terms and Conditions Page") {
			CommerceOrders.openTermsAndConditionsAdmin();
		}

		task ("When the sales manager creates a payment term entry") {
			CommercePaymentTerms.addTermsAndConditions(
				termsAndConditionsName = "Test Terms and Condition",
				termsAndConditionsType = "Payment Terms",
				valuePosition = 0);
		}

		task ("And the sales manager goes back to the Terms and Conditions Page") {
			CommerceOrders.openTermsAndConditionsAdmin();
		}

		task ("Then the new entry is present") {
			CommercePaymentTerms.viewTermsAndConditionsEntry(
				termsAndConditionsActiveStatus = "No",
				termsAndConditionsName = "Test Terms and Condition",
				termsAndConditionsType = "Payment Terms",
				valuePosition = 0);
		}

		task ("When the Sales Manager opens the new payment term entry") {
			CommerceNavigator.gotoEntry(entryName = "Test Terms and Condition");
		}

		task ("Then the correct payment term data is shown") {
			CommercePaymentTerms.assertTermsAndConditionsEntry(
				termsAndConditionsName = "Test Terms and Condition",
				valuePosition = "0.0");
		}
	}

	@description = "This is a test for COMMERCE-8295. Assert Payment terms are automatically sorted by Priority by default"
	@priority = 4
	test AssertPaymentTermIsSortedByPriority {
		property portal.acceptance = "false";

		task ("Given a new entry with "Priority" set to 2 into the "Terms and Conditions".") {
			CommerceNavigator.gotoPortlet(
				category = "Order Management",
				portlet = "Terms and Conditions");

			CommercePaymentTerms.addTermsAndConditions(
				termsAndConditionsName = "Terms and Condition Entry 1",
				termsAndConditionsType = "Payment Terms",
				valuePosition = 2);

			Click(locator1 = "Button#BACK");
		}

		task ("When the Sales Manager create a new entry with "Priority" set to 1.") {
			CommercePaymentTerms.addTermsAndConditions(
				termsAndConditionsName = "Terms and Condition Entry 2",
				termsAndConditionsType = "Payment Terms",
				valuePosition = 1);

			Click(locator1 = "Button#BACK");
		}

		task ("Then after the redirect if the Sales Manager return on the dashboard the second entry that we created it's the first.") {
			CommercePaymentTerms.viewTermsAndConditionsEntrySortByPriority(
				rowNumber = 1,
				termsAndConditionsName = "Terms and Condition Entry 2",
				valuePosition = 1);
		}
	}

	@description = "This is a test for COMMERCE-8283. Payment Term Name needs to be unique"
	@priority = 4
	test AssertPaymentTermNameIsUnique {
		property portal.acceptance = "false";

		task ("Given a new entry into the "Terms and Conditions" with a specific name, and the Type is set to "Payment Terms".") {
			CommerceNavigator.gotoPortlet(
				category = "Order Management",
				portlet = "Terms and Conditions");

			CommercePaymentTerms.addTermsAndConditions(
				termsAndConditionsName = "Terms and Condition Entry 1",
				termsAndConditionsType = "Payment Terms",
				valuePosition = "");

			Click(locator1 = "Button#BACK");
		}

		task ("When the Sales Manager tries to create another new entry with the same name, and the Type is set as "Delivery Terms".") {
			CommercePaymentTerms.addTermsAndConditions(
				termsAndConditionsName = "Terms and Condition Entry 1",
				termsAndConditionsType = "Delivery Terms",
				valuePosition = "");
		}

		task ("Then the Sales Manager receives an error.") {
			SelectFrame.selectFrameNoLoading(locator1 = "IFrame#MODAL_BODY");

			Alert.viewErrorMessage(errorMessage = "Error:Please enter a valid name.");
		}
	}

	@description = "This is a test for COMMERCE-8293. Assert payment term priority using different field types."
	@priority = 4
	test AssertPaymentTermPriority {
		property portal.acceptance = "false";

		task ("Given I am on the Terms and Conditions page") {
			CommerceNavigator.gotoPortlet(
				category = "Order Management",
				portlet = "Terms and Conditions");
		}

		task ("When I add a new entry with the priority field as a number") {
			CommercePaymentTerms.addTermsAndConditions(
				termsAndConditionsName = "Terms and Condition Entry 1",
				termsAndConditionsType = "Payment Terms",
				valuePosition = 1);
		}

		task ("Then I am redirected to the details page, and the number value of the priority field is correctly shown") {
			CommercePaymentTerms.assertTermsAndConditionsEntry(
				termsAndConditionsName = "Terms and Condition Entry 1",
				valuePosition = "1.0");
		}

		task ("When I add a new entry with the priority field as a decimal number") {
			Click(locator1 = "Button#BACK");

			CommercePaymentTerms.addTermsAndConditions(
				termsAndConditionsName = "Terms and Condition Entry 2",
				termsAndConditionsType = "Payment Terms",
				valuePosition = "1.2");
		}

		task ("Then I am redirected to the details page, and the decimal value of the priority field is correctly shown") {
			CommercePaymentTerms.assertTermsAndConditionsEntry(
				termsAndConditionsName = "Terms and Condition Entry 2",
				valuePosition = "1.2");
		}

		task ("When I add a new entry with the priority field as a negative number") {
			Click(locator1 = "Button#BACK");

			CommercePaymentTerms.addTermsAndConditions(
				termsAndConditionsName = "Terms and Condition Entry 3",
				termsAndConditionsType = "Payment Terms",
				valuePosition = "-1");
		}

		task ("Then I am redirected to the details page, and the negative value of the priority field is correctly shown") {
			CommercePaymentTerms.assertTermsAndConditionsEntry(
				termsAndConditionsName = "Terms and Condition Entry 3",
				valuePosition = "-1.0");
		}

		task ("When I add a new entry with the priority field as empty ") {
			Click(locator1 = "Button#BACK");

			CommercePaymentTerms.addTermsAndConditions(
				termsAndConditionsName = "Terms and Condition Entry 4",
				termsAndConditionsType = "Payment Terms",
				valuePosition = "");
		}

		task ("Then I am redirected to the details page, and the value of the priority field defaults to 0") {
			CommercePaymentTerms.assertTermsAndConditionsEntry(
				termsAndConditionsName = "Terms and Condition Entry 4",
				valuePosition = "0.0");
		}
	}

	@description = "This is a test for COMMERCE-8296. Payment Term Priority needs to be unique"
	@priority = 4
	test AssertPaymentTermPriorityIsUnique {
		property portal.acceptance = "false";

		task ("Given a new entry "Test" into the "Terms and Conditions".") {
			CommerceNavigator.gotoPortlet(
				category = "Order Management",
				portlet = "Terms and Conditions");

			CommercePaymentTerms.addTermsAndConditions(
				termsAndConditionsName = "Test",
				termsAndConditionsType = "Payment Terms",
				valuePosition = 1);

			Click(locator1 = "Button#BACK");
		}

		task ("When the Sales Manager create a new entry with a different name from "Test".") {
			CommercePaymentTerms.addTermsAndConditions(
				termsAndConditionsName = "Terms and Condition Entry 1",
				termsAndConditionsType = "Payment Terms",
				valuePosition = 1);
		}

		task ("Then the Sales Manager receives an error because the priority is not unique.") {
			SelectFrame.selectFrameNoLoading(locator1 = "IFrame#MODAL_BODY");

			Alert.viewErrorMessage(errorMessage = "Error:Please enter a valid priority.");
		}
	}

	@description = "This is a test for COMMERCE-8286. Cancel the creation of a new entry and return to the dashboard view"
	@priority = 4
	test CanCancelTheCreationOfANewEntry {
		property portal.acceptance = "false";

		task ("Given I am creating a new Terms and Conditions entry") {
			CommerceNavigator.gotoPortlet(
				category = "Order Management",
				portlet = "Terms and Conditions");
		}

		task ("When I cancel creating the entry") {
			CommercePaymentTerms.cancelAddTermsAndConditions(
				termsAndConditionsName = "Test Terms and Condition",
				termsAndConditionsType = "Payment Terms",
				valuePosition = "");

			SelectFrameTop();
		}

		task ("Then the entry should not be created") {
			CommerceNavigator.gotoPortlet(
				category = "Order Management",
				portlet = "Terms and Conditions");

			CommercePaymentTerms.viewNoPG();
		}
	}

	@description = "COMMERCE-8281. Delete a new payment term to Terms and Conditions."
	@priority = 5
	test CanDeleteAPaymentTerm {
		property portal.acceptance = "true";

		task ("Given a new entry into the Terms and Conditions with the status to Active") {
			CommerceNavigator.gotoPortlet(
				category = "Order Management",
				portlet = "Terms and Conditions");

			CommercePaymentTerms.addTermsAndConditions(
				active = "true",
				termsAndConditionsName = "Test Terms and Condition",
				termsAndConditionsType = "Payment Terms",
				valuePosition = "");

			Click(locator1 = "Icon#BACK");

			CommercePaymentTerms.viewTermsAndConditionsEntry(
				termsAndConditionsActiveStatus = "Yes",
				termsAndConditionsName = "Test Terms and Condition",
				termsAndConditionsType = "Payment Terms",
				valuePosition = 0);
		}

		task ("When I delete the terms and conditions") {
			CommercePaymentTerms.gotoEntryMenuItem(
				menuItem = "Delete",
				termsAndConditionsName = "Test Terms and Condition");
		}

		task ("Then the entry is deleted") {
			CommercePaymentTerms.viewNoPG();
		}
	}

	@description = "This is a test for COMMERCE-8282. Verify can edit payment term"
	@priority = 5
	test CanEditAPaymentTerm {
		property portal.acceptance = "true";

		task ("Given a new entry into the Terms and Conditions") {
			CommerceNavigator.gotoPortlet(
				category = "Order Management",
				portlet = "Terms and Conditions");

			CommercePaymentTerms.addTermsAndConditions(
				active = "true",
				termsAndConditionsName = "Test Terms and Condition",
				termsAndConditionsType = "Payment Terms",
				valuePosition = "");
		}

		task ("When I edit the name and description of the payment term and publish it") {
			CommercePaymentTerms.openTermsAndConditionsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Test Terms and Condition");

			CommercePaymentTerms.editTermsAndConditions(
				termsAndConditionsDescription = "Test Terms and Condition Description Edit",
				termsAndConditionsName = "Test Terms and Condition Edit");
		}

		task ("Then the payment term name and description should be updated") {
			Navigator.gotoBack();

			CommerceNavigator.gotoEntry(entryName = "Test Terms and Condition Edit");

			CommercePaymentTerms.assertTermsAndConditionsEntry(
				termsAndConditionsDescription = "Test Terms and Condition Description Edit",
				termsAndConditionsName = "Test Terms and Condition Edit");
		}
	}

	@description = "COMMERCE-8291. The Sales Manager can use the search bar to search a specific entry."
	@priority = 4
	test CanSearchForSpecificEntry {
		property portal.acceptance = "false";

		task ("Given active Terms & Conditions entries") {
			CommerceNavigator.gotoPortlet(
				category = "Order Management",
				portlet = "Terms and Conditions");

			CommercePaymentTerms.addTermsAndConditions(
				termsAndConditionsName = "Terms1",
				termsAndConditionsType = "Payment Terms",
				valuePosition = "");

			Click(locator1 = "Icon#BACK");

			CommercePaymentTerms.addTermsAndConditions(
				termsAndConditionsName = "Terms2",
				termsAndConditionsType = "Payment Terms",
				valuePosition = 1);

			Click(locator1 = "Icon#BACK");
		}

		task ("When I search for a specific term") {
			Search.searchCP(searchTerm = "Terms1");
		}

		task ("Then the search results should display the searched entry") {
			AssertTextPresent(
				locator1 = "CommercePaymentTerms#ORDER_DETAILS_TERMS_AND_CONDITIONS_NAME",
				value1 = "Terms1");

			AssertTextNotPresent(
				locator1 = "CommercePaymentTerms#ORDER_DETAILS_TERMS_AND_CONDITIONS_NAME",
				value1 = "Terms2");
		}
	}

	@description = "COMMERCE-8284. The Sales Manager can use the localization to change the entry's name based on a language."
	@priority = 4
	test CanUseLocalization {
		property portal.acceptance = "false";

		task ("Given a new entry into the Terms and Conditions") {
			CommerceNavigator.gotoPortlet(
				category = "Order Management",
				portlet = "Terms and Conditions");

			CommercePaymentTerms.addTermsAndConditions(
				active = "true",
				termsAndConditionsName = "Test Terms and Condition",
				termsAndConditionsType = "Payment Terms",
				valuePosition = 1);

			Click(locator1 = "Icon#BACK");
		}

		task ("When I open the new entry, presented on the details page of the entry") {
			CommerceNavigator.gotoEntry(entryName = "Test Terms and Condition");

			CommercePaymentTerms.assertTermsAndConditionsEntry(
				termsAndConditionsName = "Test Terms and Condition",
				valuePosition = "1.0");
		}

		task ("And when I use localization to change the entry's name based on a language") {
			CommercePaymentTerms.changeLocale(locale = "zh-CN");

			Type(
				locator1 = "CommerceEntry#NAME_FIELD_SPECIAL",
				value1 = "测试条款和条件");

			CommercePaymentTerms.changeLocale(locale = "en-US");

			CommercePaymentTerms.assertTermsAndConditionsEntry(
				termsAndConditionsName = "Test Terms and Condition",
				valuePosition = "1.0");

			CommercePaymentTerms.changeLocale(locale = "zh-CN");
		}

		task ("Then the entry name should be localized") {
			CommercePaymentTerms.assertTermsAndConditionsEntry(
				termsAndConditionsName = "测试条款和条件",
				valuePosition = "1.0");
		}
	}

	@description = "Verify the sales manager is able to change the status of payment terms."
	@priority = 5
	test ChangeTheStatus {
		property portal.acceptance = "true";

		task ("Given an active payment term") {
			CommerceOrders.openTermsAndConditionsAdmin();

			CommercePaymentTerms.addTermsAndConditions(
				active = "true",
				termsAndConditionsName = "Test Terms and Condition",
				termsAndConditionsType = "Payment Terms",
				valuePosition = 0);

			CommerceOrders.openTermsAndConditionsAdmin();
		}

		task ("When the sales manager opens the payment term entry and disables the active toggle") {
			CommerceNavigator.gotoEntry(entryName = "Test Terms and Condition");

			Uncheck.uncheckToggleSwitch(locator1 = "CommerceEntry#ACTIVE_CHECKBOX");

			Button.clickPublish();
		}

		task ("Then the entry appears as inactive in the Terms and Conditions dashboard") {
			CommerceOrders.openTermsAndConditionsAdmin();

			CommercePaymentTerms.viewTermsAndConditionsEntry(
				termsAndConditionsActiveStatus = "No",
				termsAndConditionsName = "Test Terms and Condition",
				termsAndConditionsType = "Payment Terms",
				valuePosition = 0);
		}

		task ("When the sales manager opens the payment term entry and enables the active switch") {
			CommerceNavigator.gotoEntry(entryName = "Test Terms and Condition");

			Check.checkToggleSwitch(locator1 = "CommerceEntry#ACTIVE_CHECKBOX");

			Button.clickPublish();
		}

		task ("Then the entry appears as active in the Terms and Conditions dashboard") {
			CommerceOrders.openTermsAndConditionsAdmin();

			CommercePaymentTerms.viewTermsAndConditionsEntry(
				termsAndConditionsActiveStatus = "Yes",
				termsAndConditionsName = "Test Terms and Condition",
				termsAndConditionsType = "Payment Terms",
				valuePosition = 0);
		}
	}

}