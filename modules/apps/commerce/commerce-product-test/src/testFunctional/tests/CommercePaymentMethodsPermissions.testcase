@component-name = "portal-commerce"
definition {

	property custom.properties = "jsonws.web.service.paths.excludes=${line.separator}company.security.strangers.verify=false";
	property portal.release = "true";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Product Info Management";

	setUp {
		CommerceConfiguration.commerceSetUp(minium = "true");

		task ("Setup: Create an account with a buyer user") {
			CommerceEntry.addAccountEntryUser(
				accountName = "Commerce Account",
				accountType = "Business",
				agreeToTermsAndAnswerReminderQuery = "true",
				createNewAccount = "true",
				requireReset = "false",
				userEmailAddress = "buyer@liferay.com",
				userFirstName = "Buyer",
				userLastName = "User",
				userRole = "Buyer",
				userScreenName = "buyeruser",
				userSiteMembership = "Minium");
		}
	}

	tearDown {
		CommerceConfiguration.commerceTearDown();
	}

	@description = "COMMERCE-9506. As a user of the storefront, I should have access only to payment methods that are assigned to my role"
	@priority = "4"
	test CanSFUsersSelectPaymentMethodsDuringCheckoutBasedOnViewPermissionForEachPaymentMethod {
		property portal.acceptance = "false";

		task ("Given Power User role is given to the buyer user") {
			JSONRole.assignRoleToUser(
				roleTitle = "Power User",
				userEmailAddress = "buyer@liferay.com");
		}

		task ("And given 3 Payment Methods are enabled(Money Order, Authorize.Net, PayPal)") {
			CommerceChannels.openChannelsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium Portal");

			for (var paymentMethod : list "Money Order, Authorize.Net,PayPal") {
				CommerceChannels.editPaymentMethod(
					active = "true",
					paymentMethod = "${paymentMethod}");
			}
		}

		task ("And on PayPal payment method, View permission is removed for User and Guest") {
			CommerceEntry.clickEntryDropdownMenuItem(
				entryName = "PayPal",
				menuItem = "Permissions");

			CommercePermissions.removePermissionsPG(
				permissionDefinitionKey = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
				roleTitles = "Guest,User");
		}

		task ("And on PayPal payment method, assert that Power User does not have View permission") {
			Role.viewPermissionPG(
				permissionDefinitionKey = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
				permissionFlagged = "false",
				roleTitle = "Power User");

			IFrame.closeFrame();
		}

		task ("And on Money Order payment method, View permission is removed for User and Guest") {
			CommerceEntry.clickEntryDropdownMenuItem(
				entryName = "Money Order",
				menuItem = "Permissions");

			CommercePermissions.removePermissionsPG(
				permissionDefinitionKey = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
				roleTitles = "Guest,User");

			IFrame.closeFrame();
		}

		task ("And View permission is given to Power User") {
			CommerceEntry.clickEntryDropdownMenuItem(
				entryName = "Money Order",
				menuItem = "Permissions");

			CommercePermissions.definePermissionsPG(
				permissionDefinitionKey = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
				roleTitles = "Power User");
		}

		task ("When the buyer makes an order") {
			User.logoutPG();

			CommerceLogin.miniumLogin(
				password = "test",
				urlAppend = "web/minium",
				userEmailAddress = "buyer@liferay.com");

			FrontStore.addInitializerProductToCart(
				productName = "U-Joint",
				productQuantity = "1");

			CommerceCheckout.gotoCheckout();
		}

		task ("Then during the Payment Method step, PayPal is not selectable while Money Order and Authorize.Net can be selected") {
			CommerceCheckout.initializersCheckout(
				eligiblePaymentMethods = "Authorize.Net,Money Order",
				newAddressAddress = "Test Address",
				newAddressCity = "Test City",
				newAddressCountry = "United States",
				newAddressName = "Address Name",
				newAddressZipCode = "Test Zip Code",
				notEligiblePaymentMethods = "PayPal",
				paymentMethod = "Money Order",
				productName = "U-Joint",
				productQuantity = "1",
				productUnitPrice = "24",
				shippingMethod = "Standard");
		}
	}

	@description = "COMMERCE-9505. As a Channel Manager, I can configure an active payment method to be accessible by some roles"
	@priority = "4"
	test CanUsersSelectPaymentMethodsBasedOnViewPermissionForEachPaymentMethod {
		property portal.acceptance = "false";

		task ("Given a user with the Operations Manager role") {
			JSONUser.addUserWithRole(
				roleTitle = "Operations Manager",
				userEmailAddress = "om@liferay.com",
				userFirstName = "Operations",
				userLastName = "Manager",
				userScreenName = "operationsmanager");

			Permissions.definePermissionViaJSONAPI(
				resourceAction = "MANAGE_COMMERCE_ORDER_PAYMENT_METHODS",
				resourceName = "com.liferay.commerce.order",
				roleTitle = "Operations Manager");
		}

		task ("And given 3 Payment Methods are enabled(Money Order, Authorize.Net, PayPal)") {
			CommerceChannels.openChannelsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium Portal");

			for (var paymentMethod : list "Money Order, Authorize.Net,PayPal") {
				CommerceChannels.editPaymentMethod(
					active = "true",
					paymentMethod = "${paymentMethod}");
			}
		}

		task ("And given an order is created") {
			CommerceJSONOrdersAPI._addCommerceOrder(
				accountEntryName = "Commerce Account",
				orderStatus = "1",
				paymentMethod = "paypal",
				paymentStatus = "2");

			CommerceJSONOrdersAPI._addCommerceOrderItem(productSKU = "MIN55861");
		}

		task ("And on PayPal payment method, View permission is removed for User and Guest") {
			CommerceEntry.clickEntryDropdownMenuItem(
				entryName = "PayPal",
				menuItem = "Permissions");

			CommercePermissions.removePermissionsPG(
				permissionDefinitionKey = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
				roleTitles = "Guest,User");
		}

		task ("And on PayPal payment method, assert that Operations Manager does not have View permission") {
			Role.viewPermissionPG(
				permissionDefinitionKey = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
				permissionFlagged = "false",
				roleTitle = "Operations Manager");

			IFrame.closeFrame();
		}

		task ("And on Money Order payment method, View permission is removed for User and Guest") {
			CommerceEntry.clickEntryDropdownMenuItem(
				entryName = "Money Order",
				menuItem = "Permissions");

			CommercePermissions.removePermissionsPG(
				permissionDefinitionKey = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
				roleTitles = "Guest,User");

			IFrame.closeFrame();
		}

		task ("And View permission is given to Operations Manager") {
			CommerceEntry.clickEntryDropdownMenuItem(
				entryName = "Money Order",
				menuItem = "Permissions");

			CommercePermissions.definePermissionsPG(
				permissionDefinitionKey = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
				roleTitles = "Operations Manager");
		}

		task ("When the Operations Manager opens the order from the orders page") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "om@liferay.com",
				userLoginFullName = "Operations Manager");

			CommerceOrders.openOrdersAdmin();

			CommerceNavigator.gotoOrderEntryViaAdmin(status = "Pending");
		}

		task ("And on the Payments tab, assert that PayPal is set as Payment Method") {
			Click(
				key_menuTab = "Payments",
				locator1 = "CommerceEntry#MENU_TAB");

			AssertTextEquals.assertPartialText(
				key_paymentMethod = "PayPal",
				locator1 = "CommerceOrders#ORDER_DETAILS_PAYMENT_METHOD_NAME",
				value1 = "PayPal");
		}

		task ("Then the Operations Manager edits the payment method: Money Order and Authorize.Net are eligible while PayPal is not") {
			CommerceOrders.editPaymentMethod(
				eligiblePaymentMethods = "Authorize.Net,Money Order",
				notEligiblePaymentMethods = "PayPal",
				paymentMethod = "Money Order");

			AssertTextEquals.assertPartialText(
				locator1 = "CommerceOrders#ORDER_DETAILS_PAYMENT_METHOD_NAME",
				value1 = "Money Order");
		}
	}

	@description = "Verify users with the 'Manage Commerce Order Payment Methods' permission can edit the payment method of an order"
	@priority = "3"
	test UsersWithManagePaymentMethodsPermissionCanEditSelectedPaymentMethod {
		property portal.acceptance = "false";

		task ("Given multiple active payment methods") {
			CommerceChannels.openChannelsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium Portal");

			CommerceChannels.activatePaymentMethod(paymentMethod = "Money Order");

			CommerceChannels.activatePaymentMethod(paymentMethod = "PayPal");
		}

		task ("And given a user with the Operations Manager role") {
			JSONUser.addUserWithRole(
				roleTitle = "Operations Manager",
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");
		}

		task ("And given the Operations Manager role is given the 'Manage Commerce Order Payment Methods' permission") {
			Permissions.definePermissionViaJSONAPI(
				resourceAction = "MANAGE_COMMERCE_ORDER_PAYMENT_METHODS",
				resourceName = "com.liferay.commerce.order",
				roleTitle = "Operations Manager");
		}

		task ("And given a buyer has checked out an order using one of the active payment methods") {
			User.logoutPG();

			CommerceLogin.miniumLogin(
				password = "test",
				urlAppend = "web/minium",
				userEmailAddress = "buyer@liferay.com");

			FrontStore.addInitializerProductToCart(
				productName = "U-Joint",
				productQuantity = "1");

			CommerceNavigator.gotoMiniCart();

			Button.click(button = "View Details");

			Button.click(button = "Checkout");

			CommerceCheckout.initializersCheckout(
				newAddressAddress = "Test Address",
				newAddressCity = "Test City",
				newAddressCountry = "United States",
				newAddressName = "Address Name",
				newAddressZipCode = "Test Zip Code",
				paymentMethod = "Money Order",
				productName = "U-Joint",
				productQuantity = "1",
				productUnitPrice = "24",
				shippingMethod = "Standard");
		}

		task ("When the Operations Manager opens the order from the orders page") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "userea@liferay.com",
				userLoginFullName = "userfn userln");

			CommerceOrders.openOrdersAdmin();

			CommerceNavigator.gotoOrderEntryViaAdmin(status = "Pending");
		}

		task ("Then the Operations Manager can edit the payment method of the order") {
			Click(
				key_menuTab = "Payments",
				locator1 = "CommerceEntry#MENU_TAB");

			AssertTextEquals.assertPartialText(
				key_paymentMethod = "Money Order",
				locator1 = "CommerceOrders#ORDER_DETAILS_PAYMENT_METHOD_NAME",
				value1 = "Money Order");

			CommerceOrders.editPaymentMethod(paymentMethod = "PayPal");

			AssertTextEquals.assertPartialText(
				locator1 = "CommerceOrders#ORDER_DETAILS_PAYMENT_METHOD_NAME",
				value1 = "PayPal");
		}
	}

	@description = "Verify users with the 'Manage Commerce Order Payment Methods' permission can select payment method during checkout"
	@priority = "3"
	test UsersWithManagePermissionCanSelectPaymentMethodDuringCheckout {
		property portal.acceptance = "false";

		task ("Given multiple active payment methods") {
			CommerceChannels.openChannelsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium Portal");

			CommerceChannels.activatePaymentMethod(paymentMethod = "Money Order");

			CommerceChannels.activatePaymentMethod(paymentMethod = "PayPal");
		}

		task ("When a buyer with 'Manage Commerce Order Payment Methods' permission checks out an order") {
			User.logoutPG();

			CommerceLogin.miniumLogin(
				password = "test",
				urlAppend = "web/minium",
				userEmailAddress = "buyer@liferay.com");

			FrontStore.addInitializerProductToCart(
				productName = "U-Joint",
				productQuantity = "1");

			CommerceNavigator.gotoMiniCart();

			Button.click(button = "View Details");

			Button.click(button = "Checkout");

			CommerceCheckout.setAddress(
				newAddressAddress = "Test Address",
				newAddressCity = "Test City",
				newAddressCountry = "United States",
				newAddressName = "Address Name",
				newAddressZipCode = "Test Zip Code");

			Click(locator1 = "Button#CONTINUE");

			Click(
				key_radioOption = "Standard",
				locator1 = "CommerceEntry#RADIO_BUTTON");

			Click(locator1 = "Button#CONTINUE");
		}

		task ("Then at the Payment Method step, the buyer can select one of the active payment methods") {
			AssertElementPresent(
				key_radioOption = "Money Order",
				locator1 = "CommerceEntry#RADIO_BUTTON");

			Click(
				key_radioOption = "PayPal",
				locator1 = "CommerceEntry#RADIO_BUTTON");

			Click(locator1 = "Button#CONTINUE");

			AssertTextEquals(
				key_paymentMethod = "PayPal",
				locator1 = "CommerceAccelerators#CHECKOUT_SUMMARY_PAYMENT_METHOD",
				value1 = "PayPal");

			Click(locator1 = "Button#CONTINUE");
		}
	}

	@description = "Verify users without the 'Manage Commerce Order Payment Methods' permission cannot edit the payment method of an order"
	@priority = "3"
	test UsersWithoutManagePermissionCannotEditSelectedPaymentMethod {
		property portal.acceptance = "false";

		task ("Given multiple active payment methods") {
			CommerceChannels.openChannelsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium Portal");

			CommerceChannels.activatePaymentMethod(paymentMethod = "Money Order");

			CommerceChannels.activatePaymentMethod(paymentMethod = "PayPal");
		}

		task ("And given a user with the Operations Manager role") {
			JSONUser.addUserWithRole(
				roleTitle = "Operations Manager",
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");
		}

		task ("And given a buyer has checked out an order using one of the active payment methods") {
			User.logoutPG();

			CommerceLogin.miniumLogin(
				password = "test",
				urlAppend = "web/minium",
				userEmailAddress = "buyer@liferay.com");

			FrontStore.addInitializerProductToCart(
				productName = "U-Joint",
				productQuantity = "1");

			CommerceNavigator.gotoMiniCart();

			Button.click(button = "View Details");

			Button.click(button = "Checkout");

			CommerceCheckout.initializersCheckout(
				newAddressAddress = "Test Address",
				newAddressCity = "Test City",
				newAddressCountry = "United States",
				newAddressName = "Address Name",
				newAddressZipCode = "Test Zip Code",
				paymentMethod = "Money Order",
				productName = "U-Joint",
				productQuantity = "1",
				productUnitPrice = "24",
				shippingMethod = "Standard");
		}

		task ("When the Operations Manager opens the order from the orders page") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "userea@liferay.com",
				userLoginFullName = "userfn userln");

			CommerceOrders.openOrdersAdmin();

			CommerceNavigator.gotoOrderEntryViaAdmin(status = "Pending");
		}

		task ("Then the Operations Manager cannot edit the payment method of the order") {
			Click(
				key_menuTab = "Payments",
				locator1 = "CommerceEntry#MENU_TAB");

			AssertTextEquals.assertPartialText(
				key_paymentMethod = "Money Order",
				locator1 = "CommerceOrders#ORDER_DETAILS_PAYMENT_METHOD_NAME",
				value1 = "Money Order");

			AssertElementNotPresent(
				key_titleName = "Payment Method",
				locator1 = "CommerceOrders#ORDER_DETAILS_EDIT_BUTTON");
		}
	}

	@description = "Verify the payment method selection step is skipped when a buyer without the 'Manage Commerce Order Payment Methods' permission checks out an order"
	@priority = "3"
	test UsersWithoutManagePermissionSkipsPaymentMethodStepDuringCheckout {
		property portal.acceptance = "false";

		task ("Given multiple active payment methods") {
			CommerceChannels.openChannelsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium Portal");

			CommerceChannels.activatePaymentMethod(paymentMethod = "Money Order");

			CommerceChannels.activatePaymentMethod(paymentMethod = "PayPal");
		}

		task ("When a buyer without 'Manage Commerce Order Payment Methods' permission checks out an order") {
			Permissions.removePermissionViaAPI(
				groupName = "Minium",
				resourceAction = "MANAGE_COMMERCE_ORDER_PAYMENT_METHODS",
				resourceName = "com.liferay.commerce.order",
				roleTitle = "Buyer",
				roleType = "account");

			User.logoutPG();

			CommerceLogin.miniumLogin(
				password = "test",
				urlAppend = "web/minium",
				userEmailAddress = "buyer@liferay.com");

			FrontStore.addInitializerProductToCart(
				productName = "U-Joint",
				productQuantity = "1");

			CommerceNavigator.gotoMiniCart();

			Button.click(button = "View Details");

			Button.click(button = "Checkout");

			CommerceCheckout.setAddress(
				newAddressAddress = "Test Address",
				newAddressCity = "Test City",
				newAddressCountry = "United States",
				newAddressName = "Address Name",
				newAddressZipCode = "Test Zip Code");

			Click(locator1 = "Button#CONTINUE");

			Click(
				key_radioOption = "Standard",
				locator1 = "CommerceEntry#RADIO_BUTTON");

			Click(locator1 = "Button#CONTINUE");
		}

		task ("Then the payment method selection step is skipped and the default payment method is applied") {
			AssertElementNotPresent(
				key_radioOption = "Money Order",
				locator1 = "CommerceEntry#RADIO_BUTTON");

			AssertElementNotPresent(
				key_radioOption = "PayPal",
				locator1 = "CommerceEntry#RADIO_BUTTON");

			AssertTextEquals(
				key_paymentMethod = "Money Order",
				locator1 = "CommerceAccelerators#CHECKOUT_SUMMARY_PAYMENT_METHOD",
				value1 = "Money Order");

			Click(locator1 = "Button#CONTINUE");
		}
	}

}