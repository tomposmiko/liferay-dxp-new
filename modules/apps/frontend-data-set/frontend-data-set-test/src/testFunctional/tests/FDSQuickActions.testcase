@component-name = "portal-frontend-infrastructure"
definition {

	property osgi.modules.includes = "frontend-data-set-sample-web";
	property portal.acceptance = "false";
	property portal.release = "false";
	property portal.upstream = "quarantine";
	property testray.component.names = "Frontend Dataset";
	property testray.main.component.name = "Frontend Dataset";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		task ("Given Frontend Dataset sample portlet") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page",
				widgetName = "Frontend Data Set Sample");

			JSONLayout.updateLayoutTemplateOfPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page",
				layoutTemplate = "1 Column");

			Navigator.gotoPage(pageName = "Frontend Data Set Test Page");

			WaitForElementPresent(
				key_itemName = "Sample1",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");

			MouseOver(
				key_itemName = "Sample1",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONLayout.deletePublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page");
		}
	}

	@description = "LPS-155465. Assert that clicking quick action is equivalent to clicking the ellipsis dropdown menu."
	@priority = "5"
	test BehaviorIsEqualToEllipsisDropdownEquivalentAction {
		var portalURL = PropsUtil.get("portal.url");

		task ("When click on Pencil Icon") {
			ClickNoError(
				key_title = "Sample1",
				locator1 = "FrontendDataSet#EDIT_ACTION_BUTTON");
		}

		task ("And when copy the browser URL (#test-pencil is appended)") {
			AssertLocation(value1 = "${portalURL}/frontend-data-set-test-page#test-pencil");
		}

		task ("And when click on ellipsis of 1st row item in FDS table") {
			Click(
				key_title = "Sample1",
				locator1 = "FrontendDataSet#ENTRY_VERTICAL_ELLIPSIS");
		}

		task ("And when click on Pencil Icon") {
			Click(locator1 = "FrontendDataSet#DROPDOWN_MENU_SHOW_ITEM_EDIT_ICON");
		}

		task ("Then browser URL matches copied URL (#test-pencil)") {
			AssertLocation(value1 = "${portalURL}/frontend-data-set-test-page#test-pencil");
		}
	}

	@description = "LPS-155457. Assert that hover over mouse off of the table body quick action menu is not visible."
	@priority = "5"
	test CanDisappearWhenNotHovered {
		task ("When hover mouse off of the table body") {
			MouseOver(locator1 = "FrontendDataSet#TABLE_BODY");
		}

		task ("Then quick action menu is not visible") {
			FDSQuickActions.viewQuickActionMenuNotPresent(title = "Sample1");
		}
	}

	@description = "LPS-155458. Assert can be displayed on one an active row."
	@priority = "4"
	test CanDisplayOnOneActiveRow {
		task ("When hover over 2nd row item") {
			MouseOver(
				key_itemName = "Sample2",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");
		}

		task ("Then quick action menu is displayed on the 2nd row") {
			FDSQuickActions.viewQuickActionMenuPresent(title = "Sample2");
		}

		task ("Then quick action menu is not displayed on 1st row") {
			FDSQuickActions.viewQuickActionMenuNotPresent(title = "Sample1");
		}
	}

	@description = "LPS-155466. Assert that test-pencil is appended to browser URL after clicking."
	@priority = "5"
	test CanExecuteAnAction {
		var portalURL = PropsUtil.get("portal.url");

		task ("When click on Pencil Icon ") {
			ClickNoError(
				key_title = "Sample1",
				locator1 = "FrontendDataSet#EDIT_ACTION_BUTTON");
		}

		task ("Then #test-pencil is appended to browser URL") {
			AssertLocation(value1 = "${portalURL}/frontend-data-set-test-page#test-pencil");
		}
	}

	@description = "LPS-155467. Assert the quick action is not visible when the row checkbox is checked."
	@priority = "3"
	test IsNotVisibleOnCheckedRow {
		task ("When mark a row checkbox as checked") {
			Check.checkToggleSwitch(
				key_title = "Sample1",
				locator1 = "FrontendDataSet#ENTRY_CHECKBOX");
		}

		task ("And when hover mouse over checked row") {
			MouseOver(
				key_itemName = "Sample1",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");
		}

		task ("Then the quick action icons are not visible") {
			FDSQuickActions.viewQuickActionMenuNotPresent(title = "Sample1");
		}
	}

}