definition {

	macro _curlUserAccount {
		if (isSet(virtualHost)) {
			if (!(isSet(port))) {
				var port = 8080;
			}

			var portalURL = "http://${virtualHost}:${port}";
		}
		else {
			var portalURL = JSONCompany.getPortalURL();
		}

		if (isSet(userAccountId)) {
			var api = "user-accounts/${userAccountId}";

			if (isSet(parameter)) {
				var api = "user-accounts/${userAccountId}?${parameter}=${parameterValue}";
			}

			if (isSet(relationshipName) && isSet(customObjectEntryId) && isSet(userAccountId)) {
				var api = "user-accounts/${userAccountId}/${relationshipName}/${customObjectEntryId}";
			}
		}
		else {
			var api = "user-accounts";
		}

		var curl = '''
			${portalURL}/o/headless-admin-user/v1.0/${api} \
				-H accept: application/json \
				-H Content-Type: application/json \
				-u test@liferay.com:test \
		''';

		return ${curl};
	}

	macro _nUsersBodyAsNestedFieldToCreate {
		Variables.assertDefined(parameterList = "${body},${nestedField},${numberOfRelatedUsers},${alternateName},${emailDomain},${familyName},${givenName}");

		var size = ${numberOfRelatedUsers};

		if (${oneToManyChild} == "true") {
			var nestedUserBody = '''"${nestedField}": {''';
		}
		else {
			var nestedUserBody = '''"${nestedField}": [''';
		}

		var i = 0;

		while ((${i} != ${size}) && (maxIterations = "10")) {
			var j = ${i} + 1;

			var relatedUser = '''
					"alternateName": "${alternateName}${j}",
					"emailAddress": "${alternateName}${j}@${emailDomain}",
					"familyName": "${familyName}${j}",
					"givenName": "${givenName}${j}"
				''';

			if (${oneToManyChild} == "true") {
				var nestedUserBody = StringUtil.add(${nestedUserBody}, "${relatedUser}}", "");
			}
			else {
				if (${j} == ${size}) {
					var nestedUserBody = StringUtil.add(${nestedUserBody}, "{${relatedUser}}]", "");
				}
				else {
					var nestedUserBody = StringUtil.add(${nestedUserBody}, "{${relatedUser}},", "");
				}
			}

			var i = ${i} + 1;
		}

		var body = StringUtil.add("${body},", ${nestedUserBody}, "");

		return ${body};
	}

	macro _nUsersBodyAsNestedFieldToUpdate {
		Variables.assertDefined(parameterList = "${body},${nestedField},${alternateName},${emailDomain},${familyName},${givenName},${userExternalReferenceCodes}");

		if (${oneToManyChild} == "true") {
			var nestedUserBody = '''"${nestedField}": {''';
		}
		else {
			var nestedUserBody = '''"${nestedField}": [''';
		}

		var i = 0;
		var userExternalReferenceCodes = ListUtil.newListFromString(${userExternalReferenceCodes});

		var size = ListUtil.size(${userExternalReferenceCodes});

		for (var externalReferenceCode : list ${userExternalReferenceCodes}) {
			var j = ${i} + 1;

			var relatedUser = '''
				"alternateName": "${alternateName}${j}-update",
				"emailAddress": "${alternateName}${j}-update@${emailDomain}",
				"familyName": "${familyName}${j}-update",
				"givenName": "${givenName}${j}-update",
				"externalReferenceCode": "${externalReferenceCode}"
			''';

			if (${oneToManyChild} == "true") {
				var nestedUserBody = StringUtil.add(${nestedUserBody}, "${relatedUser}}", "");
			}
			else {
				if (${j} == ${size}) {
					var nestedUserBody = StringUtil.add(${nestedUserBody}, "{${relatedUser}}]", "");
				}
				else {
					var nestedUserBody = StringUtil.add(${nestedUserBody}, "{${relatedUser}},", "");
				}
			}

			var i = ${i} + 1;
		}

		var body = StringUtil.add("${body},", ${nestedUserBody}, "");

		return ${body};
	}

	macro assertCorrectUserAlternateNameInResponse {
		Variables.assertDefined(parameterList = ${expectedNames});

		var actualAlternateNames = UserAccountAPI.getUserFieldWithFilter(
			filter = "alternateName%20ne%20%27test%27",
			jsonPath = "$..alternateName");

		TestUtils.assertEquals(
			actual = ${actualAlternateNames},
			expected = ${expectedNames});
	}

	macro assertResponseHasCorrectRelatedEntryName {
		Variables.assertDefined(parameterList = "${userAccountId},${relationshipName},${expectedValue}");

		var response = UserAccountAPI.getRelationshipByUserAccountId(
			relationshipName = ${relationshipName},
			userAccountId = ${userAccountId});

		var actual = JSONUtil.getWithJSONPath(${response}, "$.items[?(@.id==${customObjectEntryId})].name");

		TestUtils.assertEquals(
			actual = ${actual},
			expected = ${expectedValue});
	}

	macro assertResponseNotIncludeDetailsOfDeletedObjectEntry {
		Variables.assertDefined(parameterList = "${userAccountId},${relationshipName},${expectedValue}");

		var response = UserAccountAPI.getRelationshipByUserAccountId(
			relationshipName = ${relationshipName},
			userAccountId = ${userAccountId});

		var actual = JSONUtil.getWithJSONPath(${response}, "$..items");

		TestUtils.assertEquals(
			actual = ${actual},
			expected = ${expectedValue});
	}

	macro createUserAccount {
		Variables.assertDefined(parameterList = "${alternateName},${emailAddress},${familyName},${givenName}");

		var curl = UserAccountAPI._curlUserAccount(virtualHost = ${virtualHost});
		var body = '''"alternateName": "${alternateName}","emailAddress": "${emailAddress}","familyName": "${familyName}","givenName": "${givenName}"''';

		if (isSet(fieldName) && isSet(fieldValue)) {
			var body = StringUtil.add("${body},", "\"${fieldName}\":\"${fieldValue}\"", "");
		}

		var curl = StringUtil.add(${curl}, "-d {${body}}", " ");

		var response = JSONCurlUtil.post(${curl});

		return ${response};
	}

	macro createUserAccountAndRelatedObjects {
		Variables.assertDefined(parameterList = "${alternateName},${emailAddress},${familyName},${givenName},${nestedField},${numberOfRelatedObjectEntries},${relatedEntryFieldName}");

		var curl = UserAccountAPI._curlUserAccount(virtualHost = ${virtualHost});
		var body = '''"alternateName": "${alternateName}","emailAddress": "${emailAddress}","familyName": "${familyName}","givenName": "${givenName}"''';

		if (isSet(fieldName) && isSet(fieldValue)) {
			var body = StringUtil.add("${body},", "\"${fieldName}\":\"${fieldValue}\"", "");
		}

		if (isSet(nestedField)) {
			var body = CustomObjectAPI.nObjectEntryBodyAsNestedField(
				body = ${body},
				nestedField = ${nestedField},
				numberOfRelatedObjectEntries = ${numberOfRelatedObjectEntries},
				oneToManyChild = ${oneToManyChild},
				relatedEntryExternalReferenceCode = ${relatedEntryExternalReferenceCode},
				relatedEntryFieldName = ${relatedEntryFieldName});
		}

		var curl = StringUtil.add(${curl}, "-d {${body}}", " ");

		var response = JSONCurlUtil.post(${curl});

		return ${response};
	}

	macro createUserRoleByUserAccountId {
		Variables.assertDefined(parameterList = "${roleName},${streetNumber},${userAccountId}");

		var portalURL = JSONCompany.getPortalURL();
		var roleId = UserAccountAPI.getRoleId(
			roleName = ${roleName},
			streetNumber = ${streetNumber});

		var curl = '''
			${portalURL}/o/headless-admin-user/v1.0/roles/${roleId}/association/user-account/${userAccountId} \
				-H accept: application/json \
				-u test@liferay.com:test \
		''';

		var response = JSONCurlUtil.post(${curl});

		return ${response};
	}

	macro deleteObjectEntryRelatedToUserAccount {
		Variables.assertDefined(parameterList = "${userAccountId},${relationshipName},${customObjectEntryId}");

		var curl = UserAccountAPI._curlUserAccount(
			customObjectEntryId = ${customObjectEntryId},
			relationshipName = ${relationshipName},
			userAccountId = ${userAccountId});

		JSONCurlUtil.delete(${curl});
	}

	macro getRelationshipByUserAccountId {
		Variables.assertDefined(parameterList = "${userAccountId},${relationshipName}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/o/headless-admin-user/v1.0/user-accounts/${userAccountId}/${relationshipName} \
				-H accept: application/json \
				-H Content-Type: application/json \
				-u test@liferay.com:test \
		''';

		var response = JSONCurlUtil.get(${curl});

		return ${response};
	}

	macro getRoleId {
		Variables.assertDefined(parameterList = "${roleName},${streetNumber}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/o/headless-admin-user/v1.0/roles \
				-H accept: application/json \
				-u test@liferay.com:test \
		''';

		var response = JSONCurlUtil.get(${curl});

		if (isSet(streetNumber)) {
			var roleId = JSONUtil.getWithJSONPath(${response}, "$.items[${streetNumber}].id");

			return ${roleId};
		}
		else {
			return ${response};
		}
	}

	macro getSystemObjectsWithNestedField {
		var curl = UserAccountAPI._curlUserAccount(
			parameter = ${parameter},
			parameterValue = ${parameterValue},
			userAccountId = ${userAccountId});

		var response = JSONCurlUtil.get(${curl});

		return ${response};
	}

	macro getUpdatedFieldOfUserAccountWithPatchRequest {
		Variables.assertDefined(parameterList = "${fieldName},${updatedFieldValue}");

		if (!(isSet(userAccountId))) {
			var userAccountId = ${UserAccountId};
		}

		var curl = UserAccountAPI._curlUserAccount(userAccountId = ${userAccountId});
		var body = '''-d {"${fieldName}":"${updatedFieldValue}"}''';

		var curl = StringUtil.add(${curl}, ${body}, "");

		var valueOfField = JSONCurlUtil.patch(${curl}, "$.${fieldName}");

		return ${valueOfField};
	}

	macro getUpdatedFieldOfUserAccountWithPutRequest {
		Variables.assertDefined(parameterList = "${alternateName},${emailAddress},${familyName},${givenName},${fieldName},${updatedFieldValue}");

		if (!(isSet(userAccountId))) {
			var userAccountId = ${UserAccountId};
		}

		var curl = UserAccountAPI._curlUserAccount(userAccountId = ${userAccountId});
		var body = '''
			-d {
				"alternateName": "${alternateName}",
				"emailAddress": "${emailAddress}",
				"familyName": "${familyName}",
				"givenName": "${givenName}",
				"${fieldName}":"${updatedFieldValue}"
			}
		''';

		var curl = StringUtil.add(${curl}, ${body}, "");

		var valueOfField = JSONCurlUtil.put(${curl}, "$.${fieldName}");

		return ${valueOfField};
	}

	macro getUserFieldWithFilter {
		Variables.assertDefined(parameterList = "${filter},${jsonPath}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/o/headless-admin-user/v1.0/user-accounts?filter='${filter}' \
				-H accept: application/json \
				-H Content-Type: application/json \
				-u test@liferay.com:test \
		''';

		var fieldValue = JSONCurlUtil.get(${curl}, ${jsonPath});

		return ${fieldValue};
	}

	macro partialUpdateUserAccountAndRelatedObjects {
		Variables.assertDefined(parameterList = "${emailAddress},${nestedField},${numberOfRelatedObjectEntries},${relatedEntryFieldName},${userAccountId}");

		var curl = UserAccountAPI._curlUserAccount(
			userAccountId = ${userAccountId},
			virtualHost = ${virtualHost});
		var body = '''"emailAddress": "${emailAddress}"''';

		if (isSet(fieldName) && isSet(fieldValue)) {
			var body = StringUtil.add("${body},", "\"${fieldName}\":\"${fieldValue}\"", "");
		}

		if (isSet(nestedField)) {
			var body = CustomObjectAPI.nObjectEntryBodyAsNestedField(
				body = ${body},
				nestedField = ${nestedField},
				numberOfRelatedObjectEntries = ${numberOfRelatedObjectEntries},
				oneToManyChild = ${oneToManyChild},
				relatedEntryExternalReferenceCode = ${relatedEntryExternalReferenceCode},
				relatedEntryFieldName = ${relatedEntryFieldName},
				userAccountId = ${userAccountId});
		}

		var curl = StringUtil.add(${curl}, "-d {${body}}", " ");

		var response = JSONCurlUtil.patch(${curl});

		return ${response};
	}

	macro relateObjectEntries {
		Variables.assertDefined(parameterList = "${userAccountId},${customObjectId},${relationshipName}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/o/headless-admin-user/v1.0/user-accounts/${userAccountId}/${relationshipName}/${customObjectId} \
				-H accept: application/json \
				-H Content-Type: application/json \
				-u test@liferay.com:test \
		''';

		var response = JSONCurlUtil.put(${curl});

		return ${response};
	}

	macro setUpGlobalUserAccountId {
		var response = UserAccountAPI.createUserAccount(
			alternateName = ${alternateName},
			emailAddress = ${emailAddress},
			familyName = ${familyName},
			fieldName = ${fieldName},
			fieldValue = ${fieldValue},
			givenName = ${givenName});

		var userAccountId = JSONPathUtil.getIdValue(response = ${response});
		static var staticResponse = ${response};
		static var staticUserAccountId = ${userAccountId};

		return ${staticResponse};

		return ${staticUserAccountId};
	}

	macro setUpGlobalUserAccountIds {
		var response1 = UserAccountAPI.createUserAccount(
			alternateName = ${alternateName1},
			emailAddress = ${emailAddress1},
			familyName = ${familyName1},
			givenName = ${givenName1},
			virtualHost = ${virtualHost});
		var response2 = UserAccountAPI.createUserAccount(
			alternateName = ${alternateName2},
			emailAddress = ${emailAddress2},
			familyName = ${familyName2},
			givenName = ${givenName2},
			virtualHost = ${virtualHost});
		var userAccountId1 = JSONPathUtil.getIdValue(response = ${response1});
		var userAccountId2 = JSONPathUtil.getIdValue(response = ${response2});

		if (isSet(virtualHost)) {
			static var staticUserAccountId3 = ${userAccountId1};
			static var staticUserAccountId4 = ${userAccountId2};

			return ${staticUserAccountId3};

			return ${staticUserAccountId4};
		}
		else {
			static var staticUserAccountId1 = ${userAccountId1};
			static var staticUserAccountId2 = ${userAccountId2};

			return ${staticUserAccountId1};

			return ${staticUserAccountId2};
		}
	}

	macro updateUserAccountAndRelatedObjects {
		Variables.assertDefined(parameterList = "${alternateName},${emailAddress},${familyName},${givenName},${nestedField},${numberOfRelatedObjectEntries},${relatedEntryFieldName},${userAccountId}");

		var curl = UserAccountAPI._curlUserAccount(
			userAccountId = ${userAccountId},
			virtualHost = ${virtualHost});
		var body = '''"alternateName": "${alternateName}","emailAddress": "${emailAddress}","familyName": "${familyName}","givenName": "${givenName}"''';

		if (isSet(fieldName) && isSet(fieldValue)) {
			var body = StringUtil.add("${body},", "\"${fieldName}\":\"${fieldValue}\"", "");
		}

		if (isSet(nestedField)) {
			var body = CustomObjectAPI.nObjectEntryBodyAsNestedField(
				body = ${body},
				nestedField = ${nestedField},
				numberOfRelatedObjectEntries = ${numberOfRelatedObjectEntries},
				oneToManyChild = ${oneToManyChild},
				relatedEntryFieldName = ${relatedEntryFieldName},
				userAccountId = ${userAccountId});
		}

		var curl = StringUtil.add(${curl}, "-d {${body}}", " ");

		var response = JSONCurlUtil.put(${curl});

		return ${response};
	}

}