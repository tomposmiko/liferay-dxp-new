definition {

	macro addColumnsViaUI {
		LexiconEntry.gotoAdd();

		SelectFrame(locator1 = "ObjectCustomViews#VIEW_MODAL_CONTENT");

		for (var columnsOptions : list ${addColumns}) {
			Click.clickAtNotVisible(
				key_columnOption = ${columnsOptions},
				locator1 = "ObjectCustomViews#CHECKBOX_COLUMN_OPTION");
		}

		PortletEntry.save();

		SelectFrame(locator1 = "IFrame#IFRAME");
	}

	macro addDefaultSort {
		if (IsElementPresent(key_text = "No default sort was created yet", locator1 = "ObjectCustomViews#VIEW_BUILDER_EMPTY_STATE_TEXT")) {
			ObjectCustomViews.addNewDefaultSort();
		}
		else {
			Click(locator1 = "ObjectCustomViews#DEFAULT_SORT_ADD_BUTTON");
		}

		Click(locator1 = "ObjectCustomViews#SELECT_AUTO_COMPLETE");

		Click(
			key_columnOption = ${columnOption},
			locator1 = "ObjectCustomViews#SELECT_AUTO_COMPLETE_OPTION");

		Click(locator1 = "ObjectCustomViews#SELECT_SORTING_ON_DEFAULT_SORT");

		Click(
			key_sortOption = ${sortOption},
			locator1 = "ObjectCustomViews#SORT_OPTION");

		Click(
			key_tab = "Save",
			locator1 = "Button#TAB_SUBMIT_BTN");
	}

	macro addNewAggregationFilterViaUI {
		Click(locator1 = "ObjectCustomViews#ADD_BUTTON_FILTERS");

		Click(locator1 = "ObjectCustomViews#VIEW_MODAL_CONTENT");

		Click(
			key_picklistOption = ${filterBy},
			locator1 = "ObjectCustomViews#FIELD_VIEW_FILTER_DROPDOWN_OPTION");

		Click(
			key_filterLabel = "Filter Type",
			locator1 = "ObjectCustomViews#SELECT_FILTER_GENERIC");

		Click(
			key_fieldType = ${filterType},
			locator1 = "ObjectField#TYPE_OPTION");

		if (${filterType} == "Range") {
			var dateIndex = 1;

			for (var dateValue : list ${dateValueList}) {
				Type(
					key_dateIndex = ${dateIndex},
					locator1 = "ObjectCustomViews#FIELD_VIEW_FILTER_DATE_INPUT",
					value1 = ${dateValue});

				var dateIndex = ${dateIndex} + 1;
			}
		}
		else {
			Type(
				key_text = "Value",
				locator1 = "TextInput#ANY",
				value1 = ${filterValue});
		}

		ObjectCustomViews.clickSaveOnModal();
	}

	macro addNewDefaultSort {
		Click(locator1 = "ObjectCustomViews#ADD_NEW_DEFAULT_SORT");
	}

	macro addNewFilterViaUI {
		Click(locator1 = "ObjectCustomViews#ADD_BUTTON_FILTERS");

		Click(locator1 = "ObjectCustomViews#VIEW_MODAL_CONTENT");

		MenuItem.clickNoError(menuItem = ${filterBy});

		if (isSet(filterType)) {
			Click(
				key_filterLabel = "Filter Type",
				locator1 = "ObjectCustomViews#SELECT_FILTER_GENERIC");

			Click(
				key_fieldType = ${filterType},
				locator1 = "ObjectField#TYPE_OPTION");
		}

		if (isSet(filterValue)) {
			Click(
				key_filterLabel = "Value",
				locator1 = "ObjectCustomViews#SELECT_FILTER_GENERIC");

			for (var filterValueList : list ${filterValue}) {
				Click.clickAtNotVisible(
					key_columnOption = ${filterValueList},
					locator1 = "ObjectCustomViews#CHECKBOX_COLUMN_OPTION");
			}
		}

		ObjectCustomViews.clickSaveOnModal();
	}

	macro addObjectViewViaUI {
		LexiconEntry.gotoAdd();

		Type(
			locator1 = "ObjectCustomViews#ADD_VIEW_NAME",
			value1 = ${viewName});

		PortletEntry.save();
	}

	macro assertColumnPresentOnDefaultSort {
		AssertElementPresent(
			key_columnName = ${columnName},
			locator1 = "ObjectCustomViews#DEFAULT_SORT_SEARCH_RESULT");

		AssertTextEquals(
			key_sortingLabel = ${sortingLabel},
			locator1 = "ObjectCustomViews#DEFAULT_SORT_SORTING_LABEL",
			value1 = ${sortingLabel});
	}

	macro assertColumnVisibleOnAddColumnModal {
		SelectFrame(locator1 = "ObjectCustomViews#VIEW_MODAL_CONTENT");

		AssertVisible(
			key_columnName = ${columnOption},
			locator1 = "ObjectCustomViews#ADD_COLUMNS_COLUMN_LABEL");
	}

	macro assertFilterPresent {
		Click(locator1 = "ObjectCustomViews#ADD_BUTTON_FILTERS");

		Click(locator1 = "ObjectCustomViews#VIEW_MODAL_CONTENT");

		AssertElementPresent(
			key_menuItem = ${filterBy},
			locator1 = "MenuItem#ANY_MENU_ITEM");
	}

	macro assertNoColumnPresentInDefaultSort {
		ObjectCustomViews.addNewDefaultSort();

		Click(locator1 = "ObjectCustomViews#SELECT_AUTO_COMPLETE");

		AssertElementPresent(locator1 = "ObjectCustomViews#DEFAULT_SORT_NO_COLUMN_OPTION");
	}

	macro assertObjectView {
		var key_viewLabel = ${viewLabel};

		AssertElementPresent(locator1 = "ObjectCustomViews#OBJECT_VIEW_LABEL");
	}

	macro assertPresentEntriesOnFilters {
		AssertElementPresent(
			key_columnName = ${filterBy},
			locator1 = "ObjectCustomViews#VIEW_BUILDER_SEARCH_RESULT");

		if (isSet(filterValue)) {
			for (var filterValueList : list ${filterValue}) {
				AssertElementPresent(
					key_columnLabel = ${filterValueList},
					locator1 = "ObjectCustomViews#BUILDER_SEARCH_RESULT_COLUMN_LABEL");
			}
		}
	}

	macro clickOnFilterOnEntries {
		Click(
			key_buttonName = "Filter",
			locator1 = "ObjectCustomViews#DROPDOWN_BUTTON_ON_OBJECT_VIEW");

		SelectFrame(locator1 = "ObjectCustomViews#VIEW_DROPDOWN_MENU_ON_MODAL_DEFAULT_SORT");
	}

	macro clickSaveOnModal {
		Click(
			key_name = "Save",
			locator1 = "Button#GENERAL_BUTTON_MODAL_FOOTER");
	}

	macro defineDateOnFilter {
		var displayDate = "${displayDateMonth}/${displayDateDay}/${displayDateYear}";

		Type(
			locator1 = "ObjectCustomViews#SEARCH_DROPDOWN_MENU_FILTER",
			value1 = ${filterName});

		Click(
			key_fieldName = ${filterName},
			locator1 = "FormViewBuilder#OBJECT_FIELD_BUTTON_DROPDOWN");

		Type(
			key_fieldName = "yyyy-mm-dd",
			locator1 = "ObjectCustomViews#FROM_DATE_INPUT",
			value1 = ${displayDate});

		Click(
			key_text = "Add Filter",
			locator1 = "Button#ANY");
	}

	macro deleteObjectCustomViewViaUI {
		Click(
			key_objectCustomViewLabel = ${viewLabel},
			locator1 = "ObjectCustomViews#OBJECT_VIEW_KEBAB_MENU");

		MenuItem.clickNoError(menuItem = "Delete");
	}

	macro deleteSpecificColumn {
		Click(
			key_columnName = ${columnName},
			locator1 = "ObjectCustomViews#VIEW_BUILDER_DROPDOWN_SPECIFIC_COLUMN");

		DropdownMenuItem.click(menuItem = "Delete");
	}

	macro duplicateObjectCustomView {
		Click(
			key_objectCustomViewLabel = ${viewLabel},
			locator1 = "ObjectCustomViews#OBJECT_VIEW_KEBAB_MENU");

		MenuItem.clickNoError(menuItem = "Duplicate");
	}

	macro editName {
		Type(
			locator1 = "ObjectCustomViews#OBJECT_VIEWS_NAME",
			value1 = ${name});
	}

	macro editSortingType {
		Click(
			key_cardName = "Default Sort",
			locator1 = "ObjectCustomViews#SELECT_DROPDOWN_ON_LABEL");

		DropdownMenuItem.click(menuItem = "Edit");

		Click(
			key_sorting = ${actualSort},
			locator1 = "ObjectCustomViews#EDIT_SORTING");

		Click(
			key_sortOption = ${newSortOption},
			locator1 = "ObjectCustomViews#SORT_OPTION");

		Click(
			key_tab = "Save",
			locator1 = "Button#TAB_SUBMIT_BTN");
	}

	macro editSpecificColumn {
		Click(
			key_columnName = ${columnName},
			locator1 = "ObjectCustomViews#VIEW_BUILDER_DROPDOWN_SPECIFIC_COLUMN");

		DropdownMenuItem.click(menuItem = "Edit");
	}

	macro goToDefaultSortTab {
		Click(locator1 = "ObjectCustomViews#DEFAULT_SORT_TAB");
	}

	macro goToFiltersTab {
		Click(locator1 = "ObjectCustomViews#FILTERS_TAB");
	}

	macro gotoPermissions {
		Click(
			key_objectLabel = ${entryName},
			locator1 = "ObjectAdmin#OBJECT_KEBAB_MENU");

		MenuItem.click(menuItem = "Permissions");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");
	}

	macro goToViewsBuilderTab {
		if (IsElementPresent(locator1 = "ObjectCustomViews#VIEW_BUILDER_TAB")) {
			Click(locator1 = "ObjectCustomViews#VIEW_BUILDER_TAB");
		}
	}

	macro goToViewsDetails {
		Click(
			key_label = ${label},
			locator1 = "ObjectCustomViews#SELECT_OBJECT_VIEWS");

		SelectFrame(locator1 = "IFrame#IFRAME");
	}

	macro goToViewsTab {
		Click(locator1 = "ObjectCustomViews#OBJECT_VIEWS_TAB");
	}

	macro markViewAsDefault {
		Click(locator1 = "ObjectAdmin#LAYOUT_INFO_TAB");

		Click.clickAtNotVisible(locator1 = "ObjectCustomViews#MARK_AS_DEFAULT");
	}

	macro saveColumnsModal {
		Click(
			key_tab = "Save",
			locator1 = "Button#TAB_SUBMIT_BTN");
	}

	macro searchColumnsOnModalDefaultSort {
		Click(locator1 = "ObjectCustomViews#SELECT_AUTO_COMPLETE");

		Type(
			locator1 = "ObjectPortlet#COLUMN_SEARCH_FIELD",
			value1 = ${columnName});

		Click(locator1 = "ObjectCustomViews#VIEW_DROPDOWN_MENU_ON_MODAL_DEFAULT_SORT");

		AssertElementPresent(
			key_columnName = ${columnName},
			locator1 = "ObjectCustomViews#VIEW_FIELDS_ON_DROPDOWN_ITEM");
	}

	macro searchOnAddColumnModal {
		SelectFrame(locator1 = "ObjectCustomViews#VIEW_MODAL_CONTENT");

		Click(locator1 = "ObjectCustomViews#ADD_COLUMNS_SEARCH_BAR");

		Type(
			locator1 = "ObjectCustomViews#ADD_COLUMNS_SEARCH_BAR",
			value1 = ${columnName});
	}

	macro searchOnDefaultSort {
		Click(locator1 = "ObjectCustomViews#DEFAULT_SORT_SEARCH_BAR");

		Type(
			locator1 = "ObjectCustomViews#DEFAULT_SORT_SEARCH_BAR",
			value1 = ${columnName});
	}

	macro searchOnViewBuilder {
		Click(locator1 = "ObjectCustomViews#VIEW_BUILDER_SEARCH_BAR");

		Type(
			locator1 = "ObjectCustomViews#VIEW_BUILDER_SEARCH_BAR",
			value1 = ${columnName});
	}

	macro selectAnyPermissions {
		for (var permissionsName : list ${permissionsList}) {
			if (isSet(analyticsAdministrator)) {
				Click(
					key_permissionName = ${permissionsName},
					key_roleName = "analytics-administrator",
					locator1 = "ObjectCustomViews#SELECT_ANY_PERMISSIONS");
			}

			if (isSet(guest)) {
				Click(
					key_permissionName = ${permissionsName},
					key_roleName = "guest",
					locator1 = "ObjectCustomViews#SELECT_ANY_PERMISSIONS");
			}

			if (isSet(owner)) {
				Click.clickAtNotVisible(
					key_permissionName = ${permissionsName},
					key_roleName = "owner",
					locator1 = "ObjectCustomViews#SELECT_ANY_PERMISSIONS");
			}

			if (isSet(powerUser)) {
				Click(
					key_permissionName = ${permissionsName},
					key_roleName = "power-user",
					locator1 = "ObjectCustomViews#SELECT_ANY_PERMISSIONS");
			}

			if (isSet(portalContentReviewer)) {
				Click(
					key_permissionName = ${permissionsName},
					key_roleName = "portal-content-reviewer",
					locator1 = "ObjectCustomViews#SELECT_ANY_PERMISSIONS");
			}

			if (isSet(publicationsUser)) {
				Click(
					key_permissionName = ${permissionsName},
					key_roleName = "publications-user",
					locator1 = "ObjectCustomViews#SELECT_ANY_PERMISSIONS");
			}

			if (isSet(siteContentReviewer)) {
				Click(
					key_permissionName = ${permissionsName},
					key_roleName = "site-content-reviewer",
					locator1 = "ObjectCustomViews#SELECT_ANY_PERMISSIONS");
			}

			if (isSet(siteMember)) {
				Click(
					key_permissionName = ${permissionsName},
					key_roleName = "site-member",
					locator1 = "ObjectCustomViews#SELECT_ANY_PERMISSIONS");
			}

			if (isSet(user)) {
				Click(
					key_permissionName = ${permissionsName},
					key_roleName = "user",
					locator1 = "ObjectCustomViews#SELECT_ANY_PERMISSIONS");
			}
		}
	}

	macro selectPermissionToRegularRole {
		for (var permissionsName : list ${permissionsList}) {
			Click.clickAtNotVisible(
				key_permissionName = ${permissionsName},
				locator1 = "ObjectCustomViews#SELECT_PERMISSIONS_TO_REGULAR_ROLE");
		}
	}

	macro viewOptionNotPresentInKebabMenu {
		if (IsElementPresent(key_objectLabel = ${entryName}, locator1 = "ObjectAdmin#OBJECT_KEBAB_MENU")) {
			Click(
				key_objectLabel = ${entryName},
				locator1 = "ObjectAdmin#OBJECT_KEBAB_MENU");

			AssertElementNotPresent(
				key_menuItem = ${menuItem},
				locator1 = "MenuItem#ANY_MENU_ITEM");
		}
		else {
			AssertElementNotPresent(
				key_objectCustomViewLabel = ${entryName},
				locator1 = "ObjectCustomViews#OBJECT_VIEW_KEBAB_MENU");
		}
	}

	macro viewOrdersOfTheColumns {
		AssertVisible(
			key_columnName1 = ${columnName},
			key_position = ${position},
			locator1 = "ObjectCustomViews#VIEW_LIST_COLUMNS_BY_POSITION");
	}

}