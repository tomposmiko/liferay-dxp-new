@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Display Pages";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Import test fragments from dependencies folder") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdmin.importCollectionFromDependenciesFolder(collectionZipFile = "fragment-collection.zip");
		}

		task ("Add a display page") {
			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

			DisplayPageTemplatesAdmin.addDisplayPage(displayPageName = "Display Page Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "User can rename a display page and add a display page."
	@priority = "3"
	test AddBlankDisplayPage {
		task ("Rename the display page") {
			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

			DisplayPageTemplatesAdmin.renameDisplayPage(
				displayPageName = "Display Page Name",
				newDisplayPageName = "Display Page Name New");
		}

		task ("Assert the display page was renamed") {
			DisplayPageTemplatesAdmin.viewDisplayPagePresent(displayPageName = "Display Page Name New");
		}

		task ("Navigate back to the display page") {
			DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Display Page Name");
		}

		AssertElementPresent(
			key_text = "Publish",
			locator1 = "Button#ANY");

		task ("Publish the display page") {
			Button.clickPublish();

			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");
		}

		task ("View the display page") {
			DisplayPageTemplatesAdmin.viewDisplayPagePresent(displayPageName = "Display Page Name New");
		}
	}

	@description = "User can add a display page with fragments."
	@priority = "5"
	test AddDisplayPage {
		property portal.acceptance = "true";

		task ("Add a fragment") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Title");

			AssertVisible(
				key_content = "Lorem ipsum dolor sit amet",
				key_fragmentClass = "header",
				key_position = "1",
				locator1 = "Fragment#PAGE_EDITOR_FRAGMENT");
		}

		task ("Assert the display page autosaves") {
			PageEditor.viewAutoSaveMessage();
		}

		task ("Publish the display page") {
			Button.clickPublish();

			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");
		}

		task ("Assert the fragment was added to the display page") {
			DisplayPageTemplatesAdmin.gotoDisplayPageEllipsisItem(
				displayPageName = "Display Page Name",
				item = "Edit");

			AssertVisible(
				key_content = "Lorem ipsum dolor sit amet",
				key_fragmentClass = "header",
				key_position = "1",
				locator1 = "Fragment#PAGE_EDITOR_FRAGMENT");
		}
	}

	@description = "User can only map a display page to an asset type once."
	@priority = "4"
	test AssertAssetTypeOnlySelectableOnce {
		task ("Assert the select button to select asset type no longer appears") {
			PageEditorMapping.assertAssetTypeUnselectable();
		}
	}

	@description = "In the display page editor, user can add and remove fragments, user can remove sections, and user can edit text in fragment editable fields."
	@priority = "5"
	test AssertDisplayPageEditorFunctionality {
		task ("Add the Title, Rich Text, Button, and Content fragments") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Title");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Rich Text");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Button");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Content");
		}

		task ("Edit the Rich Text fragment's editable text") {
			PageEditor.editFragmentText(
				fragmentName = "Rich Text",
				id = "rich-text",
				text = "Editing Finished");
		}

		task ("Remove the Button fragment") {
			PageEditor.removeFragment(fragmentName = "Button");
		}

		task ("Assert the Rich Text fragment's editable text shows the new value") {
			PageEditor.viewFragmentEditableText(
				editableText = "Editing Finished",
				fragmentName = "Rich Text",
				id = "rich-text");
		}

		task ("Assert the Button fragment was removed, but all other fragments are present") {
			AssertVisible(
				key_content = "Lorem ipsum dolor sit amet",
				key_fragmentClass = "header",
				key_position = "1",
				locator1 = "Fragment#PAGE_EDITOR_FRAGMENT");

			AssertVisible(
				key_content = "Editing Finished",
				key_fragmentClass = "rich-text",
				key_position = "2",
				locator1 = "Fragment#PAGE_EDITOR_FRAGMENT");

			AssertElementNotPresent(
				key_content = "Submit",
				key_fragmentClass = "footer",
				key_position = "3",
				locator1 = "Fragment#PAGE_EDITOR_FRAGMENT");

			Button.viewNotPresent(button = "Submit");

			AssertVisible(
				key_content = "Lorem ipsum dolor sit amet, consectetur adipiscing elit. Aenean tempus eleifend lacus nec porttitor. Vestibulum pharetra ullamcorper felis volutpat commodo. Nam id ipsum placerat, hendrerit orci eu, congue nisi. Proin.",
				key_fragmentClass = "body",
				key_position = "3",
				locator1 = "Fragment#PAGE_EDITOR_FRAGMENT");
		}
	}

	@description = "This is a test for LPS-86191. User can configure display page settings for any created display page, even if the display page is in draft status."
	@priority = "5"
	test ConfigureDraftStatusDisplayPage {
		task ("Configure the display page template's 'Look and Feel' while it is still in draft status") {
			PageEditor.gotoTab(tabName = "Look and Feel");

			Click(locator1 = "Radio#SPECIFIC_LOOK");

			Uncheck.uncheckToggleSwitch(
				key_toggleSwitchLabel = "Show Footer",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

			Button.clickSave();
		}

		task ("Assert the configuration saved properly") {
			PageEditor.gotoTab(tabName = "Look and Feel");

			AssertNotChecked.assertNotCheckedNotVisible(
				key_toggleSwitchLabel = "Show Footer",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
		}
	}

	@description = "User can delete display pages."
	@priority = "5"
	test DeleteDisplayPage {

		// Ignoring test from acceptance until LRQA-52642 is resolved

		property portal.acceptance = "false";

		task ("Delete the display page") {
			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

			DisplayPageTemplatesAdmin.deleteDisplayPage(displayPageName = "Display Page Name");
		}

		task ("Assert the display page was deleted") {
			DisplayPageTemplatesAdmin.viewDisplayPageNotPresent(displayPageName = "DisplayPage Name");
		}
	}

	@description = "User can edit a display page. This includes adding and removing fragments and includes editing text in fragment editable fields."
	@priority = "5"
	test EditDisplayPage {
		task ("Add the Title, Rich Text, and Button fragments") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Title");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Rich Text");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Button");
		}

		task ("Publish the display page") {
			Button.clickPublish();

			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");
		}

		task ("Navigate back to the display page") {
			DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Display Page Name");
		}

		task ("Add a Content fragment") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Content");
		}

		task ("Edit the Rich Text fragment's editable text") {
			PageEditor.editFragmentText(
				fragmentName = "Rich Text",
				id = "rich-text",
				text = "Editing Finished");
		}

		task ("Remove the Button fragment") {
			PageEditor.removeFragment(fragmentName = "Button");
		}

		task ("Publish the display page with the new edits") {
			Button.clickPublish();

			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");
		}

		task ("Navigate back to the display page") {
			DisplayPageTemplatesAdmin.gotoDisplayPageEllipsisItem(
				displayPageName = "Display Page Name",
				item = "Edit");
		}

		task ("Assert the Rich Text fragment's new editable text displays") {
			PageEditor.viewFragmentEditableText(
				editableText = "Editing Finished",
				fragmentName = "Rich Text",
				id = "rich-text");
		}

		task ("Assert the Button fragment was removed, but all other fragments are present") {
			AssertVisible(
				key_content = "Lorem ipsum dolor sit amet",
				key_fragmentClass = "header",
				key_position = "1",
				locator1 = "Fragment#PAGE_EDITOR_FRAGMENT");

			AssertVisible(
				key_content = "Editing Finished",
				key_fragmentClass = "rich-text",
				key_position = "2",
				locator1 = "Fragment#PAGE_EDITOR_FRAGMENT");

			AssertElementNotPresent(
				key_content = "Submit",
				key_fragmentClass = "footer",
				key_position = "3",
				locator1 = "Fragment#PAGE_EDITOR_FRAGMENT");

			Button.viewNotPresent(button = "Submit");

			AssertVisible(
				key_content = "Lorem ipsum dolor sit amet, consectetur adipiscing elit. Aenean tempus eleifend lacus nec porttitor. Vestibulum pharetra ullamcorper felis volutpat commodo. Nam id ipsum placerat, hendrerit orci eu, congue nisi. Proin.",
				key_fragmentClass = "body",
				key_position = "3",
				locator1 = "Fragment#PAGE_EDITOR_FRAGMENT");
		}
	}

	@description = "User cannot delete a display page that is in use by an asset."
	@priority = "4"
	test ViewCannotDeleteActiveDisplayPage {
		task ("Rename the display page") {
			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

			DisplayPageTemplatesAdmin.renameDisplayPage(
				displayPageName = "Display Page Name",
				newDisplayPageName = "Display Page Name New");
		}

		task ("Assert the display page was renamed") {
			DisplayPageTemplatesAdmin.viewDisplayPagePresent(displayPageName = "Display Page Name New");
		}

		task ("Navigate back to the display page") {
			DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Display Page Name");
		}

		task ("Publish the display page") {
			Button.clickPublish();

			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");
		}

		task ("Add a web content article that uses the display page") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContent.addArticleCP(
				webContentContent = "Webcontent Content",
				webContentTitle = "Webcontent Title");

			WebContentNavigator.gotoEditCP(webContentTitle = "Webcontent Title");

			WebContent.editDisplayPage(
				pageName = "Display Page Name New",
				pageType = "Display Page Templates");

			PortletEntry.publish();
		}

		task ("Assert the display page cannot be deleted") {
			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

			DisplayPageTemplatesAdmin.deleteDisplayPage(displayPageName = "Display Page Name New");

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR",
				value1 = "Error:One or more entries could not be deleted.");

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_2",
				value1 = "Error:You cannot delete asset display page templates that are used by one or more assets.");

			DisplayPageTemplatesAdmin.viewDisplayPagePresent(displayPageName = "Display Page Name New");
		}
	}

}