@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Fragments";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Add a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Content Page Name",
				type = "content");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a use case for LPS-109298. Each tab of Tabs fragment is able to contain any elements in content page."
	@priority = "5"
	@uitest
	test AddElementToEachPanel {
		property portal.acceptance = "true";

		task ("Add a Tabs fragment to a content page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Tabs");
		}

		task ("Add elements into different tabs") {
			PageEditor.addElementToTab(
				collectionName = "Layout Elements",
				fragmentName = "Container",
				index = "2",
				navItem = "Tab 1",
				navTab = "Fragments",
				position = "1");

			var n = "2";

			for (var fragmentName : list "Heading,Tabs") {
				var index = ${n} + 1;

				PageEditor.addElementToTab(
					collectionName = "Basic Components",
					fragmentName = "${fragmentName}",
					index = "${index}",
					navItem = "Tab ${n}",
					navTab = "Fragments",
					position = "1");

				var n = ${n} + 1;
			}

			PageEditor.addElementToTab(
				index = "9",
				navItem = "Tab 4",
				navTab = "Widgets",
				portletName = "Asset Publisher",
				position = "5");

			PageEditor.publish();
		}

		task ("View no NPE thrown in server console") {
			AssertConsoleTextNotPresent(value1 = "java.lang.NullPointerException");
		}
	}

	@description = "This is for LPS-109298. Each tab of Tabs fragment is able to contain any elements in custom master."
	@priority = "4"
	test AddElementToEachPanelInMasterPageTemplate {
		property testray.main.component.name = "Master Page Templates";

		task ("Add a custom master") {
			JSONLayoutpagetemplate.addMasterPageTemplateEntry(
				groupName = "Test Site Name",
				masterPageTemplateEntryName = "Custom Masters Page Template");

			MastersPageTemplatesAdmin.openMastersAdmin(siteURLKey = "test-site-name");
		}

		task ("Add a Tabs fragment to custom master footer") {
			MastersPageTemplatesAdmin.gotoMasters(masterLayoutName = "Custom Masters Page Template");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				dropTarget = "Footer",
				fragmentName = "Tabs",
				scope = "Master");
		}

		task ("Add Container to Tab1 and Heading to Tab2, Tabs to Tab3, Asset Publisher to Tab4") {
			PageEditor.addElementToTab(
				collectionName = "Layout Elements",
				fragmentName = "Container",
				index = "2",
				navItem = "Tab 1",
				navTab = "Fragments",
				position = "1");

			var n = "2";

			for (var fragmentName : list "Heading,Tabs") {
				var index = ${n} + 1;

				PageEditor.addElementToTab(
					collectionName = "Basic Components",
					fragmentName = "${fragmentName}",
					index = "${index}",
					navItem = "Tab ${n}",
					navTab = "Fragments",
					position = "1");

				var n = ${n} + 1;
			}

			PageEditor.addElementToTab(
				index = "9",
				navItem = "Tab 4",
				navTab = "Widgets",
				portletName = "Asset Publisher",
				position = "5");
		}

		task ("Assert Publish custom master successfully without NPE") {
			Button.click(button = "Publish Master");

			Alert.viewSuccessMessage();

			AssertConsoleTextNotPresent(value1 = "java.lang.NullPointerException");
		}
	}

	@description = "This is a use case for LPS-109298. Can add elements above and below Tabs fragment to page."
	@priority = "4"
	test AddFragmentsAroundTabs {
		task ("Add a Tabs fragment to a content page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Tabs");
		}

		task ("Add a Button fragment on the top of Tabs") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				dropTarget = "Header",
				fragmentName = "Button",
				targetFragmentName = "Tabs");
		}

		task ("Add a Heading fragment on the bottom of Tabs") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading",
				targetFragmentName = "Tabs");
		}

		task ("View the order of fragments") {
			var n = "1";

			for (var fragmentName : list "Button,Tabs,Heading") {
				PageEditor.viewFragmentOrder(
					fragmentName = "${fragmentName}",
					position = "${n}");

				var n = ${n} + 1;
			}
		}
	}

	@description = "This is a use case for LPS-109298. Can add Tabs fragment to container of Layout elements."
	@priority = "3"
	test AddTabsToLayoutElements {
		task ("Add a Container to a content page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Layout Elements",
				fragmentName = "Container");
		}

		task ("Add a Grid below Container") {
			PageEditor.addFragment(
				collectionName = "Layout Elements",
				fragmentName = "Grid",
				targetFragmentName = "Container");
		}

		task ("Add the first Tabs into Container") {
			PageEditor.addElementToContainer(
				collectionName = "Basic Components",
				fragmentName = "Tabs",
				navTab = "Fragments");
		}

		task ("Add the second Tabs into the first module of Grid") {
			PageEditor.addElementToColumn(
				collectionName = "Basic Components",
				columnNumber = "1",
				fragmentName = "Tabs",
				navTab = "Fragments");
		}
	}

	@description = "This is for LPS-109298. If move the Drop Zone of master to one tab, only can add elements to that tab in content page."
	@priority = "4"
	test MoveDropZoneIntoPanel {
		property testray.main.component.name = "Master Page Templates";

		task ("Add a custom master") {
			JSONLayoutpagetemplate.addMasterPageTemplateEntry(
				groupName = "Test Site Name",
				masterPageTemplateEntryName = "Custom Masters Page Template");

			MastersPageTemplatesAdmin.openMastersAdmin(siteURLKey = "test-site-name");
		}

		task ("Add a Tabs fragment to custom master footer") {
			MastersPageTemplatesAdmin.gotoMasters(masterLayoutName = "Custom Masters Page Template");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				dropTarget = "Footer",
				fragmentName = "Tabs",
				scope = "Master");
		}

		task ("Move master page template drop zone to Tab1 of Tab Fragment") {
			PageEditor.clickConfigurableField(fragmentName = "Drop Zone");

			DragAndDrop.javaScriptDragAndDropToObject(
				key_dragFragmentName = "Drop Zone",
				key_index = "2",
				locator1 = "Fragment#FRAGMENT_HEADER_DRAG_HANDLER",
				locator2 = "PageEditor#FRAGMENT_WITH_DROP_TARGET",
				value1 = "");
		}

		task ("Publish master") {
			Button.click(button = "Publish Master");

			Alert.viewSuccessMessage();
		}

		task ("Add a page basing on custom master") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(
				masterLayoutName = "Custom Masters Page Template",
				pageName = "Test Page Name");
		}

		task ("Assert could drop fragment message appears in tab1") {
			AssertVisible(
				key_fragmentName = "tabs",
				locator1 = "PageEditor#FRAGMENT_INHERITED_FROM_CUSTOM_MASTERS");

			AssertVisible(
				key_content = "Place fragments here.",
				key_fragmentClass = "content",
				key_position = "1",
				locator1 = "Fragment#DROP_ZONE_NO_FRAGMENTS_MESSAGE");
		}

		task ("Add a card fragment to tab1") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Card");

			AssertElementNotPresent(
				key_index = "2",
				locator1 = "PageEditor#FRAGMENT_WITH_DROP_TARGET");
		}

		task ("Assert could not add fragment in Tab 2,3,4") {
			for (var navItem : list "Tab 2,Tab 3,Tab 4") {
				PageEditor.gotoTabsFragmentNavItem(navItem = "${navItem}");

				AssertElementNotPresent(
					key_content = "Place fragments here.",
					key_fragmentClass = "content",
					key_position = "1",
					locator1 = "Fragment#DROP_ZONE_NO_FRAGMENTS_MESSAGE");

				AssertElementNotPresent(
					key_index = "2",
					locator1 = "PageEditor#FRAGMENT_WITH_DROP_TARGET");
			}
		}

		task ("Assert no NPE after publishing content page") {
			PageEditor.publish();

			AssertConsoleTextNotPresent(value1 = "java.lang.NullPointerException");
		}
	}

	@description = "This is a test for LPS-154576. The Tabs fragment should persist selected tab when navigate through the site."
	@priority = "5"
	test PersistSelectedTabWhenNavigateThroughTheSite {
		task ("Given a page creator has a widget page and a content page with a Tabs fragment") {
			task ("Add a widget page") {
				JSONLayout.addPublicLayout(
					groupName = "Test Site Name",
					layoutName = "Test Page Name");
			}

			task ("Add a Tabs fragment to a content page") {
				ContentPagesNavigator.openEditContentPage(
					pageName = "Content Page Name",
					siteName = "Test Site Name");

				PageEditor.addFragment(
					collectionName = "Basic Components",
					fragmentName = "Tabs");
			}

			task ("Add a different fragment to each tab") {
				var n = "1";

				for (var fragmentName : list "Button,Card,Heading,HTML") {
					var index = ${n} + 1;

					PageEditor.addElementToTab(
						collectionName = "Basic Components",
						fragmentName = "${fragmentName}",
						index = "${index}",
						navItem = "Tab ${n}",
						navTab = "Fragments",
						position = "1");

					var n = ${n} + 1;
				}
			}
		}

		task ("When the page creator selects the Tab 2") {
			PageEditor.gotoTabsFragmentNavItem(navItem = "Tab 2");

			PageEditor.publish();
		}

		task ("Then the Tab 2 should be still selected after navigating through the site") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			Click(
				key_pageName = "Content Page Name",
				locator1 = "Home#PAGE");

			AssertElementPresent(
				key_navItem = "Tab 2",
				locator1 = "Fragment#ACTIVE_NAV_ITEM_TAB");

			ContentPages.viewFragmentText(
				contributed = "false",
				fragmentName = "card",
				id = "title",
				position = "7",
				text = "Card Title example");
		}
	}

	@description = "This is a test for LPS-154576. The Tabs fragment should persist selected tab when reloading page."
	@priority = "5"
	test PersistSelectedTabWhenReloadPage {
		task ("Given a page creator has a content page with a Tabs fragment") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Tabs");
		}

		task ("When the page creator accesses to the General panel of Tabs fragment") {
			PageEditor.gotoConfiguration(fragmentName = "Tabs");
		}

		task ("Then the page creator could see the Persist Selected Tab checked by default and relevant description") {
			AssertChecked.assertCheckedNotVisible(
				checkboxName = "Persist Selected Tab",
				locator1 = "Checkbox#ANY_CHECKBOX");

			AssertTextEquals(
				fieldLabel = "Persist Selected Tab",
				locator1 = "Message#SUBTITLE_WITH_LABEL",
				value1 = "Selected tab will be persisted when users reload the page or navigate through the site.");
		}

		task ("When the page creator selects the Tab 2") {
			PageEditor.gotoTabsFragmentNavItem(navItem = "Tab 2");
		}

		task ("Then the Tab 2 should be still selected after reloading page") {
			Refresh();

			PageEditor.assertFragmentSidebarLoaded();

			AssertElementPresent(
				key_navItem = "Tab 2",
				locator1 = "Fragment#ACTIVE_NAV_ITEM_TAB");
		}
	}

	@description = "This is a test for LPS-154576. If disable the Persist Selected Tab, the selected tab of Tabs fragment should be the first tab on page reload."
	@priority = "4"
	test ViewFirstTabSelectedAfterReloadPageWhenDisablePersistSelectedTab {
		task ("Given a page creator has a content page with a Tabs fragment and disable the Persist Selected Tab") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Tabs");

			PageEditor.editTabs(
				panel = "General",
				persistSelectedTab = "false");
		}

		task ("When the page creator selects the Tab 2") {
			PageEditor.gotoTabsFragmentNavItem(navItem = "Tab 2");
		}

		task ("Then the Tab 1 should be selected after reloading page") {
			Refresh();

			PageEditor.assertFragmentSidebarLoaded();

			AssertElementPresent(
				key_navItem = "Tab 1",
				locator1 = "Fragment#ACTIVE_NAV_ITEM_TAB");
		}
	}

	@description = "This is for LPS-109298. Can not add elements to each tab of Tabs fragment that is inherited from custom master in content page."
	@priority = "4"
	test ViewInheriatedTabsWithoutDropzoneInContentPage {
		property testray.main.component.name = "Master Page Templates";

		task ("Add a custom master") {
			JSONLayoutpagetemplate.addMasterPageTemplateEntry(
				groupName = "Test Site Name",
				masterPageTemplateEntryName = "Custom Masters Page Template");

			MastersPageTemplatesAdmin.openMastersAdmin(siteURLKey = "test-site-name");
		}

		task ("Add a Tabs fragment to custom master footer") {
			MastersPageTemplatesAdmin.gotoMasters(masterLayoutName = "Custom Masters Page Template");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				dropTarget = "Footer",
				fragmentName = "Tabs",
				scope = "Master");
		}

		task ("Publish master") {
			Button.click(button = "Publish Master");

			Alert.viewSuccessMessage();
		}

		task ("Add a page basing on custom master") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(
				masterLayoutName = "Custom Masters Page Template",
				pageName = "Test Page Name");
		}

		task ("Assert could drop fragment message appears in tab1") {
			AssertVisible(
				key_fragmentName = "tabs",
				locator1 = "PageEditor#FRAGMENT_INHERITED_FROM_CUSTOM_MASTERS");

			AssertTextEquals(
				locator1 = "PageEditor#FRAGMENT_DROP_TARGET",
				value1 = "Place fragments here.");

			AssertElementNotPresent(
				key_content = "Place fragments here.",
				key_fragmentClass = "content",
				key_position = "1",
				locator1 = "Fragment#DROP_ZONE_NO_FRAGMENTS_MESSAGE");
		}

		task ("Assert could not add fragment in Tab 2,3,4") {
			for (var navItem : list "Tab 2,Tab 3,Tab 4") {
				PageEditor.gotoTabsFragmentNavItem(navItem = "${navItem}");

				AssertElementNotPresent(
					key_fragmentName = "Place fragments here.",
					locator1 = "PageEditor#FRAGMENT_INHERITED_FROM_CUSTOM_MASTERS");

				AssertElementNotPresent(
					key_index = "2",
					locator1 = "PageEditor#FRAGMENT_WITH_DROP_TARGET");
			}
		}

		task ("Assert no NPE after publishing content page") {
			PageEditor.publish();

			AssertConsoleTextNotPresent(value1 = "java.lang.NullPointerException");
		}
	}

	@description = "This is a test for LPS-121223. View nested content when increase number of tabs."
	@priority = "4"
	test ViewNestedContentWhenIncreaseNumberOfTabs {
		task ("Add a Tabs fragment to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Tabs");
		}

		task ("Edit text in editable field of nav buttons") {
			for (var n : list "1,2,3,4") {
				PageEditor.editFragmentText(
					fragmentName = "Tabs",
					id = "title${n}",
					text = "My Tab ${n}");
			}
		}

		task ("Add fragments to drop zone of each tab") {
			var n = "1";

			for (var fragmentName : list "Button,Card,Heading,HTML") {
				var index = ${n} + 1;

				PageEditor.addElementToTab(
					collectionName = "Basic Components",
					fragmentName = "${fragmentName}",
					index = "${index}",
					navItem = "My Tab ${n}",
					navTab = "Fragments",
					position = "1");

				var n = ${n} + 1;
			}
		}

		task ("Increase number of tabs") {
			PageEditor.editTabs(
				numberOfTabs = "6",
				panel = "General");
		}

		task ("View nested content in drop zone of each tab") {
			var n = "1";

			for (var fragmentName : list "Button,Card,Heading,HTML") {
				PageEditor.gotoTabsFragmentNavItem(navItem = "My Tab ${n}");

				AssertElementPresent(
					key_fragmentName = "${fragmentName}",
					key_inFragmentName = "Tabs",
					locator1 = "Fragment#FRAGMENT_BODY_IN_FRAGMENT");

				var n = ${n} + 1;
			}

			var position = "1";

			for (var n : list "5,6") {
				PageEditor.gotoTabsFragmentNavItem(navItem = "Tab ${n}");

				AssertVisible(
					key_content = "Place fragments here.",
					key_fragmentClass = "content",
					key_position = "${position}",
					locator1 = "Fragment#DROP_ZONE_NO_FRAGMENTS_MESSAGE");

				var position = ${position} + 1;
			}
		}
	}

	@description = "This is a test for LPS-154576. The selected tab of Tabs fragment should be the first tab when access to page on a new window."
	@priority = "4"
	test ViewSelectedTabWhenAccessToPageOnNewWindow {
		task ("Given a page creator has a content page with a Tabs fragment") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Tabs");
		}

		task ("When the page creator selects the Tab 2") {
			PageEditor.gotoTabsFragmentNavItem(navItem = "Tab 2");
		}

		task ("Then the Tab 1 should be selected after accessing to page on a new window") {
			var baseURL = PropsUtil.get("portal.url");

			Open.openInTheNewTab(locator1 = "${baseURL}/web/test-site-name/content-page-name?p_l_mode=edit");

			PageEditor.assertFragmentSidebarLoaded();

			AssertElementPresent(
				key_navItem = "Tab 1",
				locator1 = "Fragment#ACTIVE_NAV_ITEM_TAB");
		}
	}

	@description = "This is a use case for LPS-111274. The selected Tabs fragment should always stay selected when click on the tabs of it."
	@priority = "5"
	@uitest
	test ViewTopperBarWhenSwitchToOtherTabs {
		property portal.acceptance = "true";

		task ("Add a Tabs fragment to a content page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Tabs");
		}

		task ("View the topper bar shown when switch to other tabs") {
			for (var navItem : list "Tab 2,Tab 3,Tab 4") {
				PageEditor.gotoTabsFragmentNavItem(navItem = "${navItem}");

				PageEditor.viewFragmentHeaderVisible(fragmentName = "Tabs");
			}
		}
	}

}