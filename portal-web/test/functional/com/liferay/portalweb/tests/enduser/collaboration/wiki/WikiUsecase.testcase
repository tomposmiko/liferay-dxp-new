@component-name = "portal-collaboration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Wiki";
	var pageName = "Wiki Test Page";
	var portletName = "Wiki";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Wiki Test Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Wiki Test Page",
			widgetName = "Wiki");

		Navigator.gotoPage(pageName = "Wiki Test Page");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			WikiPage.tearDownCP();

			WikiNode.tearDownCP();

			Page.tearDownCP();
		}
	}

	@description = "This test covers LPS-95501. It asserts added wiki attachment file via item selector."
	@priority = "4"
	test ActivateInitialStagingWithAttachment {
		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		JSONGroup.addGroup(groupName = "Site Name");

		SitePages.openPagesAdmin(siteURLKey = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Staging Test Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Site Name",
			layoutName = "Staging Test Page",
			widgetName = "Wiki");

		Navigator.gotoSitePage(
			pageName = "Staging Test Page",
			siteName = "Site Name");

		WikiNavigator.gotoAllPagesPG();

		WikiPage.addPG(
			wikiPageContent = "Wiki Page Content",
			wikiPageTitle = "Wiki Page Title");

		WikiNavigator.gotoPG(wikiPageTitle = "Wiki Page Title");

		WikiPage.addAttachmentViaPageEdit(
			attachmentFileName = "Document_1.jpg",
			wikiPageTitle = "Wiki Page Title");

		Navigator.openSiteURL(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Publishing",
			panel = "Site Administration",
			portlet = "Staging");

		Staging.activateStagingCP(
			siteName = "Site Name",
			uncheckContent = "Wiki");

		Navigator.gotoSitePage(
			pageName = "Staging Test Page",
			siteName = "Site Name");

		WikiNavigator.gotoPG(wikiPageTitle = "Wiki Page Title");

		WikiPage.viewPageWithAttachment(
			attachmentFileName = "Document_1.jpg",
			wikiPageContent = "Wiki Page Content",
			wikiPageTitle = "Wiki Page Title");

		Navigator.gotoStagedSitePage(
			pageName = "Staging Test Page",
			siteName = "Site Name");

		WikiNavigator.gotoPG(wikiPageTitle = "Wiki Page Title");

		WikiPage.viewAttachmentPGViaEdit(
			attachmentFileName = "Document_1.jpg",
			wikiPageTitle = "Wiki Page Title");

		ItemSelector.viewWikiAttachment(imageFileName = "Document_1.jpg");
	}

	@priority = "4"
	test ConfigurePortletDisplaySettings {
		Navigator.gotoPage(pageName = "Wiki Test Page");

		Portlet.gotoPortletOptions(portletOption = "Configuration");

		WikiPortlet.viewConfigurationTabs(
			isNotPresent = "NAVIGATION_EMAIL_FROM,NAVIGATION_PAGE_ADDED_EMAIL,NAVIGATION_PAGE_UPDATED_EMAIL,NAVIGATION_RSS",
			isPresent = "SETUP_DISPLAY_SETTINGS_PANEL");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Wiki");

		LexiconEntry.gotoHeaderMenuItem(menuItem = "Configuration");

		WikiPortlet.viewConfigurationTabs(
			isNotPresent = "SETUP_DISPLAY_SETTINGS",
			isPresent = "NAVIGATION_EMAIL_FROM,NAVIGATION_PAGE_ADDED_EMAIL,NAVIGATION_PAGE_UPDATED_EMAIL,NAVIGATION_RSS");

		Navigator.gotoPage(pageName = "Wiki Test Page");

		Portlet.gotoPortletOptions(portletOption = "Configuration");

		WikiPortlet.configureDisplaySettings(uncheckSetting = "ENABLE_COMMENTS,ENABLE_COMMENT_RATINGS,ENABLE_PAGE_RATINGS,SHOW_RELATED_ASSETS");

		Navigator.gotoPage(pageName = "Wiki Test Page");

		Portlet.gotoPortletOptions(portletOption = "Configuration");

		WikiPortlet.viewDisplaySettings(uncheckedSetting = "ENABLE_COMMENTS,ENABLE_COMMENT_RATINGS,ENABLE_PAGE_RATINGS,SHOW_RELATED_ASSETS");
	}

	@priority = "5"
	test ImportLARLinkNode {
		property testray.component.names = "Training";

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.addPrivatePage(pageName = "Research Projects");

		Page.view(
			pageName = "Research Projects",
			privacy = "Private Pages");

		var portalURL = PropsUtil.get("portal.url");

		Navigator.gotoSpecificURLPage(
			pageName = "Research Projects",
			url = "${portalURL}/group/guest/research-projects");

		Portlet.addPG(portletName = "Wiki");

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Wiki");

		LAR.importPortlet(
			larFileName = "research-projects-update-node.lar",
			mirrorWithOverwriting = "true",
			uploadFrom = "dependencies");

		Refresh();

		WikiNavigator.gotoNode(wikiNodeName = "Organic Research Projects");

		WikiNavigator.gotoCP(wikiPageTitle = "FrontPage");

		AssertClick(
			locator1 = "WikiEntry#BODY_LINK",
			value1 = "Sweet Potatoes");

		AssertTextEquals(
			locator1 = "Portlet#H3_HEADER",
			value1 = "Sweet Potatoes");
	}

	@priority = "4"
	test NavigationPortletSmoke {
		Navigator.openURL(pageName = "Wiki Test Page");

		Portlet.addPG(portletName = "Tree Menu");

		AntCommand.antCommand(
			locator1 = "build-test.xml",
			value1 = "remove-plugin -Dplugin.name=wiki-navigation-portlet");
	}

	@priority = "5"
	test RateAndEditPageComment {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Wiki");

		WikiNavigator.gotoNode(wikiNodeName = "Main");

		WikiPage.addCP(
			wikiPageContent = "Wiki Page Content 1",
			wikiPageTitle = "Wiki Page 1");

		Navigator.gotoPage(pageName = "Wiki Test Page");

		WikiNavigator.gotoAllPagesPG();

		WikiTable.viewEntry(
			draftStatus = "APPROVED",
			wikiPageTitle = "Wiki Page 1");

		WikiNavigator.gotoEntryViaPGTable(wikiPageTitle = "Wiki Page 1");

		WikiEntry.viewPageContentBody(wikiPageContent = "Wiki Page Content 1");

		WikiPage.addComment(entryComment = "Wiki Page Comment");

		Ratings.rate(ratingResult = "Thumb Up");

		WikiPage.editComment(
			entryComment = "Wiki Page Comment",
			entryCommentEdit = "Wiki Page Comment Edited");

		Navigator.gotoPage(pageName = "Wiki Test Page");

		WikiNavigator.gotoAllPagesPG();

		WikiNavigator.gotoEntryViaPGTable(wikiPageTitle = "Wiki Page 1");

		WikiPage.viewCommentPG(
			entryComment = "Wiki Page Comment Edited",
			wikiPageTitle = "Wiki Page 1");

		Ratings.viewRating(
			ratingResult = "Thumb Up",
			voteCount = "1");

		Ratings.viewRating(
			ratingResult = "Thumb Down",
			voteCount = "0");
	}

	@priority = "5"
	test RatePage {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Wiki");

		WikiNavigator.gotoNode(wikiNodeName = "Main");

		WikiPage.addCP(
			wikiPageContent = "Wiki Page Content 1",
			wikiPageTitle = "Wiki Page 1");

		Navigator.gotoPage(pageName = "Wiki Test Page");

		WikiNavigator.gotoAllPagesPG();

		WikiTable.viewEntry(
			draftStatus = "APPROVED",
			wikiPageTitle = "Wiki Page 1");

		WikiNavigator.gotoEntryViaPGTable(wikiPageTitle = "Wiki Page 1");

		WikiEntry.viewPageContentBody(wikiPageContent = "Wiki Page Content 1");

		WikiPage.rate4StarsPG(wikiPageTitle = "Wiki Page 1");

		Navigator.gotoPage(pageName = "Wiki Test Page");

		WikiNavigator.gotoAllPagesPG();

		WikiNavigator.gotoEntryViaPGTable(wikiPageTitle = "Wiki Page 1");

		WikiEntry.viewPageContentBody(wikiPageContent = "Wiki Page Content 1");

		WikiPage.viewRate4StarsPG(wikiPageTitle = "Wiki Page 1");
	}

	@priority = "5"
	test RestoreDeletedFrontPage {
		property portal.acceptance = "true";

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Wiki");

		WikiNavigator.gotoNode(wikiNodeName = "Main");

		WikiNavigator.gotoCP(wikiPageTitle = "FrontPage");

		WikiPage.addFrontPageCP(
			wikiFrontPageContent = "Wiki FrontPage Content",
			wikiPageTitle = "FrontPage");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Wiki");

		WikiNavigator.gotoNode(wikiNodeName = "Main");

		WikiPage.moveToRecycleBinCP(
			wikiNodeName = "Main",
			wikiPageTitle = "FrontPage");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Recycle Bin",
			panel = "Site Administration",
			portlet = "Recycle Bin");

		RecycleBin.viewCP(
			assetName = "FrontPage",
			assetType = "Wiki Page",
			deleteUser = "Test Test");

		RecycleBin.restoreOverwriteCP(assetName = "FrontPage");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Wiki");

		WikiNavigator.gotoNode(wikiNodeName = "Main");

		WikiNavigator.gotoCP(wikiPageTitle = "FrontPage");

		WikiPage.viewFrontPageCP(
			wikiFrontPageContent = "Wiki FrontPage Content",
			wikiFrontPageTitle = "FrontPage",
			wikiNodeName = "Main");
	}

	@priority = "4"
	test SearchChildPageComment {
		WikiPage.addFrontPagePG(
			wikiNodeName = "Main",
			wikiPageContent = "Wiki FrontPage Content");

		Navigator.gotoPage(pageName = "Wiki Test Page");

		WikiPage.addChildPagePG(
			wikiChildPageContent = "Wiki FrontPage Child Page Content",
			wikiChildPageTitle = "Wiki FrontPage Child Page Title",
			wikiPageTitle = "FrontPage");

		Navigator.gotoPage(pageName = "Wiki Test Page");

		WikiNavigator.gotoPG(wikiPageTitle = "Wiki FrontPage Child Page Title");

		WikiPage.addComment(entryComment = "Wiki FrontPage Child Page Comment Body document");

		Navigator.gotoPage(pageName = "Wiki Test Page");

		WikiPage.searchPG(searchTerm = "document");

		WikiPage.viewSearchResultsPG(
			commentBody = "Wiki FrontPage Child Page Comment Body document",
			wikiComment = "true",
			wikiPageTitle = "Wiki FrontPage Child Page Title");
	}

	@priority = "4"
	test SearchFrontPageAttachments {
		WikiPage.addFrontPagePG(
			wikiNodeName = "Main",
			wikiPageContent = "Wiki FrontPage Content");

		Navigator.gotoPage(pageName = "Wiki Test Page");

		WikiPage.addAttachmentViaPageEdit(
			attachmentFileName = "Document_1.txt",
			wikiPageTitle = "FrontPage");

		Navigator.gotoPage(pageName = "Wiki Test Page");

		WikiPage.searchPG(searchTerm = "document");

		WikiPage.viewSearchResultsPG(
			wikiAttachment = "true",
			wikiAttachmentBody = "Document_1.txt",
			wikiPageTitle = "FrontPage");
	}

	@priority = "4"
	test ViewDraftPagesViaBreadcrumb {
		Navigator.gotoPage(pageName = "Wiki Test Page");

		Portlet.addPG(portletName = "Breadcrumb");

		AssertClick(
			locator1 = "WikiTable#NAVIGATION_RECENT_CHANGES",
			value1 = "Recent Changes");

		Breadcrumb.viewPG(
			breadcrumbListVisible = "Wiki Test Page",
			pathLocator = "PAGE");

		Breadcrumb.viewPG(
			breadcrumbListVisible = "Recent Changes",
			pathLocator = "PAGE");

		AssertClick(
			locator1 = "WikiTable#NAVIGATION_ALL_PAGES",
			value1 = "All Pages");

		Breadcrumb.viewPG(
			breadcrumbListVisible = "Wiki Test Page",
			pathLocator = "CURRENT_LOCATION");

		Breadcrumb.viewPG(
			breadcrumbListVisible = "All Pages",
			pathLocator = "CURRENT_LOCATION");

		AssertClick(
			locator1 = "WikiTable#NAVIGATION_DRAFT_PAGES",
			value1 = "Draft Pages");

		Breadcrumb.viewPG(
			breadcrumbListVisible = "Wiki Test Page",
			pathLocator = "PAGE");

		Breadcrumb.viewPG(
			breadcrumbListVisible = "Draft Pages",
			pathLocator = "CURRENT_LOCATION");
	}

	@priority = "3"
	test ViewNoXSSViaAddPage {
		Navigator.gotoPage(pageName = "Wiki Test Page");

		WikiPage.editFrontPagePG(wikiPageContentEdit = '''<script>alert(123);</script>''');

		AssertClick(
			locator1 = "Link#EDIT",
			value1 = "Edit");

		AssertAlertNotPresent();

		var actualScript = '''<script>alert(123);</script>''';

		var escapedScript = '''&lt;script&gt;alert(123);&lt;/script&gt;''';

		AssertHTMLSourceTextNotPresent(value1 = "${actualScript}");

		AssertHTMLSourceTextPresent(value1 = "${escapedScript}");
	}

}