@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Display Pages";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Import test fragments from dependencies folder") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdmin.importCollectionFromDependenciesFolder(collectionZipFile = "fragment-collection.zip");
		}

		task ("Navigate to Build > Pages > Display Page Templates") {
			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "test@liferay.com",
				userLoginFullName = "Test Test");

			JSONGroup.deleteGroupByName(groupName = "Test Site Name");

			MyAccount.openMyAccountAdmin();

			User.deleteProfileImage();

			Button.click(button = "Save");

			User.tearDownCP();
		}
	}

	@description = "This is a test for LPS-90999. A user with permissions has access to edit the web content article or display page while viewing the article through its display page."
	@priority = "5"
	test EditWhenViewingInDisplayPageAsUser {
		task ("Add a display page with fragments mapped to Basic Web Content") {
			DisplayPageTemplatesAdmin.addDisplayPage(displayPageName = "Display Page Name");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Title");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Rich Text");

			PageEditorMapping.mapEditableTextToField(
				field = "Title",
				fragmentName = "Title",
				id = "title",
				source = "Basic Web Content (Default)");

			PageEditorMapping.mapEditableTextToField(
				field = "Content",
				fragmentName = "Rich Text",
				id = "rich-text",
				source = "Basic Web Content (Default)");

			Button.clickPublish();
		}

		task ("Mark the display page as default") {
			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

			DisplayPageTemplatesAdmin.markDisplayPageAsDefault(displayPageName = "Display Page Name");
		}

		task ("Add a web content article") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoAddCP();

			WebContent.addCP(
				webContentContent = "Webcontent Content",
				webContentTitle = "Webcontent Title");

			PortletEntry.publish();
		}

		task ("Add a user with permission to edit web content articles and display pages") {
			JSONUser.addUser(
				userEmailAddress = "userea1@liferay.com",
				userFirstName = "Userfn1",
				userLastName = "Userln1",
				userScreenName = "usersn1");

			User.openUsersAdmin();

			User.editPasswordCP(
				userEmailAddress = "userea1@liferay.com",
				userScreenName = "usersn1");

			User.openUsersAdmin();

			User.assignRegularRoleCP(
				roleTitle = "Administrator",
				userScreenName = "usersn1");
		}

		task ("Add a second user without any permissions") {
			JSONUser.addUser(
				userEmailAddress = "userea2@liferay.com",
				userFirstName = "Userfn2",
				userLastName = "Userln2",
				userScreenName = "usersn2");

			User.openUsersAdmin();

			User.editPasswordCP(
				userEmailAddress = "userea2@liferay.com",
				userScreenName = "usersn2");
		}

		task ("Sign in as the first user") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "userea1@liferay.com",
				userLoginFullName = "Userfn1 Userln1");
		}

		task ("View the web content article through its friendly URL") {
			Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/w/webcontent-title");
		}

		task ("Click the pencil icon > Edit Display Page Template") {
			DisplayPageTemplate.clickPencilItem(item = "Edit Display Page Template");
		}

		task ("Edit the display page by adding a Static Text fragment to the display page") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Static Text");

			Button.clickPublish();
		}

		task ("View the web content article through its friendly URL") {
			Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/w/webcontent-title");
		}

		task ("Assert the web content article displays with the additional Content fragment in the staging site") {
			AssertVisible(
				key_content = "Webcontent Title",
				key_fragmentClass = "header",
				key_position = "1",
				locator1 = "Fragment#FRAGMENT");

			AssertVisible(
				key_content = "Webcontent Content",
				key_fragmentClass = "rich-text",
				key_position = "2",
				locator1 = "Fragment#FRAGMENT");

			AssertVisible(
				key_content = "This is static text.",
				key_fragmentClass = "static-text",
				key_position = "3",
				locator1 = "Fragment#FRAGMENT");
		}

		task ("View the web content article through its friendly URL") {
			Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/w/webcontent-title");
		}

		task ("Click the pencil icon > Edit Webcontent Title") {
			DisplayPageTemplate.clickPencilItem(item = "Edit Webcontent Title");
		}

		task ("Edit the web content article") {
			Click(locator1 = "TextInput#TITLE");

			PortletEntry.inputTitle(title = "Webcontent Title Edit");

			AlloyEditor.addContent(content = "Webcontent Content Edit");

			Button.clickPublish();
		}

		task ("Assert the new version of the web content article displays through the display page") {
			AssertVisible(
				key_content = "Webcontent Title Edit",
				key_fragmentClass = "header",
				key_position = "1",
				locator1 = "Fragment#FRAGMENT");

			AssertVisible(
				key_content = "Webcontent Content Edit",
				key_fragmentClass = "rich-text",
				key_position = "2",
				locator1 = "Fragment#FRAGMENT");

			AssertVisible(
				key_content = "This is static text.",
				key_fragmentClass = "static-text",
				key_position = "3",
				locator1 = "Fragment#FRAGMENT");
		}

		task ("Sign in as the second user") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "userea2@liferay.com",
				userLoginFullName = "Userfn2 Userln2");
		}

		task ("View the web content article through its friendly URL") {
			Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/w/webcontent-title");
		}

		task ("Assert the user cannot edit the web content article or display page") {
			AssertElementNotPresent(locator1 = "Button#PENCIL");
		}
	}

	@description = "This is a test for LPS-86550. Users should be able to map author name and author profile image to a fragment editable field in a display page."
	@priority = "5"
	test MapWebContentAuthor {
		task ("Add a display page with author name and profile fields mapped") {
			DisplayPageTemplatesAdmin.addDisplayPage(displayPageName = "Display Page Name");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Rich Text");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Image");

			PageEditorMapping.mapEditableTextToField(
				field = "Author Name",
				fragmentName = "Rich Text",
				id = "rich-text",
				source = "Basic Web Content (Default)");

			PageEditorMapping.mapEditableTextToField(
				field = "Author Profile Image",
				fragmentName = "Image",
				id = "image",
				source = "Basic Web Content (Default)");

			Button.clickPublish();
		}

		task ("Add a profile image for the super admin user") {
			MyAccount.openMyAccountAdmin();

			User.changeProfileImage(imageName = "marketplace_icon.png");

			Button.click(button = "Save");
		}

		task ("Add a web content article that uses the display page") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoAddCP();

			WebContent.addCP(
				webContentContent = "Webcontent Content",
				webContentTitle = "Webcontent Title");

			WebContent.editDisplayPage(
				pageName = "Display Page Name",
				pageType = "Display Page Templates");

			PortletEntry.publish();
		}

		task ("Assert the super admin's name and profile image appear in the display page") {
			Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/w/webcontent-title");

			AssertVisible(
				key_content = "Test Test",
				key_fragmentClass = "rich-text",
				key_position = "1",
				locator1 = "Fragment#FRAGMENT");

			AssertVisible(
				key_content = "",
				key_fragmentClass = "image",
				key_position = "2",
				locator1 = "Fragment#FRAGMENT");

			SikuliAssertElementPresent(locator1 = "Fragment#MARKETPLACE_ICON_PNG");
		}
	}

	@description = "This case ensures a user can map the editable background image of a fragment to an existing image content."
	@priority = "5"
	test MapWebContentImageToFragmentBackground {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DynamicDataMapping.addField(
			field = "Image",
			fieldFieldLabel = "Image",
			fieldName = "Image");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoTemplates();

		WebContentTemplates.addCP(
			structureName = "WC Structure Name",
			templateDescription = "WC Template Description",
			templateFieldNames = "Image",
			templateName = "WC Template Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addWithStructureCP(
			structureName = "WC Structure Name",
			templateName = "WC Template Name",
			webContentImage = "Document_2.jpeg",
			webContentTitle = "WC WebContent Title");

		PortletEntry.publish();

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		ContentPages.addPage(pageName = "Test Content Page Name");

		PageEditor.addBasicComponent(fragmentName = "Image");

		PageEditor.editFragmentImage(
			fileName = "Document_1.png",
			fragmentName = "Image",
			id = "image");

		PageEditorMapping.gotoEditableFieldMapping(
			fragmentName = "Image",
			id = "image",
			imageFrament = "Image");

		PageEditorMapping.mapEditableFieldToAsset(
			assetField = "Web Content Article",
			webContentTitle = "WC WebContent Title");

		task ("Assert the image field contains the mapped image.") {
			PageEditor.clickPublish();

			Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/test-content-page-name");

			AssertElementPresent(locator1 = "WCD#WEB_CONTENT_IMAGE_PNG");
		}
	}

	@description = "This case ensures a user can map the layout of a fragment to an existing image content."
	@priority = "5"
	test MapWebContentImageToLayoutSection {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DynamicDataMapping.addField(
			field = "Image",
			fieldFieldLabel = "Image",
			fieldName = "Image");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoTemplates();

		WebContentTemplates.addCP(
			structureName = "WC Structure Name",
			templateDescription = "WC Template Description",
			templateFieldNames = "Image",
			templateName = "WC Template Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addWithStructureCP(
			structureName = "WC Structure Name",
			templateName = "WC Template Name",
			webContentImage = "Document_2.jpeg",
			webContentTitle = "WC WebContent Title");

		PortletEntry.publish();

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		ContentPages.addPage(pageName = "Test Content Page Name");

		PageEditor.addLayoutSection(columnCount = "2");

		PageEditor.editLayoutSectionImage(
			assetField = "Web Content Article",
			webContentTitle = "WC WebContent Title");

		task ("Assert the layout background field contains the mapped image.") {
			PageEditor.clickPublish();

			Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/test-content-page-name");

			AssertElementPresent(
				key_imageName = "Document_2.jpeg",
				locator1 = "Fragment#FRAGMENT_LAYOUT_BACKGROUND_IMAGE");
		}
	}

	@description = "This is a test for LPS-86550. Users should be able to map last editor name and author profile image to a fragment editable field in a display page."
	@priority = "5"
	test MapWebContentLastEditor {
		task ("Add a display page with last editor name and profile fields mapped") {
			DisplayPageTemplatesAdmin.addDisplayPage(displayPageName = "Display Page Name");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Rich Text");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Image");

			PageEditorMapping.mapEditableTextToField(
				field = "Last Editor Name",
				fragmentName = "Rich Text",
				id = "rich-text",
				source = "Basic Web Content (Default)");

			PageEditorMapping.mapEditableTextToField(
				field = "Last Editor Profile Image",
				fragmentName = "Image",
				id = "image",
				source = "Basic Web Content (Default)");

			Button.clickPublish();
		}

		task ("Add a profile image for the super admin user") {
			MyAccount.openMyAccountAdmin();

			User.changeProfileImage(imageName = "marketplace_icon.png");

			Button.click(button = "Save");
		}

		task ("Add a second user with permission to edit web content articles") {
			JSONUser.addUser(
				userEmailAddress = "userea@liferay.com",
				userFirstName = "Userfn",
				userLastName = "Userln",
				userScreenName = "usersn");

			User.openUsersAdmin();

			User.editPasswordCP(
				userEmailAddress = "userea@liferay.com",
				userScreenName = "usersn");

			User.openUsersAdmin();

			User.assignRegularRoleCP(
				roleTitle = "Administrator",
				userScreenName = "usersn");
		}

		task ("Add a profile image for the second user") {
			User.openUsersAdmin();

			UserNavigator.gotoUser(userScreenName = "usersn");

			User.changeProfileImage(imageName = "Document_4.jpg");

			Button.click(button = "Save");
		}

		task ("As the super admin user, add a web content article that uses the display page") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoAddCP();

			WebContent.addCP(
				webContentContent = "Webcontent Content",
				webContentTitle = "Webcontent Title");

			WebContent.editDisplayPage(
				pageName = "Display Page Name",
				pageType = "Display Page Templates");

			PortletEntry.publish();
		}

		task ("Assert the super admin name and profile image appear in the display page") {
			Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/w/webcontent-title");

			AssertVisible(
				key_content = "Test Test",
				key_fragmentClass = "rich-text",
				key_position = "1",
				locator1 = "Fragment#FRAGMENT");

			AssertVisible(
				key_content = "",
				key_fragmentClass = "image",
				key_position = "2",
				locator1 = "Fragment#FRAGMENT");
		}

		task ("Sign in as the second user") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "userea@liferay.com",
				userLoginFullName = "Userfn Userln");
		}

		task ("As the second user, edit the web content article") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoEditCP(webContentTitle = "Webcontent Title");

			WebContent.editCP(
				webContentContentEdit = "Webcontent Content Edit",
				webContentTitleEdit = "Webcontent Title Edit");
		}

		task ("Assert the second user's name and profile image appear in the display page") {
			Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/w/webcontent-title");

			AssertVisible(
				key_content = "Userfn Userln",
				key_fragmentClass = "rich-text",
				key_position = "1",
				locator1 = "Fragment#FRAGMENT");

			AssertVisible(
				key_content = "",
				key_fragmentClass = "image",
				key_position = "2",
				locator1 = "Fragment#FRAGMENT");

			SikuliAssertElementPresent(locator1 = "Fragment#DOCUMENT_4_PNG");
		}
	}

	@description = "Add a fragment with multiple editable fields in it."
	@priority = "4"
	test ViewDisplayPageWithComplexRichTextFragments {
		property testray.component.names = "Training";

		task ("Add a display page") {
			DisplayPageTemplatesAdmin.addDisplayPage(displayPageName = "Display Page Name");
		}

		task ("Add a fragment with multiple editable fields in it and map it to Basic Web Content") {
			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Multiple Editables");

			PageEditorMapping.mapEditableTextToField(
				field = "Title",
				fragmentName = "Multiple Editables",
				id = "test1",
				source = "Basic Web Content (Default)");

			PageEditorMapping.mapEditableTextToField(
				field = "Content",
				fragmentName = "Multiple Editables",
				id = "test2",
				source = "Basic Web Content (Default)");

			PageEditorMapping.mapEditableTextToField(
				field = "Tags",
				fragmentName = "Multiple Editables",
				id = "test3",
				source = "Basic Web Content (Default)");

			PageEditorMapping.mapEditableTextToField(
				field = "Publish Date",
				fragmentName = "Multiple Editables",
				id = "test4",
				source = "Basic Web Content (Default)");
		}

		task ("Publish the display page") {
			Button.clickPublish();
		}

		task ("Add a web content article with tags that uses the display page") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContent.addArticleCP(
				webContentContent = "Webcontent Content",
				webContentTitle = "Webcontent Title");

			WebContentNavigator.gotoEditCP(webContentTitle = "Webcontent Title");

			WebContent.addWithTagCP(tagNameList = "tag1,tag2,tag3");

			WebContentNavigator.gotoEditCP(webContentTitle = "Webcontent Title");

			WebContent.editDisplayPage(
				pageName = "Display Page Name",
				pageType = "Display Page Templates");

			PortletEntry.publish();
		}

		task ("Add a page to the site") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			SitePages.addPublicPage(pageName = "Site Page Name");
		}

		task ("Add an Asset Publisher widget to the page") {
			Navigator.gotoSitePage(
				pageName = "Site Page Name",
				siteName = "Test Site Name");

			Portlet.addPG(portletName = "Asset Publisher");
		}

		task ("Assert the display page displays correctly along with its tags") {
			AssertTextEquals(
				key_assetTitle = "Webcontent Title",
				locator1 = "AP#ASSET_ABSTRACT_TITLE",
				value1 = "Webcontent Title");

			AssertTextEquals.assertPartialText(
				key_assetTitle = "Webcontent Title",
				locator1 = "AP#ASSET_ABSTRACT_DESCRIPTION",
				value1 = "Webcontent Content");

			AssetPublisherPortlet.clickAssetTitle(assetTitle = "Webcontent Title");

			for (var content : list "Webcontent Title,Webcontent Content,tag1,tag2,tag3") {
				AssertVisible(
					key_text = "${content}",
					locator1 = "Fragment#EDITABLE_FIELD_CONTENT");
			}
		}
	}

	@description = "Add a fragment with new added structure in it."
	@priority = "4"
	test ViewDisplayPageWithNewStructureMapInFragments {
		property testray.component.names = "Training";

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Web Content");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DynamicDataMapping.addField(
			field = "Text Box",
			fieldFieldLabel = "Content",
			fieldName = "Content");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Web Content");

		NavItem.gotoTemplates();

		WebContentTemplates.addCP(
			structureName = "WC Structure Name",
			templateDescription = "WC Template Description",
			templateFieldNames = "Content",
			templateName = "WC Template Name");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		DisplayPageTemplatesAdmin.addDisplayPage(
			displayPageName = "Display Page Name",
			subType = "WC Structure Name");

		PageEditor.addFragment(
			collectionName = "Collection Name",
			fragmentName = "Multiple Editables");

		PageEditorMapping.mapEditableTextToField(
			field = "Title",
			fragmentName = "Multiple Editables",
			id = "test1",
			source = "WC Structure Name (Default)");

		PageEditorMapping.mapEditableTextToField(
			field = "Content",
			fragmentName = "Multiple Editables",
			id = "test2",
			source = "WC Structure Name (Default)");

		PageEditorMapping.mapEditableTextToField(
			field = "Tags",
			fragmentName = "Multiple Editables",
			id = "test3",
			source = "WC Structure Name (Default)");

		PageEditorMapping.mapEditableTextToField(
			field = "Publish Date",
			fragmentName = "Multiple Editables",
			id = "test4",
			source = "WC Structure Name (Default)");

		Button.clickPublish();

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addWithStructureCP(
			fieldName = "Content",
			webContentTextBox = "Webcontent Content",
			webContentTitle = "Webcontent Title");

		WebContent.addWithTagCP(tagNameList = "tag1,tag2,tag3");

		WebContentNavigator.gotoEditCP(webContentTitle = "Webcontent Title");

		WebContent.editDisplayPage(
			pageName = "Display Page Name",
			pageType = "Display Page Templates");

		PortletEntry.publish();

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.addPublicPage(pageName = "Site Page Name");

		Navigator.gotoSitePage(
			pageName = "Site Page Name",
			siteName = "Test Site Name");

		Portlet.addPG(portletName = "Asset Publisher");

		AssetPublisherPortlet.configureAssetLinkBehaviorPG(assetLinkBehavior = "View in Context");

		DisplayPageTemplatesAdmin.viewPGViaAP(
			contentList = "Webcontent Title,Webcontent Content,tag1,tag2,tag3",
			webContentContent = "Webcontent Content",
			webContentTitle = "Webcontent Title");
	}

	@description = "This is a test for LPS-96187. When there is a Language Selector widget on a display page template, selecting a different language should not redirect the user to a different place."
	@priority = "4"
	test ViewlanguageSelectorRedirect {
		property test.name.skip.portal.instance = "DisplayPagesUseCase#ViewlanguageSelectorRedirect";

		task ("Add a display page with a Language Selector widget and with a fragment mapped to Basic Web Content") {
			DisplayPageTemplatesAdmin.addDisplayPage(displayPageName = "Display Page Name");

			PageEditor.addWidget(portletName = "Language Selector");

			PageEditor.gotoTab(tabName = "Sections");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Title");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Rich Text");

			PageEditorMapping.mapEditableTextToField(
				field = "Title",
				fragmentName = "Title",
				id = "title",
				source = "Basic Web Content (Default)");

			PageEditorMapping.mapEditableTextToField(
				field = "Content",
				fragmentName = "Rich Text",
				id = "rich-text",
				source = "Basic Web Content (Default)");

			Button.clickPublish();
		}

		task ("Mark the display page as default") {
			DisplayPageTemplatesAdmin.markDisplayPageAsDefault(displayPageName = "Display Page Name");
		}

		task ("Add a web content article") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoAddCP();

			WebContent.addCP(
				webContentContent = "Webcontent Content",
				webContentTitle = "Webcontent Title");

			PortletEntry.publish();
		}

		task ("View the web content article through its friendly URL") {
			Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/w/webcontent-title");
		}

		task ("Use the Language Selector to change the language to Spanish") {
			LanguagePortlet.selectLanguagePG(
				locale = "es_ES",
				textIcon = "español-España");
		}

		task ("Assert the page did not redirect to a different page but is still viewing the web content through the display page, only in Spanish") {
			AssertTextEquals.assertPartialText(
				key_portletName = "LanguagePortlet",
				locator1 = "Language#DROPDOWN_LANGUAGE",
				value1 = "es-ES");

			AssertVisible(
				key_content = "Webcontent Title",
				key_fragmentClass = "header",
				key_position = "1",
				locator1 = "Fragment#FRAGMENT");

			AssertVisible(
				key_content = "Webcontent Content",
				key_fragmentClass = "rich-text",
				key_position = "2",
				locator1 = "Fragment#FRAGMENT");
		}

		task ("Change the language back to English") {
			LanguagePortlet.selectLanguagePG(
				locale = "en_US",
				textIcon = "english-United States");
		}

		task ("Assert the page did not redirect to a different page but is still viewing the web content through the display page, only in English") {
			AssertTextEquals.assertPartialText(
				key_portletName = "LanguagePortlet",
				locator1 = "Language#DROPDOWN_LANGUAGE",
				value1 = "en-US");

			AssertVisible(
				key_content = "Webcontent Title",
				key_fragmentClass = "header",
				key_position = "1",
				locator1 = "Fragment#FRAGMENT");

			AssertVisible(
				key_content = "Webcontent Content",
				key_fragmentClass = "rich-text",
				key_position = "2",
				locator1 = "Fragment#FRAGMENT");
		}
	}

}