@component-name = "portal-web-forms-and-data-lists"
definition {
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Dynamic Data Lists";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Site Administration",
			portlet = "Site Settings"
		);

		Site.configureCurrentLanguagesCP(
			currentSiteLanguages = "English (United States),Spanish (Spain),Portuguese (Brazil)",
			defaultSiteLanguage = "Portuguese (Brazil)",
			displaySettings = "Display Settings"
		);

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Dynamic Data Lists"
		);

		DDLDataDefinition.addCP();

		var sourceJSON = TestCase.getFileContent(fileName = "complexDDMStructure.json");

		DynamicDataMapping.addFieldViaJSON(sourceJSON = "${sourceJSON}");

		DDLDataDefinition.setInputFieldLocalization(languageValue = "pt_BR");

		DDLDataDefinition.editName();

		DDLDataDefinition.save();

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Dynamic Data Lists"
		);

		DDLList.addCP();

		DDLDataDefinition.selectCP();

		DDLDataDefinition.save();

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content",
			panel = "Site Administration",
			portlet = "Dynamic Data Lists"
		);
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.firstLoginPG();

			DDLList.tearDownCP();

			DDLDataDefinition.tearDownCP();
		}
	}

	@description = "This is a use case for LPS-69325."
	@priority = "4"
	test ViewFieldInputWhenSwitchingTranslations {
		DDLRecord.addCP();

		DDLRecord.populateFieldText(
			ddlRecordFieldData = "pt-BR Categoria",
			fieldName = "Categoria"
		);

		DDLRecord.populateFieldText(
			ddlRecordFieldData = "pt-BR Pergunta",
			fieldName = "Pergunta"
		);

		for (var language : list "es-ES,en-US") {
			PortletEntry.changeLocale(locale = "${language}");

			DDLRecord.populateFieldText(
				ddlRecordFieldData = "${language} Categoria",
				fieldName = "Categoria"
			);

			DDLRecord.populateFieldText(
				ddlRecordFieldData = "${language} Pergunta",
				fieldName = "Pergunta"
			);
		}

		for (var language : list "pt-BR,en-US,es-ES,") {
			PortletEntry.changeLocale(locale = "${language}");

			FormFields.viewTextValue(
				fieldName = "Categoria",
				fieldValue = "${language} Categoria"
			);

			FormFields.viewTextValue(
				fieldName = "Pergunta",
				fieldValue = "${language} Pergunta"
			);
		}
	}
}