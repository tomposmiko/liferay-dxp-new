@component-name = "portal-collaboration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property custom.properties = "default.layout.template.id=1_column";
	property testray.main.component.name = "Notifications";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Site.tearDownCP();

			BlogsEntry.tearDownCP();

			Page.tearDownCP();

			User.tearDownCP();
		}
	}

	@priority = "4"
	test MarkNotificationAsReadAndAcceptRequest {
		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			password = "password",
			userEmailAddress = "userea@liferay.com",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.assignRegularRoleCP(
			roleTitle = "Administrator",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.addPublicPage(
			pageName = "Blogs Page",
			sitePageType = "Widget Page");

		Navigator.gotoPage(pageName = "Blogs Page");

		Portlet.addPG(portletName = "Blogs");

		Navigator.gotoPage(pageName = "Blogs Page");

		ProductMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addCP(
			siteName = "Community Site Name",
			siteType = "Community");

		Navigator.gotoSitePage(
			pageName = "Home",
			siteName = "Community Site Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		SitePages.addPublicPage(
			pageName = "Blank Page",
			sitePageType = "Widget Page");

		SitePages.addPublicPage(
			pageName = "Invite Members Page",
			sitePageType = "Widget Page");

		Navigator.gotoSitePage(
			pageName = "Invite Members Page",
			siteName = "Community Site Name");

		Portlet.addPG(portletName = "Invite Members");

		Navigator.gotoSitePage(
			pageName = "Invite Members Page",
			siteName = "Community Site Name");

		InviteMembers.inviteMembers(inviteUserFullName = "userfn userln");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.gotoPage(pageName = "Blogs Page");

		Blogs.subscribePG();

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		Navigator.gotoPage(pageName = "Blogs Page");

		Blogs.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Notifications.viewBadgeCount(notificationCount = "2");

		Notifications.gotoNotifications();

		Notifications.viewNewContent(
			assetType = "Blogs Entry",
			contentBody = "Blogs Entry Title",
			user = "Test Test");

		Notifications.viewRequest(requestsTitle = "Test Test invited you to join Community Site Name.");

		Notifications.acceptRequest(requestsTitle = "Test Test invited you to join Community Site Name.");

		Refresh();

		Notifications.viewNoRequest();

		Notifications.viewNoNotifications();

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "test@liferay.com");
	}

	@description = "This test covers LPS-102749. It views a notification of the edited Web Content Article when staging is turned on."
	@priority = "4"
	test StagingViewEditedArticleNotification {
		JSONGroup.addGroup(groupName = "Subscribe Site");

		JSONLayout.addPublicLayout(
			groupName = "Subscribe Site",
			layoutName = "Staging Test Page");

		SitePages.openPagesAdmin(siteURLKey = "Subscribe Site");

		ProductMenu.gotoPortlet(
			category = "Publishing",
			panel = "Site Administration",
			portlet = "Staging");

		Staging.activateStagingCP(siteName = "Subscribe Site");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			password = "password",
			userEmailAddress = "userea@liferay.com",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.assignRegularRoleCP(
			roleTitle = "Administrator",
			userScreenName = "usersn");

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea@liferay.com",
			userLoginFullName = "userfn userln");

		Navigator.openSiteURL(siteName = "Subscribe Site");

		ProductMenu.gotoLiveSite();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Web Content");

		WebContent.subscribeToCurrentFolderCP();

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		Navigator.openSiteURL(siteName = "Subscribe Site");

		Navigator.gotoStagedView();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Web Content");

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title");

		PortletEntry.publish();

		Staging.publishToLiveNowViaPortletCP();

		Navigator.openSiteURL(siteName = "Subscribe Site");

		Navigator.gotoStagedView();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Web Content");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC WebContent Title");

		WebContent.editCP(
			webContentTitle = "WC WebContent Title",
			webContentTitleEdit = "WC WebContent Title Edit");

		Staging.publishToLiveNowViaPortletCP();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea@liferay.com",
			userLoginFullName = "userfn userln");

		Notifications.viewBadgeCount(notificationCount = "2");

		Notifications.gotoNotifications();

		Notifications.viewUpdatedContent(
			assetType = "web content article",
			contentBody = "WC WebContent Title Edit",
			user = "Test Test");

		Notifications.navigateToContent(contentBody = "WC WebContent Title Edit");

		WebContent.viewTitle(
			onLive = "true",
			webContentTitle = "WC WebContent Title Edit");

		AssertConsoleTextNotPresent(value1 = "java.lang.IllegalArgumentException");
	}

	@priority = "3"
	test ViewDefaultNotificationsPortletDeliveryOptions {
		property osgi.app.includes = "bookmarks";

		Navigator.openURL();

		NotificationsPortlet.gotoNotificationDelivery(notificationPG = "true");

		IFrame.selectConfigurationFrame();

		NotificationsPortlet.viewDefaultNotificationDeliveries();
	}

	@priority = "5"
	test ViewNoNotification {
		Notifications.viewNoNotifications(activePanel = "Profile");

		Notifications.viewNoRequest();

		Notifications.viewNoNotificationEntry();
	}

}