@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Fragments";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Add a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-118100. 	Validate if the user can configure other languages to the contributed fragments."
	@priority = "4"
	test ViewContributedFragmentLocalization {
		task ("Add a Heading fragment to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");
		}

		task ("Change to instance language to es-ES") {
			var baseURL = PropsUtil.get("portal.url");
			var currentURL = Navigator.getCurrentURL();

			var localizedURL = StringUtil.replace("${currentURL}", "${baseURL}", "${baseURL}/es");

			Navigator.openSpecificURL(url = "${localizedURL}");
		}

		task ("Assert the fragment name is localized") {
			PageEditor.clickConfigurableField(fragmentName = "Título");

			PageEditor.viewFragmentHeaderVisible(fragmentName = "Título");
		}

		task ("Assert the field labels are localized") {
			Click(
				key_navItem = "General",
				locator1 = "PageEditor#NAV_ITEM_BUTTON");

			AssertVisible(
				key_fieldSetLabel = "Marco",
				locator1 = "PageEditor#ITEM_CONFIGURATION_FIELDSET_LABEL");

			AssertSelectedLabel(
				key_fieldLabel = "Desbordamiento",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Valor por defecto");

			AssertSelectedLabel(
				key_fieldLabel = "Nivel de título",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "H1");

			Click(
				key_navItem = "Estilos",
				locator1 = "PageEditor#NAV_ITEM_BUTTON");

			for (var fieldSetLabel : list "Espaciado,Texto,Fondo,Bordes,Efectos") {
				AssertVisible(
					key_fieldSetLabel = "${fieldSetLabel}",
					locator1 = "PageEditor#ITEM_CONFIGURATION_FIELDSET_LABEL");
			}

			for (var fieldLabel : list "Margen,Relleno") {
				AssertTextEquals(
					ariaLabel = "${fieldLabel} superior",
					locator1 = "Button#ANY_WITH_ARIA_LABEL",
					value1 = "0");
			}

			AssertTextEquals(
				key_text = "bold",
				locator1 = "Select#GENERIC_SELECT_FIELD_WITH_ICON_TEXT",
				value1 = "Heredado · 400");

			AssertSelectedLabel(
				key_fieldLabel = "Origen de la imagen",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Directo");

			AssertTextEquals(
				key_text = "corner-radius",
				locator1 = "Select#GENERIC_SELECT_FIELD_WITH_ICON_TEXT",
				value1 = "Valor por defecto · 0px");

			AssertTextEquals(
				key_text = "shadow",
				locator1 = "Select#GENERIC_SELECT_FIELD_WITH_ICON_TEXT",
				value1 = "Valor por defecto · none");
		}
	}

	@description = "This is a test for LPS-118100. View localizable fragment configuration field value in translated languages."
	@priority = "4"
	test ViewLocalizableConfigurationFieldValueInTranslatedLanguages {
		task ("Add a fragment collection via JSON") {
			JSONFragment.addFragmentCollection(
				groupName = "Test Site Name",
				name = "Collection Name");
		}

		task ("Navigate to fragments admin") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");
		}

		task ("Navigate to the fragment collection") {
			FragmentsAdminNavigator.gotoCollection(collectionName = "Collection Name");
		}

		task ("Add a fragment") {
			FragmentsAdmin.addFragment(fragmentName = "Fragment Name");
		}

		task ("Type html codes in HTML section") {
			var htmlSnippet = '''
<div class="fragment-example">
	<button type="button" class="btn btn-primary">${configuration.buttonText}</button>
</div>
			''';

			FragmentsEditor.typeHtmlEditor(content = "${htmlSnippet}");

			FragmentsEditor.waitForAutoSave();
		}

		task ("Type css codes in CSS section") {
			var cssSnippet = '''
.fragment-example {
}
		''';

			FragmentsEditor.typeCssEditor(content = "${cssSnippet}");

			FragmentsEditor.waitForAutoSave();
		}

		task ("Type json codes in Configuration section") {
			FragmentsEditor.gotoConfigurationTab();

			FragmentsEditor.waitForAutoSave();

			var jsonSnippet = '''
{
	"fieldSets": [
		{
			"label": "Button",
			"fields": [
				{
					"dataType": "string",
					"defaultValue": "Go Somewhere",
					"label": "Button Text",
					"localizable": true,
					"name": "buttonText",
					"type": "text"
				}
			]
		}
	]
}
			''';

			FragmentsEditor.typeConfigurationEditor(content = "${jsonSnippet}");

			FragmentsEditor.waitForAutoSave();

			PortletEntry.publish();
		}

		task ("Add the new fragment to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Fragment Name");
		}

		task ("Change to es_ES via language selector") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "es-ES");
		}

		task ("Type localized content in Button Text field") {
			PageEditorConfiguration.changeConfigText(
				editableText = "Ir a alguna parte",
				fragmentName = "Fragment Name",
				textConfigName = "Button Text");
		}

		task ("Assert localized content is shown in Button Text field") {
			InputField.viewText(
				fieldLabel = "Button Text",
				text = "Ir a alguna parte");
		}

		task ("Assert localized content is shown in fragment") {
			ContentPages.viewFragmentText(
				contributed = "false",
				fragmentName = "example",
				text = "Ir a alguna parte");
		}

		task ("Change to en_US via language selector") {
			PageEditor.changeLocale(
				currentLocale = "es-ES",
				locale = "en-US");
		}

		task ("Assert Go Somewhere is shown in Button Text field") {
			InputField.viewText(
				fieldLabel = "Button Text",
				text = "Go Somewhere");
		}

		task ("Assert Go Somewhere is shown in fragment") {
			ContentPages.viewFragmentText(
				contributed = "false",
				fragmentName = "example",
				text = "Go Somewhere");
		}

		task ("Change to es_ES via language selector") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "es-ES");
		}

		task ("Assert Ir a alguna parte is shown in Button Text field") {
			InputField.viewText(
				fieldLabel = "Button Text",
				text = "Ir a alguna parte");
		}

		task ("Assert Ir a alguna parte is shown in fragment") {
			ContentPages.viewFragmentText(
				contributed = "false",
				fragmentName = "example",
				text = "Ir a alguna parte");
		}
	}

}