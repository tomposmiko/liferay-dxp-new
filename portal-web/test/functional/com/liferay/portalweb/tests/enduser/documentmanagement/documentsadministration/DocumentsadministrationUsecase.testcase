@component-name = "portal-document-management"
definition {

	property custom.properties = "default.layout.template.id=1_column";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Document Management";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.firstLoginPG();

			DMDocument.tearDownCP();

			DMFolder.tearDownCP();

			DMDocumentType.tearDownCP();

			DMMetadataSet.tearDownCP();

			Page.tearDownCP();

			JSONUser.tearDownNonAdminUsers();

			Site.tearDownCP();
		}
	}

	@priority = "4"
	test AddAndRemoveMetadataSets {
		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoMetadataSets();

		DMMetadataSet.add(
			dmMetadataSetFieldNames = "Text Box",
			metadataSetName = "DM Metadata Set Name");

		DDLDataDefinition.save();

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoDocumentType();

		DMDocumentType.add(
			dmDocumentTypeDescription = "DM Document Type Description",
			dmDocumentTypeFieldNames = "Text Box",
			dmDocumentTypeName = "DM Document Type Name",
			metadataSetName = "DM Metadata Set Name");

		DMNavigator.gotoMetadataSets();

		DMMetadataSet.delete(
			metadataSetHasReference = "true",
			metadataSetName = "DM Metadata Set Name");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoDocumentType();

		DMDocumentType.edit(
			dmDocumentTypeName = "DM Document Type Name",
			metadataSetName = "DM Metadata Set Name",
			removeMetadataSet = "true");

		DMNavigator.gotoMetadataSets();

		DMMetadataSet.delete(metadataSetName = "DM Metadata Set Name");
	}

	@description = "This test covers LPS-94657. It ensures that auto complete for edit categories."
	@priority = "5"
	test AddAutoAssertionCategoryViaDMDocument {
		property test.name.skip.portal.instance = "DocumentsadministrationUsecase#AddAutoAssertionCategoryViaDMDocument";

		JSONCategory.addVocabulary(
			groupName = "Guest",
			title = "Topic");

		JSONCategory.addCategory(
			groupName = "Guest",
			title = "category",
			vocabularyName = "Topic");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			groupName = "Guest",
			mimeType = "text/plain",
			sourceFileName = "Document_1.txt");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_dmDocumentTitle = "DM Document Title";

		Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");

		Click(locator1 = "Icon#EDIT_CATEGORIES");

		SelectFrame(value1 = "relative=top");

		AssertTextEquals(
			locator1 = "Modal#HEADER",
			value1 = "Edit Categories");

		AssertTextEquals.assertPartialText(
			locator1 = "Modal#BODY",
			value1 = "You are editing the categories for the selected item.");

		Type.sendKeys(
			locator1 = "AssetCategorization#TAGS_FIELD",
			value1 = "cat");

		AssertClick(
			locator1 = "AssetCategorization#TAG_AUTOCOMPLETE_CONTAINER",
			value1 = "category");

		Button.clickSave();

		SelectFrame(value1 = "relative=top");

		Alert.viewSuccessMessageText(successMessage = "Changes saved.");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "DM Document Title");

		DMDocument.viewCategories(categoryNameList = "category");
	}

	@priority = "5"
	test AddDocumentTypeAndMetadata {
		property portal.acceptance = "true";
		property testray.component.names = "Training";

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoMetadataSets();

		DMMetadataSet.add(
			dmMetadataSetFieldNames = "Boolean",
			metadataSetName = "Document Metadata Set Name");

		DDLDataDefinition.save();

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoDocumentType();

		DMDocumentType.add(
			dmDocumentTypeDescription = "DM Document Type Description",
			dmDocumentTypeFieldNames = "Date",
			dmDocumentTypeName = "DM Document Type Name",
			metadataSetName = "Document Metadata Set Name");

		DMDocumentType.edit(
			dmDocumentTypeFieldRequiredEdit = "Date",
			dmDocumentTypeName = "DM Document Type Name");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoAddDocumentTypeCP(dmDocumentTypeName = "DM Document Type Name");

		AssertElementPresent(
			key_fieldFieldLabel = "Date",
			locator1 = "Icon#ASTERISK_DATE_FIELD");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMDocument.addCP(
			dateValue = "01/01/2022",
			dmDocumentDescription = "DM Document Type Contract Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Type Contract Title",
			dmDocumentTypeName = "DM Document Type Name");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMNavigator.gotoDocumentCP(dmDocumentTitle = "DM Document Type Contract Title");

		DMDocument.viewCP(dmDocumentTitle = "DM Document Type Contract Title");
	}

	@priority = "4"
	test AllowMultiplePortletConfigurationsPerSite {
		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Site Page 1");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Site Page 2");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Site Name",
			layoutName = "Site Page 1",
			widgetName = "Documents and Media");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Site Name",
			layoutName = "Site Page 2",
			widgetName = "Documents and Media");

		Navigator.gotoSitePage(
			pageName = "Site Page 1",
			siteName = "Site Name");

		DMPortlet.configureDisplaySettingsPG(
			enableDisplaySettingsRelatedAssets = "false",
			enableDisplaySettingsShowSearch = "false",
			entriesToDisplayCount = "60");

		Navigator.gotoSitePage(
			pageName = "Site Page 2",
			siteName = "Site Name");

		DMPortlet.viewDefaultConfigurationDisplaySettingsPG();
	}

	@description = "This is a use case for LPS-50760."
	@priority = "4"
	test AssertFileSizeValidation {
		property test.name.skip.portal.instance = "DocumentsadministrationUsecase#AssertFileSizeValidation";

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			groupName = "Guest",
			mimeType = "audio/x-mpeg",
			sourceFileName = "Document_1.mp3");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoDocumentCP(dmDocumentTitle = "DM Document Title");

		DMDocument.viewCP(
			dmDocumentSize = "4.3 MB",
			dmDocumentTitle = "DM Document Title");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		LAR.exportPortlet(larFileName = "DM.portlet.lar");

		SystemSettings.openSystemSettingsAdmin();

		SystemSettings.gotoConfiguration(
			configurationCategory = "Documents and Media",
			configurationName = "Service",
			configurationScope = "System Scope");

		Type(
			key_fieldLabel = "Maximum File Size",
			locator1 = "TextInput#GENERIC_TEXT_INPUT",
			value1 = "300000");

		ScrollWebElementIntoView(locator1 = "Button#SAVE");

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		VerifyElementPresent(locator1 = "Message#SUCCESS");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoAddDocumentTypeCP(dmDocumentTypeName = "${dmDocumentTypeName}");

		AssertTextEquals(
			locator1 = "Message#INFO",
			value1 = "Upload documents no larger than 293 KB.");

		DMDocument.editCmd(
			dmDocumentFileEdit = "Document_1.mp4",
			dmDocumentTitle = "Document_1.mp4");

		Button.clickPublish();

		AssertTextEquals(
			locator1 = "Message#MAX_FILESIZE_ALERT",
			value1 = "Please enter a file with a valid file size no larger than 293 KB.");

		Button.clickCancel();

		LAR._gotoExportImport();

		AssertElementPresent(locator1 = "IFrame#MODAL_BODY");

		LAR._export(
			exportPermissions = "${exportPermissions}",
			larFileName = "DM.portlet.lar");

		SelectFrame(value1 = "relative=top");
	}

	@priority = "5"
	test CannotViewFolderAsSiteMember {
		property custom.properties = "jsonws.web.service.paths.excludes=";
		property testray.component.names = "Training";

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Site Name",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		Site.assignUserSitesCP(
			siteName = "Site Name",
			userScreenName = "usersn");

		Navigator.gotoSitePage(
			pageName = "Documents and Media Page",
			siteName = "Site Name");

		Portlet.addPG(portletName = "Documents and Media");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoSitePage(
			pageName = "Documents and Media Page",
			siteName = "Site Name");

		DMFolder.addPG(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media");

		DMNavigator.gotoDocumentPermissionsCP(dmDocumentTitle = "DM Folder Name");

		PermissionsInline.addOrRemoveAccessPermissionFromAllRoles(
			addOrRemoveViewPermission = "Add",
			roleList = "guest");

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea@liferay.com",
			userLoginFullName = "userfn userln");

		Navigator.gotoSitePage(
			pageName = "Documents and Media Page",
			siteName = "Site Name");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMFolder.addPG(
			dmFolderDescription = "DM Subfolder1 Description",
			dmFolderName = "Subfolder1 Name");

		DMNavigator.gotoFolder(dmFolderName = "Subfolder1 Name");

		DMFolder.viewPG(dmFolderName = "Subfolder1 Name");

		DMDocument.addPG(
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "Document_1.doc");

		DMFolder.subscribePG();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "test@liferay.com",
			userLoginFullName = "Test Test");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "site-name");

		DMNavigator.gotoFolderPermissions(dmFolderName = "DM Folder Name");

		for (var permissionsInLineUncheckListsCount : list "INLINE_PERMISSIONS_ADD_SUBFOLDER_CHECKBOX,INLINE_PERMISSIONS_ADD_SHORTCUT_CHECKBOX,INLINE_PERMISSIONS_ADD_DOCUMENT_CHECKBOX,INLINE_PERMISSIONS_SUBSCRIBE_CHECKBOX,INLINE_PERMISSIONS_VIEW_CHECKBOX") {
			Role.removePermissionPG(
				permissionDefinitionKey = "${permissionsInLineUncheckListsCount}",
				roleTitle = "Site Member");
		}

		Role.removePermissionPG(
			permissionDefinitionKey = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
			roleTitle = "Guest");

		IFrame.selectTopFrame();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea@liferay.com",
			userLoginFullName = "userfn userln");

		Navigator.gotoSitePage(
			pageName = "Documents and Media Page",
			siteName = "Site Name");

		DMPortlet.viewDefaultPG();
	}

	@priority = "5"
	test CannotViewMultipleDocumentsAsSiteMember {
		property custom.properties = "jsonws.web.service.paths.excludes=";
		property testray.component.names = "Training";

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "2",
			groupName = "Site Name",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		Navigator.gotoSitePage(
			pageName = "Documents and Media Page",
			siteName = "Site Name");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoSitePage(
			pageName = "Documents and Media Page",
			siteName = "Site Name");

		DMDocument.addMultiplePG(
			dmDocumentFileList = "Document_1.jpeg,Document_2.docx,Document_3.mp3",
			viewableBy = "Owner");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.gotoSitePage(
			pageName = "Documents and Media Page",
			siteName = "Site Name");

		for (var dmDocumentTitle : list "Document_1.jpeg,Document_2.docx,Document_3.mp3") {
			DMDocument.cannotViewCP(dmDocumentTitle = "${dmDocumentTitle}");
		}
	}

	@description = "This test covers LPS-109066. It ensures that the localizable field available languages can be displayed correctly."
	@priority = "3"
	test CanViewLocalizableFieldCorrectAvailableLanguages {
		CustomFields.addCP(
			customFieldName = "Document Text Field",
			customFieldType = "Input Field",
			customInputDataType = "Text",
			localization = "true",
			modelResource = "com.liferay.document.library.kernel.model.DLFileEntry");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoDocumentType();

		DMDocumentType.add(
			dmDocumentTypeDescription = "DM Document Type Description",
			dmDocumentTypeFieldNames = "Text",
			dmDocumentTypeName = "DM Document Type Name");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoAddDocumentTypeCP(dmDocumentTypeName = "DM Document Type Name");

		DMDocument.viewTypeFieldLocalizableLanguages(localizableLanguages = "ar-SA,ca-ES,zh-CN,nl-NL,fi-FI,fr-FR,de-DE,hu-HU,ja-JP,pt-BR,es-ES,sv-SE");

		DMDocument.viewCustomFieldLocalizableLanguages(
			fieldName = "Text",
			localizableLanguages = "ar-SA,ca-ES,zh-CN,nl-NL,fi-FI,fr-FR,de-DE,hu-HU,ja-JP,pt-BR,es-ES,sv-SE");
	}

	@description = "This test ensures that a checked out document cannot be edited by another user."
	@priority = "5"
	test CheckOutDocument {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Roles");

		JSONRole.assignRoleToUser(
			roleTitle = "Administrator",
			userEmailAddress = "userea@liferay.com");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "People",
			portlet = "Memberships");

		Site.assignUserAsMemberCP(
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.enableActionsMenuOnPortlet();

		DMDocument.checkoutPG(dmDocumentTitle = "DM Document Title");

		User.logoutUserPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.editCmdPG(
			checkedOut = "true",
			dmDocumentTitle = "DM Document Title",
			userName = "Test Test");
	}

	@description = "This test checks if the file extension upload restrictions in System Settings works as expected."
	@priority = "5"
	test CheckValidationForFileEntries {
		property test.name.skip.portal.instance = "DocumentsadministrationUsecase#CheckValidationForFileEntries";

		SystemSettings.openSystemSettingsAdmin();

		SystemSettings.gotoConfiguration(
			configurationCategory = "Documents and Media",
			configurationName = "Service",
			configurationScope = "System Scope");

		ScrollWebElementIntoView(
			key_text = "fileExtensions",
			locator1 = "TextArea#ANY");

		Type(
			key_text = "fileExtensions",
			locator1 = "TextArea#ANY",
			value1 = ".doc");

		SystemSettings.saveConfiguration();

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoDocumentCP(dmDocumentTitle = "DM Document Title");

		DMDocument.viewCP(dmDocumentTitle = "DM Document Title");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		for (var dmDocumentTitle : list "Document_1.docx,Document_1.jpeg,Document_1.jpg,Document_1.mp4,Document_1.pdf,Document_1.png,Document_1.ppt,Document_1.pptx,Document_1.txt,Document_1.xls,Document_1.xlsx") {
			ProductMenu.gotoPortlet(
				category = "Content & Data",
				portlet = "Documents and Media");

			DMDocument.addCP(
				dmDocumentFile = "${dmDocumentTitle}",
				dmDocumentTitle = "${dmDocumentTitle}",
				validationError = "Please enter a file with a valid extension (.doc).");
		}
	}

	@priority = "4"
	test Comments {
		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMFolder.addCP(dmFolderName = "Folder1");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMFolder.addCP(
			dmFolderDescription = "Description of Folder2",
			dmFolderName = "Folder2");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMFolder.viewCP(dmFolderName = "Folder1");

		DMDocument.addCP(
			dmDocumentDescription = "This is a file with zero bytes",
			dmDocumentTitle = "Zero Document File",
			nullFile = "true");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMFolder.viewCP(dmFolderName = "Folder1");

		DMNavigator.gotoDocumentCP(dmDocumentTitle = "Zero Document File");

		DMDocument.viewCP(
			dmDocumentSize = "0 B",
			dmDocumentTitle = "Zero Document File");

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		Portlet.addPG(portletName = "Activities");

		AssertTextEquals(
			key_dmFolderName = "Folder1",
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE",
			value1 = "Folder1");

		AssertClick(
			key_assetName = "Zero Document File",
			locator1 = "Activities#ACTIVITY_TITLE_ASSET_NAME",
			value1 = "Zero Document File");

		DMDocument.viewDocumentTitleCmdPG(dmDocumentTitle = "Zero Document File");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoFolder(dmFolderName = "Folder1");

		DMDocumentComment.addPG(
			commentBody = "This is the first comment",
			dmDocumentTitle = "Zero Document File");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoFolder(dmFolderName = "Folder1");

		DMDocumentComment.addPG(
			commentBody = "This is comment #2",
			dmDocumentTitle = "Zero Document File");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoFolder(dmFolderName = "Folder1");

		DMDocumentComment.viewPG(
			commentBody = "This is the first comment",
			dmDocumentTitle = "Zero Document File");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoFolder(dmFolderName = "Folder1");

		DMDocumentComment.viewPG(
			commentBody = "This is comment #2",
			dmDocumentTitle = "Zero Document File");
	}

	@priority = "3"
	test ConfigureXugglerVideoPreviewSizePG {
		property custom.properties = "xuggler.enabled=true${line.separator}dl.file.entry.preview.video.width=0${line.separator}dl.file.entry.preview.video.height=0";
		property dummy.socket.proxy.disabled = "true";
		property test.assert.warning.exceptions = "false";
		property test.name.skip.portal.instance = "DocumentsadministrationUsecase#ConfigureXugglerVideoPreviewSizePG";

		ServerAdministration.openServerAdmin();

		ServerAdministration.configureExternalServices(installXuggler = "true");

		Portlet.shutdownServer();

		Portlet.startServer(deleteLiferayHome = "false");

		ServerAdministration.openServerAdmin();

		ServerAdministration.configureExternalServices(enableXuggler = "true");

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_1.mp4",
			groupName = "Guest",
			mimeType = "video/mp4",
			sourceFileName = "Document_1.mp4");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Pause(locator1 = "180000");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoDocumentPG(dmDocumentTitle = "Document_1.mp4");

		Pause(locator1 = "30000");

		var videoGenerationTimeCounter = "0";
		var videoGenerationTimeMaxWaitTime = "6";

		while (("${videoGenerationTimeCounter}" != "${videoGenerationTimeMaxWaitTime}") && (IsElementPresent(locator1 = "Message#INFO"))) {
			var videoGenerationTimeCounter = ${videoGenerationTimeCounter} + 1;

			Pause(locator1 = "30000");

			Navigator.gotoPage(pageName = "Documents and Media Page");

			DMNavigator.gotoDocumentPG(dmDocumentTitle = "Document_1.mp4");
		}

		var videoPreviewThumbnailLink = selenium.getAttribute("//video@poster");

		Navigator.openSpecificURL(url = "${videoPreviewThumbnailLink}");

		Pause(locator1 = "1000");

		AssertElementNotPresent(
			key_videoGenerationThumbnailResolution = "640",
			locator1 = "DocumentsAndMediaDocument#VIDEO_GENERATION_THUMBNAIL_RESOLUTION");

		AssertElementNotPresent(
			key_videoGenerationThumbnailResolution = "360",
			locator1 = "DocumentsAndMediaDocument#VIDEO_GENERATION_THUMBNAIL_RESOLUTION");

		AssertElementPresent.assertElementPresent(
			key_videoGenerationThumbnailResolution = "854",
			locator1 = "DocumentsAndMediaDocument#VIDEO_GENERATION_THUMBNAIL_RESOLUTION");

		AssertElementPresent.assertElementPresent(
			key_videoGenerationThumbnailResolution = "480",
			locator1 = "DocumentsAndMediaDocument#VIDEO_GENERATION_THUMBNAIL_RESOLUTION");
	}

	@description = "This test covers LPS-100096. It ensures that a document type cannot be edited without permissions."
	@priority = "4"
	test EntryCanNotBeEditedWithoutPermissions {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "Userfn",
			userLastName = "Userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		JSONRole.addRegularRole(roleTitle = "Depot Regrole Name");

		JSONRole.assignRoleToUser(
			roleTitle = "Depot Regrole Name",
			userEmailAddress = "userea@liferay.com");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW_CONTROL_PANEL",
			resourceName = "90",
			roleTitle = "Depot Regrole Name");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "ACCESS_IN_CONTROL_PANEL",
			resourceName = "com_liferay_document_library_web_portlet_DLAdminPortlet",
			roleTitle = "Depot Regrole Name");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoDocumentType();

		DMDocumentType.add(
			dmDocumentTypeDescription = "DM Document Type Description",
			dmDocumentTypeFieldNames = "Text",
			dmDocumentTypeName = "DM Document Type Name");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "DM Document Type Name");

		Pause(locator1 = "3000");

		var documentTypeURL = selenium.getLocation();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea@liferay.com",
			userLoginFullName = "userfn userln");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoDocumentType();

		LexiconEntry.viewNoEntryMenu(rowEntry = "DM Document Type Name");

		Navigator.openSpecificURL(url = "${documentTypeURL}");

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR",
			value1 = "You do not have the required permissions.");
	}

	@description = "This test covers LPS-112571. It ensures that a gif file can be added to editor normally after disabling the gif mime type from the adaptive media of the system settings."
	@priority = "3"
	test GifFileCanBeAddedToEditorAfterDisablingGifMimeTypeInAM {
		property test.name.skip.portal.instance = "DocumentsadministrationUsecase#GifFileCanBeAddedToEditorAfterDisablingGifMimeTypeInAM";

		SystemSettings.openSystemSettingsAdmin();

		SystemSettings.gotoConfiguration(
			configurationCategory = "Adaptive Media",
			configurationName = "Images",
			configurationScope = "System Scope");

		Click(
			key_fieldValue = "image/gif",
			locator1 = "DDMField#REMOVE_SPECIFIC_REPEATED_FIELD");

		SystemSettings.saveConfiguration();

		BlogsNavigator.openBlogsAdmin(siteURLKey = "guest");

		BlogsNavigator.gotoAddEntry();

		BlogsEntry.addTitle(entryTitle = "Blogs Entry Title");

		BlogsEntry.addContentWithInlineImage(
			entryContent = "Blogs Entry Content",
			navTab = "Documents and Media",
			uploadFileName = "static.gif");

		PortletEntry.publish();

		BlogsNavigator.openBlogsAdmin(siteURLKey = "guest");

		BlogsNavigator.gotoEntryCP(entryTitle = "Blogs Entry Title");

		BlogsEntry.viewInlineImage(uploadFileName = "static.gif");
	}

	@description = "This test covers LPS-106905. It ensures that a site admin user is able to select an image from the global site in WCD."
	@priority = "3"
	test ImageFromGlobalSiteCanBeSelectedWithPermissionsInWCD {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "global");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.png",
			dmDocumentTitle = "Document_1.png");

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Test Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Test Page",
			widgetName = "Web Content Display");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		JSONUser.addUserToSite(
			groupName = "Site Name",
			userEmailAddress = "userea@liferay.com");

		JSONRole.assignSiteRoleToUser(
			groupName = "Site Name",
			roleTitle = "Site Administrator",
			userEmailAddress = "userea@liferay.com");

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea@liferay.com",
			userLoginFullName = "userfn userln");

		Navigator.gotoSitePage(
			pageName = "Test Page",
			siteName = "Site Name");

		WebContent.addPGViaWCD(
			embedImage = "true",
			imageFileName = "Document_1.png",
			siteName = "Global",
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title");

		WebContent.viewPGViaWCD(
			image = "true",
			key_image = "Document_1.png",
			webContentContent = "Web Content Content",
			webContentTitle = "Web Content Title");
	}

	@priority = "5"
	test ImportAudioAndLimitPermissionsToSubfolder {
		property custom.properties = "jsonws.web.service.paths.excludes=";
		property testray.component.names = "Training";

		JSONGroup.addGroup(groupName = "Moon Rocks 101");

		JSONLayout.addPublicLayout(
			groupName = "Moon Rocks 101",
			layoutName = "Moon Rocks Page");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Roles");

		Role.add(roleTitle = "Regular Role Name");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Roles");

		Role.definePermissionsCP(
			permissionDefinitionKeys = "SITE_ADMIN_CONTENT_DOCUMENTS_AND_MEDIA_GENERAL_PERMISSIONS_ACCESS_IN_SITE_ADMINISTRATION_CHECKBOX,SITE_ADMIN_CONTENT_DOCUMENTS_AND_MEDIA_GENERAL_PERMISSIONS_VIEW_CHECKBOX,SITE_ADMIN_CONTENT_DOCUMENTS_AND_MEDIA_RESOURCE_PERMISSIONS_DOCUMENTS_ADD_FOLDER_CHECKBOX,SITE_ADMIN_CONTENT_DOCUMENTS_AND_MEDIA_RESOURCE_PERMISSIONS_DOCUMENTS_VIEW_CHECKBOX,SITE_ADMIN_CONTENT_DOCUMENTS_AND_MEDIA_RESOURCE_PERMISSIONS_DOCUMENTS_FOLDER_ADD_SUBFOLDER_CHECKBOX,SITE_ADMIN_CONTENT_DOCUMENTS_AND_MEDIA_RESOURCE_PERMISSIONS_DOCUMENTS_FOLDER_VIEW_CHECKBOX,SITE_ADMIN_CONTENT_DOCUMENTS_AND_MEDIA_RESOURCE_PERMISSIONS_DOCUMENT_UPDATE_CHECKBOX",
			roleTitle = "Regular Role Name");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		SitePages.openPagesAdmin(siteURLKey = "Moon Rocks 101");

		ProductMenu.gotoPortlet(
			category = "People",
			portlet = "Memberships");

		Site.assignUserAsMemberCP(
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.assignRegularRoleCP(
			roleTitle = "Regular Role Name",
			userScreenName = "usersn");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "moon-rocks-101");

		DMFolder.addCP(
			dmFolderName = "Unit 1 - Moon Rock Chemistry",
			siteName = "Moon Rocks 101");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "moon-rocks-101");

		DMFolder.viewCP(
			dmFolderName = "Unit 1 - Moon Rock Chemistry",
			specificNameFolder = "true");

		DMFolder.addCP(dmFolderName = "The Properties of Moon Rocks");

		DMFolder.viewCP(
			dmFolderName = "The Properties of Moon Rocks",
			specificNameFolder = "true");

		DMFolder.addCP(dmFolderName = "Audio Lectures");

		DMFolder.viewCP(dmFolderName = "Audio Lectures");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Dr. Winters Introductory Audio Lecture.ogg",
			dmDocumentTitle = "Dr. Winters Introductory Audio Lecture.ogg");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "moon-rocks-101");

		DMFolder.viewCP(
			dmFolderName = "Unit 1 - Moon Rock Chemistry",
			specificNameFolder = "true");

		DMFolder.viewCP(
			dmFolderName = "The Properties of Moon Rocks",
			specificNameFolder = "true");

		DMFolder.removePermissions(
			dmFolder = "Audio Lectures",
			permissionNameList = "ADD_DOCUMENT,ADD_SHORTCUT,ADD_SUBFOLDER",
			roleTitle = "Site Member");

		IFrame.closeFrame();

		Refresh();

		SitePages.openPagesAdmin(siteURLKey = "Moon Rocks 101");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media");

		LAR.importPortlet(
			larFileName = "moon-rocks-101-materials-structure.lar",
			uploadFrom = "dependencies");
	}

	@priority = "3"
	test ImportDocumentWithDBStore {
		property custom.properties = "dl.store.impl=com.liferay.portal.store.db.DBStore";
		property test.assert.warning.exceptions = "false";

		JSONGroup.addGroup(groupName = "Site Name");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			groupName = "Guest",
			mimeType = "image/jpeg",
			sourceFileName = "Document_1.jpg");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		LAR.exportPortlet(larFileName = "DM_Test_DB_Store.lar");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "site-name");

		LAR.importPortlet(larFileName = "DM_Test_DB_Store.lar");

		Navigator.openURL();

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "site-name");

		DMNavigator.gotoDocumentCP(dmDocumentTitle = "DM Document Title");

		DMDocument.viewCP(dmDocumentTitle = "DM Document Title");
	}

	@priority = "4"
	test RemoveCurrentDisplayStyleViews {
		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMPortlet.configureDisplaySettingsPG(currentDisplayStyleViews = "");
	}

	@description = "This case covers LPS-97708. It ensures that a document can be restored from the Recycle Bin even if its original folder no longer exists."
	@priority = "3"
	test RestoreDocumentWithoutOriginalFolder {
		JSONDocument.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name",
			groupName = "Guest");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description1",
			dmDocumentTitle = "DM Document Title",
			folderName = "DM Folder Name",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMDocument.moveToRecycleBinCP(
			assetType = "Document",
			dmDocumentTitle = "DM Document Title");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMFolder.moveToRecycleBinCP(
			assetType = "Documents Folder",
			dmFolderName = "DM Folder Name");

		RecycleBin.openRecycleBinAdmin(siteURLKey = "guest");

		RecycleBin.deleteCP(
			assetName = "DM Folder Name",
			assetType = "Documents Folder",
			multipleAssets = "true");

		RecycleBin.restoreContentWithoutOriginalFolder(
			assetName = "DM Document Title",
			assetRestoreLocation = "Home",
			assetType = "Document");

		AssertTextNotPresent(value1 = "NoSuchFolderException");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMDocument.viewCardTitle(dmDocumentTitle = "DM Document Title");
	}

	@description = "This is a use case for LPS-98669."
	@priority = "4"
	test SelectLinkToPagePage {
		for (var pageName : list "P1,P2,P3,P4,P5,P6,P7,P8,P9,P10,P11,P12") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "${pageName}");
		}

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media");

		DMNavigator.gotoDocumentType();

		DMDocumentType.add(
			dmDocumentTypeDescription = "DM Document Type Description",
			dmDocumentTypeFieldNames = "Link to Page",
			dmDocumentTypeName = "DM Document Type Name");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.jpg",
			dmDocumentTitle = "DM Document Title",
			dmDocumentTypeName = "DM Document Type Name",
			linkedPageName = "P11",
			linkFieldName = "LinktoPage");

		Click(
			key_dmDocumentTitle = "DM Document Title",
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Edit");

		AssertTextEquals(
			key_fieldName = "LinktoPage",
			locator1 = "TextInput#SELECTOR_INPUT",
			value1 = "P11");
	}

	@description = "This test covers LPS-91597. It ensures that a user can delete multiple documents simultaneously."
	@priority = "4"
	test SelectMultipleDocumentsAndDeleteViaActionsButton {
		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.enableActionsMenuOnPortlet();

		for (var entryCount : list "1,2,3") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "Document_${entryCount}.doc",
				groupName = "Guest",
				mimeType = "application/msword",
				sourceFileName = "Document_${entryCount}.doc");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		for (var entryCount : list "1,2,3") {
			var dmDocumentTitle = "Document_${entryCount}.doc";

			LexiconCard.checkCardCheckbox(card = "${dmDocumentTitle}");
		}

		Click(locator1 = "Icon#MOVE_TO_RECYCLE_BIN");

		VerifyElementPresent(locator1 = "Message#SUCCESS");

		DMDocument.viewDefaultCP();
	}

	@priority = "5"
	test UpdateDocumentTypeWithPermissions {
		property custom.properties = "jsonws.web.service.paths.excludes=";
		property test.name.skip.portal.instance = "DocumentsadministrationUsecase#UpdateDocumentTypeWithPermissions";
		property testray.component.names = "Training";

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media");

		DMNavigator.gotoDocumentType();

		DMDocumentType.add(
			dmDocumentTypeDescription = "Images",
			dmDocumentTypeFieldNames = "Text Box,Date",
			dmDocumentTypeName = "Photos");

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Server Administration");

		ServerAdministration.executeServerResourcesActions(actionsDescription = "Clean up permissions.");

		Navigator.openURL();

		JSONRole.addRegularRole(
			roleKey = "Technical Writer",
			roleTitle = "Technical Writer");

		for (var resourceActionIndex : list "DELETE,VIEW,UPDATE") {
			Permissions.definePermissionViaJSONAPI(
				resourceAction = "${resourceActionIndex}",
				resourceName = "com.liferay.document.library.kernel.model.DLFileEntryType",
				roleTitle = "Technical Writer");
		}

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW",
			resourceName = "com_liferay_document_library_web_portlet_DLAdminPortlet",
			roleTitle = "Technical Writer");

		for (var resourceActionIndex : list "VIEW,ADD_FOLDER,ADD_DOCUMENT") {
			Permissions.definePermissionViaJSONAPI(
				resourceAction = "${resourceActionIndex}",
				resourceName = "com.liferay.document.library",
				roleTitle = "Site Member",
				roleType = "site");
		}

		for (var resourceActionIndex : list "UPDATE,DELETE") {
			Permissions.definePermissionViaJSONAPI(
				resourceAction = "${resourceActionIndex}",
				resourceName = "com.liferay.document.library.kernel.model.DLFileEntry",
				roleTitle = "Site Member",
				roleType = "site");
		}

		for (var resourceActionIndex : list "UPDATE,ADD_SUBFOLDER,ADD_DOCUMENT") {
			Permissions.definePermissionViaJSONAPI(
				resourceAction = "${resourceActionIndex}",
				resourceName = "com.liferay.document.library.kernel.model.DLFolder",
				roleTitle = "Site Member",
				roleType = "site");
		}

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW_SITE_ADMINISTRATION",
			resourceName = "com.liferay.portal.kernel.model.Group",
			roleTitle = "Site Member",
			roleType = "site");

		for (var resourceActionIndex : list "VIEW,ACCESS_IN_CONTROL_PANEL") {
			Permissions.definePermissionViaJSONAPI(
				resourceAction = "${resourceActionIndex}",
				resourceName = "com_liferay_document_library_web_portlet_DLAdminPortlet",
				roleTitle = "Site Member",
				roleType = "site");
		}

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		Site.assignUserSitesCP(
			siteName = "${siteName}",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.assignRegularRoleCP(
			roleTitle = "Technical Writer",
			userScreenName = "usersn");

		JSONUser.addUser(
			userEmailAddress = "userea2@liferay.com",
			userFirstName = "userfn2",
			userLastName = "userln2",
			userScreenName = "usersn2");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea2@liferay.com");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		Site.assignUserSitesCP(
			siteName = "${siteName}",
			userScreenName = "usersn2");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages",
			site = "${siteName}");

		SitePages.addPublicPage(
			pageName = "Documents and Media Page",
			sitePageType = "Widget Page");

		SitePages.configurePage(
			columnName = "1 Column",
			pageName = "Documents and Media Page",
			privacy = "Public Pages",
			sitePageType = "Widget Page");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		Portlet.addPG(portletName = "Documents and Media");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media");

		AssertVisible(
			locator1 = "Portlet#HEADER",
			value1 = "Documents and Media");

		DMNavigator.gotoDocumentType();

		DMDocumentType.edit(
			dmDocumentTypeFieldNames = "Radio",
			dmDocumentTypeName = "Photos");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea2@liferay.com");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.viewCannotAddRepository();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media");

		DMDocumentType.viewNoAvailableActionsViaDocumentTypesPG();
	}

	@description = "This test covers LPS-118268. It ensures that the user without document view permission can not view the document via the display page."
	@priority = "4"
	test ViewDocumentViaDisplayPage {
		JSONLayoutpagetemplate.addDisplayPageTemplateEntry(
			contentType = "Document",
			displayPageTemplateEntryName = "Display Page Name",
			groupName = "Guest",
			subType = "Basic Document");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "guest");

		DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Display Page Name");

		PageEditor.addWidget(portletName = "Documents and Media");

		PageEditor.clickPublish();

		DisplayPageTemplatesAdmin.markDisplayPageAsDefault(displayPageName = "Display Page Name");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.jpg",
			dmDocumentTitle = "DM Document Title");

		var dmFileEntryID = DMDocument.getFileEntryID(dmDocumentTitle = "DM Document Title");

		User.logoutPG();

		Navigator.openWithAppendToBaseURL(urlAppend = "group/guest/d/${dmFileEntryID}");

		AssertTextEquals(
			key_dmDocumentTitle = "DM Document Title",
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_TITLE",
			value1 = "DM Document Title");

		User.loginUserPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoDocumentPermissionsCP(dmDocumentTitle = "DM Document Title");

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = "Remove",
			roleList = "guest");

		User.logoutPG();

		Navigator.openWithAppendToBaseURL(urlAppend = "group/guest/d/${dmFileEntryID}");

		Alert.viewErrorMessage(errorMessage = "You do not have the required permissions to view the content of this page.");
	}

	@priority = "5"
	test ViewDocumentWithGlobalScope {
		property testray.component.names = "Training";

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page - Global");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page - Global",
			widgetName = "Documents and Media");

		PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = "global");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media");

		DMFolder.addCP(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name");

		DMFolder.viewCP(dmFolderName = "DM Folder Name");

		DMFolder.addCP(
			dmFolderDescription = "DM SubFolder Description",
			dmFolderName = "DM SubFolder Name");

		PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = "global");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media");

		DMFolder.viewCP(dmFolderName = "DM Folder Name");

		DMFolder.viewCP(
			dmFolderName = "DM SubFolder Name",
			specificNameFolder = "true");

		Refresh();

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.txt",
			dmDocumentTitle = "DM Document Title");

		Navigator.gotoPage(pageName = "Documents and Media Page - Global");

		DMPortlet.configureScopetoGlobalPG(currentScope = "Liferay");

		Navigator.gotoPage(pageName = "Documents and Media Page - Global");

		DMPortlet.configureRootFolderPG(dmFolderName = "DM Folder Name");

		Navigator.gotoPage(pageName = "Documents and Media Page - Global");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Navigator.gotoPage(pageName = "Documents and Media Page - Global");

		DMNavigator.gotoFolder(dmFolderName = "DM SubFolder Name");

		Refresh();

		DMDocument.viewPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMPortlet.viewDefaultPG(portletName = "Documents and Media");

		SiteTemplates.addCP(siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Manage",
			rowEntry = "Site Template Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		SitePages.addPrivatePage(
			pageName = "DM Site Page Name - Global",
			siteTemplate = "true");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addSiteTemplateCP(
			siteName = "DM Site Name",
			siteTemplateName = "Site Template Name");

		SitePages.openPagesAdmin(siteURLKey = "DM Site Name");

		Navigator.gotoSitePage(
			pageName = "DM Site Page Name - Global",
			siteName = "DM Site Name");

		Portlet.addPG(portletName = "Documents and Media");

		Navigator.gotoSitePage(
			pageName = "DM Site Page Name - Global",
			siteName = "DM Site Name");

		DMPortlet.configureScopetoGlobalPG(currentScope = "DM Site Name");

		Navigator.gotoSitePage(
			pageName = "DM Site Page Name - Global",
			siteName = "DM Site Name");

		DMPortlet.configureRootFolderPG(dmFolderName = "DM Folder Name");

		Navigator.gotoSitePage(
			pageName = "DM Site Page Name - Global",
			siteName = "DM Site Name");

		DMNavigator.gotoFolder(dmFolderName = "DM SubFolder Name");

		Refresh();

		DMDocument.viewPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title");
	}

	@priority = "5"
	test ViewFileCategoryAndTags {
		property testray.component.names = "Training";

		JSONCategory.addVocabulary(
			groupName = "Guest",
			title = "Vocabulary Name");

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		ProductMenu.gotoPortlet(
			category = "Categorization",
			portlet = "Categories");

		Category.addCP(
			categoryDescription = "Category Description",
			categoryName = "Category Name",
			vocabularyName = "Vocabulary Name");

		Navigator.openSiteURL(siteName = "Guest");

		ProductMenu.gotoPortlet(
			category = "Categorization",
			portlet = "Tags");

		var tagNameList = "tag1,tag2";

		for (var tagName : list "${tagNameList}") {
			Tag.addCP(tagName = "${tagName}");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		Portlet.addPG(portletName = "Documents and Media");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.addPG(
			categoryName = "Category Name",
			dmDocumentDescription = "Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "Document Title",
			tagNameList = "tag1,tag2",
			vocabularyName = "Vocabulary Name");

		Click(
			key_dmDocumentTitle = "Document Title",
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Edit");

		Panel.expandPanel(panel = "Categorization");

		AssertTextEquals(
			key_categoryName = "Category Name",
			locator1 = "AssetCategorization#CATEGORY",
			value1 = "Category Name");

		for (var tagName : list "${tagNameList}") {
			var key_tagName = "${tagName}";

			AssertTextEquals(
				locator1 = "AssetCategorization#TAG",
				value1 = "${tagName}");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.viewPG(
			categoryName = "Category Name",
			dmDocumentDescription = "Document Description",
			dmDocumentTitle = "Document Title",
			tagNameList = "tag1,tag2");
	}

	@priority = "5"
	test ViewFolderAsOrganizationUser {
		property custom.properties = "jsonws.web.service.paths.excludes=";
		property testray.component.names = "Training";

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		Organization.addCP(
			orgName = "Organization Name",
			orgType = "Organization");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		UsersAndOrganizationsNavigator.gotoOrganizations();

		Organization.assignUser(
			orgName = "Organization Name",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		UsersAndOrganizationsNavigator.gotoOrganizations();

		Organization.addSiteCP(orgName = "Organization Name");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		UsersAndOrganizationsNavigator.gotoOrganizations();

		Organization.gotoCP(
			orgName = "Organization Name",
			searchToken = "Organization Name");

		User.searchCP(searchTerm = "usersn");

		Organization.viewAssignMemberCP(
			userName = "userfn userln",
			userScreenName = "usersn");

		SitePages.openPagesAdmin(siteURLKey = "Organization Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		SitePages.addPublicPage(
			pageName = "Documents and Media Page",
			sitePageType = "Widget Page");

		SitePages.configurePage(
			columnName = "1 Column",
			pageName = "Documents and Media Page",
			privacy = "Public Pages",
			sitePageType = "Widget Page");

		Navigator.gotoSitePage(
			pageName = "Documents and Media Page",
			siteName = "Organization Name");

		Portlet.addPG(portletName = "Documents and Media");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoSitePage(
			pageName = "Documents and Media Page",
			siteName = "Organization Name");

		DMFolder.addPG(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media");

		DMNavigator.gotoFolderPermissions(dmFolderName = "DM Folder Name");

		for (var permissionsInLineUncheckListsCount : list "INLINE_PERMISSIONS_ADD_SUBFOLDER_CHECKBOX,INLINE_PERMISSIONS_ADD_SHORTCUT_CHECKBOX,INLINE_PERMISSIONS_ADD_DOCUMENT_CHECKBOX,INLINE_PERMISSIONS_SUBSCRIBE_CHECKBOX,INLINE_PERMISSIONS_VIEW_CHECKBOX") {
			Role.removePermissionPG(
				permissionDefinitionKey = "${permissionsInLineUncheckListsCount}",
				roleTitle = "Site Member");
		}

		Role.removePermissionPG(
			permissionDefinitionKey = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
			roleTitle = "Guest");

		IFrame.selectTopFrame();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea@liferay.com",
			userLoginFullName = "userfn userln");

		Navigator.gotoSitePage(
			pageName = "Documents and Media Page",
			siteName = "Organization Name");

		DMPortlet.viewDefaultPG();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "test@liferay.com",
			userLoginFullName = "Test Test");

		Navigator.gotoSitePage(
			pageName = "Documents and Media Page",
			siteName = "Organization Name");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media");

		DMNavigator.gotoFolderPermissions(dmFolderName = "DM Folder Name");

		for (var permissionsInLineUncheckListsCount : list "INLINE_PERMISSIONS_ADD_DOCUMENT_CHECKBOX,INLINE_PERMISSIONS_VIEW_CHECKBOX,CONTENT_PERMISSIONS_ACCESS_CHECKBOX") {
			Role.definePermissionPG(
				permissionDefinitionKey = "${permissionsInLineUncheckListsCount}",
				roleTitle = "Organization User");
		}

		IFrame.selectTopFrame();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea@liferay.com",
			userLoginFullName = "userfn userln");

		Navigator.gotoSitePage(
			pageName = "Documents and Media Page",
			siteName = "Organization Name");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMDocument.addPG(
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "Document_1.doc");
	}

	@description = "This test covers LPS-107966. It ensures that the image description can be propagated after creating a new image via image editor in web content."
	@priority = "3"
	test ViewImageDescriptionAfterImageEditedViaWebContent {
		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_2.jpg",
			groupName = "Guest",
			mimeType = "image/jpeg",
			sourceFileName = "Document_2.jpg");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		WebContentNavigator.gotoAddCP();

		ImageEditor.gotoImageEditorViaItemSelector(
			ckEditor = "true",
			imageFileName = "Document_2.jpg",
			navTab = "Documents and Media");

		ImageEditor.rotateImage(rotateDirection = "right");

		Pause(locator1 = "3000");

		ImageEditor.applyChangesAndSaveFile();

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "Document_2 (1).jpg");

		AssertTextEquals(
			locator1 = "TextArea#DESCRIPTION",
			value1 = "DM Document Description");
	}

	@description = "This test covers LPS-93601 and LPS-91661. It ensures that the user can check all files in the current pagination using the DM toolbar and can move documents not on the current pagination to another folder."
	@priority = "4"
	test ViewPaginationToolbarCheckboxAndMoveMultiplePaginationDocuments {
		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		JSONDocument.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name",
			groupName = "Guest");

		for (var documentName : list "Document_2.doc,Document_1.xls,Document_4.jpg,Document_3.jpg,Document_2.xls,Document_1.txt,Document_4.txt,Document_3.xls,Document_2.jpg,Document_1.doc") {
			var extension = StringUtil.extractLast("${documentName}", ".");

			if ("${extension}" == "doc") {
				var mimeType = "application/msword";
			}
			else if ("${extension}" == "jpg") {
				var mimeType = "image/jpg";
			}
			else if ("${extension}" == "png") {
				var mimeType = "image/png";
			}
			else if ("${extension}" == "xls") {
				var mimeType = "application/vnd.ms-excel";
			}
			else if ("${extension}" == "txt") {
				var mimeType = "text/plain";
			}

			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "${documentName}",
				folderName = "DM Folder Name",
				groupName = "Guest",
				mimeType = "${mimeType}",
				sourceFileName = "${documentName}");
		}

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMFolder.gotoEntry(dmFolderName = "DM Folder Name");

		Pagination.changePagination(itemsPerPage = "4");

		Pagination.viewResults(results = "Showing 1 to 4 of 10 entries.");

		DMDocument.moveToFolderCP(
			dmFolderName = "Home",
			selectAll = "true");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		Pagination.changePagination(itemsPerPage = "20");

		Pagination.viewResults(results = "Showing 1 to 11 of 11 entries.");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		LexiconEntry.changeDisplayStyle(displayStyle = "List");

		Pagination.changePagination(itemsPerPage = "4");

		Pagination.viewResults(results = "Showing 1 to 4 of 11 entries.");

		DMDocument.checkDocumentCheckboxPG(dmDocumentTitle = "Document_2.jpg");

		AssertTextEquals(
			locator1 = "ManagementBar#NAVBAR_TEXT",
			value1 = "1 of 11 Selected");

		Click(locator1 = "Pagination#NEXT_LINK");

		Click(locator1 = "DocumentsAndMedia#TOOLBAR_SELECT_ALL_CHECKBOX");

		AssertTextEquals(
			locator1 = "ManagementBar#NAVBAR_TEXT",
			value1 = "5 of 11 Selected");
	}

	@priority = "4"
	test ViewPortletAfterRenamingDocumentWithDuplicateTitlePG {
		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title");

		DMDocument.addPG(
			dmDocument = "Duplicate",
			dmDocumentDescription = "DM Document 2 Description",
			dmDocumentFile = "Document_2.doc",
			dmDocumentTitle = "DM Document Title");

		PortletEntry.inputTitle(title = "DM Document 2 Title");

		PortletEntry.publish();

		Pause(locator1 = "1000");

		AssertTextNotPresent(value1 = "Documents and Media is temporarily unavailable.");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		Pause(locator1 = "1000");

		AssertTextNotPresent(value1 = "Documents and Media is temporarily unavailable.");

		DMDocument.viewPG(
			dmDocumentDescription = "DM Document 2 Description",
			dmDocumentTitle = "DM Document 2 Title");
	}

	@priority = "4"
	test ViewSingleDocumentSelectionDoesNotCheckAllFileSelectionCP {
		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document1 Title",
			groupName = "Guest",
			mimeType = "image/jpeg",
			sourceFileName = "Document_1.jpg");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document2 Description",
			dmDocumentTitle = "DM Document2 Title",
			groupName = "Guest",
			mimeType = "image/jpeg",
			sourceFileName = "Document_1.jpg");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMDocument.checkDocumentCheckboxCP(dmDocumentTitle = "DM Document1 Title");

		AssertElementPresent(locator1 = "Checkbox#SELECT_ALL_CHECKBOX_TICK");

		DMDocument.uncheckDocumentCheckboxCP(dmDocumentTitle = "DM Document1 Title");

		AssertElementNotPresent(locator1 = "Checkbox#SELECT_ALL_CHECKBOX_TICK");

		DMDocument.checkDocumentCheckboxCP(dmDocumentTitle = "DM Document1 Title");

		DMDocument.checkDocumentCheckboxCP(dmDocumentTitle = "DM Document2 Title");

		AssertElementPresent(locator1 = "Checkbox#LEXICON_SELECT_ALL_INPUT");

		AssertElementPresent(locator1 = "Checkbox#LEXICON_SELECT_ALL_CHECKED");
	}

}