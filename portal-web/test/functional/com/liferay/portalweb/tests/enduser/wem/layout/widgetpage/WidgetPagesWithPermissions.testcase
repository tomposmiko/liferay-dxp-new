@component-name = "portal-wcm"
definition {

	property custom.properties = "jsonws.web.service.paths.excludes=";
	property portal.release = "true";
	property portal.upstream = "true";
	property testcase.url = "http://localhost:8080";
	property testray.main.component.name = "Page Administration";

	static var randomSiteName = StringUtil.randomString(8);

	var siteURLKey = StringUtil.toLowerCase(${randomSiteName});
	static var randomUserEmailAddress = StringUtil.randomString(8);
	var userFirstName = StringUtil.randomString(8);
	var userLastName = StringUtil.randomString(8);
	var userScreenName = StringUtil.randomString(8);

	setUp {
		task ("Sign in") {
			User.firstLoginPG();
		}

		task ("Add a site") {
			HeadlessSite.addSite(siteName = ${randomSiteName});
		}

		task ("Add a new user") {
			JSONUser.addUser(
				userEmailAddress = StringUtil.toLowerCase("${randomUserEmailAddress}@liferay.com"),
				userFirstName = ${userFirstName},
				userLastName = ${userLastName},
				userScreenName = StringUtil.toLowerCase(${userScreenName}));

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = StringUtil.toLowerCase("${randomUserEmailAddress}@liferay.com"));
		}
	}

	@description = "Cannot reset changes as user without permissions."
	@priority = 3
	test CannotResetChangesAsUserWithoutPermissions {
		property testray.component.names = "Site and Page Templates";

		var siteName = StringUtil.randomString(8);

		var siteURLKey = StringUtil.toLowerCase(${siteName});

		task ("Add a site template") {
			SiteTemplates.addCP(siteTemplateName = "Site Template Name");
		}

		task ("Add a site based on template") {
			HeadlessSite.addSite(
				siteName = ${siteName},
				templateName = "Site Template Name",
				templateType = "Site Template");
		}

		task ("Assign the user to site") {
			JSONUser.addUserToSite(
				groupName = ${siteName},
				userEmailAddress = StringUtil.toLowerCase("${randomUserEmailAddress}@liferay.com"));
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(userLoginEmailAddress = StringUtil.toLowerCase("${randomUserEmailAddress}@liferay.com"));
		}

		task ("View Home page is shown") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Home",
				siteName = ${siteName});

			Page.viewDefaultSiteTemplatePagesPG(
				pageName = "Home",
				siteName = ${siteName});
		}

		task ("Sign in as Test Test") {
			User.logoutAndLoginPG(userLoginEmailAddress = "test@liferay.com");
		}

		task ("Add a Blogs widget to home page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Home",
				siteName = ${siteName});

			PageEditor.addWidget(portletName = "Blogs");
		}

		task ("Publish the page") {
			PageEditor.publish();
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(userLoginEmailAddress = StringUtil.toLowerCase("${randomUserEmailAddress}@liferay.com"));
		}

		task ("View info icon is not shown") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Home",
				siteName = ${siteName});

			SiteTemplates.viewNoInfoMessage();
		}
	}

	@description = "The guest cannot view private content and page."
	@priority = 3
	test CannotViewPrivateContentAndPageAsGuest {
		property osgi.module.configuration.file.names = "com.liferay.login.web.internal.configuration.AuthLoginConfiguration.config";
		property osgi.module.configurations = "promptEnabled=\"true\"";
		property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
		property testray.component.names = "Message Boards";

		task ("Add a private page") {
			JSONLayout.addPrivateLayout(
				groupName = ${randomSiteName},
				layoutName = "Test Private Page");
		}

		task ("Add a public page") {
			JSONLayout.addPublicLayout(
				groupName = ${randomSiteName},
				layoutName = "Test Page Name");
		}

		task ("Add a Blogs and Message Boards widgets to public page") {
			for (var widgetName : list "Blogs,Message Boards") {
				JSONLayout.addWidgetToPublicLayout(
					groupName = ${randomSiteName},
					layoutName = "Test Page Name",
					widgetName = ${widgetName});
			}
		}

		task ("Add a blogs entry") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = ${randomSiteName});

			Blogs.addEntryWithPermissions(
				entryContent = "Blogs Entry Content",
				entryTitle = "Blogs Entry Title",
				viewableBy = "Site Members");
		}

		task ("Add a message boards thread") {
			MessageboardsThread.addPGWithPermissions(
				threadBody = "MB Thread Message Body",
				threadSubject = "MB Thread Message Subject",
				viewableBySelection = "Site Members");
		}

		task ("Get blogs and message boards URL") {
			var portalURL = PropsUtil.get("portal.url");
			var blogEntryId = JSONBlog.getBlogEntryId(
				entryTitle = "Blogs Entry Title",
				groupName = ${randomSiteName});

			var blogEntryURL = "${portalURL}/web/${siteURLKey}/test-page-name/-/blogs/${blogEntryId}";
			var messageId = JSONMBMessage.getMessageIdByName(
				groupName = ${randomSiteName},
				threadSubject = "MB Thread Message Subject");

			var messageBoardsThreadURL = "${portalURL}/web/${siteURLKey}/test-page-name/-/message_boards/message/${messageId}";
		}

		task ("Log out") {
			UserBar.signOut();
		}

		task ("View login page when reaching private page") {
			Navigator.openPrivateSitePageURL(
				pageName = "Test Private Page",
				siteName = ${randomSiteName});

			User.viewLoginPG();

			AssertElementNotPresent(locator1 = "Message#ERROR");

			AssertElementNotPresent(locator1 = "Home#ERROR_MESSAGE_ALERT_ERROR");
		}

		task ("View blogs entry is not shown for Guest") {
			Navigator.openSpecificURL(url = ${blogEntryURL});

			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR",
				value1 = "You do not have the required permissions.");
		}

		task ("View message boards not shown for Guest") {
			Navigator.openSpecificURL(url = ${messageBoardsThreadURL});

			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR",
				value1 = "You do not have the required permissions.");
		}
	}

	@description = "The user without permissions cannot view private content and page."
	@priority = 5
	test CannotViewPrivateContentAndPageAsUserWithoutPermissions {
		property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
		property testray.component.names = "Message Boards,Training";

		task ("Add a private page") {
			JSONLayout.addPrivateLayout(
				groupName = ${randomSiteName},
				layoutName = "Test Private Page");
		}

		task ("Add a public page") {
			JSONLayout.addPublicLayout(
				groupName = ${randomSiteName},
				layoutName = "Test Page Name");
		}

		task ("Add a Blogs and Message Boards widget to public page") {
			for (var widgetName : list "Blogs,Message Boards") {
				JSONLayout.addWidgetToPublicLayout(
					groupName = ${randomSiteName},
					layoutName = "Test Page Name",
					widgetName = ${widgetName});
			}
		}

		task ("Add a blogs entry") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = ${randomSiteName});

			Blogs.addEntryWithPermissions(
				entryContent = "Blogs Entry Content",
				entryTitle = "Blogs Entry Title",
				viewableBy = "Site Members");
		}

		task ("Add a message boards thread") {
			MessageboardsThread.addPGWithPermissions(
				threadBody = "MB Thread Message Body",
				threadSubject = "MB Thread Message Subject",
				viewableBySelection = "Site Members");
		}

		task ("Get blogs and message boards URL") {
			var portalURL = PropsUtil.get("portal.url");
			var blogEntryId = JSONBlog.getBlogEntryId(
				entryTitle = "Blogs Entry Title",
				groupName = ${randomSiteName});

			var blogEntryURL = "${portalURL}/web/${siteURLKey}/test-page-name/-/blogs/${blogEntryId}";
			var messageId = JSONMBMessage.getMessageIdByName(
				groupName = ${randomSiteName},
				threadSubject = "MB Thread Message Subject");

			var messageBoardsThreadURL = "${portalURL}/web/${siteURLKey}/test-page-name/-/message_boards/message/${messageId}";
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(userLoginEmailAddress = StringUtil.toLowerCase("${randomUserEmailAddress}@liferay.com"));
		}

		task ("View private page is not shown for new user") {
			Navigator.openPrivateSitePageURL(
				pageName = "Test Page Name",
				siteName = ${randomSiteName});

			AssertTextEquals(
				locator1 = "Home#ERROR_MESSAGE_ALERT_ERROR",
				value1 = "Not Found");
		}

		task ("View blogs entry is not shown for new user") {
			Navigator.openSpecificURL(url = ${blogEntryURL});

			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR",
				value1 = "You do not have the required permissions.");
		}

		task ("View message boards is not shown for new user") {
			Navigator.openSpecificURL(url = ${messageBoardsThreadURL});

			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR",
				value1 = "You do not have the required permissions.");
		}
	}

	@description = "User admin can choose when a private widget page will be visible to assigned role."
	@priority = 5
	test CannotViewPrivatePageAsUserWithoutPermissions {
		property portal.acceptance = "true";
		property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
		property testray.component.names = "Permissions,Roles,Training";

		var siteName = StringUtil.randomString(8);

		var siteURLKey = StringUtil.toLowerCase(${siteName});
		var roleKey = StringUtil.randomString(8);

		task ("Add a site template") {
			SiteTemplates.addCP(siteTemplateName = "Site Template Name");
		}

		task ("Add a private site based on new site template") {
			HeadlessSite.addSite(
				membershipType = "Private",
				siteName = ${siteName},
				templateName = "Site Template Name",
				templateType = "Site Template");
		}

		task ("Add a private widget page") {
			JSONLayout.addPrivateLayout(
				groupName = ${siteName},
				layoutName = "Test Page Name");
		}

		task ("Add a new regular role") {
			JSONRole.addRegularRole(roleTitle = ${roleKey});
		}

		task ("Assign the user to site") {
			JSONUser.addUserToSite(
				groupName = ${siteName},
				userEmailAddress = StringUtil.toLowerCase("${randomUserEmailAddress}@liferay.com"));
		}

		task ("Assign the regular role to new user") {
			JSONRole.assignRoleToUser(
				roleTitle = ${roleKey},
				userEmailAddress = StringUtil.toLowerCase("${randomUserEmailAddress}@liferay.com"));
		}

		task ("Remove View permission of private widget page on Site Member") {
			PagesAdmin.openPagesAdmin(siteURLKey = ${siteURLKey});

			PermissionsInline.gotoSitePageCP(
				pageName = "Test Page Name",
				privacy = "Private Pages");

			Role.removePermissionPG(
				permissionDefinitionKey = "CONTENT_PERMISSIONS_VIEW_CHECKBOX",
				roleTitle = "Site Member");
		}

		task ("Grant View permission of private widget page on Regular Role") {
			Role.definePermissionPG(
				permissionDefinitionKey = "CONTENT_PERMISSIONS_VIEW_CHECKBOX",
				roleTitle = ${roleKey});
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(userLoginEmailAddress = StringUtil.toLowerCase("${randomUserEmailAddress}@liferay.com"));
		}

		task ("View private page is shown") {
			Navigator.openPrivateSitePageURL(
				pageName = "Test Page Name",
				siteName = ${siteName});

			Page.viewCurrentPage(pageName = "Test Page Name");

			AssertTextNotPresent(value1 = "The requested resource could not be found");
		}

		task ("Sign in as Test Test") {
			UserBar.signOut();

			User.loginPG(userEmailAddress = "test@liferay.com");
		}

		task ("Remove View permission of private widget page on Regular Role") {
			PagesAdmin.openPagesAdmin(siteURLKey = ${siteURLKey});

			PermissionsInline.gotoSitePageCP(
				pageName = "Test Page Name",
				privacy = "Private Pages");

			Role.removePermissionPG(
				permissionDefinitionKey = "CONTENT_PERMISSIONS_VIEW_CHECKBOX",
				roleTitle = ${roleKey});
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(userLoginEmailAddress = StringUtil.toLowerCase("${randomUserEmailAddress}@liferay.com"));
		}

		task ("View private page is not shown") {
			Navigator.openPrivateSitePageURL(
				pageName = "Test Page Name",
				siteName = ${siteName});

			AssertTextPresent(value1 = "The requested resource could not be found");
		}

		task ("Delete new site and site template") {
			User.logoutAndLoginPG(userLoginEmailAddress = "test@liferay.com");

			JSONGroup.deleteGroupByName(groupName = ${siteName});

			SiteTemplates.tearDownCP();
		}
	}

	@description = "This is a use case for LPS-121346. The Home page is shown when access to a non existing page."
	@priority = 3
	test ViewNotFoundWhenAccessToNonExistingPage {
		task ("Assign the power role to new user") {
			JSONRole.assignRoleToUser(
				roleTitle = "Power User",
				userEmailAddress = StringUtil.toLowerCase("${randomUserEmailAddress}@liferay.com"));
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(userLoginEmailAddress = StringUtil.toLowerCase("${randomUserEmailAddress}@liferay.com"));
		}

		task ("Access a non existing page") {
			var portalURL = PropsUtil.get("portal.url");

			Navigator.openSpecificURL(url = "${portalURL}/web/usersn/page-name");
		}

		task ("View not found error in home page") {
			AssertTextEquals(
				locator1 = "Home#ERROR_MESSAGE_ALERT_ERROR",
				value1 = "Not Found");

			AssertTextPresent(value1 = "The requested resource could not be found.");

			AssertElementNotPresent(
				key_portletTitleName = "Profile",
				locator1 = "Home#PORTLET_HEADER");
		}
	}

	@description = "The site member could view private content and page."
	@priority = 3
	test ViewPrivateContentAndPageAsSiteMember {
		property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
		property testray.component.names = "Message Boards";

		task ("Assign the new user to site") {
			JSONUser.addUserToSite(
				groupName = ${randomSiteName},
				userEmailAddress = StringUtil.toLowerCase("${randomUserEmailAddress}@liferay.com"));
		}

		task ("Add a private page") {
			JSONLayout.addPrivateLayout(
				groupName = ${randomSiteName},
				layoutName = "Test Private Page");
		}

		task ("Add a public page") {
			JSONLayout.addPublicLayout(
				groupName = ${randomSiteName},
				layoutName = "Test Page Name");
		}

		task ("Add a Blogs and Message Boards widget to public page") {
			for (var widgetName : list "Blogs,Message Boards") {
				JSONLayout.addWidgetToPublicLayout(
					groupName = ${randomSiteName},
					layoutName = "Test Page Name",
					widgetName = ${widgetName});
			}
		}

		task ("Add a blogs entry") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = ${randomSiteName});

			Blogs.addEntryWithPermissions(
				entryContent = "Blogs Entry Content",
				entryTitle = "Blogs Entry Title",
				viewableBy = "Site Members");
		}

		task ("Add a message boards thread") {
			MessageboardsThread.addPGWithPermissions(
				threadBody = "MB Thread Message Body",
				threadSubject = "MB Thread Message Subject",
				viewableBySelection = "Site Members");
		}

		task ("Get blogs and message boards URL") {
			var portalURL = PropsUtil.get("portal.url");
			var blogEntryId = JSONBlog.getBlogEntryId(
				entryTitle = "Blogs Entry Title",
				groupName = ${randomSiteName});

			var blogEntryURL = "${portalURL}/web/${siteURLKey}/test-page-name/-/blogs/${blogEntryId}";
			var messageId = JSONMBMessage.getMessageIdByName(
				groupName = ${randomSiteName},
				threadSubject = "MB Thread Message Subject");

			var messageBoardsThreadURL = "${portalURL}/web/${siteURLKey}/test-page-name/-/message_boards/message/${messageId}";
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(userLoginEmailAddress = StringUtil.toLowerCase("${randomUserEmailAddress}@liferay.com"));
		}

		task ("View private page is shown for site member") {
			Navigator.gotoPrivateSitePage(
				pageName = "Test Private Page",
				siteName = ${randomSiteName});

			AssertElementNotPresent(locator1 = "Message#ERROR");

			AssertElementNotPresent(locator1 = "Home#ERROR_MESSAGE_ALERT_ERROR");
		}

		task ("View blogs entry is shown for site member") {
			Navigator.openSpecificURL(url = ${blogEntryURL});

			BlogsNavigator.gotoPG(
				blogEntryURL = ${blogEntryURL},
				entryContent = "Blogs Entry Content",
				entryTitle = "Blogs Entry Title");
		}

		task ("View message boards is shown for site member") {
			Navigator.openSpecificURL(url = ${messageBoardsThreadURL});

			MessageboardsThread.gotoPG(
				lastPostUserName = "Test Test",
				messageBoardsThreadURL = ${messageBoardsThreadURL},
				replyCount = 1,
				threadBody = "MB Thread Message Body",
				threadSubject = "MB Thread Message Subject");

			MessageboardsThread.viewPG(
				threadBody = "MB Thread Message Body",
				threadSubject = "MB Thread Message Subject");
		}
	}

}