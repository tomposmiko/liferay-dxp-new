@component-name = "portal-collaboration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Asset Sharing";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea@liferay.com",
			userScreenName = "usersn");

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			DMDocument.tearDownCP();

			DMFolder.tearDownCP();

			User.logoutPG();
		}
	}

	@description = "This test asserts that users get the proper notification based on sharing permissions granted to him, also covers LPS-94448"
	@priority = "4"
	test AssertNotifications {
		DMDocumentSharing.addDocumentWithOnlyOwnerPermissions(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title 1");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title 1");

		DMDocument.sharePG(
			noAutocomplete = "true",
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocumentSharing.addDocumentWithOnlyOwnerPermissions(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.png",
			dmDocumentTitle = "DM Document Title 2");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title 2");

		DMDocument.sharePG(
			sharingPermissions = "Update",
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocumentSharing.addDocumentWithOnlyOwnerPermissions(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.odt",
			dmDocumentTitle = "DM Document Title 3");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title 3");

		DMDocument.sharePG(
			sharingPermissions = "Comment",
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Notifications.viewBadgeCount(notificationCount = "3");

		Notifications.gotoNotifications();

		Notifications.viewNewSharedContent(
			contentSharingPermission = "viewing",
			contentTitle = "DM Document Title 1",
			ownerName = "Test Test");

		Notifications.gotoNotifications();

		Notifications.viewNewSharedContent(
			contentSharingPermission = "updating",
			contentTitle = "DM Document Title 2",
			openNotification = "true",
			ownerName = "Test Test");

		NotificationsPortlet.configureNotificationPG(menuItem = "Edit");

		IFrame.selectModalFrame();

		DMDocument.editCmd(dmDocumentTitleEdit = "DM Document Title");

		Button.clickPublish();

		Notifications.gotoNotifications();

		Notifications.viewNewSharedContent(
			contentSharingPermission = "commenting",
			contentTitle = "DM Document Title 3",
			ownerName = "Test Test");
	}

	@description = "This test covers LPS-94858. It asserts deleted entry displays in a user's Shared Content using share button from info panel."
	@priority = "4"
	test AssertSingleDeleteEntry {
		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocumentSharing.addDocumentWithOnlyOwnerPermissions(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title");

		DMNavigator.gotoShareInfoPanel(dmDocumentTitle = "DM Document Title");

		DMDocument.sharePG(
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.deletePG(dmDocumentTitle = "DM Document Title");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Notifications.viewBadgeCount(notificationCount = "1");

		Notifications.gotoNotifications();

		AssertTextEquals(
			locator1 = "Sharing#NOTIFICATIONS_DELETE_ENTRY_TITLE",
			value1 = "Notification no longer applies.");
		AssertTextEquals(
			locator1 = "Sharing#NOTIFICATIONS_DELETE_ENTRY_CONTENT",
			value1 = "Notification for Sharing was deleted.");

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "Shared Content");

		AssertElementPresent(
			key_status = "Not Visible",
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_STATUS");
		AssertElementNotPresent(
			key_contentTitle = "DM Document Title",
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_OPTIONS");
		AssertElementNotPresent(
			key_contentTitle = "DM Document Title",
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_TITLE");
	}

	@description = "This test asserts only one entry displays in a user's Shared Content app when multiple users share the same document to him."
	@priority = "4"
	test AssertSingleEntry {
		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.assignRegularRoleCP(
			roleTitle = "Administrator",
			userScreenName = "usersn");

		JSONUser.addUser(
			userEmailAddress = "userea2@liferay.com",
			userFirstName = "userfn2",
			userLastName = "userln2",
			userScreenName = "usersn2");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea2@liferay.com",
			userScreenName = "usersn2");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocumentSharing.addDocumentWithOnlyOwnerPermissions(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title");

		DMDocument.sharePG(
			userEmailAddress = "userea2@liferay.com",
			userName = "userfn2 userln2");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title");

		DMDocument.sharePG(
			userEmailAddress = "userea2@liferay.com",
			userName = "userfn2 userln2");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea2@liferay.com");

		Notifications.viewBadgeCount(notificationCount = "2");

		Notifications.gotoNotifications();

		Notifications.viewNewSharedContent(
			contentSharingPermission = "viewing",
			contentTitle = "DM Document Title",
			openNotification = "true",
			ownerName = "Test Test");

		Navigator.openURL();

		Notifications.viewBadgeCount(notificationCount = "1");

		Notifications.gotoNotifications();

		Notifications.viewNewSharedContent(
			contentSharingPermission = "viewing",
			contentTitle = "DM Document Title",
			openNotification = "true",
			ownerName = "userfn userln");

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "Shared Content");

		var key_contentTitle = "DM Document Title";

		AssertTextEquals(
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_TITLE",
			value1 = "DM Document Title");
		AssertElementNotPresent(
			key_contentTitle = "DM Document Title",
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_TITLE_2");
	}

	@description = "This test ensures that users receives an email when a document is shared with them. This email contains a link that navigates user to Shared Content app."
	@priority = "4"
	test GoToSharedContentViaEmail {
		property custom.properties = "message.boards.message.formats.default=html${line.separator}pop.server.notifications.enabled=true${line.separator}pop.server.subdomain=${line.separator}mail.session.mail.smtp.auth=true${line.separator}mail.session.mail.store.protocol=pop3${line.separator}mail.session.mail.transport.protocol=smtp";
		property dummy.socket.proxy.disabled = "true";
		property test.name.skip.portal.instance = "DMSharing#GoToSharedContentViaEmail";
		var portalURL = PropsUtil.get("portal.url");

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		Gmail.login(
			userEmailAddress = PropsUtil.get("email.address.3"),
			userPassword = PropsUtil.get("email.password.3"));

		Gmail.gotoHtmlMode();

		Gmail.deleteSpecificMail(gmailMessageTitleList = "Test Test has shared DM Document Title with you for viewing.,Your New Account,Your New Password");

		Navigator.openSpecificURL(url = "${portalURL}");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Server Administration");

		ServerAdministration.configureMailServerSettings(
			needsReply = "true",
			userName = PropsUtil.get("email.address.2"),
			userPassword = PropsUtil.get("email.password.2"));

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.editCP(
			mailDomain = "gmail.com",
			virtualHost = "localhost");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		JSONUser.addUser(
			userEmailAddress = PropsUtil.get("email.address.3"),
			userFirstName = "userfn1",
			userLastName = "userln1",
			userScreenName = "usersn1");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = PropsUtil.get("email.address.3"),
			userScreenName = "usersn1");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocumentSharing.addDocumentWithOnlyOwnerPermissions(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title");

		DMDocument.sharePG(
			userEmailAddress = PropsUtil.get("email.address.3"),
			userName = "userfn1 userln1");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = PropsUtil.get("email.address.3"));

		Pause(locator1 = "30000");

		Gmail.gotoHtmlMode();

		Gmail.viewMail(
			emailFromUserName = "Joe Bloggs",
			gmailAssetType = "DM Document Title",
			gmailMessageBody = "Test Test has shared DM Document Title with you for viewing.",
			gmailMessageTitle = "Test Test has shared DM Document Title with you for viewing.");

		AssertClick(
			locator1 = "Sharing#EMAIL_VIEW_DOCUMENT_BUTTON",
			value1 = "View Document");

		SelectWindow(locator1 = "title=- ${siteName}");

		DMDocumentSharing.viewDocumentViaSharedContent(
			contentSharingPermission = "View",
			dmDocumentTitle = "DM Document Title");
	}

	@description = "This test ensures that users receive a notification in portal when a document is shared to them, and that clicking on that notification navigates them to the Shared Content app."
	@priority = "4"
	test GoToSharedContentViaNotifications {
		DMDocumentSharing.addDocumentWithOnlyOwnerPermissions(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title");

		DMDocument.sharePG(
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Notifications.viewBadgeCount(notificationCount = "1");

		Notifications.gotoNotifications();

		Notifications.viewNewSharedContent(
			contentSharingPermission = "viewing",
			contentTitle = "DM Document Title",
			openNotification = "true",
			ownerName = "Test Test");

		DMDocumentSharing.viewDocumentViaSharedContent(
			contentSharingPermission = "View",
			dmDocumentTitle = "DM Document Title");
	}

	@description = "This test covers LPS-94294. It ensures that sharing a document to a user, and changing its sharing permissions via Manage Collaborators. This test also covers permissions and making sure users only have access to sharing permissions assigned to him."
	@priority = "5"
	test ManageCollaborators {
		DMDocumentSharing.addDocumentWithOnlyOwnerPermissions(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title");

		DMDocument.sharePG(
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.viewPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title");

		AssertElementNotPresent(locator1 = "Comments#COMMENT_FIELD");

		AssertElementNotPresent(locator1 = "Button#EDIT");

		User.logoutPG();

		User.loginPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocumentSharing.changePermission(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			selectPermission = "Can Comment",
			userName = "userfn userln");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocumentComment.addPG(
			commentBody = "DM Document Comment Body",
			dmDocumentTitle = "DM Document Title");

		AssertElementNotPresent(locator1 = "Button#EDIT");

		User.logoutPG();

		User.loginPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocumentSharing.changePermission(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			selectPermission = "Can Update",
			userName = "userfn userln");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.editPG(
			dmDocumentDescriptionEdit = "DM Document Description Edited",
			dmDocumentTitle = "DM Document Title",
			dmDocumentTitleEdit = "DM Document Title Edited");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.viewPG(
			dmDocumentDescription = "DM Document Description Edited",
			dmDocumentTitle = "DM Document Title Edited");
	}

	@description = "This test covers LPS-101927. It ensures that a user can reply to a comment on an image via shared content"
	@priority = "4"
	test ReplyToCommentViaSharedContent {
		JSONUser.addUser(
			userEmailAddress = "userea2@liferay.com",
			userFirstName = "userfn2",
			userLastName = "userln2",
			userScreenName = "usersn2");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea2@liferay.com",
			userScreenName = "usersn2");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocumentSharing.addDocumentWithOnlyOwnerPermissions(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.jpg",
			dmDocumentTitle = "DM Document Title");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title");

		DMDocument.sharePG(
			sharingPermissions = "Comment",
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title");

		DMDocument.sharePG(
			sharingPermissions = "Comment",
			userEmailAddress = "userea2@liferay.com",
			userName = "userfn2 userln2");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "Shared Content");

		AssertClick(
			key_contentTitle = "DM Document Title",
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_TITLE",
			value1 = "DM Document Title");

		Click(locator1 = "Comments#COMMENT_FIELD");

		Type(
			locator1 = "Comments#COMMENT_FIELD",
			value1 = "Userea Comment");

		AssertClick(
			locator1 = "Button#REPLY",
			value1 = "Reply");

		Alert.viewSuccessMessage();

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea2@liferay.com");

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "Shared Content");

		AssertClick(
			key_contentTitle = "DM Document Title",
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_TITLE",
			value1 = "DM Document Title");

		Click(locator1 = "Sharing#SHARED_CONTENT_REPLY_TO_COMMENT");

		Type(
			locator1 = "Comments#COMMENT_FIELD_LAST",
			value1 = "Userea2 Reply To Comment");

		AssertClick(
			locator1 = "Button#REPLY_LAST",
			value1 = "Reply");

		Alert.viewSuccessMessage();
	}

	@description = "This test covers LPS-94284. It ensures that the invalid email still does not add if it has a space."
	@priority = "4"
	test ShareViaInvalidAddress {
		DMDocumentSharing.addDocumentWithOnlyOwnerPermissions(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title 1");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title 1");

		IFrame.selectModalFrame();

		Type.sendKeys(
			locator1 = "Sharing#MODAL_INVITE_TO_COLLABORATE_INPUT",
			value1 = "userea2@liferay.com ");

		KeyPress(
			locator1 = "Sharing#MODAL_INVITE_TO_COLLABORATE_INPUT",
			value1 = "\RETURN");

		AssertElementPresent(
			key_userEmailAddress = "userea2@liferay.com",
			locator1 = "Sharing#MODAL_INVITE_TO_COLLABORATE_FEEDBACK_ITEM");

		var key_userName = "userea2@liferay.com";

		AssertElementNotPresent(locator1 = "Sharing#MODAL_INVITE_TO_COLLABORATE_TAG");
	}

	@description = "This test covers LPS-94585. It ensures that users can share via Shared Content app."
	@priority = "5"
	test ShareViaSharedContent {
		JSONUser.addUser(
			userEmailAddress = "userea2@liferay.com",
			userFirstName = "userfn2",
			userLastName = "userln2",
			userScreenName = "usersn2");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea2@liferay.com",
			userScreenName = "usersn2");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocumentSharing.addDocumentWithOnlyOwnerPermissions(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title");

		DMDocument.sharePG(
			shareable = "true",
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "Shared Content");

		DMNavigator.gotoShareViaSharedContent(dmDocumentTitle = "DM Document Title");

		DMDocument.shareWithNoSharingPermissionsUser(userEmailAddress = "userea2@liferay.com");

		AssertConsoleTextNotPresent(value1 = "ArrayIndexOutOfBoundsException");

		User.logoutPG();

		User.loginPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.assignRegularRoleCP(
			roleTitle = "Administrator",
			userScreenName = "usersn");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "Shared Content");

		DMNavigator.gotoShareViaSharedContent(dmDocumentTitle = "DM Document Title");

		IFrame.selectModalFrame();

		Type(
			locator1 = "Sharing#MODAL_INVITE_TO_COLLABORATE_INPUT",
			value1 = "userea2@liferay.com");

		AssertElementPresent(locator1 = "Sharing#MODAL_INVITE_TO_COLLABORATE_DROPDOWN");

		User.logoutPG();

		User.loginPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.UnassignRegularRoleCP(
			regularRoleName = "Administrator",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "People",
			panel = "Site Administration",
			portlet = "Memberships");

		Site.assignUserAsMemberCP(
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		Site.assignUserAsMemberCP(
			userFirstName = "userfn2",
			userLastName = "userln2",
			userScreenName = "usersn2");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "Shared Content");

		DMNavigator.gotoShareViaSharedContent(dmDocumentTitle = "DM Document Title");

		DMDocument.sharePG(
			shareable = "true",
			userEmailAddress = "userea2@liferay.com",
			userName = "userfn2 userln2");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea2@liferay.com");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.viewPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			dmDocumentUploader = "Test Test");
	}

	@description = "This test share an content to a user and disable the sharing option. This test asserts that a collaborator without sharing permissions will not be able to share this content."
	@priority = "5"
	test ShareWithNoSharingPermissions {
		JSONUser.addUser(
			userEmailAddress = "userea1@liferay.com",
			userFirstName = "userfn1",
			userLastName = "userln1",
			userScreenName = "usersn1");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea1@liferay.com",
			userScreenName = "usersn1");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocumentSharing.addDocumentWithOnlyOwnerPermissions(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title");

		DMDocument.sharePG(
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		UserBar.gotoDropdownItem(dropdownItem = "Shared Content");

		var key_contentTitle = "DM Document Title";
		var key_dmDocumentTitle = "DM Document Title";

		Click(locator1 = "Sharing#SHARED_CONTENT_CONTENT_OPTIONS");

		MenuItem.viewNotPresent(menuItem = "Share");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_VERTICAL_ELLIPSIS");

		MenuItem.viewNotPresent(menuItem = "Share");
	}

	@description = "This test covers LPS-94661. It ensures that the user can share a document with an apostrophe in the title."
	@priority = "3"
	test ShareWithSpecialCharacter {
		property test.name.skip.portal.instance = "DMSharing#ShareWithSpecialCharacter";

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document&apos;s Title",
			viewableBy = "Owner");

		Click(
			key_dmDocumentTitle = "DM Document&apos;s Title",
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_VERTICAL_ELLIPSIS_SPECIFIC");

		ClickNoError(
			key_menuItem = "Permissions",
			locator1 = "MenuItem#ANY_MENU_ITEM");

		Role.removePermissionPG(
			permissionDefinitionKey = "INLINE_PERMISSIONS_ADD_DISCUSSION_CHECKBOX",
			roleTitle = "Guest");

		IFrame.closeFrame();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		Click(
			key_dmDocumentTitle = "DM Document&apos;s Title",
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_VERTICAL_ELLIPSIS_SPECIFIC");

		ClickNoError(
			key_menuItem = "Share",
			locator1 = "MenuItem#ANY_MENU_ITEM");

		DMDocument.sharePG(
			noAutocomplete = "true",
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Notifications.viewBadgeCount(notificationCount = "1");

		Notifications.gotoNotifications();

		Notifications.viewNewSharedContent(
			contentSharingPermission = "viewing",
			contentTitle = "DM Document&apos;s Title",
			ownerName = "Test Test",
			specialCharacter = "true");
	}

	@description = "This test makes sure all collaborators will view all collaborators of the document that was shared to him. This usecase also tests that a user can successfully remove collaborators through Manage Collaborators and asserts that empty message displays when all collaborators are removed."
	@priority = "5"
	test ViewAndRemoveCollaborators {
		JSONUser.addUser(
			userEmailAddress = "userea2@liferay.com",
			userFirstName = "userfn2",
			userLastName = "userln2",
			userScreenName = "usersn2");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea2@liferay.com",
			userScreenName = "usersn2");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocumentSharing.addDocumentWithOnlyOwnerPermissions(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title");

		DMDocument.sharePG(
			noAutocomplete = "true",
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoShare(dmDocumentTitle = "DM Document Title");

		DMDocument.sharePG(
			userEmailAddress = "userea2@liferay.com",
			userName = "userfn2 userln2");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.viewPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title");

		DMDocumentSharing.viewCollaborators(collaboratorList = "userfn,userfn2");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea2@liferay.com");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.viewPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			dmDocumentUploader = "Test Test");

		var key_collaborator = "userfn";
		var key_text = "Manage Collaborators";

		AssertVisible(
			locator1 = "DocumentsAndMediaDocument#DOCUMENT_INFO_COLLABORATOR_TOOLTIP",
			value1 = "userfn");

		AssertElementNotPresent(
			locator1 = "Button#ANY",
			value1 = "Manage Collaborators");

		User.logoutPG();

		User.loginPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.viewPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			dmDocumentUploader = "Test Test");

		DMDocumentSharing.removeCollaborator(
			collaboratorList = "userfn,userfn2",
			emptyStateMessage = "true");
	}

}