@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Asset Publisher";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Asset Publisher Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Asset Publisher Page",
			widgetName = "Asset Publisher");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureAssetLinkBehaviorPG(assetLinkBehavior = "Show Full Content");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.firstLoginPG();

			JSONGroup.deleteGroupByName(groupName = "Test Site Name");

			Site.tearDownCP();
		}
	}

	@priority = "3"
	test AddAndRemoveCustomTitleViaAP {
		Portlet.gotoPortletOptions(portletOption = "Look and Feel Configuration");

		IFrame.selectConfigurationFrame();

		Portlet.configureCustomTitleViaLookandfeelPG(portletName = "Asset Publisher");

		PortletEntry.save();

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Portlet.viewSpecificPG(portletName = "Asset Publisher Custom");

		Portlet.gotoPortletOptions(portletOption = "Look and Feel Configuration");

		IFrame.selectConfigurationFrame();

		Portlet.selectApplicationDecorator(decorator = "Barebone");

		PortletEntry.save();

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Portlet.viewNoSpecificPG(portletName = "Asset Publisher Custom");
	}

	@priority = "5"
	test AddAPMultiplePortlet {
		property portal.acceptance = "true";
		property testray.component.names = "Asset Publisher,Training";

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		var i = "0";

		while ("${i}" != "3") {
			var i = ${i} + 1;

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Asset Publisher Page",
				widgetName = "Asset Publisher");
		}

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Portlet.viewDuplicatePG(
			portletName = "Asset Publisher",
			totalPortletCount = "4");
	}

	@description = "This is a use case for LPS-47619 and LPS-67326."
	@priority = "5"
	test AddAssetTypesViaAPAndAddRespectivePortletsToSamePage {
		property plugins.deployment.type = "osgi";
		property portal.acceptance = "true";

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Blogs.addViaAP(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		BlogsEntry.viewEntryViaAPPG(
			assetTitle = "Blogs Entry Title",
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		CalendarEvent.addPGViaAP(eventTitle = "Calendar Event Title");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		CalendarEvent.viewViaAssetPublisher(
			calendarName = "Test Site Name",
			eventTitle = "Calendar Event Title");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		DMDocument.addPGViaAPWithValidationPosition(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		DMDocument.viewPGViaAP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title");

		Navigator.openSiteURL(siteName = "Test Site Name");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Dynamic Data Lists");

		DDLDataDefinition.addCP(ddlDataDefinitionName = "Data Definition");

		DynamicDataMapping.addField(
			field = "Text",
			fieldFieldLabel = "Text",
			fieldName = "Text");

		DDLDataDefinition.save();

		Navigator.openSiteURL(siteName = "Test Site Name");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Dynamic Data Lists");

		DDLList.addCP(ddlListName = "List Name");

		DDLDataDefinition.selectCP(ddlDataDefinitionName = "Data Definition");

		DDLDataDefinition.save();

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		DDLRecord.addPGViaAP(
			ddlRecordFieldData = "Text Field",
			fieldName = "Text");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		DDLRecord.viewPGViaAP(
			ddlListName = "New Data Definition for List: List Name",
			ddlRecordFieldData = "Text Field");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		KBArticle.addPGViaAP(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		KBArticle.viewPGViaAP(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		WebContent.addPGViaAP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC Webcontent Title");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		WebContent.viewPGViaAP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC Webcontent Title");

		for (var portletName : list "Blogs,Calendar,Documents and Media,Dynamic Data Lists Display,Knowledge Base Article,Web Content Display") {
			Navigator.gotoSitePage(
				pageName = "Asset Publisher Page",
				siteName = "Test Site Name");

			Portlet.addPG(portletName = "${portletName}");
		}
	}

	@priority = "4"
	test AddBookmarkViaAP {
		property portal.release = "false";
		property testray.component.names = "Asset Publisher,Bookmarks";

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Bookmarks Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Bookmarks Page",
			widgetName = "Bookmarks");

		JSONBookmarks.addFolder(
			folderDescription = "Test Folder Description",
			folderName = "Test Folder Name",
			groupName = "Test Site Name");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Bookmark.addBookmarkViaAP(
			bookmarkDescription = "Test Bookmark Description",
			bookmarkFolderName = "Test Folder Name",
			bookmarkName = "Test Bookmark Name",
			bookmarkURL = "https://www.liferay.com");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Bookmark.viewBookmarkViaAP(bookmarkName = "Test Bookmark Name");
	}

	@priority = "3"
	test AddCustomTitleViaAP {
		property testray.component.names = "Asset Publisher";

		Portlet.gotoPortletOptions(portletOption = "Look and Feel Configuration");

		IFrame.selectConfigurationFrame();

		Portlet.configureCustomTitleViaLookandfeelPG(portletName = "Asset Publisher");

		PortletEntry.save();

		Portlet.configureApplicationDecoratorViaLookandfeelPG(applicationDecorator = "Decorate");

		PortletEntry.save();

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Portlet.viewSpecificPG(portletName = "Asset Publisher Custom");
	}

	@priority = "4"
	test ConfigureAPPortletDisplaytemplateWithBookmarks {
		property portal.release = "false";
		property testray.component.names = "Asset Publisher,Bookmarks,Content and Display Templates,Training";

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Bookmarks Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Bookmarks Page",
			widgetName = "Bookmarks");

		JSONBookmarks.addFolder(
			folderDescription = "Test Folder Description",
			folderName = "Test Folder Name",
			groupName = "Test Site Name");

		JSONBookmarks.addBookmarks(
			description = "Test Bookmark Description",
			groupName = "Test Site Name",
			name = "Test Bookmark Name",
			url = "https://www.liferay.com");

		User.firstLoginPG();

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Portlet.gotoPortletOptions(
			portletName = "Asset Publisher",
			portletOption = "Configuration");

		AssetPublisherPortlet.configureDisplayTemplatePG(displayTemplate = "Full Content");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Portlet.gotoPortletOptions(
			portletName = "Asset Publisher",
			portletOption = "Configuration");

		AssetPublisherPortlet.configureDisplayTemplatePG(displayTemplate = "Table");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewConfigureAPDisplayTemplateTableBookmarksPG(
			assetTitle = "Test Bookmark Name",
			bookmarkDescription = "Test Bookmark Description",
			bookmarkName = "Test Bookmark Name");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Portlet.gotoPortletOptions(
			portletName = "Asset Publisher",
			portletOption = "Configuration");

		AssetPublisherPortlet.configureDisplayTemplatePG(displayTemplate = "Title List");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewConfigureAPDisplayTemplateTitleListBookmarksPG(
			assetTitle = "Test Bookmark Name",
			bookmarkDescription = "Test Bookmark Description",
			bookmarkName = "Test Bookmark Name");
	}

	@priority = "4"
	test ConfigureAPPortletEnableRatings {
		property testray.component.names = "Asset Publisher";

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureDisplaySettings(displaySettings = "Ratings");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewDisplaySettings(displaySettings = "Ratings");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Site Name",
			title = "WC WebContent Title");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Portlet.gotoPortletOptions(
			portletName = "Asset Publisher",
			portletOption = "Configuration");

		AssetPublisherPortlet.configureDisplayTemplatePG(displayTemplate = "Full Content");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Ratings.rateStar(ratingStarCount = "4");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Ratings.viewRateStar(
			averageRatingStarCount = "4.0",
			ratingStarCount = "4");
	}

	@priority = "5"
	test ConfigureAPPortletScopeMultipleSites {
		property portal.acceptance = "true";
		property testray.component.names = "Asset Publisher,Training";

		JSONGroup.addGroup(groupName = "Site Name 2");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Asset Publisher Page",
			widgetName = "Asset Publisher");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureAddSiteScopes(otherSite = "Site Name 2");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewMultipleSiteScopesAddDropdowns(
			siteName1 = "Test Site Name",
			siteName2 = "Site Name 2");
	}

	@priority = "4"
	test ConfigureAPPortletSelectedBlogsEntry {
		property testray.component.names = "Asset Publisher";

		AssetPublisherPortlet.configureSelectedPG(selectedAsset = "Blogs Entry");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewSelectedPG(
			selectedAsset = "Blogs Entry",
			selectedAssetConfiguration = "Blogs Entry");
	}

	@priority = "4"
	test ConfigureAPPortletSelectedBookmarksEntry {
		property portal.release = "false";
		property testray.component.names = "Asset Publisher";

		AssetPublisherPortlet.configureSelectedPG(selectedAsset = "Bookmarks Entry");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewSelectedPG(
			selectedAsset = "Bookmarks Entry",
			selectedAssetConfiguration = "Bookmarks Entry");
	}

	@priority = "4"
	test ConfigureAPPortletSelectedDocument {
		property testray.component.names = "Asset Publisher";

		AssetPublisherPortlet.configureSelectedPG(selectedAsset = "Document");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewSelectedPG(
			selectedAsset = "Basic Document",
			selectedAssetConfiguration = "Document");
	}

	@priority = "5"
	test ConfigureAPPortletSelectedWebContent {
		property portal.acceptance = "true";
		property testray.component.names = "Asset Publisher,Training";

		AssetPublisherPortlet.configureAssetTypePG(
			assetSubtype = "Basic Web Content",
			selectedAsset = "Web Content Article");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewSelectedPG(
			assetSubtypeConfiguration = "Basic Web Content",
			selectedAsset = "Basic Web Content",
			selectedAssetConfiguration = "Web Content Article");
	}

	@priority = "4"
	test ConfigureAPPortletSetAsDefault {
		property testray.component.names = "Asset Publisher";

		AssetPublisherPortlet.configureDisplaySettings(displaySettings = "Set as the Default Asset Publisher for This Page");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewDisplaySettings(displaySettings = "Set as the Default Asset Publisher for This Page");
	}

	@priority = "4"
	test ConfigureAPPortletWithSelectedBookmarksEntry {
		property portal.release = "false";
		property testray.component.names = "Asset Publisher,Bookmarks";

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Bookmarks Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Bookmarks Page",
			widgetName = "Bookmarks");

		JSONBookmarks.addFolder(
			folderDescription = "Test Folder Description",
			folderName = "Test Folder Name",
			groupName = "Test Site Name");

		JSONBookmarks.addBookmarks(
			description = "Test Bookmark Description",
			groupName = "Test Site Name",
			name = "Test Bookmark Name",
			url = "https://www.liferay.com");

		User.firstLoginPG();

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureAPSelectMoreThanOnePG(selectContent = "Bookmarks Entry");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewConfigureAPMoveToAvailablePG(assetTitle = "Test Bookmark Name");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureAPSelectMoreThanOnePG(
			moveSelected = "true",
			selectContent = "Bookmarks Entry");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewConfigureAPMoveToSelectedPG(assetTitle = "Test Bookmark Name");
	}

	@priority = "4"
	test FilterTagsByNameAP {
		property testray.component.names = "Asset Publisher,Tags";

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Tags Navigation Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Tags Navigation Page",
			widgetName = "Tags Navigation");

		Navigator.gotoSitePage(
			pageName = "Tags Navigation Page",
			siteName = "Test Site Name");

		TagsNavigationPortlet.configureDisplayStylePG(displayTemplate = "Number");

		Navigator.gotoSitePage(
			pageName = "Tags Navigation Page",
			siteName = "Test Site Name");

		TagsNavigationPortlet.configureFlaggableSettingsPG(showAssetCount = "true");

		IFrame.closeFrame();

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Tags Navigation Page",
			widgetName = "Asset Publisher");

		JSONWebcontent.addWebContent(
			assetTagNames = "first tag,second tag",
			content = "Web Content Content",
			groupName = "Test Site Name",
			title = "Web Content Title");

		JSONDocument.addFileWithUploadedFile(
			assetTagNames = "first tag",
			dmDocumentTitle = "DM Document Title",
			groupName = "Test Site Name",
			mimeType = "text/plain",
			sourceFileName = "Document_1.txt");

		Navigator.gotoSitePage(
			pageName = "Tags Navigation Page",
			siteName = "Test Site Name");

		TagsNavigationPortlet.clickTagView(
			tagAssetCount = "first tag (2)",
			tagName = "first tag");

		AssetPublisherPortlet.viewTagFocusPG(assetTitleList = "DM Document Title,Web Content Title");

		Navigator.gotoSitePage(
			pageName = "Tags Navigation Page",
			siteName = "Test Site Name");

		TagsNavigationPortlet.clickTagView(
			tagAssetCount = "second tag (1)",
			tagName = "second tag");

		AssetPublisherPortlet.viewTagFocusPG(
			assetTitleList = "Web Content Title",
			nonTaggedAsset = "DM Document Title");
	}

	@priority = "4"
	test FindSiteAPViaSearchScopeSelector {
		property testray.component.names = "Asset Publisher";

		JSONGroup.addGroup(groupName = "Site Name 2");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.assertSiteScopeSearch(siteName = "Site Name 2");
	}

	@priority = "3"
	test OrderContentByModifiedDateViaAP {
		property testray.component.names = "Asset Publisher";

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureAssetDisplayOrderPG(
			fieldNameOrder = "Modified Date",
			sortOrder = "Ascending");

		JSONWebcontent.addWebContent(
			content = "WebContent Content 1",
			groupName = "Test Site Name",
			title = "WebContent Title 1");

		JSONWebcontent.addWebContent(
			content = "WebContent Content 2",
			groupName = "Test Site Name",
			title = "WebContent Title 2");

		JSONWebcontent.addWebContent(
			content = "WebContent Content 3",
			groupName = "Test Site Name",
			title = "WebContent Title 3");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		var n = "1";

		for (var title : list "WebContent Title 1,WebContent Title 2,WebContent Title 3") {
			AssetPublisherPortlet.viewDisplayOrderPG(
				assetTitle = "${title}",
				rowNumber = "${n}");

			var n = ${n} + 1;
		}

		AssetPublisherPortlet.gotoEditAssetPG(assetTitle = "WebContent Title 1");

		WebContent.addCP(
			webContentContent = "WebContent Content 1 Edit",
			webContentTitle = "WebContent Title 1 Edit");

		Button.clickPublish();

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		var n = "1";

		for (var title : list "WebContent Title 2,WebContent Title 3,WebContent Title 1 Edit") {
			AssetPublisherPortlet.viewDisplayOrderPG(
				assetTitle = "${title}",
				rowNumber = "${n}");

			var n = ${n} + 1;
		}
	}

	@priority = "3"
	test OrderWCByModifiedDateViaAP {
		property testray.component.names = "Asset Publisher";

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureAssetTypePG(
			assetSubtype = "Any",
			selectedAsset = "Web Content Article");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureAssetDisplayOrderPG(
			fieldNameOrder = "Modified Date",
			sortOrder = "Ascending");

		JSONWebcontent.addWebContent(
			content = "WebContent Content 1",
			groupName = "Test Site Name",
			title = "WebContent Title 1");

		JSONWebcontent.addWebContent(
			content = "WebContent Content 2",
			groupName = "Test Site Name",
			title = "WebContent Title 2");

		JSONWebcontent.addWebContent(
			content = "WebContent Content 3",
			groupName = "Test Site Name",
			title = "WebContent Title 3");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		var n = "1";

		for (var title : list "WebContent Title 1,WebContent Title 2,WebContent Title 3") {
			AssetPublisherPortlet.viewDisplayOrderPG(
				assetTitle = "${title}",
				rowNumber = "${n}");

			var n = ${n} + 1;
		}

		AssetPublisherPortlet.gotoEditAssetPG(assetTitle = "WebContent Title 1");

		WebContent.addCP(
			webContentContent = "WebContent Content 1 Edit",
			webContentTitle = "WebContent Title 1 Edit");

		Button.clickPublish();

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		var n = "1";

		for (var title : list "WebContent Title 2,WebContent Title 3,WebContent Title 1 Edit") {
			AssetPublisherPortlet.viewDisplayOrderPG(
				assetTitle = "${title}",
				rowNumber = "${n}");

			var n = ${n} + 1;
		}
	}

	@priority = "4"
	test OrderWCByPriorityViaAP {
		property testray.component.names = "Asset Publisher";

		for (var i : list "1,5,3") {
			ProductMenu.gotoPortlet(
				category = "Content & Data",
				portlet = "Web Content");

			WebContentNavigator.gotoAddCP();

			WebContent.addCP(
				webContentContent = "WC WebContent Content",
				webContentTitle = "WC Priority ${i} Title");

			WebContent.addPriority(priority = "${i}");

			PortletEntry.publish();
		}

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewDisplayOrderPG(
			assetTitle = "WC Priority 3 Title",
			rowNumber = "1");

		AssetPublisherPortlet.viewDisplayOrderPG(
			assetTitle = "WC Priority 5 Title",
			rowNumber = "2");

		AssetPublisherPortlet.viewDisplayOrderPG(
			assetTitle = "WC Priority 1 Title",
			rowNumber = "3");

		AssetPublisherPortlet.configureAssetTypePG(
			assetSubtype = "Any",
			selectedAsset = "Web Content Article");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureAssetDisplayOrderPG(
			fieldNameOrder = "Priority",
			sortOrder = "Ascending");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewDisplayOrderPG(
			assetTitle = "WC Priority 1 Title",
			rowNumber = "1");

		AssetPublisherPortlet.viewDisplayOrderPG(
			assetTitle = "WC Priority 3 Title",
			rowNumber = "2");

		AssetPublisherPortlet.viewDisplayOrderPG(
			assetTitle = "WC Priority 5 Title",
			rowNumber = "3");

		AssetPublisherPortlet.configureAssetDisplayOrderPG(
			fieldNameOrder = "Priority",
			sortOrder = "Descending");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewDisplayOrderPG(
			assetTitle = "WC Priority 5 Title",
			rowNumber = "1");

		AssetPublisherPortlet.viewDisplayOrderPG(
			assetTitle = "WC Priority 3 Title",
			rowNumber = "2");

		AssetPublisherPortlet.viewDisplayOrderPG(
			assetTitle = "WC Priority 1 Title",
			rowNumber = "3");
	}

	@priority = "3"
	test OrderWCByPublishDateViaAP {
		property testray.component.names = "Asset Publisher,Training";

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureAssetTypePG(
			assetSubtype = "Any",
			selectedAsset = "Web Content Article");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureAssetDisplayOrderPG(
			fieldNameOrder = "Publish Date",
			sortOrder = "Ascending");

		JSONWebcontent.addWebContent(
			content = "WebContent Content 1",
			groupName = "Test Site Name",
			title = "WebContent Title 1");

		Pause(locator1 = "65000");

		JSONWebcontent.addWebContent(
			content = "WebContent Content 2",
			groupName = "Test Site Name",
			title = "WebContent Title 2");

		Pause(locator1 = "65000");

		JSONWebcontent.addWebContent(
			content = "WebContent Content 3",
			groupName = "Test Site Name",
			title = "WebContent Title 3");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		var n = "1";

		for (var title : list "WebContent Title 1,WebContent Title 2,WebContent Title 3") {
			AssetPublisherPortlet.viewDisplayOrderPG(
				assetTitle = "${title}",
				rowNumber = "${n}");

			var n = ${n} + 1;
		}

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Portlet.configureCustomTitlePG(portletTitleName = "Asset Publisher");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Portlet.viewSpecificPG(portletName = "Asset Publisher Custom");
	}

	@priority = "5"
	test RestoreAPArchivedSetup {
		property portal.acceptance = "true";
		property testray.component.names = "Asset Publisher,Training";

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Site Name",
			title = "WC WebContent Title");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureManualAssetSelectionPG();

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.saveConfigurationTemplatePG(configurationName = "AP Manual Display");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		Portlet.deletePG(portletName = "Asset Publisher");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Asset Publisher Page",
			widgetName = "Asset Publisher");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.restoreConfigurationTemplatePG(configurationName = "AP Manual Display");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.viewManualNoAssetsMessagePG();

		AssetPublisherPortlet.viewRestoredSetupPG();

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.deleteConfigurationTemplatePG(configurationName = "AP Manual Display");
	}

}