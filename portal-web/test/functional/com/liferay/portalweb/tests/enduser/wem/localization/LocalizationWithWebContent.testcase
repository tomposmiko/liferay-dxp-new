@component-name = "portal-wcm"
definition {
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Web Content Administration";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		ProductMenu.gotoPortlet(
			category = "Build",
			panel = "Site Administration",
			portlet = "Pages"
		);

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page Name"
		);
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@priority = "4"
	test AddWCWithMultipleTranslations {
		property test.name.skip.portal.instance = "LocalizationWithWebContent#AddWCWithMultipleTranslations";
		property testray.component.names = "Localization,Web Content Administration";

		WebContent.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithThreeTranslations();

		PortletEntry.publish();

		Navigator.gotoSitePage(pageName = "Test Page Name", siteName = "Test Site Name");

		Portlet.addPG(portletName = "Web Content Display");

		WebContentDisplayPortlet.selectWebContent(webContentTitle = "WC Title");

		Navigator.gotoSitePage(pageName = "Test Page Name", siteName = "Test Site Name");

		Portlet.addPG(portletName = "Language Selector");

		Refresh();

		LanguagePortlet.selectLanguagePG(locale = "es_ES", textIcon = "español-España");

		Refresh();

		WebContent.viewPGViaWCD(webContentContent = "WC Contenido");

		WebContentNavigator.gotoEditPGViaWCD(
			menuItem = "Editar contenido web",
			webContentTitle = "WC Titulo"
		);

		PortletEntry.changeLocale(fieldName = "Título", locale = "es-ES");

		WebContent.viewCP(
			webContentContent = "WC Contenido",
			webContentTitle = "WC Titulo"
		);

		Navigator.gotoSitePage(pageName = "Test Page Name", siteName = "Test Site Name");

		Refresh();

		LanguagePortlet.selectLanguagePG(
			locale = "de-DE",
			textIcon = "Deutsch-Deutschland"
		);

		Refresh();

		WebContent.viewPGViaWCD(webContentContent = "WC Inhalt");

		WebContentNavigator.gotoEditPGViaWCD(
			menuItem = "Webcontent bearbeiten",
			webContentTitle = "WC Titel"
		);

		PortletEntry.changeLocale(fieldName = "Titel", locale = "de-DE");

		WebContent.viewCP(webContentContent = "WC Inhalt", webContentTitle = "WC Titel");

		Navigator.gotoSitePage(pageName = "Test Page Name", siteName = "Test Site Name");

		Refresh();

		LanguagePortlet.selectLanguagePG(locale = "fi-FI", textIcon = "suomi-Suomi");

		Refresh();

		WebContent.viewPGViaWCD(webContentContent = "WC Pitoisuus");

		WebContentNavigator.gotoEditPGViaWCD(
			menuItem = "Muokkaa web-sisältöä",
			webContentTitle = "WC Otsikko"
		);

		PortletEntry.changeLocale(fieldName = "Otsikko", locale = "fi-FI");

		WebContent.viewCP(
			webContentContent = "WC Pitoisuus",
			webContentTitle = "WC Otsikko"
		);
	}

	@priority = "5"
	test AddWCWithTranslation {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "LocalizationWithWebContent#AddWCWithTranslation";
		property testray.component.names = "Localization,Web Content Administration";

		WebContent.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(webContentContent = "WC Content", webContentTitle = "WC Title");

		PortletEntry.changeLocale(fieldName = "Title", locale = "es-ES");

		WebContent.addCP(
			webContentContent = "WC Contenido",
			webContentTitle = "WC Titulo"
		);

		PortletEntry.publish();

		Navigator.gotoSitePage(pageName = "Test Page Name", siteName = "Test Site Name");

		Content.addPG(assetTitle = "WC Title");

		Portlet.addPG(portletName = "Language Selector");

		LanguagePortlet.selectLanguagePG(locale = "es_ES", textIcon = "español-España");

		WebContent.viewPGViaWCD(webContentContent = "WC Contenido");

		WebContentNavigator.gotoEditPGViaWCD(
			menuItem = "Editar contenido web",
			webContentTitle = "WC Titulo"
		);

		PortletEntry.changeLocale(fieldName = "Título", locale = "es-ES");

		WebContent.viewCP(
			webContentContent = "WC Contenido",
			webContentTitle = "WC Titulo"
		);
	}

	@priority = "4"
	test ViewChangedDefaultPortalLanguageViaWebContent {
		property test.name.skip.portal.instance = "LocalizationWithWebContent#ViewChangedDefaultPortalLanguageViaWebContent";
		property testray.component.names = "Localization";

		PortalSettings.openInstanceSettingsAdmin();

		PortalSettings.configureCurrentLanguagesCP(
			defaultPortalLanguage = "Spanish (Spain)"
		);

		WebContent.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		PortletEntry.viewLocale(locale = "es-ES");
	}
}