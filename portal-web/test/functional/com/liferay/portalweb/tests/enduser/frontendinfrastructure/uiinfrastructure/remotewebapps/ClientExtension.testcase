definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Client Extensions";
	property testray.main.component.name = "Client Extensions";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Navigator.gotoPage(pageName = "Home");

			while (IsElementPresent(locator1 = "Navigation#ANY_SITE_NAV_BUT_HOME")) {
				var layoutName = selenium.getText("Navigation#ANY_SITE_NAV_BUT_HOME");

				JSONLayout.deletePublicLayout(
					groupName = "Guest",
					layoutName = ${layoutName});

				Navigator.gotoPage(pageName = "Home");

				PortalInstances.tearDownCP();
			}

			ClientExtensionGeneral.tearDown();
		}
	}

	@description = "LPS-141236. Verify remote app can be displayed under a portlet category"
	@priority = 3
	test CanBeAssignedAPortletCategory {
		property portal.acceptance = "true";

		task ("Create remote app entry with portlet category") {
			ClientExtensionGeneral.goToRemoteAppsPortlet();

			ClientExtensionGeneral.addEntryWithPortletCategory();
		}

		task ("Add a public widget page with JSON") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "Test Page");

			Navigator.openURL();

			Navigator.gotoPage(pageName = "Test Page");
		}

		task ("Assert remote app is categorized under correct portlet category") {
			Click(locator1 = "ControlMenu#ADD");

			AssertElementPresent(locator1 = "ControlMenuAddPanel#SIDEBAR_HEADER");

			Navigator.gotoNavTab(navTab = "Widgets");

			WaitForElementPresent(locator1 = "NavBar#APPLICATION_SEARCH_FIELD");

			Type(
				locator1 = "NavBar#APPLICATION_SEARCH_FIELD",
				value1 = "Test Remote App");

			AssertElementPresent(
				key_category = "Collaboration",
				locator1 = "ControlMenuAddPanel#WIDGET_CATEGORY");
		}
	}

	@description = "Verify remote app can be deleted"
	@priority = 5
	@refactordone
	test CanBeDeleted {
		property portal.acceptance = "true";
		property testray.component.names = "Frontend Dataset,Remote Apps";

		var remoteAppName = "My New Remote App To Delete";

		JSONClientExtension.addIFrameRemoteAppEntry(
			iFrameURL = "http://www.liferay.com/my_new_remote_app",
			name = ${remoteAppName});

		ClientExtensionGeneral.goToRemoteAppsPortlet();

		ClientExtensionGeneral.deleteRemoteApp(tableEntry = ${remoteAppName});

		ClientExtensionGeneral.viewEmptyRemoteTable();
	}

	@description = "Add Client Extension with DB Partitioning enabled."
	@priority = 5
	test CanBeDisplayedWithDBPartitioning {
		property database.partition.enabled = "true";
		property database.types = "mysql";

		task ("Given Client Extension admin page") {
			ClientExtensionGeneral.goToRemoteAppsPortlet();
		}

		task ("When Click Add Button") {
			ClientExtensionGeneral.addCustomElement(
				entryHtmlName = "vanilla-counter",
				entryName = "Vanilla Counter",
				entryURL = " http://remote-component-test.wincent.com/packages/vanilla-counter/index.js");
		}

		task ("Then Custom Elements admin page lists the Name and Type") {
			ClientExtensionGeneral.viewTableEntries(
				entryName = "Vanilla Counter",
				entryType = "Custom Element");
		}
	}

	@description = "Verify that the client extension is not available on the workflow page"
	@priority = 3
	test CannotConfigureWorkflowSiteLevel {
		task ("Given go to Workflow page") {
			ProductMenu.gotoPortlet(
				category = "Configuration",
				portlet = "Workflow",
				site = ${siteName});
		}

		task ("When View entry list table") {
			AssertElementPresent(locator1 = "Pagination#ITEMS_PER_PAGE_SELECT");
		}

		task ("Then Client Extension* is not") {
			Search.searchCP(searchTerm = "Client Extension");

			AssertTextPresent(value1 = "No entries were found.");
		}
	}

	@description = "This is a test for LPS-162031. The site administrator could add a CSS remote app."
	@priority = 3
	test CSSCanBeCreated {
		task ("Given a site administrator accesses to the Client Extensions admin") {
			JSONGroup.addGroup(groupName = "Test Site Name");

			JSONDocument.addFileWithUploadedFile(
				dmDocumentTitle = "Font Family",
				groupName = "Test Site Name",
				mimeType = "text/css",
				sourceFileName = "font_family.css");

			var url = JSONDocument.getLatestVersionURL(
				dmDocumentTitle = "Font Family",
				groupName = "Test Site Name");

			ClientExtensionGeneral.goToRemoteAppsPortlet();
		}

		task ("When the site administrator creates a CSS remote app") {
			ClientExtensionGeneral.addType(type = "Add CSS");

			ClientExtensionGeneral.addApp(
				entryName = "Font Family",
				entryURL = ${url});
		}

		task ("Then the site administrator sees the created CSS remote app") {
			ClientExtensionGeneral.viewTableEntryName(entryName = "Font Family");

			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "Verify that remote app of type Custom Element can be created"
	@priority = 5
	test CustomElementCanBeCreated {
		property app.server.types = "tomcat";
		property database.types = "mysql";
		property environment.acceptance = "true";
		property operating.system.types = "alpine,amazonlinux,centos,debian,fedora,orcllinux,osx,redhat,rockylinux,solaris,suse,ubuntu,windows";
		property portal.acceptance = "true";
		property testray.component.names = "Frontend Dataset,Remote Apps";

		task ("Create Vanilla Counter as a Custom Element") {
			ClientExtensionGeneral.goToRemoteAppsPortlet();

			ClientExtensionGeneral.addCustomElementAllFields(
				entryCssurl = "https://liferay.github.io/liferay-frontend-projects/index.css",
				entryHtmlName = "vanilla-counter",
				entryName = "Vanilla Counter",
				entryProperties = "test-data-user=QAuser",
				entryURL = "https://liferay.github.io/liferay-frontend-projects/vanilla-counter/index.js");
		}

		task ("Assert that the Vanilla Counter is listed") {
			ClientExtensionGeneral.viewTableEntries(
				entryName = "Vanilla Counter",
				entryType = "Custom Element");
		}

		task ("Assert that the Vanilla Counter fields have been saved correctly") {
			Click(
				key_tableEntryName = "Vanilla Counter",
				locator1 = "ClientExtension#TABLE_ENTRY_NAME_REMOTE_TABLE");

			ClientExtensionGeneral.assertCustomElementFields(
				entryCssurl = "https://liferay.github.io/liferay-frontend-projects/index.css",
				entryHtmlName = "vanilla-counter",
				entryName = "Vanilla Counter",
				entryProperties = "test-data-user=QAuser",
				entryURL = "https://liferay.github.io/liferay-frontend-projects/vanilla-counter/index.js");
		}
	}

	@description = "LPS-158083 Remote App using non-root portal context"
	@priority = 4
	test CustomElementCanBeCreatedInNonRootContext {
		property app.server.types = "tomcat";
		property database.types = "mysql";
		property portal.acceptance = "true";
		property portal.context = "liferay";
		property skip.clean-app-server-deploy-dir = "true";
		property test.assert.warning.exceptions = "true";
		property test.name.skip.portal.instance = "ClientExtension#CustomElementCanBeCreatedInNonRootContext";
		property testray.component.names = "Client Extensions,Frontend Dataset";

		task ("Given a non-root portal context") {
			var portalURL = JSONCompany.getPortalURL();

			var portalContextIsCorrect = StringUtil.extractLast(${portalURL}, "/");

			TestUtils.assertEquals(
				actual = ${portalContextIsCorrect},
				expected = "liferay");
		}

		task ("Given a Custom Element with relative path URLs") {
			task ("create es-element files in Documents") {
				JSONDocument.addFileWithUploadedFile(
					dmDocumentTitle = "index",
					groupName = "Guest",
					mimeType = "text/javascript",
					sourceFileName = "index.js");

				JSONDocument.addFileWithUploadedFile(
					dmDocumentTitle = "main",
					groupName = "Guest",
					mimeType = "text/css",
					sourceFileName = "main.css");
			}

			task ("create custom element") {
				JSONClientExtension.addCustomElementRemoteAppEntry(
					customElementCssurl = "/documents/d/guest/main",
					customElementHtmlName = "es-element",
					customElementName = "ES Element",
					customElementProperties = "test-data-user=QAuser",
					customElementURL = "/documents/d/guest/index",
					customElementUseESM = "true");
			}
		}

		task ("When ES Element is added to widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "My Widget Page");

			Navigator.gotoPage(pageName = "My Widget Page");

			Click(locator1 = "ControlMenu#ADD");

			Type(
				locator1 = "NavBar#APPLICATION_SEARCH_FIELD",
				value1 = "ES Element");

			Portlet._clickAddPortlet(portletName = "ES Element");

			Refresh();
		}

		task ("Then remote app src is rendered with non-root context") {
			AssertElementPresent(locator1 = "//script[contains(@src,'/documents/d/guest/index') and contains(@type,'module')]");
		}
	}

	@description = "Verify that remote app of type Custom Element can be deleted"
	@priority = 4
	test CustomElementCanBeDeleted {
		property portal.acceptance = "true";

		task ("Create Vanilla Counter as a Custom Element") {
			JSONClientExtension.addCustomElementRemoteAppEntry(
				customElementHtmlName = "vanilla-counter",
				customElementName = "Vanilla Counter",
				customElementURL = "https://liferay.github.io/liferay-frontend-projects/vanilla-counter/index.js");
		}

		task ("Delete Vanilla Counter") {
			ClientExtensionGeneral.goToRemoteAppsPortlet();

			ClientExtensionGeneral.deleteRemoteApp(tableEntry = "Vanilla Counter");
		}

		task ("Assert that Vanilla Counter has been deleted") {
			ClientExtensionGeneral.assertTableEntryNameNotPresent(entryName = "Vanilla Counter");
		}
	}

	@description = "Verify that remote app of type Custom Element can be displayed by portlet"
	@priority = 5
	test CustomElementCanBeDisplayedByPortlet {
		property portal.acceptance = "true";

		var customElementName = "Vanilla Counter";
		var customElementHtmlName = "vanilla-counter";
		var customElementURL = "https://liferay.github.io/liferay-frontend-projects/vanilla-counter/index.js";

		task ("Add a public page with JSON") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "Test Page",
				type = "content");
		}

		task ("Create Vanilla Counter as a Custom Element") {
			ClientExtensionGeneral.goToRemoteAppsPortlet();

			ClientExtensionGeneral.addCustomElement(
				entryHtmlName = ${customElementHtmlName},
				entryName = ${customElementName},
				entryURL = ${customElementURL});
		}

		task ("Add Vanilla Counter to Test Page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page",
				siteName = "Guest");

			PageEditor.addWidget(portletName = "Vanilla Counter");

			PageEditor.publish();
		}

		task ("Assert Vanilla Counter is displayed") {
			Navigator.gotoPage(pageName = "Test Page");

			AssertElementPresent(locator1 = "ClientExtension#VANILLA_COUNTER_REMOTE_APP");
		}
	}

	@description = "Verify that remote app of type Custom Element can be edited"
	@priority = 4
	test CustomElementCanBeEdited {
		property portal.acceptance = "true";

		var remoteAppName = "New Counter";

		task ("Create a Custom Element") {
			JSONClientExtension.addCustomElementRemoteAppEntry(
				customElementHtmlName = "new-counter",
				customElementName = "New Counter",
				customElementURL = "https://liferay.github.io/liferay-frontend-projects/vanilla-counter/index.js");
		}

		task ("Edit the fields of the Custom Element") {
			ClientExtensionGeneral.goToRemoteAppsPortlet();

			Click(
				key_tableEntryName = ${remoteAppName},
				locator1 = "ClientExtension#TABLE_ENTRY_NAME_REMOTE_TABLE");

			ClientExtensionGeneral.editCustomElement(
				entryCssurl = "https://liferay.github.io/liferay-frontend-projects/index.css",
				entryHtmlName = "vanilla-counter-edited",
				entryName = "Vanilla Counter Edited",
				entryProperties = "test-data-user=QAuser");
		}

		task ("Assert that the Custom Element is listed") {
			ClientExtensionGeneral.viewTableEntryName(entryName = "Vanilla Counter Edited");
		}

		task ("Assert that the fields have been updated") {
			Click(
				key_tableEntryName = "Vanilla Counter Edited",
				locator1 = "ClientExtension#TABLE_ENTRY_NAME_REMOTE_TABLE");

			ClientExtensionGeneral.assertCustomElementFields(
				entryCssurl = "https://liferay.github.io/liferay-frontend-projects/index.css",
				entryHtmlName = "vanilla-counter-edited",
				entryName = "Vanilla Counter Edited",
				entryProperties = "test-data-user=QAuser",
				entryURL = "https://liferay.github.io/liferay-frontend-projects/vanilla-counter/index.js");
		}
	}

	@description = "Verify that remote app of type Custom Element can be instanceable in a Content Page"
	@priority = 3
	test CustomElementCanBeInstanceable {
		property portal.acceptance = "true";

		task ("Add a public page with JSON") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "Test Page",
				type = "content");

			JSONLayout.publishLayout(
				groupName = "Guest",
				layoutName = "Test Page");
		}

		task ("Create Vanilla Counter as a Custom Element") {
			JSONClientExtension.addCustomElementRemoteAppEntry(
				customElementHtmlName = "vanilla-counter",
				customElementName = "Vanilla Counter",
				customElementURL = "https://liferay.github.io/liferay-frontend-projects/vanilla-counter/index.js");
		}

		task ("Add a Grid to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page",
				siteName = "Guest");

			PageEditor.addFragment(
				collectionName = "Layout Elements",
				fragmentName = "Grid");
		}

		task ("Change the Number of Modules to 2") {
			PageEditor.editLayoutGrid(
				columnCount = 2,
				panelList = "General");
		}

		task ("Add Vanilla Counter in each module") {
			for (var n : list "1,2") {
				PageEditor.addElementToColumn(
					columnNumber = ${n},
					navTab = "Widgets",
					portletName = "Vanilla Counter");
			}

			PageEditor.publish();
		}

		task ("Assert Vanilla Counter is instanceable") {
			Navigator.gotoPage(pageName = "Test Page");

			for (var n : list "1,2") {
				AssertElementPresent(
					column_number = ${n},
					locator1 = "ClientExtension#VANILLA_COUNTER_PAGE_COLUMN_N");
			}
		}
	}

	@description = "Verify remote app can be configured one its portlet name has been changed"
	@priority = 3
	test CustomElementCanConfigurePortletName {
		property portal.acceptance = "true";

		task ("Given Create Vanilla Counter as a Custom Element") {
			JSONClientExtension.addCustomElementRemoteAppEntry(
				customElementHtmlName = "vanilla-counter",
				customElementName = "Vanilla Counter",
				customElementURL = "https://liferay.github.io/liferay-frontend-projects/vanilla-counter/index.js");
		}

		task ("When adding created portlet to a widget page") {
			ClientExtension.addPortletToWidgetPage(
				portletName = "Vanilla Counter",
				widgetPageName = "Test Page");
		}

		task ("Change Vanilla Counter portlet name") {
			ClientExtensionGeneral.goToRemoteAppsPortlet();

			Click(
				key_tableEntryName = "Vanilla Counter",
				locator1 = "ClientExtension#TABLE_ENTRY_NAME_REMOTE_TABLE");

			Type(
				key_text = "Name",
				locator1 = "TextInput#ANY",
				value1 = "Vanilla Counter Modified");

			PortletEntry.publish();
		}

		task ("Assert that Vanilla Counter name has been changed in Widget Page Editor") {
			Navigator.gotoPage(pageName = "Test Page");

			Click(locator1 = "ControlMenu#ADD");

			AssertElementPresent(locator1 = "ControlMenuAddPanel#SIDEBAR_HEADER");

			Navigator.gotoNavTab(navTab = "Widgets");

			WaitForElementPresent(locator1 = "NavBar#APPLICATION_SEARCH_FIELD");

			Type(
				locator1 = "NavBar#APPLICATION_SEARCH_FIELD",
				value1 = "Vanilla Counter Modified");

			AssertElementPresent(
				key_remoteAppName = "Vanilla Counter Modified",
				locator1 = "ClientExtension#APPLICATION_SEARCH_FIELD_WIDGET_SEARCH_REMOTE_APP");
		}

		task ("Assert Vanilla Counter continues displayed") {
			Navigator.gotoPage(pageName = "Test Page");

			AssertElementPresent(locator1 = "ClientExtension#VANILLA_COUNTER_REMOTE_APP");
		}
	}

	@description = "Verify that remote app of type Custom Element can displayed properties in HTML"
	@priority = 3
	test CustomElementCanInjectHTMLProperties {
		property portal.acceptance = "true";

		task ("When create custom element remote application with property QAuser") {
			JSONClientExtension.addCustomElementRemoteAppEntry(
				customElementCssurl = "http://remote-component-test.wincent.com/index.css",
				customElementHtmlName = "vanilla-counter",
				customElementName = "Vanilla Counter",
				customElementProperties = "test-data-user=QAuser",
				customElementURL = "https://liferay.github.io/liferay-frontend-projects/vanilla-counter/index.js");
		}

		task ("When adding created portlet to a widget page") {
			ClientExtension.addPortletToWidgetPage(
				portletName = "Vanilla Counter",
				widgetPageName = "Test Page");
		}

		task ("Then custom element property is present on DOM") {
			Navigator.gotoPage(pageName = "Test Page");

			AssertElementPresent(
				key_customElementHtmlName = "vanilla-counter",
				key_customElementProperty = "QAuser",
				locator1 = "ClientExtension#REMOTE_APP_PROPERTY_HTML");
		}
	}

	@description = "Verify that remote app of type Custom Element can save multiple URLs"
	@priority = 4
	test CustomElementCanSaveMultipleURLs {
		property portal.acceptance = "true";

		task ("Create Vanilla Counter as a Custom Element") {
			ClientExtensionGeneral.goToRemoteAppsPortlet();

			ClientExtensionGeneral.addCustomElementMultipleURLs();
		}

		task ("Go to edit entry") {
			Click(
				key_tableEntryName = "Vanilla Counter",
				locator1 = "ClientExtension#TABLE_ENTRY_NAME_REMOTE_TABLE");
		}

		task ("Assert multiple URL fields are saved") {
			ClientExtensionGeneral.assertCustomElementMultipleURLFields();
		}
	}

	@description = "LPS-159013 ES module custom element portlets render incorrect script node when dragged in the UI"
	@priority = 4
	test CustomElementRendersCorrectlyWhenPlacedOnWidgetPage {
		property portal.acceptance = "true";
		property testray.component.names = "Client Extensions";

		task ("Given a custom element with "Use ES Modules" option created") {
			JSONClientExtension.addCustomElementRemoteAppEntry(
				customElementCssurl = "https://liferay.github.io/liferay-frontend-projects/index.css",
				customElementHtmlName = "vanilla-counter",
				customElementName = "Vanilla Counter",
				customElementProperties = "test-data-user=QAuser",
				customElementURL = "https://liferay.github.io/liferay-frontend-projects/vanilla-counter/index.js",
				customElementUseESM = "true");
		}

		task ("And Given a widget page created") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "My Widget Page");
		}

		task ("When adding the app portlet to the page") {
			Navigator.gotoPage(pageName = "My Widget Page");

			Click(locator1 = "ControlMenu#ADD");

			Type(
				locator1 = "NavBar#APPLICATION_SEARCH_FIELD",
				value1 = "Vanilla Counter");

			Portlet._clickAddPortlet(portletName = "Vanilla Counter");

			WaitForElementPresent(locator1 = "Message#SUCCESS");
		}

		task ("Then the custom element renders correctly") {
			AssertElementPresent(locator1 = "//vanilla-counter[@test-data-user='QAuser']");

			Refresh();

			AssertElementPresent(locator1 = "//script[contains(@src,'https://liferay.github.io/liferay-frontend-projects/vanilla-counter/index.js') and contains(@type,'module')]");
		}
	}

	@description = "LPS-165425. Validate that HTML eror is thrown if left empty."
	@priority = 3
	test ErrorAppearsWhenHTMLBlank {
		task ("Given Add Custom Element Client Extensions admin page") {
			ClientExtensionGeneral.goToRemoteAppsPortlet();

			ClientExtensionGeneral.addType(type = "Custom Element");
		}

		task ("When required fields Name, HTML Element Name, and JS URL are blank and When attempt to save remote app") {
			Button.clickPublish();
		}

		task ("Then error message 'HTML element name is empty'") {
			AssertElementPresent(
				key_text = "The HTML Element Name field is required",
				locator1 = "ClientExtensionEntry#WARNING_SPECIFIC");
		}

		task ("And Then Remote App is not added") {
			Navigator.gotoBack();

			AssertElementPresent(
				locator1 = "Message#EMPTY_STATE_INFO",
				value1 = "No Results Found");
		}
	}

	@description = "LPS-163525. Validate Client Extensions UI has an understandable error message when user inputs an invalid URL."
	@priority = 4
	test HasAnUnderstandableErrorMessageWhenURLInvalid {
		task ("Given Add IFrame Client Extensions") {
			ApplicationsMenu.gotoPortlet(
				category = "Custom Apps",
				panel = "Applications",
				portlet = "Client Extensions");

			ClientExtensionGeneral.addType(type = "IFrame");
		}

		task ("When type testurl in URL field and a valid name is entered") {
			ClientExtensionGeneral.addApp(
				entryName = "Test Name",
				entryURL = ${testURL});
		}

		task ("And When attempt to save the remote app") {
			Button.clickPublish();
		}

		task ("Then error message 'Please enter a valid URL' appears") {
			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_ENTER_A_VALID_URL",
				value1 = "Please enter a valid URL.");
		}
	}

	@description = "LPS-163525. Validate Client Extensions UI has a clear error message when all the mandatory fields are empty."
	@ignore = "true"
	@priority = 4
	test HasMandatoryFieldErrorMessagesWhenEmpty {
		task ("Given Add IFrame Client Extensions admin page") {
			ApplicationsMenu.gotoPortlet(
				category = "Custom Apps",
				panel = "Applications",
				portlet = "Client Extensions");

			ClientExtensionGeneral.addType(type = "IFrame");
		}

		task ("When attempt to save remote app") {
			Button.clickPublish();
		}

		task ("Then an error message 'field is required' appears under URL") {
			AssertTextEquals.assertPartialText(
				locator1 = "Message#WARNING_FEEDBACK",
				value1 = "The URL field is required");
		}

		task ("And then Remote App is not added") {
			Navigator.gotoBack();

			AssertTextEquals(
				locator1 = "Message#EMPTY_STATE_INFO",
				value1 = "No Results Found");
		}
	}

	@description = "LPS-165745. Error thrown when HTML Element contains empty/white space."
	@priority = 3
	test HTMLElementConatinsInvlaidCharacters {
		task ("Given Add Custom Element Client Extensions") {
			ClientExtensionGeneral.goToRemoteAppsPortlet();

			ClientExtensionGeneral.addType(type = "Custom Element");
		}

		task ("When type 'vanilla counter' in HTML Element field and attempt to save the remote app") {
			ClientExtensionGeneral.addApp(
				entryHTMLElementName = "vanilla counter",
				entryName = "RemoteApp Test Name",
				entryURL = ${testURL});
		}

		task ("Then error message 'HTML Element name contains invalid character' with empty/white space appears under HTML Element field") {
			AssertElementPresent(
				locator1 = "ClientExtensionEntry#REMOTE_APPS_ERROR_WITH_MESSAGE",
				text = "HTML element name contains invalid character");
		}
	}

	@description = "LPS-165835. Error thrown when HTML Element doesn't have at least one hyphen."
	@priority = 3
	test HTMLElementMustHaveOneHyphen {
		task ("Given Add Custom Element Client Extensions") {
			ClientExtensionGeneral.goToRemoteAppsPortlet();

			ClientExtensionGeneral.addType(type = "Custom Element");
		}

		task ("When type 'vanilla.counter' in HTML Element Name field and attempt to save the remote app") {
			ClientExtensionGeneral.addApp(
				entryHTMLElementName = "vanilla.counter",
				entryName = "RemoteApp Test",
				entryURL = ${testURL});
		}

		task ("Then error message 'HTML element name must contain at least one hyphen' appears under HTML Element field") {
			AssertElementPresent(
				locator1 = "ClientExtensionEntry#REMOTE_APPS_ERROR_WITH_MESSAGE",
				text = "HTML element name must contain at least one hyphen");
		}
	}

	@description = "LPS-165834. Error thrown when HTML Element doesn't start with a lowercase letter."
	@priority = 3
	test HTMLElementMustStartWithLowercase {
		task ("Given Add Custom Element Client Extensions") {
			ClientExtensionGeneral.goToRemoteAppsPortlet();

			ClientExtensionGeneral.addType(type = "Custom Element");
		}

		task ("When type 'Vanilla counter' in HTML Element Name field") {
			ClientExtensionGeneral.addApp(
				entryHTMLElementName = "Vanilla counter",
				entryName = "RemoteApp Test",
				entryURL = ${testURL});
		}

		task ("And When attempt to save the remote app") {
			Button.clickPublish();
		}

		task ("Then error message 'HTML element name must start with a lowercase letter' appears under HTML Element field") {
			AssertElementPresent(
				locator1 = "ClientExtensionEntry#REMOTE_APPS_ERROR_WITH_MESSAGE",
				text = "HTML element name must start with a lowercase letter");
		}
	}

	@description = "Verify an iframe remote app can be created"
	@priority = 5
	test IFrameCanBeCreated {
		property portal.acceptance = "true";
		property testray.component.names = "Frontend Dataset,Remote Apps";

		ClientExtensionGeneral.goToRemoteAppsPortlet();

		ClientExtensionGeneral.addType(type = "IFrame");

		ClientExtensionGeneral.addApp(
			entryName = "Test Remote App",
			entryURL = "http://www.liferay.com");

		ClientExtensionGeneral.viewTableEntryName(entryName = "Test Remote App");
	}

	@description = "Verify that remote app type IFrame can display property fields"
	@priority = 4
	test IframeTypeCanDisplayProperFields {
		property portal.acceptance = "true";

		ClientExtensionGeneral.goToRemoteAppsPortlet();

		ClientExtensionGeneral.addType(type = "Custom Element");

		AssertVisible(
			key_text = "HTML Element Name",
			locator1 = "TextInput#ANY");

		AssertVisible(
			key_text = "CSS URL",
			locator1 = "TextInput#ANY");

		Click(locator1 = "Button#BACK");

		ClientExtensionGeneral.addType(type = "IFrame");

		AssertElementNotPresent(
			key_text = "HTML Element Name",
			locator1 = "TextInput#ANY");

		AssertElementNotPresent(
			key_text = "CSS URL",
			locator1 = "TextInput#ANY");
	}

	@description = "LPS-177999. Verify that URL, description, type and source code url fields are present."
	@priority = 5
	test ThemeSvgTypeCanDisplayProperFields {
		property custom.properties = "feature.flag.LPS-166479=true";
		property portal.acceptance = "true";

		task ("Given Client Extension admin page") {
			ClientExtensionGeneral.goToRemoteAppsPortlet();
		}

		task ("When Create a Theme svg extensions") {
			ClientExtensionGeneral.addType(type = "Theme SVG Spritemap");
		}

		task ("Then URL, description, type and source code url fields are present") {
			ClientExtensionGeneral.assertContentField(
				entryField = "Name",
				entryInformation = "");

			ClientExtensionGeneral.assertContentField(
				entryField = "description",
				entryInformation = "");

			ClientExtensionGeneral.assertContentField(
				entryField = "URL",
				entryInformation = "");

			ClientExtensionGeneral.assertContentField(
				entryField = "Source Code URL",
				entryInformation = "");
		}
	}

	@description = "Verify that the UI label is present when a client extension is not created through osgi"
	@priority = 3
	test UILabelIsPresent {
		task ("Given a created Client Extension") {
			JSONClientExtension.addCustomElementRemoteAppEntry(
				customElementCssurl = "/documents/d/guest/main",
				customElementHtmlName = "es-element",
				customElementName = "ES Element",
				customElementProperties = "test-data-user=QAuser",
				customElementURL = "/documents/d/guest/index",
				customElementUseESM = "true");
		}

		task ("When open the Client Extension list") {
			ClientExtensionGeneral.goToRemoteAppsPortlet();
		}

		task ("Then Modified Date column is present") {
			FDSCustomView.assertColumnPresent(key_field = "Modified Date");
		}

		task ("And Then showing Label 'UI' is present") {
			ClientExtensionGeneral.viewTableEntryType(entryType = "UI");
		}
	}

}