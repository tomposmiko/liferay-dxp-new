@component-name = "portal-publications"
definition {

	property database.types = "db2,hypersonic,mariadb,mysql,oracle,postgresql,sqlserver";
	property osgi.module.configuration.file.names = "com.liferay.change.tracking.configuration.CTSettingsConfiguration.config";
	property osgi.module.configurations = "enabled=B\"true\"";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Publications";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Publications.disablePublications();
		}
	}

	@priority = 3
	test CancelPublicationCreation {
		Publications.cancelPublicationCreation(publicationName = "Publication Name");

		AssertElementNotPresent(
			locator1 = "Publications#PUBLISHED_PUBLICATION_NAME",
			value1 = "Publication Name");
	}

	@priority = 5
	test CanMoveChangeBetweenPublications {
		property custom.properties = "feature.flag.LPS-171364=true";

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Guest",
			title = "WC WebContent Title");

		JSONPublications.addPublication(publicationName = "Publication 1 Name");

		JSONPublications.addPublication(publicationName = "Publication 2 Name");

		JSONPublications.selectPublication(publicationName = "Publication 2 Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC WebContent Title");

		WebContent.editCP(
			webContentContentEdit = "Edited in Publication 2 Name",
			webContentTitle = "Test Web Content",
			webContentTitleEdit = "Edited in Publication 2 Name");

		PublicationsNavigator.gotoReviewChanges(publicationName = "Publication 2 Name");

		PublicationsChanges.moveChangeToAnotherPublication(
			changeCategory = "Web Content Article",
			changeOrder = 1,
			changeTitle = "Edited in Publication 2 Name",
			targetPublicationName = "Publication 1 Name");

		Refresh();

		AssertElementPresent(locator1 = "PublicationsChanges#MESSAGE_NO_CHANGES");

		PublicationsNavigator.gotoReviewChanges(
			gotoPublicationsAdmin = "true",
			publicationName = "Publication 1 Name");

		PublicationsChanges.viewChanges(
			changeCategory = "Web Content Article",
			changeSite = "Guest",
			changeTitle = "Edited in Publication 2 Name",
			changeType = "Added",
			changeUser = "Test Test");

		PublicationsChanges.viewChanges(
			changeCategory = "Web Content Translation",
			changeSite = "Guest",
			changeTitle = "Edited in Publication 2 Name",
			changeType = "Added",
			changeUser = "Test Test");
	}

	@priority = 4
	test CannotMoveChangesBetweenPublicationsIfConflictsArePresent {
		property custom.properties = "feature.flag.LPS-171364=true";

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Guest",
			title = "WC WebContent Title");

		JSONPublications.addPublication(publicationName = "Publication 1 Name");

		JSONPublications.addPublication(publicationName = "Publication 2 Name");

		JSONPublications.selectPublication(publicationName = "Publication 1 Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC WebContent Title");

		WebContent.editCP(
			webContentTitle = "WC WebContent Title",
			webContentTitleEdit = "WC WebContent Title 1 Edit");

		JSONPublications.selectPublication(publicationName = "Publication 2 Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC WebContent Title");

		WebContent.editCP(
			webContentTitle = "WC WebContent Title",
			webContentTitleEdit = "WC WebContent Title 2 Edit");

		PublicationsNavigator.gotoReviewChanges(publicationName = "Publication 2 Name");

		PublicationsChanges.moveChangeToAnotherPublication(
			changeCategory = "Web Content Article",
			changeOrder = 1,
			changeTitle = "WC WebContent Title 2 Edit",
			moveFailed = "true",
			targetPublicationName = "Publication 1 Name");

		Refresh();

		PublicationsChanges.viewChanges(
			changeCategory = "Web Content Article",
			changeSite = "Guest",
			changeTitle = "WC WebContent Title 2 Edit",
			changeType = "Added",
			changeUser = "Test Test");

		PublicationsChanges.viewChanges(
			changeCategory = "Web Content Translation",
			changeSite = "Guest",
			changeTitle = "WC WebContent Title 2 Edit",
			changeType = "Added",
			changeUser = "Test Test");

		PublicationsNavigator.gotoReviewChanges(
			gotoPublicationsAdmin = "true",
			publicationName = "Publication 1 Name");

		PublicationsChanges.viewChanges(
			changeCategory = "Web Content Article",
			changeSite = "Guest",
			changeTitle = "WC WebContent Title 1 Edit",
			changeType = "Added",
			changeUser = "Test Test");

		PublicationsChanges.viewChanges(
			changeCategory = "Web Content Translation",
			changeSite = "Guest",
			changeTitle = "WC WebContent Title 1 Edit",
			changeType = "Added",
			changeUser = "Test Test");
	}

	@priority = 3
	test CreateNewPublicationWithLongName {
		PublicationsNavigator.openPublicationsAdmin();

		Click(locator1 = "Button#PLUS");

		Type(
			locator1 = "Publications#PUBLICATION_NAME",
			value1 = "Lopadotemachoselachogaleokranioleipsanodrimhypotrimmatosilphioparaomelitokatakechymenokichlepikossyphophattoperisteralektryonoptekephalliokigklopeleiolagoiosiraiobaphetraganopterygon");

		AssertElementPresent(
			key_text = "Create",
			locator1 = "Button#ANY_DISABLED");
	}

	@priority = 3
	test CreateNewPublicationWithoutName {
		PublicationsNavigator.openPublicationsAdmin();

		Click(locator1 = "Button#PLUS");

		AssertElementPresent(
			key_text = "Create",
			locator1 = "Button#ANY_DISABLED");
	}

	@priority = 5
	test DeletePublication {
		property portal.acceptance = "true";

		Publications.createNewPublication(
			gotoPublicationsAdmin = "true",
			publicationDescription = "Publication Description",
			publicationName = "Publication Name");

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Test Page Name");

		ApplicationsMenu.gotoPortlet(
			category = "Publications",
			panel = "Applications",
			portlet = "Publications");

		Publications.deletePublication(publicationName = "Publication Name");

		AssertElementNotPresent(
			key_publicationName = "Publication Name",
			locator1 = "Publications#PUBLISHED_PUBLICATION_NAME");

		AssertTextPresent(
			locator1 = "Publications#PUBLICATIONS_INDICATOR_TITLE",
			value1 = "Production");

		Navigator.openURL();

		Page.viewNoPG(pageName = "Test Page");
	}

	@priority = 4
	test SwitchPublication {
		Publications.createNewPublication(publicationName = "Publication Name");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Web Content");

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			webContentContent = "WC WebContent Content 1",
			webContentTitle = "Web Content Title 1");

		PortletEntry.publish();

		Publications.createNewPublication(
			gotoPublicationsAdmin = "true",
			publicationName = "Publication 2 Name");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Web Content");

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			webContentContent = "WC WebContent Content 2",
			webContentTitle = "Web Content Title 2");

		PortletEntry.publish();

		Publications.selectPublication(publicationName = "Publication Name");

		AssertElementPresent(
			key_webContentTitle = "Web Content Title 1",
			locator1 = "WC#ENTRY_LIST_TITLE");

		AssertElementNotPresent(
			key_webContentTitle = "Web Content Title 2",
			locator1 = "WC#ENTRY_LIST_TITLE");

		Publications.selectPublication(
			gotoPublicationsAdmin = "true",
			publicationName = "Publication 2 Name");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Web Content");

		AssertElementPresent(
			key_webContentTitle = "Web Content Title 2",
			locator1 = "WC#ENTRY_LIST_TITLE");

		AssertElementNotPresent(
			key_webContentTitle = "Web Content Title 1",
			locator1 = "WC#ENTRY_LIST_TITLE");

		Publications.workOnProduction();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No web content was found.");
	}

}