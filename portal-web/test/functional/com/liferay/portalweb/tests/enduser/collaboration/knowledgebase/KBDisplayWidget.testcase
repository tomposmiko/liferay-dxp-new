@component-name = "portal-knowledge-base"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Knowledge Base";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Knowledge Base Display Page");

		JSONLayout.addWidgetToPublicLayout(
			column = 2,
			groupName = "Guest",
			layoutName = "Knowledge Base Display Page",
			widgetName = "Knowledge Base Display");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Workflow.tearDownSiteWorkflow();

			BlogsEntry.tearDownCP();

			KBArticle.tearDownCP();

			PagesAdmin.tearDownCP();
		}
	}

	@e2etest
	@priority = 4
	@refactordone
	test CanAddChildArticle {
		property portal.acceptance = "true";

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBDisplayWidget.addChildPGViaKBDisplay(
			kbArticleContent = "Child Article Content",
			kbArticleTitle = "Child Article Title");

		Refresh();

		KBArticle.viewChildArticlePG(
			kbArticleTitle = "Knowledge Base Article Title",
			kbChildArticleContent = "Child Article Content",
			kbChildArticleTitle = "Child Article Title");
	}

	@priority = 4
	@refactorneeded
	test CanAddMultipleSuggestions {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article1 Content",
			kbArticleTitle = "Knowledge Base Article1 Title");

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article2 Content",
			kbArticleTitle = "Knowledge Base Article2 Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article1 Title");

		for (var suggestionCount : list "1,2,3") {
			KBArticleSuggestion.addPG(
				kbArticleTitle = "Knowledge Base Article1 Title",
				kbSuggestionBody = "Knowledge Base Article1 Suggestion${suggestionCount}");
		}

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		AssertTextEquals.assertPartialText(
			locator1 = "KnowledgeBase#LINK_THERE_ARE_SUGGESTIONS",
			value1 = "There are 3 suggestions. (3 Pending)");

		for (var suggestionCount : list "1,2,3") {
			KBArticleSuggestion.viewPG(
				kbSuggestionBody = "Knowledge Base Article1 Suggestion${suggestionCount}",
				kbSuggestionUserName = "Test Test",
				status = "New");
		}
	}

	@priority = 4
	@refactordone
	test CanCancelAddingChildArticle {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBDisplayWidget.addChildPGViaKBDisplay(
			cancel = "true",
			kbArticleContent = "Child Article Content",
			kbArticleTitle = "Child Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewPG(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		AssertElementNotPresent(locator1 = "KnowledgeBase#HIERARCHY_NAVIGATION_CHILD");
	}

	@priority = 4
	@refactordone
	test CanDeleteArticle {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.deletePG(kbArticleTitle = "Knowledge Base Article Title");

		KBDisplayWidget.viewDefaultViaKBDisplayPG();
	}

	@priority = 4
	@refactordone
	test CanDeleteChildArticle {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		JSONKnowledgeBase.addkBChildArticle(
			groupName = "Guest",
			kbArticleTitle = "Knowledge Base Article Title",
			kbChildArticleContent = "Child Article Content",
			kbChildArticleTitle = "Child Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewChildArticlePG(
			kbArticleTitle = "Knowledge Base Article Title",
			kbChildArticleContent = "Child Article Content",
			kbChildArticleTitle = "Child Article Title");

		KBArticle.deletePG();
	}

	@priority = 5
	@refactordone
	test CanEditArticle {
		property portal.acceptance = "true";

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBDisplayWidget.editPGViaKBDisplay(
			kbArticleContentEdit = "Knowledge Base Article Content Edit",
			kbArticleTitle = "Knowledge Base Article Title",
			kbArticleTitleEdit = "Knowledge Base Article Title Edit");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBDisplayWidget.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content Edit",
			kbArticleTitle = "Knowledge Base Article Title Edit");
	}

	@priority = 4
	@refactorneeded
	test CanEditChildArticle {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Parent Content 1",
			kbArticleTitle = "Knowledge Base Article Parent Title 1");

		JSONKnowledgeBase.addkBChildArticle(
			groupName = "Guest",
			kbArticleTitle = "Knowledge Base Article Parent Title 1",
			kbChildArticleContent = "Knowledge Base Article Parent Title 1 Child Content 1",
			kbChildArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.gotoKBChildArticleViaKBDisplayHierarchyNavigationPG(
			kbArticleTitle = "Knowledge Base Article Parent Title 1",
			kbChildArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");

		KBDisplayWidget.editPGViaKBDisplay(
			kbArticleContentEdit = "Knowledge Base Child Article Content Edited 1",
			kbArticleTitleEdit = "Knowledge Base Article Parent Title 1 Child 1");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBDisplayWidget.viewParentArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title");

		KBDisplayWidget.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");
	}

	@priority = 4
	@refactorneeded
	test CanEditDisplaySettings {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBDisplayWidget.viewDefaultKBDisplaySettingsPG();

		KBDisplayWidget.configureKBDisplaySettingsPG(
			history = "false",
			print = "false",
			subscribe = "false");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBDisplayWidget.viewKBDisplayPG(
			history = "false",
			print = "false",
			subscribe = "false");
	}

	@priority = 3
	@refactordone
	test CannotViewDuplicateBreadcrumbsWhenAddingParentArticle {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content Parent",
			kbArticleTitle = "Knowledge Base Article Title Parent");

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content Child",
			kbArticleTitle = "Knowledge Base Article Title Child");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article Title Child");

		KBDisplayWidget.moveArticleViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Title Child");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertElementPresent(locator1 = "KnowledgeBaseSelectParent#BREADCRUMB_HOME");

		AssertElementNotPresent(locator1 = "//ul[contains(@class,'breadcrumb')]//a[contains(.,'Home')]/following-sibling::ul[contains(@class,'breadcrumb')]");
	}

	@priority = 3
	@refactordone
	test CanSaveChildArticleAsDraft {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content Parent",
			kbArticleTitle = "Knowledge Base Article Title Parent");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBDisplayWidget.addChildPGViaKBDisplay(
			kbArticleContent = "Knowledge Base Child Article Content",
			kbArticleTitle = "Knowledge Base Child Article Title",
			saveAsDraft = "true");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		AssertElementNotPresent(locator1 = "KnowledgeBase#HIERARCHY_NAVIGATION_CHILD");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.gotoChildArticleDescriptiveDetails(kbArticleTitle = "Knowledge Base Article Title Parent");

		KBArticle.viewCP(
			kbArticleContent = "Knowledge Base Child Article Content",
			kbArticleTitle = "Knowledge Base Child Article Title",
			kbStatus = "Draft");
	}

	@priority = 4
	@refactordone
	test CanSelectParentArticle {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Parent Content 1",
			kbArticleTitle = "Knowledge Base Article Parent Title 1");

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Parent Content 1 Child 1",
			kbArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");

		KBDisplayWidget.moveArticleViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");

		KBDisplayWidget.selectParentViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");

		KBDisplayWidget.viewParentArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title");

		KBDisplayWidget.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");
	}

	@priority = 4
	@refactordone
	test CanViewArticlesInFolder {
		JSONKnowledgeBase.addkBFolder(
			groupName = "Guest",
			kbFolderDescription = "KB Folder 1 Description",
			kbFolderName = "KB Folder");

		JSONKnowledgeBase.addkBArticle(
			folderName = "KB Folder",
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content 1",
			kbArticleTitle = "Knowledge Base Article Title 1");

		JSONKnowledgeBase.addkBArticle(
			folderName = "KB Folder",
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content 2",
			kbArticleTitle = "Knowledge Base Article Title 2");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBDisplayWidget.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 1",
			kbArticleTitle = "Knowledge Base Article Title 1");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article Title 2");

		KBDisplayWidget.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 2",
			kbArticleTitle = "Knowledge Base Article Title 2");
	}

	@description = "Skip the instance for performance improvement."
	@priority = 3
	@refactorneeded
	test CanViewArticlesWithDifferentSiteScopes {
		property test.name.skip.portal.instance = "KBDisplayWidget#CanViewArticlesWithDifferentSiteScopes";

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Knowledge Base Display Page 2");

		JSONLayout.addWidgetToPublicLayout(
			column = 1,
			groupName = "Guest",
			layoutName = "Knowledge Base Display Page 2",
			widgetName = "Knowledge Base Display");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		Portlet.selectScopePG(
			portletName = "Knowledge Base Display",
			scope = "Knowledge Base Display Page");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page 2");

		Portlet.selectScopePG(
			portletName = "Knowledge Base Display",
			scope = "Knowledge Base Display Page 2");

		Navigator.openURL();

		ProductMenu.gotoScopedPortlet(
			category = "Content & Data",
			portlet = "Knowledge Base",
			scope = "Knowledge Base Display Page");

		WaitForElementPresent(locator1 = "KnowledgeBase#PAGE_TREE_PANEL_OPENED");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title for Page");

		Navigator.openURL();

		ProductMenu.gotoScopedPortlet(
			category = "Content & Data",
			portlet = "Knowledge Base",
			scope = "Knowledge Base Display Page 2");

		WaitForElementPresent(locator1 = "KnowledgeBase#PAGE_TREE_PANEL_OPENED");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title for Page 2");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewPG(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title for Page");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page 2");

		KBArticle.viewPG(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title for Page 2");
	}

	@priority = 4
	@refactorneeded
	test CanViewArticleViaDefaultFriendlyURL {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article2 Content",
			kbArticleTitle = "Knowledge Base Article2 Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		var kbArticleFriendlyURL = selenium.getAttribute("//div[@class='kbarticle-navigation']/ul/li/a[contains(.,'Knowledge Base Article2 Title')]@href");

		Navigator.openSpecificURL(url = ${kbArticleFriendlyURL});

		KBDisplayWidget.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article2 Content",
			kbArticleTitle = "Knowledge Base Article2 Title");
	}

	@priority = 3
	@refactorneeded
	test CanViewArticleViaNavigationLinks {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content 1",
			kbArticleTitle = "Knowledge Base Article Title 1");

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content 2",
			kbArticleTitle = "Knowledge Base Article Title 2");

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content 3",
			kbArticleTitle = "Knowledge Base Article Title 3");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBDisplayWidget.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 1",
			kbArticleTitle = "Knowledge Base Article Title 1");

		KBArticle.viewNavigationLinksPG(
			next = "true",
			previous = "false");

		Click(locator1 = "KnowledgeBaseArticle#NEXT");

		KBDisplayWidget.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 2",
			kbArticleTitle = "Knowledge Base Article Title 2");

		KBArticle.viewNavigationLinksPG(
			next = "true",
			previous = "true");

		Click(locator1 = "KnowledgeBaseArticle#NEXT");

		KBDisplayWidget.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 3",
			kbArticleTitle = "Knowledge Base Article Title 3");

		KBArticle.viewNavigationLinksPG(
			next = "false",
			previous = "true");

		Click(locator1 = "KnowledgeBaseArticle#PREVIOUS");

		KBDisplayWidget.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 2",
			kbArticleTitle = "Knowledge Base Article Title 2");

		KBArticle.viewNavigationLinksPG(
			next = "true",
			previous = "true");

		Click(locator1 = "KnowledgeBaseArticle#PREVIOUS");

		KBDisplayWidget.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 1",
			kbArticleTitle = "Knowledge Base Article Title 1");

		KBArticle.viewNavigationLinksPG(
			next = "true",
			previous = "false");
	}

	@priority = 4
	@refactorneeded
	test CanViewArticleViaUserDefinedFriendlyURL {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article2 Content",
			kbArticleFriendlyURL = "Friendly Universal Resource Locator",
			kbArticleTitle = "Knowledge Base Article2 Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		var kbArticleFriendlyURL = selenium.getAttribute("//div[@class='kbarticle-navigation']/ul/li/a[contains(.,'Knowledge Base Article2 Title')]@href");

		Navigator.gotoPage(pageName = "Home");

		Navigator.openSpecificURL(url = ${kbArticleFriendlyURL});

		KBDisplayWidget.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article2 Content",
			kbArticleTitle = "Knowledge Base Article2 Title");
	}

	@priority = 4
	@refactordone
	test CanViewChildArticleViaNextLink {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Parent Content 1",
			kbArticleTitle = "Knowledge Base Article Parent Title 1");

		JSONKnowledgeBase.addkBChildArticle(
			groupName = "Guest",
			kbArticleTitle = "Knowledge Base Article Parent Title 1",
			kbChildArticleContent = "This is a child article. Lengthy articles are no longer summarized on the KB Display portlet, and the Read More link has been deprecated. To access a child article, one must click on the parent article and then click on the child page link found either: under the article navigation, or under the parent article entry.",
			kbChildArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBDisplayWidget.viewParentArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title");

		KBArticle.viewViaNextLinkPG(
			kbArticleContent = "This is a child article. Lengthy articles are no longer summarized on the KB Display portlet, and the Read More link has been deprecated. To access a child article, one must click on the parent article and then click on the child page link found either: under the article navigation, or under the parent article entry.",
			kbArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");
	}

	@description = "This test covers LPS-91303. It ensures that the content of a selected root folder is viewable."
	@priority = 3
	@refactordone
	test CanViewContentOfConfiguredRootFolder {
		JSONKnowledgeBase.addkBFolder(
			groupName = "Guest",
			kbFolderDescription = "KB Folder 1 Description",
			kbFolderName = "KB Folder 1");

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Home Article",
			kbArticleTitle = "Knowledge Base Home Article");

		JSONKnowledgeBase.addkBArticle(
			folderName = "KB Folder 1",
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Folder Content 1",
			kbArticleTitle = "Knowledge Base Article Folder Title 1");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBFolder.gotoViaKBDisplayPG(kbFolderName = "KB Folder 1");

		KBDisplayWidget.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Folder Content 1",
			kbArticleTitle = "Knowledge Base Article Folder Title 1");
	}

	@priority = 4
	@refactordone
	test CanViewCustomFriendlyURL {
		property portal.acceptance = "true";

		var portalURL = PropsUtil.get("portal.url");

		for (var num : list "1,2") {
			JSONKnowledgeBase.addkBArticle(
				groupName = "Guest",
				kbArticleContent = "KB Article Content${num}",
				kbArticleTitle = "KB Article Title${num}",
				urlTitle = "custom-friendly-url${num}");
		}

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		for (var num : list "1,2") {
			KBDisplayWidget.gotoViaKBDisplayPG(kbArticleTitle = "KB Article Title${num}");

			AssertLocation(value1 = "${portalURL}/web/guest/knowledge-base-display-page/-/knowledge_base/custom-friendly-url${num}");
		}
	}

	@priority = 4
	@refactordone
	test CanViewDefaultFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		for (var num : list "1,2") {
			JSONKnowledgeBase.addkBArticle(
				groupName = "Guest",
				kbArticleContent = "KB Article Content${num}",
				kbArticleTitle = "KB Article Title${num}");
		}

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		for (var num : list "1,2") {
			KBDisplayWidget.gotoViaKBDisplayPG(kbArticleTitle = "KB Article Title${num}");

			AssertLocation(value1 = "${portalURL}/web/guest/knowledge-base-display-page/-/knowledge_base/kb-article-title${num}");
		}
	}

	@description = "This test ensures that the description can be displayed on the KB display widget after enabling it in the configuration."
	@priority = 4
	test CanViewDescriptionAfterEnablingInConfiguration {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "KB Article Content",
			kbArticleTitle = "KB Article Title",
			kbDescription = "KB Article Description");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewPG(
			description = "Disabled",
			kbArticleContent = "KB Article Content",
			kbArticleTitle = "KB Article Title",
			kbDescription = "KB Article Description");

		KBArticle.enableDescriptionOnPortlet(portletName = "Knowledge Base Display");

		KBArticle.viewPG(
			kbArticleContent = "KB Article Content",
			kbArticleTitle = "KB Article Title",
			kbDescription = "KB Article Description");
	}

	@priority = 4
	@refactordone
	test CanViewEditedKBArticleTitleHistory {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "KB Article Content",
			kbArticleTitle = "KB Article Title");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.editCP(
			kbArticleContentEdit = "KB Article Content Edit",
			kbArticleTitle = "KB Article Title",
			kbArticleTitleEdit = "KB Article Title Edit");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewArticleHistoryPG(
			kbArticleContent = "KB Article Content Edit",
			kbArticleContentAddedDiff = "Edit",
			kbArticleVersionNew = 2,
			kbArticleVersionOld = 1,
			viewNoXmlInTitle = "true");
	}

	@priority = 4
	@refactordone
	test CanViewEmptyState {
		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBDisplayWidget.viewDefaultViaKBDisplayPG();
	}

	@priority = 4
	@refactordone
	test CanViewSuggestionsWorkflow {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article1 Content",
			kbArticleTitle = "Knowledge Base Article1 Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticleSuggestion.addPG(
			kbArticleTitle = "Knowledge Base Article1 Title",
			kbSuggestionBody = "Knowledge Base Article1 Suggestion");

		KBArticleSuggestion.gotoSuggestionStatusNew(
			kbSuggestionBody = "Knowledge Base Article1 Suggestion",
			suggestionCount = 1);

		KBArticleSuggestion.viewPG(
			kbSuggestionBody = "Knowledge Base Article1 Suggestion",
			kbSuggestionUserName = "Test Test",
			status = "New");

		KBArticleSuggestion.moveToInProgressStatus(
			kbArticleTitle = "Knowledge Base Article1 Title",
			kbSuggestionBody = "Knowledge Base Article1 Suggestion");

		KBArticleSuggestion.viewPG(
			kbSuggestionBody = "Knowledge Base Article1 Suggestion",
			kbSuggestionUserName = "Test Test",
			status = "In Progress");

		KBArticleSuggestion.moveToResolvedStatus(
			kbArticleTitle = "Knowledge Base Article1 Title",
			kbSuggestionBody = "Knowledge Base Article1 Suggestion");

		KBArticleSuggestion.viewPG(
			kbSuggestionBody = "Knowledge Base Article1 Suggestion",
			kbSuggestionUserName = "Test Test",
			status = "Resolved");
	}

	@priority = 3
	@refactordone
	test JavascriptIsEscapedFromArticleTitle {
		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleFriendlyURL = "Title",
			kbArticleTitle = '''><img onerror="alert('xss')" src="xx" /><''',
			specialCharacters = "true");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		AssertAlertNotPresent();

		KBDisplayWidget.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = '''><img onerror="alert('xss')" src="xx" /><''');
	}

	@priority = 3
	@refactorneeded
	test ViewNoDuplicateKBSuggestionAfterRefresh {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticleSuggestion.addPG(
			kbArticleTitle = "Knowledge Base Article Title",
			kbSuggestionBody = "Knowledge Base Article Suggestion");

		Refresh();

		KBArticleSuggestion.viewPG(
			kbSuggestionBody = "Knowledge Base Article Suggestion",
			kbSuggestionUserName = "Test Test",
			status = "New");

		KBArticleSuggestion.viewNoDuplicatePG(
			kbSuggestionBody = "Knowledge Base Article Suggestion",
			kbSuggestionUserName = "Test Test",
			kbThereAreSuggestionsLink = "There is one suggestion. (1 Pending)",
			status = "New");
	}

	@description = "This test ensures that the blogs widgets follow W3C accessibility standards."
	@ignore = "true"
	@priority = 4
	test WidgetsFollowAccessibilityStandards {
		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Knowledge Base Display Page");

		for (var widgetName : list "Knowledge Base Display,Knowledge Base Article,Knowledge Base Search,Knowledge Base Section") {
			JSONLayout.addWidgetToPublicLayout(
				column = 1,
				groupName = "Guest",
				layoutName = "Knowledge Base Display Page",
				widgetName = ${widgetName});
		}

		Navigator.openSitePage(
			pageName = "Knowledge Base Display Page",
			siteName = "Guest");

		AssertAccessible();
	}

}