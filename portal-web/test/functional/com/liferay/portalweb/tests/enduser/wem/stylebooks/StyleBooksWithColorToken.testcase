@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Style Books";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			HeadlessSite.addSite(siteName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-141568. The reference will be autocompleted as 6 digit characters, if the user fill out 3 digit characters when clicking on the tab, intro or outside the output."
	@priority = 4
	test AutocompleteColorReference {
		task ("Add a Container to a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Layout Elements",
				fragmentName = "Container");
		}

		task ("Change the Background Color to Info") {
			PageEditor.editLayoutContainer(
				backgroundColor = "Info",
				panel = "Styles");
		}

		task ("Detach the linked token") {
			PageEditor.detachToken(fieldLabel = "Background Color");
		}

		task ("Type a 3 digits color reference") {
			Type(
				key_text = "Background Color",
				locator1 = "TextInput#ANY",
				value1 = "#03C");
		}

		task ("View the color reference is autocompleted after trigger auto save") {
			Click(
				key_sidebarButton = "Browser",
				locator1 = "PageEditor#FRAGMENT_SIDEBAR_TITLE");

			PageEditor.waitForAutoSave();

			AssertTextEquals(
				key_text = "Background Color",
				locator1 = "TextInput#ANY",
				value1 = "#0033CC");
		}

		task ("View the background color of Container") {
			PageEditor.viewContainerStyle(backgroundColor = "rgb(0, 51, 204)");
		}
	}

	@description = "This is a test for LPS-145650. The user could cancel publish process in style book editor if link invalid color token."
	@priority = 4
	test CancelPublishProcessInStyleBookEditorIfLinkInvalidColorToken {
		task ("Add a style book") {
			JSONStylebooks.addStyleBookEntry(
				groupName = "Test Site Name",
				styleBookEntryName = "Test Style Book Name");
		}

		task ("Change the Brand Color 1 to an invalid color token") {
			StyleBooksAdmin.openStyleBooksAdmin(siteURLKey = "test-site-name");

			StyleBooksAdmin.gotoStyleBookEllipsisItem(
				item = "Edit",
				styleBookName = "Test Style Book Name");

			Type(
				key_text = "Brand Color 1",
				locator1 = "TextInput#ANY",
				value1 = "Brand Color 1");

			StyleBooks.clickManagementBar();

			AssertVisible(
				key_alertMessage = "Tokens cannot reference itself.",
				key_fieldLabel = "Brand Color 1",
				locator1 = "TextInput#ALERT_MESSAGE");
		}

		task ("Cancel the publish process") {
			StyleBooks.clickPublish(publishing = "Cancel");
		}

		task ("View the invalid color token is still shown in Brand Color 1 field") {
			InputField.viewText(
				fieldLabel = "Brand Color 1",
				text = "Brand Color 1");
		}

		task ("Type an valid color token in Brand Color 1 field then publish") {
			Type(
				key_text = "Brand Color 1",
				locator1 = "TextInput#ANY",
				value1 = "#00CCBB");

			StyleBooks.clickManagementBar();

			StyleBooks.waitForAutoSave();

			StyleBooks.clickPublish();
		}

		task ("View the Brand Color 1 is new color token") {
			StyleBooksAdmin.gotoStyleBookEllipsisItem(
				item = "Edit",
				styleBookName = "Test Style Book Name");

			InputField.viewText(
				fieldLabel = "Brand Color 1",
				text = "#00CCBB");
		}
	}

	@description = "This is a test for LPS-141568. The user cannot refer two tokens mutually."
	@priority = 4
	test CannotReferTwoTokensMutually {
		task ("Add a style book") {
			JSONStylebooks.addStyleBookEntry(
				groupName = "Test Site Name",
				styleBookEntryName = "Test Style Book Name");

			StyleBooksAdmin.openStyleBooksAdmin(siteURLKey = "test-site-name");

			StyleBooksAdmin.gotoStyleBookEllipsisItem(
				item = "Edit",
				styleBookName = "Test Style Book Name");
		}

		task ("Navigate to the Color System form") {
			StyleBooks.selectForm(formName = "Color System");
		}

		task ("Refer Brand Color 1 to Branch Color 2") {
			StyleBooks.linkValueFromStyleBook(
				colorSwatchItem = "Brand Color 2",
				fieldLabel = "Brand Color 1");
		}

		task ("Cannot refer Brand Color 2 to Brand Color 1") {
			Click.clickNoWaitForVisible(
				key_fieldLabel = "Brand Color 2",
				key_text = "theme",
				locator1 = "Icon#ANY_ITEM_SELECTOR");

			AssertVisible(
				key_text = "Brand Color 1",
				locator1 = "Button#ANY_DISABLED");
		}
	}

	@description = "This is a test for LPS-145650 and LPS-149218. The user could continue publish process in style book editor if link invalid color token. The tokens groups should be collapsed by default except the first one."
	@priority = 5
	test ContinuePublishProcessInStyleBookEditorIfLinkInvalidColorToken {
		property portal.acceptance = "true";

		task ("Add a style book") {
			JSONStylebooks.addStyleBookEntry(
				groupName = "Test Site Name",
				styleBookEntryName = "Test Style Book Name");
		}

		task ("View only the first tokens group is expanded by default") {
			StyleBooksAdmin.openStyleBooksAdmin(siteURLKey = "test-site-name");

			StyleBooksAdmin.gotoStyleBookEllipsisItem(
				item = "Edit",
				styleBookName = "Test Style Book Name");

			AssertElementPresent(
				key_sectionName = "Brand Colors",
				locator1 = "StyleBookEditor#STYLE_BOOK_EDITOR_SIDEBAR_EXPANDED");

			AssertElementPresent(
				key_sectionName = "Grays",
				locator1 = "StyleBookEditor#STYLE_BOOK_EDITOR_SIDEBAR_COLLAPSED");

			AssertElementPresent(
				key_sectionName = "Theme Colors",
				locator1 = "StyleBookEditor#STYLE_BOOK_EDITOR_SIDEBAR_COLLAPSED");
		}

		task ("Refer the Brand Color 1 to Brand Color 2 then refer the Branch Color 2 to Branch Color 1") {
			Type(
				key_text = "Brand Color 1",
				locator1 = "TextInput#ANY",
				value1 = "Brand Color 2");

			Type(
				key_text = "Brand Color 2",
				locator1 = "TextInput#ANY",
				value1 = "Brand Color 1");

			StyleBooks.clickManagementBar();

			AssertVisible(
				key_alertMessage = "Tokens cannot be mutually referenced.",
				key_fieldLabel = "Brand Color 2",
				locator1 = "TextInput#ALERT_MESSAGE");
		}

		task ("Continue the publish process") {
			StyleBooks.clickPublish(publishing = "Continue");
		}

		task ("View the Brand Color 2 is the #6B6C7E") {
			StyleBooksAdmin.gotoStyleBookEllipsisItem(
				item = "Edit",
				styleBookName = "Test Style Book Name");

			InputField.viewText(
				fieldLabel = "Brand Color 2",
				text = "#6B6C7E");

			AssertElementPresent(
				key_fieldLabel = "Brand Color 1",
				key_text = "Brand Color 2",
				locator1 = "Button#ANY_WITH_LABEL");
		}
	}

	@description = "This is a test for LPS-136199. The user could detach the linked token."
	@priority = 5
	test DetachLinkedColorTokenInPageEditor {
		property portal.acceptance = "true";

		task ("Add a Heading fragment to a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");
		}

		task ("Change the Text Color to Success") {
			PageEditor.gotoStyle(fragmentName = "Heading");

			PageEditor.configureFragment(textColor = "Success");
		}

		task ("View the Text Color of Heading is Success") {
			PageEditor.viewFragmentStyle(
				fragmentName = "Heading",
				textColor = "success");
		}

		task ("Detach the linked token") {
			PageEditor.detachToken(fieldLabel = "Text Color");
		}

		task ("View the color reference is shown in Text Color field") {
			AssertTextEquals(
				key_text = "Text Color",
				locator1 = "TextInput#ANY",
				value1 = "#287D3C");

			PageEditor.viewFragmentStyle(
				fragmentName = "Heading",
				textColor = "rgb(40, 125, 60)");
		}
	}

	@description = "This is a test for LPS-141568. The previous reference will be maintained, if the user fill out wrong digit characters when clicking on the tab, intro or outside the output."
	@priority = 4
	test PersistPreviousColorReferenceWhenPopulateWrongDigitCharacters {
		task ("Add a Container to a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Layout Elements",
				fragmentName = "Container");
		}

		task ("Change the Background Color to Info") {
			PageEditor.editLayoutContainer(
				backgroundColor = "Info",
				panel = "Styles");
		}

		task ("Detach the linked token") {
			PageEditor.detachToken(fieldLabel = "Background Color");
		}

		task ("View the previous color reference is persisted after fill out wrong digit characters color reference") {
			for (var colorReference : list "#0,#0A,#0BC4,#0B2C2") {
				Type(
					key_text = "Background Color",
					locator1 = "TextInput#ANY",
					value1 = ${colorReference});

				Click(
					key_sidebarButton = "Browser",
					locator1 = "PageEditor#FRAGMENT_SIDEBAR_TITLE");

				PageEditor.waitForAutoSave();

				AssertTextEquals(
					key_text = "Background Color",
					locator1 = "TextInput#ANY",
					value1 = "#2E5AAC");

				PageEditor.viewContainerStyle(backgroundColor = "rgb(46, 90, 172)");
			}
		}
	}

	@description = "This is a test for LPS-160000. When the page creator types an invalid color token in page editor, the previous value is restored."
	@priority = 4
	test RestorePreviousValueInPageEditorWhenTypeInvalidColorToken {
		task ("Given a page creator has a Heading fragment on a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");

			PageEditor.gotoStyle(fragmentName = "Heading");

			PageEditor.configureFragment(textColor = "Info");

			PageEditor.detachToken(fieldLabel = "Text Color");
		}

		task ("When the page creator types an invalid color token in Text Color field") {
			Type(
				key_text = "Text Color",
				locator1 = "TextInput#ANY",
				value1 = "aaa");
		}

		task ("Then the previous value is restored once end editing") {
			Click(
				key_sidebarButton = "Browser",
				locator1 = "PageEditor#FRAGMENT_SIDEBAR_TITLE");

			AssertTextEquals(
				key_text = "Text Color",
				locator1 = "TextInput#ANY",
				value1 = "#2E5AAC");
		}
	}

	@description = "This is a test for LPS-160000. When the style book designer types an invalid color token in style book editor, the previous value is restored."
	@priority = 4
	test RestorePreviousValueInStyleBookEditorWhenTypeInvalidColorToken {
		task ("Given a style book designer has a style book") {
			JSONStylebooks.addStyleBookEntry(
				groupName = "Test Site Name",
				styleBookEntryName = "Test Style Book Name");
		}

		task ("When the style book designer types an invalid color token in Brand Color 1") {
			StyleBooksAdmin.openStyleBooksAdmin(siteURLKey = "test-site-name");

			StyleBooksAdmin.gotoStyleBookEllipsisItem(
				item = "Edit",
				styleBookName = "Test Style Book Name");

			Type(
				key_text = "Brand Color 1",
				locator1 = "TextInput#ANY",
				value1 = "aaa");
		}

		task ("Then the previous value is restored once end editing") {
			StyleBooks.clickManagementBar();

			AssertTextEquals(
				key_text = "Brand Color 1",
				locator1 = "TextInput#ANY",
				value1 = "#0B5FFF");
		}
	}

	@description = "This is a test for LPS-136199. The token changes from style book should be applied to the usages."
	@priority = 4
	test ViewColorTokenChangesAppliedToUsages {
		task ("Add a Heading fragment to a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");
		}

		task ("Change the Text Color to Success") {
			PageEditor.gotoStyle(fragmentName = "Heading");

			PageEditor.configureFragment(textColor = "Success");
		}

		task ("View the Text Color of Heading is Success") {
			PageEditor.viewFragmentStyle(
				fragmentName = "Heading",
				textColor = "success");

			PageEditor.publish();
		}

		task ("Add a style book") {
			JSONStylebooks.addStyleBookEntry(
				groupName = "Test Site Name",
				styleBookEntryName = "Test Style Book Name");
		}

		task ("Change Success of Theme Colors in Color System form") {
			StyleBooksAdmin.openStyleBooksAdmin(siteURLKey = "test-site-name");

			StyleBooksAdmin.gotoStyleBookEllipsisItem(
				item = "Edit",
				styleBookName = "Test Style Book Name");

			StyleBooks.expandSection(sectionName = "Theme Colors");

			Type(
				key_fieldLabel = "Success",
				key_groupLabel = "Theme Colors",
				locator1 = "TextInput#GROUPED_TEXT_INPUT",
				value1 = "#34f787");

			StyleBooks.clickManagementBar();

			StyleBooks.waitForAutoSave();

			StyleBooks.clickPublish();
		}

		task ("Apply the new style book to content page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.gotoTab(tabName = "Page Design Options");

			StyleBooks.selectStyleBookViaPageDesignOptions(styleBookName = "Test Style Book Name");
		}

		task ("View the color reference of Text Color is #34f787") {
			PageEditor.viewFragmentStyle(
				fragmentName = "Heading",
				textColor = "success");

			AssertCssValue(
				fragmentName = "heading",
				id = "element-text",
				index = 1,
				locator1 = "Fragment#CONTRIBUTED_FRAGMENT_TEXT",
				locator2 = "color",
				value1 = "rgba(52, 247, 135, 1)");
		}
	}

}