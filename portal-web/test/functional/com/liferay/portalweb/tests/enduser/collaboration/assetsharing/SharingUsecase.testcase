@component-name = "portal-collaboration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Asset Sharing";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site");

		JSONLayout.addPublicLayout(
			groupName = "Test Site",
			layoutName = "Test Page");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea@liferay.com",
			userScreenName = "usersn");
	}

	tearDown {
		JSONGroup.deleteGroupByName(groupName = "Test Site");
	}

	@description = "This test ensures that Sharing can be disabled at the instance level."
	@priority = "4"
	test DisableSharingAtInstanceScope {
		property test.name.skip.portal.instance = "SharingUsecase#DisableSharingAtInstanceScope";

		SystemSettings.openSystemSettingsAdmin();

		SystemSettings.gotoConfiguration(
			configurationCategory = "Sharing",
			configurationName = "Sharing",
			configurationScope = "Virtual Instance Scope");

		FormFields.disableCheckbox(fieldName = "enabled");

		SystemSettings.saveConfiguration();

		Navigator.openURL();

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site",
			layoutName = "Test Page",
			widgetName = "Documents and Media");

		Navigator.gotoSitePage(
			pageName = "Test Page",
			siteName = "Test Site");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoSitePage(
			pageName = "Test Page",
			siteName = "Test Site");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMDocument.addPG(
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "Document with sharing disabled");

		LexiconEntry.openEntryMenu(rowEntry = "Document with sharing disabled");

		AssertElementNotPresent(
			key_menuItem = "Share",
			locator1 = "MenuItem#ANY_MENU_ITEM");

		AssertClick(
			key_dmDocumentTitle = "Document with sharing disabled",
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_TITLE",
			value1 = "Document with sharing disabled");

		AssertElementNotPresent(
			key_text = "Share",
			locator1 = "Button#ANY");

		BlogsNavigator.openBlogsAdmin(siteURLKey = "guest");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Blogs.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs with sharing disabled");

		BlogsNavigator.openBlogsAdmin(siteURLKey = "guest");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Click(
			key_listEntry = "Blogs with sharing disabled",
			locator1 = "LexiconList#LIST_ENTRY_ELLIPSIS");

		AssertElementNotPresent(
			key_menuItem = "Share",
			locator1 = "MenuItem#ANY_MENU_ITEM");
	}

	@description = "This test ensures that Sharing can be disabled at the system level."
	@priority = "4"
	test DisableSharingAtSystemScope {
		property test.name.skip.portal.instance = "SharingUsecase#DisableSharingAtSystemScope";

		SystemSettings.openSystemSettingsAdmin();

		SystemSettings.gotoConfiguration(
			configurationCategory = "Sharing",
			configurationName = "Sharing",
			configurationScope = "System Scope");

		FormFields.disableCheckbox(fieldName = "enabled");

		SystemSettings.saveConfiguration();

		Navigator.openURL();

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site",
			layoutName = "Test Page",
			widgetName = "Documents and Media");

		Navigator.gotoSitePage(
			pageName = "Test Page",
			siteName = "Test Site");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoSitePage(
			pageName = "Test Page",
			siteName = "Test Site");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMDocument.addPG(
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "Document with sharing disabled");

		LexiconEntry.openEntryMenu(rowEntry = "Document with sharing disabled");

		AssertElementNotPresent(
			key_menuItem = "Share",
			locator1 = "MenuItem#ANY_MENU_ITEM");

		AssertClick(
			key_dmDocumentTitle = "Document with sharing disabled",
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_TITLE",
			value1 = "Document with sharing disabled");

		AssertElementNotPresent(
			key_text = "Share",
			locator1 = "Button#ANY");

		BlogsNavigator.openBlogsAdmin(siteURLKey = "guest");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Blogs.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs with sharing disabled");

		BlogsNavigator.openBlogsAdmin(siteURLKey = "guest");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Click(
			key_listEntry = "Blogs with sharing disabled",
			locator1 = "LexiconList#LIST_ENTRY_ELLIPSIS");

		AssertElementNotPresent(
			key_menuItem = "Share",
			locator1 = "MenuItem#ANY_MENU_ITEM");
	}

	@description = "This test ensures that users can Filter By AssetType in the Shared Content app."
	@priority = "3"
	test FilterByAssetType {
		property test.name.skip.portal.instance = "SharingUsecase#FilterByAssetType";

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site",
			layoutName = "Test Page",
			widgetName = "Documents and Media");

		Navigator.gotoSitePage(
			pageName = "Test Page",
			siteName = "Test Site");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoSitePage(
			pageName = "Test Page",
			siteName = "Test Site");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMDocument.addMultiplePG(
			dmDocumentFileList = "Document_1.jpeg,Document_2.docx",
			viewableBy = "Owner");

		DMNavigator.gotoShare(dmDocumentTitle = "Document_1.jpeg");

		DMDocument.sharePG(
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		BlogsNavigator.openBlogsAdmin(siteURLKey = "guest");

		Blogs.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title");

		BlogsNavigator.openBlogsAdmin(siteURLKey = "guest");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		BlogsNavigator.gotoShare(entryTitle = "Blogs Entry Title");

		Asset.share(
			sharingPermissions = "View",
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "Shared Content");

		ManagementBar.setFilterAndOrder(filterBy = "Asset Types");

		AssertTextEquals(
			locator1 = "Portlet#H3_MODAL_TITLE",
			value1 = "Select Asset Type");

		SelectFrame(locator1 = "IFrame#MODAL_IFRAME");

		var key_assetType = "Documents";

		AssertClick(
			locator1 = "Sharing#SHARED_CONTENT_ASSET_TYPE",
			value1 = "Documents");

		SelectFrame(value1 = "relative=top");

		AssertClick(
			locator1 = "Button#SELECT",
			value1 = "Select");

		var key_contentTitle = "Document_1.jpeg";

		AssertTextEquals(
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_TITLE",
			value1 = "Document_1.jpeg");
		AssertElementNotPresent(
			key_contentTitle = "Blogs Entry Title",
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_TITLE_2");

		ManagementBar.setFilterAndOrder(filterBy = "All");

		ManagementBar.setFilterAndOrder(filterBy = "Asset Types");

		AssertTextEquals(
			locator1 = "Portlet#H3_MODAL_TITLE",
			value1 = "Select Asset Type");

		SelectFrame(locator1 = "IFrame#MODAL_IFRAME");

		var key_assetType = "Blogs";

		AssertClick(
			locator1 = "Sharing#SHARED_CONTENT_ASSET_TYPE",
			value1 = "Blogs");

		SelectFrame(value1 = "relative=top");

		AssertClick(
			locator1 = "Button#SELECT",
			value1 = "Select");

		var key_contentTitle = "Blogs Entry Title";

		AssertTextEquals(
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_TITLE",
			value1 = "Blogs Entry Title");
		AssertElementNotPresent(
			key_contentTitle = "Document_1.jpeg",
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_TITLE_2");
	}

	@description = "This test ensures that users can Order By Shared Date in the Shared Content app."
	@priority = "3"
	test OrderBySharedDate {
		property test.name.skip.portal.instance = "SharingUsecase#OrderBySharedDate";

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site",
			layoutName = "Test Page",
			widgetName = "Documents and Media");

		Navigator.gotoSitePage(
			pageName = "Test Page",
			siteName = "Test Site");

		DMDocument.enableActionsMenuOnPortlet();

		Navigator.gotoSitePage(
			pageName = "Test Page",
			siteName = "Test Site");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMDocument.addMultiplePG(
			dmDocumentFileList = "Document_1.jpeg,Document_2.docx",
			viewableBy = "Owner");

		DMNavigator.gotoShare(dmDocumentTitle = "Document_1.jpeg");

		DMDocument.sharePG(
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		Navigator.gotoSitePage(
			pageName = "Test Page",
			siteName = "Test Site");

		DMNavigator.gotoShare(dmDocumentTitle = "Document_2.docx");

		DMDocument.sharePG(
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		BlogsNavigator.openBlogsAdmin(siteURLKey = "guest");

		Blogs.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title");

		BlogsNavigator.openBlogsAdmin(siteURLKey = "guest");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		BlogsNavigator.gotoShare(entryTitle = "Blogs Entry Title");

		Asset.share(
			sharingPermissions = "View",
			userEmailAddress = "userea@liferay.com",
			userName = "userfn userln");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "Shared Content");

		ManagementBar.setFilterAndOrder(orderBy = "Shared Date");

		ManagementBar.toggleViewOrder(order = "down");

		AssertElementPresent(
			key_contentTitle = "Blogs Entry Title",
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_TITLE");
		AssertElementPresent(
			key_contentTitle = "Document_2.docx",
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_TITLE_2");
		AssertElementPresent(
			key_contentTitle = "Document_1.jpeg",
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_TITLE_3");
	}

}