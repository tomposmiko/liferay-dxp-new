@component-name = "portal-analytics-cloud"
definition {

	property analytics.cloud.release = "true";
	property analytics.cloud.upstream = "true";
	property portal.release = "false";
	property portal.upstream = "true";
	property property.group = "analytics.cloud.profile";
	property testray.main.component.name = "Analytics Cloud";

	setUp {
		ACUtils.launchAC();

		ACSettings.takeTokenFromAnalyticsCloud();

		TestCase.setUpPortalInstance();

		ACUtils.launchDXP();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		ACUtils.tearDownAC();

		ACUtils.tearDownDXP();

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Site Name");
		}
	}

	@description = "Automation ID: LRAC-8832 | Test Summary: Add DXP data source and configure only one site"
	@priority = 3
	test AddDataSourceForOneSite {
		property portal.release = "true";

		task ("Add site and page") {
			ACUtils.addSiteAndPage();
		}

		task ("Connect DXP to AC") {
			var propertyName = ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");
		}

		task ("Open site Liferay DXP and close all sessions") {
			Navigator.openURL();

			ACUtils.closeAllSessionsAndWait();
		}

		task ("Launch AC, go to sites overview and change sites activities to the last 24 hours") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = ${propertyName});

			ACNavigation.goToSites();

			ACNavigation.switchTab(tabName = "Overview");

			ACTimeFilter.clickTimeFilterButton();

			ACTimeFilter.setLast24Hours();
		}

		task ("View the number of Unique Visitors in the sites activities") {
			ACCards.viewMetricValue(
				cardTitle = "${propertyName} Activities",
				metricName = "Unique Visitors",
				metricValue = 0);
		}

		task ("Launch DXP and go to site") {
			ACUtils.launchDXP();

			ACUtils.navigateToSitePage(
				pageName = "AC Page",
				siteName = "Site Name");
		}

		task ("Launch AC and go to sites overview and change sites activities to the last 24 hours") {
			ACUtils.closeAllSessionsAndWait();

			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = ${propertyName});

			ACNavigation.goToSites();

			ACNavigation.switchTab(tabName = "Overview");

			ACTimeFilter.clickTimeFilterButton();

			ACTimeFilter.setLast24Hours();
		}

		task ("View if the number of Unique Visitors in the sites activities has changed, this secure the connection ") {
			ACCards.viewMetricValue(
				cardTitle = "${propertyName} Activities",
				metricName = "Unique Visitors",
				metricValue = 1,
				timeFilter = "Last 24 hours");
		}
	}

	@description = "Automation ID: LRAC-8108 | Test Summary: Add date source page that allows user to choose type of data source"
	@ignore = "true"
	@priority = 5
	test AddDataSourcePageAllowsUserToChooseWhatKind {

		// Salesforce and CSV are hidden due to LRAC-11651 and LRAC-11571

		task ("Go to data sources and enter in each one to assert that the user can choose any of them") {
			ACUtils.launchAC();

			ACNavigation.goToSettings();

			ACSettings.goToDataSources();

			ACSettings.clickGenericAddOrConfirmationButton();

			ACSettings.assertCanChooseDataSourceOptions();
		}
	}

	@description = "Automation ID: LRAC-8835 | Test Summary: Assert Liferay Data Source Contact Count Preview"
	@priority = 4
	test AssertLiferayDataSourceContactCountPreview {
		property portal.release = "true";

		task ("Connect DXP to AC") {
			var propertyName = ACDXPSettings.connectDXPtoAnalyticsCloud();
		}

		task ("Go to contact data and get the string with number of contacts") {
			ACDXPSettings.navToSyncedContactDataMenu();

			Click(locator1 = "ACDXPSettings#SYNC_CONTACTS_OPTION");

			var currentContacts = selenium.getText("ACDXPSettings#SYNC_ALL_CONTACTS_NUMBER");

			var numberContacts = StringUtil.extractDigits(${currentContacts});
		}

		task ("Add a new user") {
			Navigator.openURL();

			JSONUser.addUser(
				userEmailAddress = "ac@liferay.com",
				userFirstName = "ac",
				userLastName = "ac",
				userScreenName = "ac");
		}

		task ("Add 1 into the value of the variable and format the string") {
			var numberContacts = ${numberContacts} + 1;

			var newContacts = StringUtil.add("Sync All (", ${numberContacts}, "");

			var newContacts = StringUtil.add(${newContacts}, " Contacts)", "");
		}

		task ("Go to contact data and assert that the number of contacts updated") {
			ACDXPSettings.goToInstanceSettingsAC();

			ACDXPSettings.navToSyncedContactDataMenu();

			Click(locator1 = "ACDXPSettings#SYNC_CONTACTS_OPTION");

			AssertTextEquals(
				locator1 = "ACDXPSettings#SYNC_ALL_CONTACTS_NUMBER",
				value1 = ${newContacts});
		}
	}

	@description = "Automation ID: LRAC-8834 | Test Summary: Assert the Liferay DXP Data Source Status"
	@priority = 3
	test AssertLiferayDataSourceStatus {
		property portal.release = "true";

		var sectionTitleList = ListUtil.newListFromString("Current Status,Synced Sites,Synced Contacts");
		var statusSyncSite = ListUtil.newListFromString("Active,Configured,Unconfigured");
		var statusSyncContact = ListUtil.newListFromString("Active,Configured,Configured");
		var statusDisconnect = ListUtil.newListFromString("Disconnected,Unconfigured,Unconfigured");
		var size = ListUtil.size(${sectionTitleList});

		task ("Connect DXP to AC, create a property, and sync the site only") {
			ACDXPSettings.goToInstanceSettingsAC();

			ACDXPSettings.connectAnalyticsCloud();

			Alert.viewSuccessMessage();

			ACDXPSettings.navToSyncedSitesMenu();

			ACDXPSettings.createCombinedProperties();

			LexiconEntry.gotoAdd();

			WaitForElementPresent(locator1 = "ACDXPSettings#SITE_NAME");

			var siteName = selenium.getText("ACDXPSettings#SITE_NAME");

			ACDXPSettings.getAssignedPropertyName(siteName = ${siteName});

			var dataSource = StringUtil.replace(${assignedPropertyName}, " Combined Property", "");
		}

		task ("Launch AC, go to settings and data source and click in the data source searched") {
			ACUtils.launchAC();

			ACNavigation.goToSettings();

			ACSettings.goToDataSources();

			ACUtils.searchBar(searchItem = ${dataSource});

			ACNavigation.openItem(itemName = ${dataSource});
		}

		task ("Assert the data source Status: Active, Sites: Configured, Contacts: Unconfigured") {
			var i = 0;

			while (${i} != ${size}) {
				var section = ListUtil.get(${sectionTitleList}, ${i});
				var status = ListUtil.get(${statusSyncSite}, ${i});

				ACSettings.assertDataSourceStatus(
					section = ${section},
					status = ${status});

				var i = ${i} + 1;

				var i = StringUtil.valueOf(${i});
			}
		}

		task ("Launch DXP and sync contact") {
			ACUtils.launchDXP();

			ACDXPSettings.goToInstanceSettingsAC();

			ACDXPSettings.navToSyncedContactDataMenu();

			ACDXPSettings.syncContactData();
		}

		task ("Launch AC, go to settings and data source and click in the data source searched") {
			ACUtils.launchAC();

			ACNavigation.goToSettings();

			ACSettings.goToDataSources();

			ACUtils.searchBar(searchItem = ${dataSource});

			ACNavigation.openItem(itemName = ${dataSource});
		}

		task ("Assert the data source Status: Active, Sites: Configured, Contacts: Configured") {
			var i = 0;

			while (${i} != ${size}) {
				var section = ListUtil.get(${sectionTitleList}, ${i});
				var status = ListUtil.get(${statusSyncContact}, ${i});

				ACSettings.assertDataSourceStatus(
					section = ${section},
					status = ${status});

				var i = ${i} + 1;

				var i = StringUtil.valueOf(${i});
			}
		}

		task ("Disconnect data source and assert the data source Status: Disconnected, Sites: Unconfigured, Contacts: Unconfigured") {
			ACSettings.disconnectDataSource();

			var i = 0;

			while (${i} != ${size}) {
				var section = ListUtil.get(${sectionTitleList}, ${i});
				var status = ListUtil.get(${statusDisconnect}, ${i});

				ACSettings.assertDataSourceStatus(
					section = ${section},
					status = ${status});

				var i = ${i} + 1;

				var i = StringUtil.valueOf(${i});
			}
		}
	}

	@description = "Automation ID: LRAC-8836 | Test Summary: Cancel Creating a DXP Data Source"
	@priority = 3
	test CancelCreatingDxpDataSource {
		property test.name.skip.portal.instance = "AnalyticsCloudConnectionTest#CancelCreatingDxpDataSource";

		task ("Create a new workspace") {
			ACUtils.launchAC(
				emailList = "acworkspace@liferay.com",
				workspaceName = "New AC Workspace");
		}

		task ("Access the data sources, select Liferay DXP and click the cancel button") {
			ACNavigation.goToSettings();

			ACSettings.goToDataSources();

			if (IsElementPresent(key_textValue = "Welcome to Analytics Cloud", locator1 = "ACUtils#GENERIC_TEXT")) {
				ACUtils.closeModal();
			}

			ACSettings.clickGenericAddOrConfirmationButton();

			Click(locator1 = "AnalyticsCloudConnection#LIFERAY_DXP_SOURCE");

			ACNavigation.clickCancelButton();
		}

		task ("Assert the data source list if a new data source has not been created") {
			ACSettings.goToDataSources();

			if (IsElementPresent(key_textValue = "Welcome to Analytics Cloud", locator1 = "ACUtils#GENERIC_TEXT")) {
				ACUtils.closeModal();
			}

			ACUtils.viewGenericText(textValueList = "No Data Sources Connected,Add a data source to get started.,Access our documentation to learn more.");
		}
	}

	@description = "Automation ID: LRAC-8107 | Test Summary: Navigates to data source page"
	@priority = 5
	test CanNavigatesToDataSourcePage {
		var propertyName = ACDXPSettings.connectDXPtoAnalyticsCloud();

		var dataSource = StringUtil.replace(${propertyName}, " Combined Property", "");

		ACDXPSettings.syncNewContactData();

		ACUtils.launchAC();

		ACProperties.switchProperty(propertyName = ${propertyName});

		ACNavigation.goToSettings();

		ACUtils.assertItemsName(itemList = ${dataSource});

		ACUtils.assertPageTitle(pageTitle = "Data Sources");
	}

	@description = "Sync Liferay Data Source Contacts"
	@priority = 5
	test CanSyncAllContactsFromDXPToAC {
		property analytics.cloud.upstream = "false";
		property portal.release = "true";

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		var propertyName = ACDXPSettings.connectDXPtoAnalyticsCloud();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea@liferay.com",
			userLoginFullName = "userfn userln");

		ACUtils.closeAllSessionsAndWait();

		ACUtils.launchAC();

		ACProperties.switchProperty(propertyName = ${propertyName});

		ACNavigation.goToIndividuals();

		ACNavigation.switchTab(tabName = "Known Individuals");

		ACUtils.searchBar(searchItem = "userfn userln");

		ACIndividualsDashboard.goToKnownIndividualDetails(individualName = "userfn userln");

		ACNavigation.switchTab(tabName = "Details");

		ACIndividualsDashboard.assertIndividualAttribute(individualAttribute = "userea@liferay.com");

		ACIndividualsDashboard.assertIndividualAttribute(individualAttribute = "userfn");
	}

	@description = "Bug: LRAC-11140 | Automation ID: LRAC-11141 | Test Summary: Check the AC connection success message with the DXP"
	@priority = 3
	test CheckConnectionSuccessMessage {
		property portal.release = "true";

		ACDXPSettings.goToInstanceSettingsAC();

		ACDXPSettings.connectAnalyticsCloud();

		ACDXPSettings.assertSuccessfulConnection();
	}

	@description = "Bug: LRAC-11423 | Automation ID: LRAC-11475 | Test Summary: Check if it is possible to connect AC with DXP using an existing data source"
	@priority = 4
	test CheckIfIsPossibleToReconnectACWithDXP {
		task ("Connect the DXP to AC") {
			ACDXPSettings.connectDXPtoAnalyticsCloud();
		}

		task ("Disconnect the DXP to AC") {
			ACDXPSettings.disconnectDXPFromAnalyticsCloud();
		}

		task ("Form the data source name") {
			var dataSourceName = StringUtil.replace(${assignedPropertyName}, " Combined Property", "");
		}

		task ("Launch AC and copy the token from an existing data source") {
			ACUtils.launchAC();

			ACSettings.takeTokenFromExistingDataSource(dataSourceName = ${dataSourceName});
		}

		task ("Reconnect AC with DXP") {
			ACDXPSettings.connectDXPtoAnalyticsCloud();
		}
	}

	@description = "Validate if the user can connect AC to DXP"
	@priority = 5
	test ConnectACtoDXP {
		property portal.release = "true";

		ACDXPSettings.connectDXPtoAnalyticsCloud();
	}

	@description = "Bug: LRAC-10629 | Automation ID: LRAC-11443 | Test Summary: Sites and Contacts connection status is not changing when renaming the data source"
	@priority = 3
	test ConnectionStatusNotChangeWhenRenameDataSource {
		task ("Launch AC then go to Data Sources and open the created data source") {
			ACDXPSettings.connectDXPtoAnalyticsCloud();

			var dataSource = StringUtil.replace(${assignedPropertyName}, " Combined Property", "");

			ACUtils.launchAC();

			ACNavigation.goToSettings();

			ACSettings.goToDataSources();

			ACUtils.searchBar(searchItem = ${dataSource});

			ACNavigation.openItem(itemName = ${dataSource});
		}

		task ("See that the status of sites and contacts are 'configured'") {
			ACUtils.viewGenericTextInOrder(
				index = 1,
				textValueList = "Configured");

			ACUtils.viewGenericTextInOrder(
				index = 2,
				textValueList = "Configured");
		}

		task ("Rename the data source and see that the status of sites and contacts are still 'configured'") {
			ACSettings.renameDataSource(dataSourceNewName = "New Data Source Name");

			ACUtils.viewGenericTextInOrder(
				index = 1,
				textValueList = "Configured");

			ACUtils.viewGenericTextInOrder(
				index = 2,
				textValueList = "Configured");
		}

		task ("Refresh the page and check the status again") {
			Refresh();

			ACUtils.viewGenericTextInOrder(
				index = 1,
				textValueList = "Configured");

			ACUtils.viewGenericTextInOrder(
				index = 2,
				textValueList = "Configured");
		}

		task ("Disconnect the data source and see that the status is 'disconnected'") {
			ACSettings.disconnectDataSource();

			ACSettings.assertDataSourceStatus(
				section = "Current Status",
				status = "Disconnected");
		}

		task ("Rename the data source and see that the status is still 'disconnected'") {
			ACSettings.renameDataSource(dataSourceNewName = "New Data Source Name 2");

			ACSettings.assertDataSourceStatus(
				section = "Current Status",
				status = "Disconnected");
		}

		task ("Refresh the page and see that the data source status is still 'disconnected'") {
			Refresh();

			ACSettings.assertDataSourceStatus(
				section = "Current Status",
				status = "Disconnected");
		}
	}

	@description = "Automation ID: LRAC-8821 | Test Summary: Data Source list is searchable"
	@priority = 5
	test DataSourceListIsSearchable {
		task ("Create data sources") {
			var propertyName = ACDXPSettings.connectDXPtoAnalyticsCloud();

			var dataSource = StringUtil.replace(${propertyName}, " Combined Property", "");

			ACUtils.launchAC();

			ACNavigation.goToSettings();

			ACUtils.assertDataSourceName(dataSourceName = ${dataSource});
		}

		task ("Assert that a data source is searchable and found") {
			ACUtils.searchBar(searchItem = ${dataSource});

			ACUtils.assertDataSourceName(dataSourceName = ${dataSource});
		}

		task ("Assert that a data source that doesn't exist is not found") {
			ACUtils.searchBar(searchItem = "Analytics Cloud");

			ACUtils.viewGenericText(textValueList = "There are no results found.,Please try a different search term.");
		}
	}

	@description = "Automation ID: LRAC-8825 | Test Summary: Validate Invalid Token"
	@priority = 4
	test InvalidToken {
		ACUtils.launchAC();

		AnalyticsCloud.takeInvalidTokenFromAnalyticsCloud();
	}

	@description = "Automation ID: LRAC-8839 | Test Summary: Preview the number of Organization contacts to sync"
	@priority = 3
	test OrganizationContactSyncPreview {
		property portal.release = "true";

		task ("Create a new user, a new organization and assign the organization for the created user") {
			JSONUser.addUser(
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");

			JSONOrganization.addOrganization(organizationName = "Organization Name 1");

			JSONOrganization.assignUserToOrganization(
				organizationName = "Organization Name 1",
				userEmailAddress = "userea@liferay.com");

			ApplicationsMenu.gotoPortlet(
				category = "Users",
				panel = "Control Panel",
				portlet = "Users and Organizations");

			UsersAndOrganizationsNavigator.gotoOrganizations();

			Organization.gotoCP(orgName = "Organization Name 1");

			Organization.viewUser(userName = "userfn userln");
		}

		task ("Connect to AC") {
			var propertyName = ACDXPSettings.connectDXPtoAnalyticsCloud(orgName = "Organization Name 1");
		}

		task ("Assert preview the number of Organization contacts to sync") {
			ACDXPSettings.navToSyncedContactDataMenu();

			Click(locator1 = "ACDXPSettings#SYNC_CONTACTS_OPTION");

			var entrieOrg = selenium.getText("ACDXPSettings#SYNC_BY_ORGANIZATIONS_PREVIEW");

			var numberOrg = StringUtil.extractDigits(${entrieOrg});

			AssertTextEquals(
				locator1 = "ACDXPSettings#SYNC_BY_ORGANIZATIONS_PREVIEW",
				value1 = ${entrieOrg});
		}

		task ("Create a new organization and assign the organization for the created user") {
			JSONOrganization.addOrganization(organizationName = "Organization Name 2");

			JSONOrganization.assignUserToOrganization(
				organizationName = "Organization Name 2",
				userEmailAddress = "userea@liferay.com");

			ApplicationsMenu.gotoPortlet(
				category = "Users",
				panel = "Control Panel",
				portlet = "Users and Organizations");

			UsersAndOrganizationsNavigator.gotoOrganizations();

			Organization.gotoCP(orgName = "Organization Name 2");

			Organization.viewUser(userName = "userfn userln");
		}

		task ("Connect to AC") {
			ACDXPSettings.goToInstanceSettingsAC();

			ACDXPSettings.navToSyncedContactDataMenu();

			ACDXPSettings.syncByOrganizations(
				index = 2,
				orgName = "Organization Name 2");
		}

		task ("Assert preview the number of Organization contacts to sync") {
			ACDXPSettings.navToSyncedContactDataMenu();

			Click(locator1 = "ACDXPSettings#SYNC_CONTACTS_OPTION");

			var newNumberOrg = ${numberOrg} + 1;

			var newEntrieOrg = StringUtil.add(${newNumberOrg}, " Organizations Selected", "");

			AssertTextEquals(
				locator1 = "ACDXPSettings#SYNC_BY_ORGANIZATIONS_PREVIEW",
				value1 = ${newEntrieOrg});
		}
	}

	@description = "Automation ID: LRAC-8822 | Test Summary: Sync Liferay Data Source Contacts by User Groups"
	@priority = 4
	test SyncDataSourceContactsByGroups {
		property analytics.cloud.upstream = "false";
		property portal.release = "true";

		task ("Add user, group and assign user to group") {
			JSONUser.addUser(
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");

			JSONUserGroup.addUserGroup(
				userGroupDescription = "User Group Description",
				userGroupName = "User Group Name");

			ApplicationsMenu.gotoPortlet(
				category = "Users",
				panel = "Control Panel",
				portlet = "Users and Organizations");

			UserNavigator.gotoUser(userScreenName = "usersn");

			User.assignUserGroupCP(userGroupName = "User Group Name");
		}

		task ("Connect to AC") {
			var propertyName = ACDXPSettings.connectDXPtoAnalyticsCloud(userGroup = "User Group Name");
		}

		task ("Login as another user and visit DXP") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "userea@liferay.com",
				userLoginFullName = "userfn userln");
		}

		task ("Close sessions") {
			ACUtils.closeAllSessionsAndWait();

			ACUtils.launchAC();
		}

		task ("Go to Known Individuals tab") {
			ACProperties.switchProperty(propertyName = ${propertyName});

			ACNavigation.goToIndividuals();

			ACNavigation.switchTab(tabName = "Known Individuals");
		}

		task ("Assert that the new user is present") {
			ACUtils.searchBar(searchItem = "userfn userln");

			ACIndividualsDashboard.goToKnownIndividualDetails(individualName = "userfn userln");

			ACNavigation.switchTab(tabName = "Details");

			ACIndividualsDashboard.assertIndividualAttribute(individualAttribute = "userea@liferay.com");

			ACIndividualsDashboard.assertIndividualAttribute(individualAttribute = "userfn");
		}
	}

	@description = "Automation ID: LRAC-8823 | Test Summary: Sync Liferay Data Source Contacts by Organizations"
	@priority = 4
	test SyncDataSourceContactsByOrganizations {
		property analytics.cloud.upstream = "false";
		property portal.release = "true";

		task ("Create a new user, a new organization and assign the organization for the created user") {
			JSONUser.addUser(
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");

			JSONOrganization.addOrganization(organizationName = "Organization Name");

			JSONOrganization.assignUserToOrganization(
				organizationName = "Organization Name",
				userEmailAddress = "userea@liferay.com");

			ApplicationsMenu.gotoPortlet(
				category = "Users",
				panel = "Control Panel",
				portlet = "Users and Organizations");

			UsersAndOrganizationsNavigator.gotoOrganizations();

			Organization.gotoCP(orgName = "Organization Name");

			Organization.viewUser(userName = "userfn userln");
		}

		task ("Connect to AC") {
			var propertyName = ACDXPSettings.connectDXPtoAnalyticsCloud(orgName = "Organization Name");
		}

		task ("Login as another user and visit DXP") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "userea@liferay.com",
				userLoginFullName = "userfn userln");
		}

		task ("Close sessions") {
			ACUtils.closeAllSessionsAndWait();

			ACUtils.launchAC();
		}

		task ("Go to Known Individuals tab") {
			ACProperties.switchProperty(propertyName = ${propertyName});

			ACNavigation.goToIndividuals();

			ACNavigation.switchTab(tabName = "Known Individuals");
		}

		task ("Assert that the new user is present") {
			ACUtils.searchBar(searchItem = "userfn userln");

			ACIndividualsDashboard.goToKnownIndividualDetails(individualName = "userfn userln");

			ACNavigation.switchTab(tabName = "Details");

			ACIndividualsDashboard.assertIndividualAttribute(individualAttribute = "userea@liferay.com");

			ACIndividualsDashboard.assertIndividualAttribute(individualAttribute = "userfn");
		}
	}

	@description = "Automation ID: LRAC-8824 | Test Summary: Preview the number of User Group contacts to sync"
	@priority = 4
	test UserGroupContactSyncPreview {
		property portal.release = "true";

		var user = "ac";
		var groupsList = "User Group1,User Group2";

		task ("Create user") {
			JSONUser.addUser(
				userEmailAddress = "${user}@liferay.com",
				userFirstName = ${user},
				userLastName = ${user},
				userScreenName = ${user});
		}

		task ("Create groups") {
			for (var groups : list ${groupsList}) {
				JSONUserGroup.addUserGroup(
					userGroupDescription = "${groups} Description",
					userGroupName = ${groups});
			}
		}

		task ("Go to users and organizations") {
			ApplicationsMenu.gotoPortlet(
				category = "Users",
				panel = "Control Panel",
				portlet = "Users and Organizations");
		}

		task ("Entry user and add to groups") {
			UserNavigator.gotoUser(userScreenName = ${user});

			for (var addUsers : list ${groupsList}) {
				User.assignUserGroupCP(userGroupName = ${addUsers});
			}
		}

		task ("Connect DXP to AC") {
			var propertyName = ACDXPSettings.connectDXPtoAnalyticsCloud(userGroup = "User Group1");
		}

		task ("Assert preview the number of users groups contacts to sync") {
			ACDXPSettings.navToSyncedContactDataMenu();

			Click(locator1 = "ACDXPSettings#SYNC_CONTACTS_OPTION");

			var entrieGrup = selenium.getText("ACDXPSettings#SYNC_BY_USER_GROUPS_PREVIEW");

			var numberGrup = StringUtil.extractDigits(${entrieGrup});

			AssertTextEquals(
				locator1 = "ACDXPSettings#SYNC_BY_USER_GROUPS_PREVIEW",
				value1 = ${entrieGrup});

			Navigator.gotoBack();
		}

		task ("Assert preview the number of users groups contacts to sync") {
			ACDXPSettings.syncContactSingleUserGroupData(userGroup = "User Group2");

			ACDXPSettings.navToSyncedContactDataMenu();

			Click(locator1 = "ACDXPSettings#SYNC_CONTACTS_OPTION");

			var newNumberGrup = ${numberGrup} + 1;

			var newEntrieGrup = StringUtil.add(${newNumberGrup}, " User Groups Selected", "");

			AssertTextEquals(
				locator1 = "ACDXPSettings#SYNC_BY_USER_GROUPS_PREVIEW",
				value1 = ${newEntrieGrup});
		}
	}

}