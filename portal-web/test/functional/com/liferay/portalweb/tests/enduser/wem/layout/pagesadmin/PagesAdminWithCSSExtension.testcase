@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testcase.url = "http://localhost:8080";
	property testray.main.component.name = "Page Administration";

	static var randomSiteName = StringUtil.randomString(8);

	var siteURLKey = StringUtil.toLowerCase(${randomSiteName});
	static var randomEntryName1 = StringUtil.randomString(8);
	static var randomEntryName2 = StringUtil.randomString(8);

	setUp {
		task ("Sign in") {
			User.firstLoginPG();
		}

		task ("Add a site") {
			HeadlessSite.addSite(siteName = ${randomSiteName});
		}

		task ("Upload two CSS extension files to Documents and Media") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentTitle = "Font Family",
				groupName = ${randomSiteName},
				mimeType = "text/css",
				sourceFileName = "font_family.css");

			JSONDocument.addFileWithUploadedFile(
				dmDocumentTitle = "Background Color",
				groupName = ${randomSiteName},
				mimeType = "text/css",
				sourceFileName = "background_color.css");
		}

		task ("Get the latest version URL of two documents") {
			var firstURL = JSONDocument.getLatestVersionURL(
				dmDocumentTitle = "Font Family",
				groupName = ${randomSiteName});
			var secondURL = JSONDocument.getLatestVersionURL(
				dmDocumentTitle = "Background Color",
				groupName = ${randomSiteName});
		}

		task ("Add two remote apps for CSS extension") {
			JSONClientExtension.addRemoteAppEntry(
				name = ${randomEntryName1},
				type = "globalCSS",
				url = ${firstURL});

			JSONClientExtension.addRemoteAppEntry(
				name = ${randomEntryName2},
				type = "globalCSS",
				url = ${secondURL});
		}
	}

	@description = "This is a test for LPS-153656. The site designer could add CSS extension to page, page template, and group of pages."
	@priority = 5
	test AddCSSExtension {
		property portal.acceptance = "true";

		task ("Given a site designer has a widget page") {
			JSONLayout.addPublicLayout(
				groupName = ${randomSiteName},
				layoutName = "Test Page Name");
		}

		task ("When the site designer accesses to Basic Settings of widget page") {
			PagesAdmin.openPagesAdmin(siteURLKey = ${siteURLKey});

			PagesAdmin.gotoPageEllipsisMenuItem(
				menuItem = "Configure",
				pageName = "Test Page Name");

			Navigator.gotoNavTab(navTab = "Design");

			AssertElementPresent(locator1 = "PagesAdmin#NO_CSS_THEME_LOADED");
		}

		task ("Then the site designer could add a CSS Extension to widget page") {
			PagesAdmin.addCSSExtensions(extensionNameList = ${randomEntryName1});

			AssertVisible(
				key_tableEntry = ${randomEntryName1},
				locator1 = "LexiconTable#TABLE_ENTRY");

			PortletEntry.save();

			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = ${randomSiteName});

			AssertCssValue(
				locator1 = "//body",
				locator2 = "font-family",
				value1 = "\"Times New Roman\"");
		}

		task ("Given a site designer has a content page template") {
			JSONLayoutpagetemplate.addLayoutPageTemplateCollection(
				groupName = ${randomSiteName},
				layoutPageTemplateCollectionName = "Page Template Collection");

			PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = ${siteURLKey});

			Navigator.gotoNavNested(navNested = "Page Template Collection");

			PageTemplatesAdmin.addPageTemplate(
				pageTemplateName = "Page Template Name",
				pageTemplateType = "Content Page Template");

			PageEditor.assertFragmentSidebarLoaded();
		}

		task ("When the site designer accesses to Basic Settings of content page template") {
			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			Navigator.gotoNavTab(navTab = "Design");

			AssertElementPresent(locator1 = "PagesAdmin#NO_CSS_THEME_LOADED");
		}

		task ("Then the site designer could add a CSS Extension to widget page") {
			PagesAdmin.addCSSExtensions(extensionNameList = ${randomEntryName1});

			AssertVisible(
				key_tableEntry = ${randomEntryName1},
				locator1 = "LexiconTable#TABLE_ENTRY");

			PortletEntry.save();

			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			AssertCssValue(
				locator1 = "//body",
				locator2 = "font-family",
				value1 = "\"Times New Roman\"");
		}

		task ("When the site designer accesses to Basic Settings of Pages") {
			PagesAdmin.openPagesAdmin(siteURLKey = ${siteURLKey});

			WaitForPageLoad();

			Click(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");

			DropdownMenuItem.click(menuItem = "Configuration");

			WaitForPageLoad();

			Navigator.gotoNavTab(navTab = "Design");

			AssertElementPresent(locator1 = "PagesAdmin#NO_CSS_THEME_LOADED");
		}

		task ("Then the site designer could add a CSS Extension to Pages") {
			PagesAdmin.addCSSExtensions(extensionNameList = ${randomEntryName2});

			AssertVisible(
				key_tableEntry = ${randomEntryName2},
				locator1 = "LexiconTable#TABLE_ENTRY");

			PortletEntry.save();

			JSONLayout.addPublicLayout(
				groupName = ${randomSiteName},
				layoutName = "Content Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = ${randomSiteName});

			AssertCssValue(
				locator1 = "//body",
				locator2 = "background-color",
				value1 = "rgba(128, 128, 0, 1)");

			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = ${randomSiteName});

			AssertCssValue(
				locator1 = "//body",
				locator2 = "font-family",
				value1 = "\"Times New Roman\"");

			AssertCssValue(
				locator1 = "//body",
				locator2 = "background-color",
				value1 = "rgba(128, 128, 0, 1)");
		}
	}

	@description = "This is a test for LPS-153656. The site designer could change CSS extension selection to an inheritor page from page template."
	@priority = 5
	test ChangeCSSExtensionOfInheritorPage {
		property portal.acceptance = "true";

		task ("Given a site designer has a content page template with a CSS extension") {
			JSONLayoutpagetemplate.addLayoutPageTemplateCollection(
				groupName = ${randomSiteName},
				layoutPageTemplateCollectionName = "Page Template Collection");

			JSONLayoutpagetemplate.addLayoutPageTemplateEntry(
				groupName = ${randomSiteName},
				layoutPageTemplateCollectionName = "Page Template Collection",
				layoutPageTemplateEntryName = "Content Page Template",
				layoutPageTemplateStatus = "Draft",
				layoutPageTemplateType = "Content Page Template");

			PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = ${siteURLKey});

			Navigator.gotoNavNested(navNested = "Page Template Collection");

			PageTemplatesAdmin.gotoPageTemplate(pageTemplateName = "Content Page Template");

			PageEditor.assertFragmentSidebarLoaded();

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			Navigator.gotoNavTab(navTab = "Design");

			PagesAdmin.addCSSExtensions(extensionNameList = ${randomEntryName1});

			PortletEntry.save();

			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			PageEditor.publish();
		}

		task ("When the site designer adds a content page based on content page template") {
			PagesAdmin.openPagesAdmin(siteURLKey = ${siteURLKey});

			ContentPages.addPage(
				collectionName = "Page Template Collection",
				pageName = "Test Page Name",
				pageTemplateName = "Content Page Template");

			AssertCssValue(
				locator1 = "//body",
				locator2 = "font-family",
				value1 = "\"Times New Roman\"");
		}

		task ("Then the site designer could change the CSS extension of content page") {
			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			Navigator.gotoNavTab(navTab = "Design");

			PagesAdmin.addCSSExtensions(extensionNameList = ${randomEntryName2});

			AssertVisible(
				key_tableEntry = ${randomEntryName2},
				locator1 = "LexiconTable#TABLE_ENTRY");

			PortletEntry.save();

			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			AssertCssValue(
				locator1 = "//body",
				locator2 = "background-color",
				value1 = "rgba(128, 128, 0, 1)");

			AssertCssValue(
				locator1 = "//body",
				locator2 = "font-family",
				value1 = "\"Times New Roman\"");

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			Navigator.gotoNavTab(navTab = "Design");

			for (var extensionName : list "${randomEntryName2},${randomEntryName1}") {
				Click(
					key_tableEntry = ${extensionName},
					locator1 = "LexiconTable#TABLE_ENTRY_ELLIPSIS");

				DropdownMenuItem.click(menuItem = "Delete");
			}

			AssertElementPresent(locator1 = "PagesAdmin#NO_CSS_THEME_LOADED");

			PortletEntry.save();

			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			AssertCssValue(
				locator1 = "//body",
				locator2 = "font-family",
				value1 = "system-ui, -apple-system, BlinkMacSystemFont, \"Segoe UI\", Roboto, Oxygen-Sans, Ubuntu, Cantarell, \"Helvetica Neue\", Arial, sans-serif, \"Apple Color Emoji\", \"Segoe UI Emoji\", \"Segoe UI Symbol\"");

			AssertCssValue(
				locator1 = "//body",
				locator2 = "background-color",
				value1 = "rgba(255, 255, 255, 1)");
		}
	}

	@description = "This is a test for LPS-153656. The site designer could delete the CSS extenion selection."
	@priority = 4
	test ClearCSSExtensionSelection {
		task ("Given a site designer has a content page with a CSS extension") {
			JSONLayout.addPublicLayout(
				groupName = ${randomSiteName},
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = ${randomSiteName});

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			Navigator.gotoNavTab(navTab = "Design");

			PagesAdmin.addCSSExtensions(extensionNameList = ${randomEntryName1});

			AssertVisible(
				key_tableEntry = ${randomEntryName1},
				locator1 = "LexiconTable#TABLE_ENTRY");

			PortletEntry.save();

			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();
		}

		task ("When the site designer accesses to the Basic Settings") {
			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();
		}

		task ("Then the site designer could clear the CSS extension selection") {
			Navigator.gotoNavTab(navTab = "Design");

			Click(
				key_tableEntry = ${randomEntryName1},
				locator1 = "LexiconTable#TABLE_ENTRY_ELLIPSIS");

			DropdownMenuItem.click(menuItem = "Delete");

			AssertTextEquals(
				key_buttonName = "Add CSS Client Extensions",
				locator1 = "PagesAdmin#CUSTOMIZATION_BUTTON_SECONDARY_TEXT",
				value1 = "No CSS Client extensions were loaded.");

			PortletEntry.save();

			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			AssertCssValue(
				locator1 = "//body",
				locator2 = "font-family",
				value1 = "system-ui, -apple-system, BlinkMacSystemFont, \"Segoe UI\", Roboto, Oxygen-Sans, Ubuntu, Cantarell, \"Helvetica Neue\", Arial, sans-serif, \"Apple Color Emoji\", \"Segoe UI Emoji\", \"Segoe UI Symbol\"");
		}
	}

	@description = "This is a test for LPS-153658. The inherited CSS extensions from master or Pages should be read-only mode."
	@priority = 4
	test ViewInheritedCSSExtensionFromMasterPageOrPages {
		task ("Given a site designer has a master page with a CSS extension") {
			JSONLayoutpagetemplate.addMasterPageTemplateEntry(
				groupName = ${randomSiteName},
				masterPageTemplateEntryName = "Master Page Template");

			MastersPageTemplatesAdmin.openMastersAdmin(siteURLKey = ${siteURLKey});

			MastersPageTemplatesAdmin.gotoMasters(masterLayoutName = "Master Page Template");

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			Navigator.gotoNavTab(navTab = "Design");

			PagesAdmin.addCSSExtensions(extensionNameList = ${randomEntryName2});

			PortletEntry.save();

			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			MastersPageTemplates.publishMaster();
		}

		task ("When the site designer adds a content page based on master page") {
			PagesAdmin.openPagesAdmin(siteURLKey = ${siteURLKey});

			ContentPages.addPage(
				masterLayoutName = "Master Page Template",
				pageName = "Test Page Name 1");
		}

		task ("Then the site designer could see the inherited CSS extension") {
			AssertCssValue(
				locator1 = "//body",
				locator2 = "background-color",
				value1 = "rgba(128, 128, 0, 1)");

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			Navigator.gotoNavTab(navTab = "Design");

			LexiconTable.viewTableEntryInfo(
				index = 3,
				tableEntry = ${randomEntryName2},
				tableEntryInfo = "From Master");

			AssertElementNotPresent(
				key_tableEntry = ${randomEntryName2},
				locator1 = "LexiconTable#TABLE_ENTRY_ELLIPSIS");
		}

		task ("Given a site designer has the Pages with a CSS extension") {
			PagesAdmin.openPagesAdmin(siteURLKey = ${siteURLKey});

			WaitForPageLoad();

			Click(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");

			DropdownMenuItem.click(menuItem = "Configuration");

			WaitForPageLoad();

			Navigator.gotoNavTab(navTab = "Design");

			PagesAdmin.addCSSExtensions(extensionNameList = ${randomEntryName2});

			PortletEntry.save();
		}

		task ("When the site designer adds a widget page") {
			JSONLayout.addPublicLayout(
				groupName = ${randomSiteName},
				layoutName = "Test Page Name 2");
		}

		task ("Then the site designer could see the inherited CSS extension") {
			PagesAdmin.openPagesAdmin(siteURLKey = ${siteURLKey});

			PagesAdmin.gotoPageEllipsisMenuItem(
				menuItem = "Configure",
				pageName = "Test Page Name 2");

			Navigator.gotoNavTab(navTab = "Design");

			LexiconTable.viewTableEntryInfo(
				index = 3,
				tableEntry = ${randomEntryName2},
				tableEntryInfo = "From Pages");

			AssertElementNotPresent(
				key_tableEntry = ${randomEntryName2},
				locator1 = "LexiconTable#TABLE_ENTRY_ELLIPSIS");
		}
	}

	@description = "This is a test for LPS-153656. Page should inherit the CSS extension from page template."
	@priority = 5
	test ViewInheritedCSSExtensionFromPageTemplate {
		property portal.acceptance = "true";

		task ("Given a site designer has a content page template with a CSS extension") {
			JSONLayoutpagetemplate.addLayoutPageTemplateCollection(
				groupName = ${randomSiteName},
				layoutPageTemplateCollectionName = "Page Template Collection");

			JSONLayoutpagetemplate.addLayoutPageTemplateEntry(
				groupName = ${randomSiteName},
				layoutPageTemplateCollectionName = "Page Template Collection",
				layoutPageTemplateEntryName = "Content Page Template",
				layoutPageTemplateStatus = "Draft",
				layoutPageTemplateType = "Content Page Template");

			PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = ${siteURLKey});

			Navigator.gotoNavNested(navNested = "Page Template Collection");

			PageTemplatesAdmin.gotoPageTemplate(pageTemplateName = "Content Page Template");

			PageEditor.assertFragmentSidebarLoaded();

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			Navigator.gotoNavTab(navTab = "Design");

			PagesAdmin.addCSSExtensions(extensionNameList = ${randomEntryName1});

			PortletEntry.save();

			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			PageEditor.publish();
		}

		task ("When the site designer adds a content page based on content page template") {
			PagesAdmin.openPagesAdmin(siteURLKey = ${siteURLKey});

			ContentPages.addPage(
				collectionName = "Page Template Collection",
				pageName = "Test Page Name",
				pageTemplateName = "Content Page Template");
		}

		task ("Then the site designer could see the inherited CSS extension") {
			AssertCssValue(
				locator1 = "//body",
				locator2 = "font-family",
				value1 = "\"Times New Roman\"");

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			Navigator.gotoNavTab(navTab = "Design");

			AssertVisible(
				key_tableEntry = ${randomEntryName1},
				locator1 = "LexiconTable#TABLE_ENTRY");
		}
	}

	@description = "This is a test for LPS-153656. The order of CSS extensions loading on page are same with the order of addition."
	@priority = 4
	test ViewOrderOfCSSExtensionLoading {
		task ("Given a site designer has a content page with two CSS extensions") {
			JSONLayout.addPublicLayout(
				groupName = ${randomSiteName},
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = ${randomSiteName});

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			Navigator.gotoNavTab(navTab = "Design");

			PagesAdmin.addCSSExtensions(extensionNameList = "${randomEntryName1},${randomEntryName2}");

			AssertVisible(
				index = 1,
				key_tableEntry = ${randomEntryName1},
				locator1 = "LexiconTable#TABLE_ENTRY_TITLE_INDEXED");

			AssertVisible(
				index = 2,
				key_tableEntry = ${randomEntryName2},
				locator1 = "LexiconTable#TABLE_ENTRY_TITLE_INDEXED");

			PortletEntry.save();
		}

		task ("When the site designer backs to the page editor") {
			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();
		}

		task ("Then the order of CSS extension loading should be same with the order of addition") {
			AssertCssValue(
				locator1 = "//body",
				locator2 = "font-family",
				value1 = "\"Times New Roman\"");

			AssertCssValue(
				locator1 = "//body",
				locator2 = "background-color",
				value1 = "rgba(128, 128, 0, 1)");

			AssertElementPresent(locator1 = "xpath=(//link[contains(@href,'documents')])[1][contains(@href,'font-family')]");

			AssertElementPresent(locator1 = "xpath=(//link[contains(@href,'documents')])[2][contains(@href,'background-color')]");
		}
	}

}