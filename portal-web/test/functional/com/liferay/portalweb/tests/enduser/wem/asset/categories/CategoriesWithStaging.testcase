@component-name = "portal-categories"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Categories";

	setUp {
		task ("Set up portal instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a new site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Enable local live") {
			JSONStaging.enableLocalStaging(
				branchingPrivate = "false",
				branchingPublic = "false",
				groupName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-154273. The site designer can view number of usages on categories admin in staging and live site."
	@priority = "4"
	test ViewUsages {
		task ("Given a site designer has a document with a category") {
			JSONCategory.addVocabulary(
				groupName = "Test Site Name (Staging)",
				site = "false",
				title = "Vocabulary Name");

			var categoryId = JSONCategory.addCategory(
				groupName = "Test Site Name (Staging)",
				site = "false",
				title = "Category Name",
				vocabularyName = "Vocabulary Name");

			JSONDocument.addFileWithUploadedFile(
				assetCategoryIds = "${categoryId}",
				dmDocumentTitle = "Document Title",
				groupName = "Test Site Name-staging",
				mimeType = "image/jpeg",
				site = "false",
				sourceFileName = "Document_1.jpg");
		}

		task ("When the site designer accesses to the Categories admin of staging site") {
			Category.openCategoriesAdmin(siteURLKey = "test-site-name-staging");
		}

		task ("Then the usage number of category is 1") {
			LexiconTable.viewTableEntryInfo(
				columnName = "usages",
				tableEntry = "Category Name",
				tableEntryInfo = "1");
		}

		task ("When the site designer publishes changes to live") {
			Staging.openStagingAdmin(siteURLKey = "test-site-name-staging");

			Staging.publishCustomPublication();
		}

		task ("Then the usage number of category is 1 in live site") {
			Category.openCategoriesAdmin(siteURLKey = "test-site-name");

			LexiconTable.viewTableEntryInfo(
				columnName = "usages",
				tableEntry = "Category Name",
				tableEntryInfo = "1");
		}

		task ("When the site designer moves the document to recycle bin in live site") {
			DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-site-name");

			DMDocument.moveToRecycleBinCP(dmDocumentTitle = "Document Title");
		}

		task ("Then the usage number of category is 1 in live site") {
			Category.openCategoriesAdmin(siteURLKey = "test-site-name");

			LexiconTable.viewTableEntryInfo(
				columnName = "usages",
				tableEntry = "Category Name",
				tableEntryInfo = "1");
		}

		task ("When the site designer delete the document in recycle bin of live site") {
			RecycleBin.openRecycleBinAdmin(siteURLKey = "test-site-name");

			RecycleBin.deleteCP(
				assetName = "Document Title",
				assetType = "Document");
		}

		task ("Then the usage number of category is 0 in live site but 1 in staging site") {
			Category.openCategoriesAdmin(siteURLKey = "test-site-name");

			LexiconTable.viewTableEntryInfo(
				columnName = "usages",
				tableEntry = "Category Name",
				tableEntryInfo = "0");

			Category.openCategoriesAdmin(siteURLKey = "test-site-name-staging");

			LexiconTable.viewTableEntryInfo(
				columnName = "usages",
				tableEntry = "Category Name",
				tableEntryInfo = "1");
		}
	}

}