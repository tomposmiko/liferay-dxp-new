@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Web Content Administration";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "LPS-100416 - Non-Repeatable text fields show incorrect translation on label at first load."
	@priority = "4"
	test AddNonRepeatableTextFieldWithTranslation {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DynamicDataMapping.addField(
			field = "Text",
			fieldFieldLabel = "English",
			fieldName = "Text");

		AssertElementPresent(
			locator1 = "DDMEditStructure#SETTINGS_REPEATABLE",
			value1 = "No");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "WC Structure Name");

		Form.addTranslation(localizedLanguage = "German (Germany)");

		DynamicDataMapping.editField(
			fieldFieldLabel = "English",
			fieldFieldLabelEdit = "German");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		NavItem.gotoTemplates();

		WebContentTemplates.addCP(
			structureName = "WC Structure Name",
			templateFieldNames = "English",
			templateName = "WC Template Name");

		Navigator.openURL();

		PortalSettings.gotoConfiguration(
			configurationCategory = "Localization",
			configurationName = "Language",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.configureCurrentLanguagesCP(defaultPortalLanguage = "German (Germany)");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		AssertVisible(locator1 = "WCEditWebContent#SIDEBAR");

		PortletEntry.viewLocale(locale = "de-DE");

		AssertElementPresent(
			fieldLabel = "German",
			locator1 = "FormViewBuilder#FIELD_LABEL");

		PortletEntry.changeLocale(locale = "en-US");

		AssertElementPresent(
			fieldLabel = "English",
			locator1 = "FormViewBuilder#FIELD_LABEL");

		PortletEntry.changeLocale(locale = "de-DE");

		AssertElementPresent(
			fieldLabel = "German",
			locator1 = "FormViewBuilder#FIELD_LABEL");
	}

	@priority = "4"
	test AddWCWithMultipleTranslations {
		property test.name.skip.portal.instance = "LocalizationWithWebContent#AddWCWithMultipleTranslations";
		property testray.component.names = "Localization,Web Content Administration";

		JSONWebcontent.addBasicWebContentLocalized(
			contentMap = "en_US:WC Content,es_ES:WC Contenido,de_DE:WC Inhalt,fi_FI:WC Pitoisuus",
			groupName = "Test Site Name",
			titleMap = "en_US:WC Title,es_ES:WC Titulo,de_DE:WC Titel,fi_FI:WC Otsikko");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page Name",
			widgetName = "Web Content Display");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page Name",
			widgetName = "Language Selector");

		Navigator.gotoSitePage(
			pageName = "Test Page Name",
			siteName = "Test Site Name");

		WebContentDisplayPortlet.selectWebContent(webContentTitle = "WC Title");

		IFrame.closeFrame();

		LanguagePortlet.selectLanguagePG(
			locale = "es_ES",
			textIcon = "español-España");

		Navigator.gotoSitePage(
			pageName = "Test Page Name",
			siteName = "Test Site Name");

		WebContent.viewPGViaWCD(webContentContent = "WC Contenido");

		WebContentDisplayPortletNavigator.gotoEdit(
			menuItem = "Editar contenido web",
			webContentTitle = "WC Titulo");

		PortletEntry.changeLocale(locale = "es-ES");

		WebContent.viewCP(
			webContentContent = "WC Contenido",
			webContentTitle = "WC Titulo");

		Navigator.gotoSitePage(
			pageName = "Test Page Name",
			siteName = "Test Site Name");

		LanguagePortlet.selectLanguagePG(
			locale = "de-DE",
			textIcon = "deutsch-Deutschland");

		Navigator.gotoSitePage(
			pageName = "Test Page Name",
			siteName = "Test Site Name");

		WebContent.viewPGViaWCD(webContentContent = "WC Inhalt");

		WebContentDisplayPortletNavigator.gotoEdit(
			menuItem = "Webcontent bearbeiten",
			webContentTitle = "WC Titel");

		PortletEntry.changeLocale(locale = "de-DE");

		WebContent.viewCP(
			webContentContent = "WC Inhalt",
			webContentTitle = "WC Titel");

		Navigator.gotoSitePage(
			pageName = "Test Page Name",
			siteName = "Test Site Name");

		LanguagePortlet.selectLanguagePG(
			locale = "fi-FI",
			textIcon = "suomi-Suomi");

		Navigator.gotoSitePage(
			pageName = "Test Page Name",
			siteName = "Test Site Name");

		WebContent.viewPGViaWCD(webContentContent = "WC Pitoisuus");

		WebContentDisplayPortletNavigator.gotoEdit(
			menuItem = "Muokkaa web-sisältöä",
			webContentTitle = "WC Otsikko");

		PortletEntry.changeLocale(locale = "fi-FI");

		WebContent.viewCP(
			webContentContent = "WC Pitoisuus",
			webContentTitle = "WC Otsikko");
	}

	@description = "This is a use case for LRQA-61632. Add web content with translated Text field."
	@priority = "4"
	test AddWebContentWithTranslatedTextField {
		task ("Add a structure with Text field") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoStructures();

			WebContentStructures.addCP(structureName = "WC Structure Name");

			DynamicDataMapping.addField(
				field = "Text",
				fieldFieldLabel = "Text1",
				fieldName = "Text1");

			DynamicDataMapping.addField(
				field = "Text",
				fieldFieldLabel = "Text2",
				fieldName = "Text2");
		}

		task ("Add the Spanish (Spain) translation to structure") {
			Form.addTranslation(localizedLanguage = "Spanish (Spain)");

			WebContentStructures.saveCP(structureName = "WC Structure Name");
		}

		task ("Add a web content based on new structure") {
			NavItem.click(navItem = "Web Content");

			WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

			AssertVisible(locator1 = "WCEditWebContent#SIDEBAR");

			PortletEntry.inputTitle(title = "Web Content Title");
		}

		task ("Type content in Text field under en-US") {
			Type(
				key_fieldName = "Text1",
				locator1 = "TextInput#DDM",
				value1 = "This is a Text field");
		}

		task ("Type content in Text field under es-ES") {
			PortletEntry.changeLocale(locale = "es-ES");

			Type(
				key_fieldName = "Text2",
				locator1 = "TextInput#DDM",
				value1 = "Este es un campo de texto");

			PortletEntry.publish();
		}

		task ("Assert localized content are shown in published web content under Localization") {
			WebContentAdmin.gotoEntry(entry = "Web Content Title");

			AssertVisible(locator1 = "WCEditWebContent#SIDEBAR");

			AssertAttributeValue(
				attribute1 = "value",
				key_fieldName = "Text1",
				locator1 = "TextInput#DDM",
				value1 = "This is a Text field");

			PortletEntry.changeLocale(locale = "es-ES");

			AssertAttributeValue(
				attribute1 = "value",
				key_fieldName = "Text2",
				locator1 = "TextInput#DDM",
				value1 = "Este es un campo de texto");
		}
	}

	@description = "This is a use case for LPS-114001. The placeholder values are the same as the default Language's value."
	@priority = "4"
	test AssertPlaceholderValuesSameAsTheDefaultLanguage {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		PortletEntry.viewLocale(locale = "en-US");

		WebContent.addCP(
			webContentContent = "Web Content Content",
			webContentTitle = "Web Content Title");

		PortletEntry.changeLocale(locale = "ca-ES");

		WebContent.viewCP(
			webContentContent = "Web Content Content",
			webContentTitle = "Web Content Title");

		PortletEntry.changeLocale(locale = "zh-CN");

		WebContent.addCP(
			webContentContent = "网页内容",
			webContentTitle = "网页内容标题");

		PortletEntry.changeLocale(locale = "nl-NL");

		WebContent.viewCP(
			webContentContent = "Web Content Content",
			webContentTitle = "Web Content Title");

		PortletEntry.publish();
	}

	@description = "This is a use case for LPS-83319. The user could change default language of web content after enable Changeable Default Language."
	@priority = "5"
	test ChangeWebContentDefaultLanguage {
		property test.name.skip.portal.instance = "LocalizationWithWebContent#ChangeWebContentDefaultLanguage";

		task ("Enable Changeable Default Language for Web Content") {
			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "System Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "Web Content",
				configurationName = "Administration",
				configurationScope = "System Scope");

			SystemSettings.configureSystemSetting(
				enableSetting = "true",
				settingFieldName = "Changeable Default Language");
		}

		task ("Add a web content with translations") {
			JSONWebcontent.addBasicWebContentLocalized(
				contentMap = "en_US:Web Content Content,es_ES:Contenido de contenido web",
				groupName = "Test Site Name",
				titleMap = "en_US:Web Content Title,es_ES:Título del contenido web");
		}

		task ("Change the default language of web content to es-ES") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

			WebContent.viewDefaultLanguage(defaultLanguage = "English (US)");

			WebContent.changeDefaultLanguage(defaultLanguage = "es_ES");

			WebContent.viewDefaultLanguage(defaultLanguage = "Spanish (ES)");

			WebContent.viewCP(
				webContentContent = "Contenido de contenido web",
				webContentTitle = "Título del contenido web");

			PortletEntry.publish();
		}

		task ("View the default language of web content is es-ES") {
			WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

			PortletEntry.viewLocale(locale = "es-ES");

			WebContent.viewChangeDefaultLanguageCP(
				webContentContent = "Contenido de contenido web",
				webContentTitle = "Título del contenido web");

			WebContent.viewDefaultLanguage(defaultLanguage = "Spanish (ES)");
		}
	}

	@description = "This is a use case for 119020. Can add web content when the portal change the default language."
	@priority = "4"
	test ViewChangedDefaultPortalLanguageViaWebContent {
		property test.name.skip.portal.instance = "LocalizationWithWebContent#ViewChangedDefaultPortalLanguageViaWebContent";
		property testray.component.names = "Localization";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			webContentContent = "Web Content Content 1",
			webContentTitle = "Web Content Title 1");

		PortletEntry.publish();

		JSONWebcontent.addBasicWebContentLocalized(
			contentMap = "en_US:Web Content Content 2,es_ES:Contenido de contenido web 2",
			groupName = "Test Site Name",
			titleMap = "en_US:Web Content Title 2,es_ES:Título del contenido web 2");

		PortalSettings.gotoConfiguration(
			configurationCategory = "Localization",
			configurationName = "Language",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.configureCurrentLanguagesCP(defaultPortalLanguage = "Spanish (Spain)");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		PortletEntry.viewLocale(
			locale = "es-ES",
			portletName = "Web Content");

		WebContent.addCP(
			webContentContent = "Web Content Content 3",
			webContentTitle = "Web Content Title 3");

		PortletEntry.publish();
	}

	@description = "This is a use case for LPS-110938. The localized Friendly URL of WC is shown in WCD when select template with Friendly URL field and it is displayed in DPT."
	@priority = "3"
	test ViewLocalizedFriendlyURLInWCD {
		task ("Add a display page template with Web Content Article and Basic Web Content") {
			JSONLayoutpagetemplate.addDisplayPageTemplateEntry(
				contentType = "Web Content Article",
				displayPageTemplateEntryName = "Display Page Name",
				groupName = "Test Site Name",
				subType = "Basic Web Content");
		}

		task ("Add a Display Page Content fragment to display page template") {
			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

			DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Display Page Name");

			PageEditor.addFragment(
				collectionName = "Content Display",
				fragmentName = "Display Page Content");

			PageEditor.clickPublish();
		}

		task ("Add a template with Content and Friendly URL for Basic Web Content") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoTemplates();

			WebContentTemplates.addCP(
				structureName = "Basic Web Content",
				templateFieldNames = "Content,Friendly URL",
				templateName = "WC Template Name");
		}

		task ("Add a web content article based on Basic Web Content") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoAddCP();
		}

		task ("Type title and content") {
			WebContent.addCP(
				webContentContent = "Web Content Content",
				webContentTitle = "Web Content Title");
		}

		task ("Select the new template") {
			WebContent.selectTemplateCP(
				structureName = "Basic Web Content",
				templateName = "WC Template Name");
		}

		task ("Select the display page template") {
			WebContent.editDisplayPage(
				pageName = "Display Page Name",
				pageType = "Display Page Templates");
		}

		task ("Set up Friendly URL in English") {
			WebContent.editFriendlyURL(webContentFriendlyURL = "webcontentenglish");
		}

		task ("Set up Friendly URL in Spanish") {
			Click(
				key_fieldName = "friendlyURL",
				locator1 = "WC#LOCALIZED_BUTTON");

			Click(
				key_locale = "es_ES",
				locator1 = "Translation#LOCALIZATION_NAME");

			Type(
				locator1 = "TextInput#FRIENDLY_URL",
				value1 = "webcontentspanish");
		}

		task ("Publish the web content article") {
			PortletEntry.publish();
		}

		task ("Add a Web Content Display and a Language Selector to widget page") {
			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				widgetName = "Web Content Display");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				widgetName = "Language Selector");
		}

		task ("Display the web content article in Web Content Display") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			WebContentDisplayPortlet.selectWebContent(webContentTitle = "Web Content Title");

			IFrame.closeFrame();

			WebContent.viewPGViaWCD(webContentContent = "Web Content Content");

			var baseURL = PropsUtil.get("portal.url");

			WebContent.viewPGViaWCD(webContentContent = "${baseURL}/web/test-site-name/w/webcontentenglish");
		}

		task ("Change the language to Spanish") {
			LanguagePortlet.selectLanguagePG(
				locale = "es_ES",
				textIcon = "español-España");

			var baseURL = PropsUtil.get("portal.url");

			WebContent.viewPGViaWCD(webContentContent = "Web Content Content");

			WebContent.viewPGViaWCD(webContentContent = "${baseURL}/web/test-site-name/w/webcontentspanish");
		}
	}

	@description = "This is a use case for LRQA-61632. View structure default value in translated Text field."
	@priority = "4"
	test ViewStructureDefaultValueInTranslatedTextField {
		task ("Add a structure with Text field") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoStructures();

			WebContentStructures.addCP(structureName = "WC Structure Name");

			DynamicDataMapping.addField(
				field = "Text",
				fieldFieldLabel = "Text",
				fieldName = "Text");
		}

		task ("Add the Spanish (Spain) translation to structure") {
			Form.addTranslation(localizedLanguage = "Spanish (Spain)");

			WebContentStructures.saveCP(structureName = "WC Structure Name");
		}

		task ("Add default value to translated Text field") {
			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Edit Default Values",
				rowEntry = "WC Structure Name");

			AssertVisible(locator1 = "WCEditWebContent#SIDEBAR");

			Type(
				key_fieldName = "Text",
				locator1 = "TextInput#DDM",
				value1 = "This is a Text field");

			PortletEntry.changeLocale(locale = "es-ES");

			Type(
				key_fieldName = "Text",
				locator1 = "TextInput#DDM",
				value1 = "Este es un campo de texto");

			WebContentStructures.saveCP(structureName = "WC Structure Name");
		}

		task ("Add a web content based on new structure") {
			NavItem.click(navItem = "Web Content");

			WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");
		}

		task ("Assert localized content are shown under Localization") {
			AssertVisible(locator1 = "WCEditWebContent#SIDEBAR");

			AssertAttributeValue(
				attribute1 = "value",
				key_fieldName = "Text",
				locator1 = "TextInput#DDM",
				value1 = "This is a Text field");

			PortletEntry.changeLocale(locale = "es-ES");

			AssertAttributeValue(
				attribute1 = "value",
				key_fieldName = "Text",
				locator1 = "TextInput#DDM",
				value1 = "Este es un campo de texto");
		}
	}

}