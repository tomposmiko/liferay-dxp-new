@component-name = "portal-wcm"
definition {

	property custom.properties = "feature.flag.LPS-162765=true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Utility Pages";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONUser.tearDownNonAdminUsers();

			JSONRole.deleteRole(roleTitle = "Regular Role Name");

			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-174454. A new user without assigning permission cannot mark a Utility Page as default."
	@priority = 4
	test CannotMark404ErrorUtilityPageAsDefaultAsUserWithoutAssignDefaultPermission {
		task ("Given a user doesn't have assign default utility page permission") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			Navigator.gotoNavTab(navTab = "Utility Pages");

			PagesAdmin.addUtilityPage(pageName = "Test Utility Page Name");

			PageEditor.publish();

			JSONRole.addRegularRole(
				roleKey = "Regular Role Name",
				roleTitle = "Regular Role Name");

			JSONUser.addUser(
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");

			Role.openRolesAdmin();

			RoleNavigator.gotoDefineRolePermissions(
				permissionNavHeader = "Site Builder",
				permissionNavItem = "Pages",
				roleTitle = "Regular Role Name");

			for (var permissionGroupTitle : list "Site,Layout Utility Page Entry,Page") {
				Check(
					locator1 = "Permissions#SELECT_ALL",
					title = ${permissionGroupTitle});
			}

			PortletEntry.save();

			Permissions.removePermissionViaAPI(
				resourceAction = "ASSIGN_DEFAULT_LAYOUT_UTILITY_PAGE_ENTRY",
				resourceName = "com.liferay.portal.kernel.model.Group",
				roleTitle = "Regular Role Name",
				roleType = "regular");

			JSONRole.assignRoleToUser(
				roleTitle = "Regular Role Name",
				userEmailAddress = "userea@liferay.com");
		}

		task ("When the user navigates to the Pages admin") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "userea@liferay.com",
				userLoginFullName = "userfn userln");

			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			Navigator.gotoNavTab(navTab = "Utility Pages");
		}

		task ("Then the new user cannot mark an Utility Page as default") {
			LexiconCard.viewMenuItemNotPresent(
				card = "Test Utility Page Name",
				item = "Mark as Default");
		}
	}

}