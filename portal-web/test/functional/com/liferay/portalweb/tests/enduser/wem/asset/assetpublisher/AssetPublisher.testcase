@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Asset Publisher";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Asset Publisher Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Asset Publisher Page",
			widgetName = "Asset Publisher");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "LPS-96038 - Global Categories do not appear as filters in Asset Publisher Configuration."
	@priority = "4"
	test ConfigureAPPortletDisplaysGlobalCategoriesAsFilters {
		Category.openCategoriesAdmin(siteURLKey = "global");

		Category.addCP(
			categoryName = "Category Name",
			vocabularyName = "Topic");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureCategoriesFilterPG(
			categoryName = "Category Name",
			portletName = "Asset Publisher");
	}

	@description = "This is a test for LPS-93357. The subscribe button can be enabled/disabled."
	@priority = "3"
	test enableDisableSubscribeButton {
		task ("Assert the subscribe button is disabled by default") {
			Navigator.gotoSitePage(
				pageName = "Asset Publisher Page",
				siteName = "Test Site Name");

			AssertElementNotPresent(locator1 = "Button#SUBSCRIBE");
		}

		task ("Enable the subscribe button") {
			AssetPublisherPortlet.configureDisplaySettings(displaySettings = "Subscribe");
		}

		task ("Assert the subscribe button displays") {
			Navigator.gotoSitePage(
				pageName = "Asset Publisher Page",
				siteName = "Test Site Name");

			AssertElementPresent(locator1 = "Button#SUBSCRIBE");
		}
	}

	@description = "This is a test for LPS-86192. The default asset link behavior should be 'View in Context'."
	@priority = "4"
	test ViewDefaultAssetLinkBehavior {
		task ("## Navigate to Asset Publisher widget display settings.") {
			Navigator.gotoSitePage(
				pageName = "Asset Publisher Page",
				siteName = "Test Site Name");

			AssetPublisherPortlet.gotoConfigurationPG();

			Navigator.gotoNavTab(navTab = "Display Settings");
		}

		task ("## Assert the default asset link behavior is 'View in Context'.") {
			AssertSelectedLabel(
				locator1 = "APConfigurationDisplaysettings#ASSET_LINK_BEHAVIOR_SELECT",
				value1 = "View in Context");
		}
	}

	@description = "LPS-135554 This test asserts that the Web Content Article Structures should appear after uncheck Search With Index configuration."
	@priority = "4"
	test WebContentArticleStructuresCanAppearAfterUncheckingSearchWithIndexConfiguration {
		property test.name.skip.portal.instance = "AssetPublisher#WebContentArticleStructuresCanAppearAfterUncheckingSearchWithIndexConfiguration";

		task ("Uncheck Search with Index Configuration") {
			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "System Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "Assets",
				configurationName = "Asset Publisher",
				configurationScope = "System Scope");

			FormFields.disableCheckbox(fieldName = "searchWithIndex");

			SystemSettings.saveConfiguration();
		}

		task ("Assert the Web Content Article Structures selector appear.") {
			ApplicationsMenu.gotoSite(site = "Test Site Name");

			SitePages.addPublicPage(pageName = "Test AP Page");

			Navigator.gotoSitePage(
				pageName = "Test AP Page",
				siteName = "Test Site Name");

			Portlet.addPG(portletName = "Asset Publisher");

			AssetPublisherPortlet.configureAssetTypePG(
				assetSubtype = "Any",
				selectedAsset = "Web Content Article");

			Navigator.gotoSitePage(
				pageName = "Test AP Page",
				siteName = "Test Site Name");

			AssetPublisherPortlet.viewSelectedPG(
				assetSubtypeConfiguration = "Any",
				selectedAsset = "Basic Web Content",
				selectedAssetConfiguration = "Web Content Article");
		}
	}

}