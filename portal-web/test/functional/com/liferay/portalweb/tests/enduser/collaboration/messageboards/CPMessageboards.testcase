@component-name = "portal-collaboration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Message Boards";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			MessageboardsCategory.tearDownCP();

			MessageboardsThread.tearDownCP();

			MessageboardsPortlet.tearDownConfigurationGeneralCP();
		}
	}

	@priority = "5"
	test AddMBCategory {
		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsCategory.addCP(categoryName = "MB Category Name");

		MessageboardsCategory.viewCP(
			categoryCount = "0 Categories",
			categoryName = "MB Category Name",
			threadCount = "0");
	}

	@priority = "4"
	test AddMBCategorySubcategory {
		JSONMBMessage.addCategory(
			categoryDescription = "MB Category Description",
			categoryName = "MB Category Name",
			userEmailAddress = "test@liferay.com");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsCategory.gotoCP(categoryName = "MB Category Name");

		MessageboardsCategory.addCP(
			categoryDescription = "MB Subcategory Description",
			categoryDescriptionEnabled = "true",
			categoryName = "MB Subcategory Name");
	}

	@priority = "5"
	test AddMBThread {
		property app.server.types = "jboss,tcserver,tomcat,weblogic,websphere,wildfly";
		property database.types = "db2,hypersonic,mariadb,mysql,oracle,postgresql,sqlserver,sybase";
		property environment.acceptance = "true";

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.addCP(
			threadBody = "MB Thread Message Body",
			threadSubject = "MB Thread Message Subject");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.viewCP(
			replyCount = "0",
			threadBody = "MB Thread Message Body",
			threadSubject = "MB Thread Message Subject");
	}

	@priority = "5"
	test CKEditorSmoke {
		property custom.properties = "editor.wysiwyg.portal-web.docroot.html.portlet.message_boards.edit_message.bb_code.jsp=ckeditor${line.separator}editor.wysiwyg.portal-web.docroot.html.portlet.message_boards.edit_message.html.jsp=ckeditor";

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		LexiconEntry.gotoAdd();

		MenuItem.click(menuItem = "Thread");

		Type(
			locator1 = "TextInput#SUBJECT",
			value1 = "CKEditor Entry Subject");

		CKEditor.assertEditorPresent();

		CKEditor.addContent(content = "CKEditor Entry Body");

		PortletEntry.publish();

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.viewCP(
			replyCount = "0",
			threadBody = "CKEditor Entry Body",
			threadSubject = "CKEditor Entry Subject");
	}

	@priority = "4"
	test DeleteMBCategories {
		Navigator.openURL();

		for (var categoryName : list "MB Category1 Name,MB Category2 Name,MB Category3 Name") {
			MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

			MessageboardsCategory.addCP(categoryName = "${categoryName}");
		}

		Navigator.openURL();

		MessageboardsCategory.tearDownCP(assetType = "3 items");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		AssertTextEquals.assertText(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no threads or categories.");
	}

	@priority = "3"
	test DeleteMBThreadWithAttachment {
		property custom.properties = "trash.entry.check.interval=1${line.separator}trash.entries.max.age=1";

		JSONGroup.addGroup(groupName = "Site Name");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "site-name");

		MessageboardsThread.addCPWithAttachment(
			mbAttachmentFile = "Document_1.doc",
			threadBody = "MB Body",
			threadSubject = "MB Subject");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "site-name");

		MessageboardsThread.moveToRecycleBinCP(threadSubject = "MB Subject");

		Pause(locator1 = "120000");

		AssertConsoleTextNotPresent(value1 = "MessageListenerException");

		AssertConsoleTextNotPresent(value1 = "com.liferay.portlet.documentlibrary.NoSuchFileEntryException");

		RecycleBin.openRecycleBinAdmin(siteURLKey = "guest");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "The Recycle Bin is empty.");
	}

	@priority = "5"
	test EditMBThreadSubjectAndBody {
		JSONMBMessage.addMessage(
			groupName = "Guest",
			threadBody = "MB Thread Message Body",
			threadSubject = "MB Thread Message Subject");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.gotoCP(threadSubject = "MB Thread Message Subject");

		MessageboardsThread.editCP(
			threadBodyEdit = "MB Thread Message Body Edit",
			threadSubjectEdit = "MB Thread Message Subject Edit");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.viewCP(
			replyCount = "0",
			threadBody = "MB Thread Message Body Edit",
			threadSubject = "MB Thread Message Subject Edit");
	}

	@priority = "5"
	test MoveToRecycleBinMBThread {
		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.addCP(
			threadBody = "MB Thread Message Body",
			threadSubject = "MB Thread Message Subject");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.moveToRecycleBinCP(threadSubject = "MB Thread Message Subject");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.viewDefaultCP(threadSubject = "MB Thread Message Subject");
	}

	@priority = "3"
	test ViewAnonymousPostingAllowedCheckboxConfiguration {
		property test.name.skip.portal.instance = "CPMessageboards#ViewAnonymousPostingAllowedCheckboxConfiguration";

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.viewAnonymousPostingCheckboxCP();

		MessageboardsPortlet.configureAnonymousPostingCP(anonymousPostingEnabled = "false");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		Portlet.gotoPortletOptions(portletOption = "Configuration");

		MessageboardsPortlet.viewAnonymousPostingConfigurationCP(anonymousPostingEnabled = "false");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.viewNoAnonymousPostingCheckboxCP();
	}

	@priority = "4"
	test ViewBBCodeNoXSS {
		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsPortlet.configureGeneralCP(
			messageFormat = "BBCode",
			messageFormatBBCodeEnabled = "true");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.addCP(
			source = "true",
			threadBody = "[img]Not An Image[font= onerror=alert(/XSS/.source)//]FF[/font][/img]",
			threadSubject = "New Thread Subject");

		MessageboardsThread.assertAlertNotPresent();

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.viewCP(
			replyCount = "0",
			threadBody = "",
			threadSubject = "New Thread Subject");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.addCP(
			fontSize = "24",
			source = "true",
			threadBody = "[size=5]Thread Body[/size]",
			threadSubject = "New Thread Subject 2");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.viewCP(
			fontSize = "24",
			replyCount = "0",
			threadBody = "Thread Body",
			threadSubject = "New Thread Subject 2");
	}

}