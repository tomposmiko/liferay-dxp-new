@component-name = "portal-util-misc"
definition {

	property ci.retries.disabled = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Ext";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Verify pop server notifications can be enabled per instance"
	@priority = 3
	test CanEnablePopServerNotificationsPerInstance {
		property database.types = "mysql";

		task ("Setup: Save default instance mail settings so they appear in database") {
			PortalSettings.gotoConfiguration(
				configurationCategory = "Email",
				configurationName = "Mail Settings",
				configurationScope = "Virtual Instance Scope");

			ServerAdministration.configureMailServerSettings(
				userName = PropsUtil.get("email.address.2"),
				userPassword = PropsUtil.get("email.password.2"));
		}

		task ("Given multiple instances") {
			HeadlessPortalInstanceAPI.addPortalInstance(
				domain = "www.able.com",
				portalInstanceId = "www.able.com",
				virtualHost = "www.able.com");
		}

		task ("When the user enables pop server notifications for an instance") {
			User.firstLoginUI(
				password = "test",
				specificURL = "http://www.able.com:8080",
				userEmailAddress = "test@www.able.com");

			PortalSettings.gotoConfiguration(
				baseURL = "http://www.able.com:8080",
				configurationCategory = "Email",
				configurationName = "Mail Settings",
				configurationScope = "Virtual Instance Scope");

			Check(
				checkboxName = "Enable POP Server Notifications",
				locator1 = "Checkbox#ANY_CHECKBOX");

			ServerAdministration.configureMailServerSettings(
				userName = PropsUtil.get("email.address.2"),
				userPassword = PropsUtil.get("email.password.2"));
		}

		task ("Then they are enabled only for that instance") {
			var companyId1 = JSONCompany.getCompanyId();
			var companyId2 = JSONCompany.getCompanyId(portalInstanceName = "www.able.com");

			SQL.assertTextInMySQLStatementResult(
				mysqlStatement = "SELECT smallValue FROM lportal.PortalPreferenceValue WHERE companyId='${companyId1}' AND key_='pop.server.notifications.enabled'",
				text = "false");

			SQL.assertTextInMySQLStatementResult(
				mysqlStatement = "SELECT smallValue FROM lportal.PortalPreferenceValue WHERE companyId='${companyId2}' AND key_='pop.server.notifications.enabled'",
				text = "true");
		}
	}

	@description = "Verify pop server notifications can be enabled via UI"
	@priority = 3
	test CanEnablePopServerNotificationsViaUI {
		property database.types = "mysql";

		task ("Given pop server notifications are disabled") {
			ServerAdministration.openServerAdmin();

			Navigator.gotoNavItem(navItem = "Properties");

			Navigator.gotoNavItem(navItem = "Portal Properties");

			Search.searchCP(searchTerm = "pop.server.notifications.enabled");

			AssertTextEquals(
				key_propertyName = "pop.server.notifications.enabled",
				locator1 = "ServerAdministrationProperties#PORTAL_SYSTEM_PROPERTY_VALUE",
				value1 = "false");
		}

		task ("Then the user can enable them via UI") {
			PortalSettings.gotoConfiguration(
				configurationCategory = "Email",
				configurationName = "Mail Settings",
				configurationScope = "Virtual Instance Scope");

			Check(
				checkboxName = "Enable POP Server Notifications",
				locator1 = "Checkbox#ANY_CHECKBOX");

			ServerAdministration.configureMailServerSettings(
				userName = PropsUtil.get("email.address.2"),
				userPassword = PropsUtil.get("email.password.2"));

			var companyId = JSONCompany.getCompanyId();

			SQL.assertTextInMySQLStatementResult(
				mysqlStatement = "SELECT smallValue FROM lportal.PortalPreferenceValue WHERE companyId='${companyId}' AND key_='pop.server.notifications.enabled'",
				text = "true");
		}
	}

}