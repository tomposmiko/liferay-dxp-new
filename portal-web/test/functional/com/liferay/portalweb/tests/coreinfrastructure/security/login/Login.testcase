@component-name = "portal-security"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Login";

	setUp {
		TestCase.setUpPortalInstance();
	}

	@priority = "5"
	test CASSSOSmoke {
		property cas.enabled = "true";
		property dummy.socket.proxy.disabled = "true";
		property test.name.skip.portal.instance = "Login#CASSSOSmoke";

		User.firstLoginPG();

		var liferayHome = PropsUtil.get("liferay.home.dir.name");

		var fileContent = FileUtil.read("${liferayHome}/tomcat-9.0.68/bin/cas.log");

		if (contains("${fileContent}", "org.jasig.cas")) {
			echo("CAS server started.");
		}
		else {
			fail("FAIL! CAS server didn't start.");
		}

		PortalSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "CAS Server",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.editConfigurationAuthenticationCAS();

		PortalSettings.gotoConfiguration(
			configurationCategory = "User Authentication",
			configurationName = "General",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.editConfigurationAuthenticationGeneral(authenticationMethod = "By Screen Name");

		User.logoutPG();

		User.loginUserWithCASEnabledPG(
			userPassword = "test",
			userScreenName = "test");
	}

	@priority = "5"
	test DefaultPortalLandingPage {
		property test.name.skip.portal.instance = "Login#DefaultPortalLandingPage";

		User.firstLoginPG();

		PortalSettings.gotoConfiguration(
			configurationCategory = "Instance Configuration",
			configurationName = "General",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.editDefaultLandingPageCP(defaultLandingPage = "/web/guest/landing");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		Navigator.openSiteURL(siteName = "${siteName}");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		SitePages.addPublicPage(pageName = "landing");

		User.logoutPG();

		User.loginPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		var portalURL = PropsUtil.get("portal.url");

		AssertLocation(value1 = "${portalURL}/web/guest/landing");
	}

	@description = "This is a use case for LPS-39744 and LPS-78508."
	@priority = "5"
	test DefaultPortalLandingPageSetByProperty {
		property custom.properties = "default.landing.page.path=/web/guest/landing${line.separator}company.default.home.url=";
		property test.name.skip.portal.instance = "Login#DefaultPortalLandingPageSetByProperty";

		User.firstLoginPG();

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		SitePages.addPublicPage(pageName = "landing");

		User.logoutPG();

		User.loginPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		var portalURL = PropsUtil.get("portal.url");

		AssertLocation(value1 = "${portalURL}/web/guest/landing");

		AssertConsoleTextNotPresent(value1 = "java.lang.StackOverflowError");
	}

	@description = "This is a use case for LPS-105151."
	@priority = "4"
	test EnabledOpenIDConnectOnSystemLevel {
		property dummy.socket.proxy.disabled = "true";
		property test.name.skip.portal.instance = "Login#EnabledOpenIDConnectOnSystemLevel";

		var openIDConnectClientID = PropsUtil.get("google.client.id.2");
		var openIDConnectClientSecret = PropsUtil.get("google.client.secret.2");
		var userEmailAddress = PropsUtil.get("email.address.2");
		var userPassword = PropsUtil.get("email.password.2");

		User.firstLoginPG();

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.addCP(
			mailDomain = "www.able.com",
			virtualHost = "www.able.com",
			webId = "www.able.com");

		PortalInstances.viewCP(virtualHost = "www.able.com");

		Navigator.openSpecificURL(url = "http://www.able.com:8080/");

		AssertClick(
			locator1 = "UserBar#USER_SIGN_IN",
			value1 = "Sign In");

		AssertElementNotPresent(locator1 = "Icon#OPENID_CONNECT");

		Navigator.openURL();

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "OpenID Connect",
			configurationScope = "System Scope");

		PortalSettings.enableOpenIDConnectCP(systemLevel = "true");

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "OpenID Connect Provider",
			configurationScope = "Virtual Instance Scope");

		OpenIDConnect.addProvider(
			openIDConnectClientID = "${openIDConnectClientID}",
			openIDConnectClientSecret = "${openIDConnectClientSecret}",
			providerName = "Google");

		Navigator.openSpecificURL(url = "http://www.able.com:8080/");

		AssertClick(
			locator1 = "UserBar#USER_SIGN_IN",
			value1 = "Sign In");

		AssertElementPresent(locator1 = "Icon#OPENID_CONNECT");
	}

	@priority = "4"
	test FacebookSSOSmoke {
		property dummy.socket.proxy.disabled = "true";
		property test.name.skip.portal.instance = "Login#FacebookSSOSmoke";

		var facebookApplicationID = PropsUtil.get("facebook.application.id");
		var facebookApplicationSecret = PropsUtil.get("facebook.application.secret");
		var facebookEmail = PropsUtil.get("facebook.email");
		var facebookPassword = PropsUtil.get("facebook.password");

		User.firstLoginPG();

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		PortalSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "Facebook Connect",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.editConfigurationAuthenticationFacebook(
			facebookApplicationID = "${facebookApplicationID}",
			facebookApplicationSecret = "${facebookApplicationSecret}");

		User.logoutPG();

		User.loginUserWithFacebookSSOEnabledPG(
			facebookEmail = "${facebookEmail}",
			facebookPassword = "${facebookPassword}",
			siteName = "${siteName}");

		User.logoutUserPG();

		User.loginPG(
			password = "test",
			userEmailAddress = "test@liferay.com",
			userScreenName = "usersn");
	}

	@priority = "4"
	test GoogleSSOSmoke {
		property dummy.socket.proxy.disabled = "true";
		property osgi.modules.includes = "portal-security-sso-google";
		property test.name.skip.portal.instance = "Login#GoogleSSOSmoke";

		var googleClientID = PropsUtil.get("google.client.id.2");
		var googleClientSecret = PropsUtil.get("google.client.secret.2");
		var userEmailAddress = PropsUtil.get("email.address.2");
		var userPassword = PropsUtil.get("email.password.2");

		User.firstLoginPG();

		PortalSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "Google Authorization",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.enableGoogleSSOCP(
			googleClientID = "${googleClientID}",
			googleClientSecret = "${googleClientSecret}");

		User.logoutPG();

		User.loginUserWithGoogleSSOEnabledPG(
			userEmailAddress = "${userEmailAddress}",
			userPassword = "${userPassword}");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "test@liferay.com");
	}

	@description = "This is a use case for LPS-72398."
	@priority = "5"
	test LoginWithSpanishOnlyEnabledLocale {
		property test.name.skip.portal.instance = "Login#LoginWithSpanishOnlyEnabledLocale";

		User.firstLoginPG();

		PortalSettings.gotoConfiguration(
			configurationCategory = "Localization",
			configurationName = "Language",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.configureCurrentLanguagesCP(
			currentPortalLanguages = "Spanish (Spain)",
			defaultPortalLanguage = "Spanish (Spain)");

		User.logoutPG();

		Portlet.shutdownServer();

		Portlet.startServer(deleteLiferayHome = "false");

		Portlet.assertServerStartup();

		User.loginPG(
			localization = "ES",
			password = "test",
			userEmailAddress = "test@liferay.com");
	}

	@description = "This is a use case for LPS-105151."
	@priority = "4"
	test MultipleOpenIDConnectProvider {
		property dummy.socket.proxy.disabled = "true";
		property test.name.skip.portal.instance = "Login#MultipleOpenIDConnectProvider";

		var openIDConnectClientID = PropsUtil.get("google.client.id.2");
		var openIDConnectClientSecret = PropsUtil.get("google.client.secret.2");
		var openIDConnectClientID1 = PropsUtil.get("google.client.id.3");
		var openIDConnectClientSecret1 = PropsUtil.get("google.client.secret.3");
		var userEmailAddress = PropsUtil.get("email.address.2");
		var userPassword = PropsUtil.get("email.password.2");

		User.firstLoginPG();

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.addCP(
			mailDomain = "www.able.com",
			virtualHost = "www.able.com",
			webId = "www.able.com");

		PortalInstances.viewCP(virtualHost = "www.able.com");

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.addCP(
			mailDomain = "www.baker.com",
			virtualHost = "www.baker.com",
			webId = "www.baker.com");

		PortalInstances.viewCP(virtualHost = "www.baker.com");

		Navigator.openURL();

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "OpenID Connect",
			configurationScope = "System Scope");

		PortalSettings.enableOpenIDConnectCP(systemLevel = "true");

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "OpenID Connect Provider",
			configurationScope = "Virtual Instance Scope");

		OpenIDConnect.addProvider(
			openIDConnectClientID = "${openIDConnectClientID}",
			openIDConnectClientSecret = "${openIDConnectClientSecret}",
			providerName = "Google");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@www.able.com",
			virtualHostsURL = "http://www.able.com:8080");

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Instance Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "OpenID Connect Provider",
			configurationScope = "Virtual Instance Scope");

		OpenIDConnect.addProvider(
			openIDConnectClientID = "${openIDConnectClientID1}",
			openIDConnectClientSecret = "${openIDConnectClientSecret1}",
			providerName = "Test Google");

		UserBar.signOut();

		Navigator.openSpecificURL(url = "http://www.able.com:8080/");

		OpenIDConnect.viewProvider();

		AssertElementPresent(
			key_providerNname = "Test Google",
			locator1 = "OpenIDConnectLogin#OPENID_CONNECT_DROPDOWN_OPTION");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@www.baker.com",
			virtualHostsURL = "http://www.baker.com:8080");

		UserBar.signOut();

		Navigator.openSpecificURL(url = "http://www.baker.com:8080/");

		OpenIDConnect.viewProvider();
	}

	@description = "This is a use case for LPS-105151."
	@priority = "4"
	test OpenIDConnectOnInstanceLevelSmoke {
		property dummy.socket.proxy.disabled = "true";
		property test.name.skip.portal.instance = "Login#OpenIDConnectOnInstanceLevelSmoke";

		var openIDConnectClientID = PropsUtil.get("google.client.id.1");
		var openIDConnectClientSecret = PropsUtil.get("google.client.secret.1");
		var userEmailAddress = PropsUtil.get("email.address.2");
		var userPassword = PropsUtil.get("email.password.2");

		User.firstLoginPG();

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.addCP(
			mailDomain = "www.able.com",
			virtualHost = "www.able.com",
			webId = "www.able.com");

		PortalInstances.viewCP(virtualHost = "www.able.com");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@www.able.com",
			virtualHostsURL = "http://www.able.com:8080");

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Instance Settings");

		PortalSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "OpenID Connect",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.enableOpenIDConnectCP();

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Instance Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "OpenID Connect Provider",
			configurationScope = "Virtual Instance Scope");

		OpenIDConnect.addProvider(
			openIDConnectClientID = "${openIDConnectClientID}",
			openIDConnectClientSecret = "${openIDConnectClientSecret}",
			providerName = "Google");

		UserBar.signOut();

		OpenIDConnect.accountSignIn(
			specificURL = "http://www.able.com:8080",
			userEmailAddress = "${userEmailAddress}",
			userPassword = "${userPassword}");

		OpenIDConnect.acceptConditions();

		AssertElementPresent(locator1 = "UserBar#USER_AVATAR_IMAGE");

		UserBar.signOut();

		User.loginPG(
			password = "test",
			specificURL = "http://www.able.com:8080",
			userEmailAddress = "test@www.able.com");
	}

	@priority = "4"
	test OpenIDConnectSmoke {
		property dummy.socket.proxy.disabled = "true";
		property test.name.skip.portal.instance = "Login#OpenIDConnectSmoke";

		// LRQA-43729

		var openIDConnectClientID = PropsUtil.get("google.client.id.3");
		var openIDConnectClientSecret = PropsUtil.get("google.client.secret.3");
		var userEmailAddress = PropsUtil.get("email.address.2");
		var userPassword = PropsUtil.get("email.password.2");

		User.firstLoginPG();

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		PortalSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "OpenID Connect",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.enableOpenIDConnectCP();

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "OpenID Connect Provider",
			configurationScope = "Virtual Instance Scope");

		OpenIDConnect.addProvider(
			openIDConnectClientID = "${openIDConnectClientID}",
			openIDConnectClientSecret = "${openIDConnectClientSecret}",
			providerName = "Google");

		User.logoutPG();

		User.loginUserWithOpenIDConnectEnabledPG(
			siteName = "${siteName}",
			userEmailAddress = "${userEmailAddress}",
			userPassword = "${userPassword}");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "test@liferay.com");
	}

	@description = "Ignore test due to OpenAM 12 doesn't support Tomcat 9"
	@ignore = "true"
	@priority = "5"
	test OpenSSOSmoke {
		property dummy.socket.proxy.disabled = "true";
		property openam.enabled = "true";
		property test.name.skip.portal.instance = "Login#OpenSSOSmoke";

		// LRQA-40534

		Navigator.openSpecificURL(url = "http://test.able.com:8080/openam");

		AntCommand(
			locator1 = "build-test-authentication.xml",
			value1 = "run-openam-tools");

		var pauseCount = "0";

		while ((IsElementNotPresent(locator1 = "TextInput#TEXT")) && ("${pauseCount}" != "20")) {
			Refresh();

			Pause(locator1 = "30000");

			var pauseCount = ${pauseCount} + 1;

			echo("Pause count: ${pauseCount}");
		}

		if ("${pauseCount}" == "20") {
			fail("OpenAM setup timed out after 10 minutes.");
		}

		User.firstLoginPG();

		Pause(locator1 = "30000");

		PortalSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "OpenSSO",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.configureOpenSSO(
			OpenSSOLoginURL = "http://test.able.com:8080/openam/UI/Login?goto=http://www.able.com:8080/c/portal/login",
			OpenSSOLogoutURL = "http://test.able.com:8080/openam/UI/Logout?goto=http://www.able.com:8080/web/guest/home",
			OpenSSOServiceURL = "http://test.able.com:8080/openam");

		PortalSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "OpenSSO",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.testOpenSSOConnection();

		User.logoutPG();

		User.loginUserWithOpenSSOEnabledPG(
			hostsURL = "http://www.able.com:8080",
			password = "password",
			userScreenName = "test");
	}

	@priority = "5"
	test TokenBasedSSOSmoke {
		property test.name.skip.portal.instance = "Login#TokenBasedSSOSmoke";

		User.firstLoginPG();

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "Token Based SSO",
			configurationScope = "System Scope");

		FormFields.enableCheckbox(fieldName = "Enabled");

		PortletEntry.save();

		AntCommand(
			locator1 = "build-test-authentication.xml",
			value1 = "verify-token-based-login -Dportal.url=localhost:8080/web/guest -Duser.token=test@liferay.com");

		FormFields.disableCheckbox(fieldName = "Enabled");

		PortletEntry.update();

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.addCP(
			mailDomain = "www.able.com",
			virtualHost = "www.able.com",
			webId = "www.able.com");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@www.able.com",
			virtualHostsURL = "http://www.able.com:8080");

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Instance Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "Token Based SSO",
			configurationScope = "Virtual Instance Scope");

		FormFields.enableCheckbox(fieldName = "enabled");

		PortletEntry.save();

		AntCommand(
			locator1 = "build-test-authentication.xml",
			value1 = "verify-token-based-login -Dportal.url=www.able.com:8080/web/guest -Duser.token=test@www.able.com");
	}

	@priority = "3"
	test ViewInstanceSettingsAuthentication {
		property osgi.modules.includes = "portal-security-sso-google,portal-security-sso-ntlm";

		User.firstLoginPG();

		PortalSettings.viewAuthenticationGeneralCP();

		PortalSettings.viewAuthenticationLDAPCP();

		PortalSettings.viewAuthenticationOpenSSOCP();

		PortalSettings.viewAuthenticationCASCP();

		PortalSettings.viewAuthenticationNTLMCP();
	}

}