@component-name = "portal-security"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Login";

	setUp {
		TestCase.setUpPortalInstance();
	}

	@description = "This is a use case for LRQA-48218."
	@priority = "5"
	test DefaultPortalLandingPage {
		property test.name.skip.portal.instance = "Login#DefaultPortalLandingPage";

		task ("Login and configure landing page CP") {
			User.firstLoginPG();

			PortalSettings.gotoConfiguration(
				baseURL = "http://localhost:8080",
				configurationCategory = "Instance Configuration",
				configurationName = "General",
				configurationScope = "Virtual Instance Scope");

			PortalSettings.editDefaultLandingPageCP(defaultLandingPage = "/web/guest/landing");
		}

		task ("Add public page") {
			ApplicationsMenu.gotoPortlet(
				category = "Sites",
				panel = "Control Panel",
				portlet = "Sites");

			var siteName = TestCase.getSiteName(siteName = "${siteName}");

			Navigator.openSiteURL(siteName = "${siteName}");

			ProductMenu.gotoPortlet(
				category = "Site Builder",
				portlet = "Pages");

			SitePages.addPublicPage(pageName = "landing");
		}

		task ("Logout and login") {
			User.logoutPG();

			User.firstLoginUI(
				password = "test",
				userEmailAddress = "test@liferay.com");
		}

		task ("Assert url value") {
			var portalURL = PropsUtil.get("portal.url");

			AssertLocation(value1 = "${portalURL}/web/guest/landing");
		}
	}

	@description = "This is a use case for LPS-39744."
	@priority = "5"
	test DefaultPortalLandingPageSetByProperty {
		property custom.properties = "default.landing.page.path=/web/guest/landing";
		property test.name.skip.portal.instance = "Login#DefaultPortalLandingPageSetByProperty";

		task ("Login and add a public page ") {
			User.firstLoginPG();

			ProductMenu.gotoPortlet(
				category = "Site Builder",
				portlet = "Pages");

			SitePages.addPublicPage(pageName = "landing");
		}

		task ("Logout and login") {
			User.logoutPG();

			User.firstLoginUI(
				password = "test",
				userEmailAddress = "test@liferay.com");
		}

		task ("Assert url value") {
			var portalURL = PropsUtil.get("portal.url");

			AssertLocation(value1 = "${portalURL}/web/guest/landing");
		}
	}

	@description = "This is a use case for LPS-105151."
	@priority = "4"
	test EnabledOpenIDConnectOnSystemLevel {
		property dummy.socket.proxy.disabled = "true";
		property test.name.skip.portal.instance = "Login#EnabledOpenIDConnectOnSystemLevel";

		var openIDConnectClientID = PropsUtil.get("google.client.id.2");
		var openIDConnectClientSecret = PropsUtil.get("google.client.secret.2");
		var userEmailAddress = PropsUtil.get("email.address.2");
		var userPassword = PropsUtil.get("email.password.2");

		task ("Login and add CP") {
			User.firstLoginPG();

			ApplicationsMenu.gotoPortlet(
				category = "System",
				panel = "Control Panel",
				portlet = "Virtual Instances");

			PortalInstances.addCP(
				mailDomain = "www.able.com",
				virtualHost = "www.able.com",
				webId = "www.able.com");
		}

		task ("View CP") {
			PortalInstances.viewCP(virtualHost = "www.able.com");
		}

		task ("Goto new CP url and sign in") {
			Navigator.openSpecificURL(url = "http://www.able.com:8080/");

			AssertClick(
				locator1 = "UserBar#USER_SIGN_IN",
				value1 = "Sign In");

			AssertElementNotPresent(locator1 = "Icon#OPENID_CONNECT");
		}

		task ("Enable openID connect CP") {
			Navigator.openURL();

			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "System Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "SSO",
				configurationName = "OpenID Connect",
				configurationScope = "System Scope");

			PortalSettings.enableOpenIDConnectCP(systemLevel = "true");
		}

		task ("Add OpenID connect provider") {
			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "System Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "SSO",
				configurationName = "OpenID Connect Provider Connection",
				configurationScope = "Virtual Instance Scope");

			OpenIDConnect.addProvider(
				openIDConnectClientID = "${openIDConnectClientID}",
				openIDConnectClientSecret = "${openIDConnectClientSecret}",
				providerName = "Google");
		}

		task ("Go to able.com and assert openId connect") {
			Navigator.openSpecificURL(url = "http://www.able.com:8080/");

			AssertClick(
				locator1 = "UserBar#USER_SIGN_IN",
				value1 = "Sign In");

			AssertElementPresent(locator1 = "Icon#OPENID_CONNECT");
		}
	}

	@description = "This is a use case for LRQA-27624."
	@priority = "4"
	test FacebookSSOSmoke {
		property portal.ssl.enabled = "true";
		property test.name.skip.portal.instance = "Login#FacebookSSOSmoke";

		var facebookApplicationID = PropsUtil.get("facebook.application.id");
		var facebookApplicationSecret = PropsUtil.get("facebook.application.secret");
		var facebookEmail = PropsUtil.get("facebook.email");
		var facebookPassword = PropsUtil.get("facebook.password");

		task ("Login and edit facebook authentication") {
			User.firstLoginUI();

			var siteName = TestCase.getSiteName(siteName = "${siteName}");

			PortalSettings.gotoConfiguration(
				baseURL = "https://localhost:8443",
				configurationCategory = "SSO",
				configurationName = "Facebook Connect",
				configurationScope = "Virtual Instance Scope");

			PortalSettings.editConfigurationAuthenticationFacebook(
				facebookApplicationID = "${facebookApplicationID}",
				facebookApplicationSecret = "${facebookApplicationSecret}");
		}

		task ("Logout and login with facebook SSO") {
			User.logoutPG();

			User.loginUserWithFacebookSSOEnabledPG(
				facebookEmail = "${facebookEmail}",
				facebookPassword = "${facebookPassword}",
				siteName = "${siteName}");
		}

		task ("Logout and login") {
			User.logoutPG();

			User.firstLoginUI();
		}
	}

	@description = "This is a use case for LPS-72398."
	@priority = "5"
	test LoginWithSpanishOnlyEnabledLocale {
		property test.name.skip.portal.instance = "Login#LoginWithSpanishOnlyEnabledLocale";

		task ("Login and configure spanish as default and current portal language") {
			User.firstLoginUI();

			var portalURL = PropsUtil.get("portal.url");

			PortalSettings.gotoConfiguration(
				configurationCategory = "Localization",
				configurationName = "Language",
				configurationScope = "Virtual Instance Scope");

			PortalSettings.configureCurrentLanguagesCP(
				currentPortalLanguages = "Spanish (Spain)",
				defaultPortalLanguage = "Spanish (Spain)");
		}

		task ("Logout, restart server and login") {
			User.logoutPG();

			Portlet.shutdownServer();

			Portlet.startServer(deleteLiferayHome = "false");

			Portlet.assertServerStartup();

			User.firstLoginUI(
				localization = "ES",
				password = "test",
				specificURL = "${portalURL}/es",
				userEmailAddress = "test@liferay.com");
		}
	}

	@description = "Since we log in through navigation by default normally, this will test the login through the UI."
	@priority = "5"
	test LoginWithUI {
		property environment.acceptance = "true";
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "Login#DefaultPortalLandingPageSetByProperty";

		task ("Login and go to control panel - roles") {
			User.firstLoginUI();

			// Standard and safe UI action to assert Portal is accessible after login

			ApplicationsMenu.gotoPortlet(
				category = "Users",
				panel = "Control Panel",
				portlet = "Roles");
		}
	}

	@description = "This is a use case for LPS-105151."
	@priority = "4"
	test MultipleOpenIDConnectProvider {
		property dummy.socket.proxy.disabled = "true";
		property test.name.skip.portal.instance = "Login#MultipleOpenIDConnectProvider";

		var openIDConnectClientID = PropsUtil.get("google.client.id.2");
		var openIDConnectClientSecret = PropsUtil.get("google.client.secret.2");
		var openIDConnectClientID1 = PropsUtil.get("google.client.id.3");
		var openIDConnectClientSecret1 = PropsUtil.get("google.client.secret.3");
		var userEmailAddress = PropsUtil.get("email.address.2");
		var userPassword = PropsUtil.get("email.password.2");

		task ("Login and add CP www.able.com") {
			User.firstLoginPG();

			ApplicationsMenu.gotoPortlet(
				category = "System",
				panel = "Control Panel",
				portlet = "Virtual Instances");

			PortalInstances.addCP(
				mailDomain = "www.able.com",
				virtualHost = "www.able.com",
				webId = "www.able.com");

			PortalInstances.viewCP(virtualHost = "www.able.com");
		}

		task ("add CP www.baker.com") {
			ApplicationsMenu.gotoPortlet(
				category = "System",
				panel = "Control Panel",
				portlet = "Virtual Instances");

			PortalInstances.addCP(
				mailDomain = "www.baker.com",
				virtualHost = "www.baker.com",
				webId = "www.baker.com");

			PortalInstances.viewCP(virtualHost = "www.baker.com");
		}

		task ("Inable OpenID connection CP") {
			Navigator.openURL();

			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "System Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "SSO",
				configurationName = "OpenID Connect",
				configurationScope = "System Scope");

			PortalSettings.enableOpenIDConnectCP(systemLevel = "true");
		}

		task ("Add SSO openID connect provider") {
			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "System Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "SSO",
				configurationName = "OpenID Connect Provider Connection",
				configurationScope = "Virtual Instance Scope");

			OpenIDConnect.addProvider(
				openIDConnectClientID = "${openIDConnectClientID}",
				openIDConnectClientSecret = "${openIDConnectClientSecret}",
				providerName = "Google");
		}

		task ("Login at www.able.com and add SSO openID connect provider") {
			User.firstLoginPG(
				password = "test",
				userEmailAddress = "test@www.able.com",
				virtualHostsURL = "http://www.able.com:8080");

			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "Instance Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "SSO",
				configurationName = "OpenID Connect Provider Connection",
				configurationScope = "Virtual Instance Scope");

			OpenIDConnect.addProvider(
				openIDConnectClientID = "${openIDConnectClientID1}",
				openIDConnectClientSecret = "${openIDConnectClientSecret1}",
				providerName = "Test Google");
		}

		task ("Sign out and go to able.com") {
			UserBar.signOut();

			Navigator.openSpecificURL(url = "http://www.able.com:8080/");
		}

		task ("View provider and assert openID connect login option") {
			OpenIDConnect.viewProvider();

			AssertElementPresent(
				key_providerNname = "Test Google",
				locator1 = "OpenIDConnectLogin#OPENID_CONNECT_DROPDOWN_OPTION");
		}

		task ("Login and logout at baker.com and view provider") {
			User.firstLoginPG(
				password = "test",
				userEmailAddress = "test@www.baker.com",
				virtualHostsURL = "http://www.baker.com:8080");

			UserBar.signOut();

			Navigator.openSpecificURL(url = "http://www.baker.com:8080/");

			OpenIDConnect.viewProvider();
		}
	}

	@description = "This is a use case for LPS-105151."
	@priority = "4"
	test OpenIDConnectOnInstanceLevelSmoke {
		property dummy.socket.proxy.disabled = "true";
		property test.name.skip.portal.instance = "Login#OpenIDConnectOnInstanceLevelSmoke";

		var openIDConnectClientID = PropsUtil.get("google.client.id.1");
		var openIDConnectClientSecret = PropsUtil.get("google.client.secret.1");
		var userEmailAddress = PropsUtil.get("email.address.2");
		var userPassword = PropsUtil.get("email.password.2");

		task ("Login and add CP") {
			User.firstLoginUI();

			ApplicationsMenu.gotoPortlet(
				category = "System",
				panel = "Control Panel",
				portlet = "Virtual Instances");

			PortalInstances.addCP(
				mailDomain = "www.able.com",
				virtualHost = "www.able.com",
				webId = "www.able.com");

			PortalInstances.viewCP(virtualHost = "www.able.com");
		}

		task ("Login at able.com and enable OpenId connect CP") {
			User.firstLoginUI(
				password = "test",
				specificURL = "http://www.able.com:8080",
				userEmailAddress = "test@www.able.com");

			PortalSettings.gotoConfiguration(
				baseURL = "http://www.able.com:8080",
				configurationCategory = "SSO",
				configurationName = "OpenID Connect",
				configurationScope = "Virtual Instance Scope");

			PortalSettings.enableOpenIDConnectCP();
		}

		task ("Add SSO openID connect provider") {
			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "Instance Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "SSO",
				configurationName = "OpenID Connect Provider Connection",
				configurationScope = "Virtual Instance Scope");

			OpenIDConnect.addProvider(
				openIDConnectClientID = "${openIDConnectClientID}",
				openIDConnectClientSecret = "${openIDConnectClientSecret}",
				providerName = "Google");
		}

		task ("Sign out and login at able.com with openID connect enabled") {
			UserBar.signOut();

			User.loginUserWithOpenIDConnectEnabledPG(
				specificURL = "http://www.able.com:8080",
				userEmailAddress = "${userEmailAddress}",
				userPassword = "${userPassword}");
		}

		task ("Sign out and login") {
			UserBar.signOut();

			User.firstLoginUI(
				password = "test",
				specificURL = "http://www.able.com:8080",
				userEmailAddress = "test@www.able.com");
		}
	}

	@description = "This is a use case for LRQA-40911."
	@priority = "4"
	test OpenIDConnectSmoke {
		property dummy.socket.proxy.disabled = "true";
		property test.name.skip.portal.instance = "Login#OpenIDConnectSmoke";

		// LRQA-43729

		var openIDConnectClientID = PropsUtil.get("google.client.id.3");
		var openIDConnectClientSecret = PropsUtil.get("google.client.secret.3");
		var userEmailAddress = PropsUtil.get("email.address.2");
		var userPassword = PropsUtil.get("email.password.2");

		task ("Login and add SSO openID connect provider") {
			User.firstLoginPG();

			PortalSettings.gotoConfiguration(
				baseURL = "http://localhost:8080",
				configurationCategory = "SSO",
				configurationName = "OpenID Connect",
				configurationScope = "Virtual Instance Scope");

			PortalSettings.enableOpenIDConnectCP();

			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "System Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "SSO",
				configurationName = "OpenID Connect Provider Connection",
				configurationScope = "Virtual Instance Scope");

			OpenIDConnect.addProvider(
				openIDConnectClientID = "${openIDConnectClientID}",
				openIDConnectClientSecret = "${openIDConnectClientSecret}",
				providerName = "Google");
		}

		task ("Logout and login with openID connect") {
			User.logoutPG();

			User.loginUserWithOpenIDConnectEnabledPG(
				userEmailAddress = "${userEmailAddress}",
				userPassword = "${userPassword}");
		}

		task ("Logout and login") {
			User.logoutPG();

			User.loginUserPG(
				password = "test",
				userEmailAddress = "test@liferay.com");
		}
	}

	@description = "This is a use case for LRQA-43015. Ignore test due to OpenAM 12 doesn't support Tomcat 9."
	@ignore = "true"
	@priority = "5"
	test OpenSSOSmoke {
		property dummy.socket.proxy.disabled = "true";
		property openam.enabled = "true";
		property test.name.skip.portal.instance = "Login#OpenSSOSmoke";

		// LRQA-40534

		task ("Go to http://test.able.com:8080/openam and run ant command") {
			Navigator.openSpecificURL(url = "http://test.able.com:8080/openam");

			AntCommand(
				locator1 = "build-test-authentication.xml",
				value1 = "run-openam-tools");
		}

		task ("Wait for OpenAM setup") {
			var pauseCount = "0";

			while ((IsElementNotPresent(locator1 = "TextInput#TEXT")) && ("${pauseCount}" != "20")) {
				Refresh();

				Pause(locator1 = "30000");

				var pauseCount = ${pauseCount} + 1;

				echo("Pause count: ${pauseCount}");
			}

			if ("${pauseCount}" == "20") {
				fail("OpenAM setup timed out after 10 minutes.");
			}
		}

		task ("Login and configure open SSO") {
			User.firstLoginPG();

			Pause(locator1 = "30000");

			PortalSettings.gotoConfiguration(
				baseURL = "http://localhost:8080",
				configurationCategory = "SSO",
				configurationName = "OpenSSO",
				configurationScope = "Virtual Instance Scope");

			PortalSettings.configureOpenSSO(
				OpenSSOLoginURL = "http://test.able.com:8080/openam/UI/Login?goto=http://www.able.com:8080/c/portal/login",
				OpenSSOLogoutURL = "http://test.able.com:8080/openam/UI/Logout?goto=http://www.able.com:8080/web/guest/home",
				OpenSSOServiceURL = "http://test.able.com:8080/openam");
		}

		task ("Test SSO connection") {
			PortalSettings.gotoConfiguration(
				baseURL = "http://localhost:8080",
				configurationCategory = "SSO",
				configurationName = "OpenSSO",
				configurationScope = "Virtual Instance Scope");

			PortalSettings.testOpenSSOConnection();
		}

		task ("Logout and login with open SSO") {
			User.logoutPG();

			User.loginUserWithOpenSSOEnabledPG(
				hostsURL = "http://www.able.com:8080",
				password = "password",
				userScreenName = "test");
		}
	}

	@description = "This is a use case for LRQA-27622."
	@priority = "5"
	test TokenBasedSSOSmoke {
		property test.name.skip.portal.instance = "Login#TokenBasedSSOSmoke";

		task ("Login and reset token based SSO configuration") {
			User.firstLoginUI();

			SystemSettings.openSystemSettingsAdmin();

			SystemSettings.gotoConfiguration(
				configurationCategory = "SSO",
				configurationName = "Token Based SSO",
				configurationScope = "System Scope");

			SystemSettings.resetConfiguration();
		}

		task ("Enable and save configuration") {
			FormFields.enableCheckbox(fieldName = "Enabled");

			SystemSettings.saveConfiguration();
		}

		task ("Run and command") {
			AntCommand(
				locator1 = "build-test-authentication.xml",
				value1 = "verify-token-based-login -Dportal.url=http://localhost:8080/web/guest -Duser.token=test@liferay.com");
		}

		task ("Disable configuration") {
			FormFields.disableCheckbox(fieldName = "Enabled");

			SystemSettings.saveConfiguration();
		}

		task ("Add CP able.com") {
			PortalInstances.openVirtualInstancesAdmin();

			PortalInstances.addCP(
				mailDomain = "www.able.com",
				virtualHost = "www.able.com",
				webId = "www.able.com");
		}

		task ("Login at able.com and enable token based SSO") {
			User.firstLoginUI(
				password = "test",
				specificURL = "http://www.able.com:8080",
				userEmailAddress = "test@www.able.com");

			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "Instance Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "SSO",
				configurationName = "Token Based SSO",
				configurationScope = "Virtual Instance Scope");

			FormFields.enableCheckbox(fieldName = "enabled");

			SystemSettings.saveConfiguration();
		}

		task ("Run ant command") {
			AntCommand(
				locator1 = "build-test-authentication.xml",
				value1 = "verify-token-based-login -Dportal.url=http://www.able.com:8080/web/guest -Duser.token=test@www.able.com");
		}
	}

	@description = "This is a use case for LRQA-27619."
	@priority = "3"
	test ViewInstanceSettingsAuthentication {
		property osgi.app.includes = "portal-security-sso-openid";

		task ("Login and view General CP authentication settings") {
			User.firstLoginPG();

			PortalSettings.viewAuthenticationGeneralCP();
		}

		task ("View LDAP CP authentication settings") {
			PortalSettings.viewAuthenticationLDAPCP();
		}

		task ("View openSSO CP authentication settings") {
			PortalSettings.viewAuthenticationOpenSSOCP();
		}
	}

}