@component-name = "portal-search-ee"
definition {

	property osgi.app.includes = "portal-search-elasticsearch7";
	property osgi.module.configuration.file.names = "com.liferay.portal.bundle.blacklist.internal.BundleBlacklistConfiguration.config";
	property osgi.module.configurations = "blacklistBundleSymbolicNames=&quot;com.liferay.portal.mobile.device.detection.fiftyonedegrees.api,com.liferay.portal.mobile.device.detection.fiftyonedegrees.enterprise,com.liferay.portal.mobile.device.detection.fiftyonedegrees.enterprise.test.data,com.liferay.portal.mobile.device.detection.fiftyonedegrees,com.liferay.portal.search.elasticsearch6.impl,com.liferay.portal.search.elasticsearch6.spi,com.liferay.portal.search.elasticsearch6.api,com.liferay.portal.search.elasticsearch6.xpack.security.impl,com.liferay.portal.search.elasticsearch6.xpack.monitoring.web&quot;";
	property portal.release = "true";
	property portal.suite.search.engine = "elasticsearch7";
	property portal.upstream = "true";
	property test.run.environment = "EE";
	property testray.main.component.name = "Elasticsearch Impl";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	test AssertCannotAddDuplicateQueriesAndAliases {
		property test.name.skip.portal.instance = "SearchTuningES7#AssertCannotAddDuplicateQueriesAndAliases";

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		SearchTuning.addResultRanking(searchQuery = "Article");

		SearchTuning.configureAliases(addAlias = "Journal");

		Button.clickSave();

		LexiconEntry.gotoAdd();

		PortletEntry.inputText(
			fieldLabel = "Search Query",
			text = "Article");

		Button.click(button = "Customize Results");

		Alert.viewErrorMessage(errorMessage = "A ranking already exists");

		Navigator.gotoBack();

		PortletEntry.inputText(
			fieldLabel = "Search Query",
			text = "Journal");

		Button.click(button = "Customize Results");

		Alert.viewErrorMessage(errorMessage = "A ranking already exists");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		AssertElementNotPresent(
			key_query = "Article",
			locator1 = "SearchTuning#RESULT_RANKINGS_DUPLICATE_ENTRY");

		AssertElementNotPresent(
			key_query = "Journal",
			locator1 = "SearchTuning#RESULT_RANKINGS_ENTRY");

		SearchTuning.addResultRanking(searchQuery = "Web Content");

		Type(
			locator1 = "SearchTuning#RESULT_RANKINGS_ALIAS_FIELD",
			value1 = "Article");

		KeyPress(
			locator1 = "SearchTuning#RESULT_RANKINGS_ALIAS_FIELD",
			value1 = ",");

		Button.clickSave();

		Alert.viewErrorMessage(errorMessage = "A ranking already exists");

		Navigator.gotoBack();

		Type(
			locator1 = "SearchTuning#RESULT_RANKINGS_ALIAS_FIELD",
			value1 = "Journal");

		KeyPress(
			locator1 = "SearchTuning#RESULT_RANKINGS_ALIAS_FIELD",
			value1 = ",");

		Button.clickSave();

		Alert.viewErrorMessage(errorMessage = "A ranking already exists");
	}

	test AssertDeletedResultRanking {
		property test.name.skip.portal.instance = "SearchTuningES7#AssertDeletedResultRanking";

		var content = "Article Article Article";

		for (var title : list "Article 1,Article 2,Article 3") {
			JSONWebcontent.addWebContent(
				content = "${content}",
				groupName = "Guest",
				title = "${title}");

			var content = StringUtil.replaceFirst("${content}", "Article ", "");
		}

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		SearchTuning.addResultRanking(searchQuery = "Article");

		SearchTuning.pinResult(assetTitle = "Article 3");

		SearchTuning.hideResult(assetTitle = "Article 2");

		Button.clickSave();

		Navigator.openURL();

		SearchPortlets.searchEmbedded(searchTerm = "Article");

		SearchResultPortlet.viewSearchResultsSpecificOrder(resultsList = "Article 3,Article 1");

		SearchResultPortlet.viewSearchResultNotPresent(
			searchAssetTitle = "Article 2",
			searchAssetType = "Web Content Article");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		LexiconEntry.delete(rowEntry = "Article");

		AssertElementNotPresent(
			key_query = "Article",
			locator1 = "SearchTuning#RESULT_RANKINGS_ENTRY");

		Navigator.openURL();

		SearchPortlets.searchEmbedded(searchTerm = "Article");

		SearchResultPortlet.viewSearchResultsSpecificOrder(resultsList = "Article 1,Article 2,Article 3");
	}

	test AssertResultRankingsMiscUI {
		property test.name.skip.portal.instance = "SearchTuningES7#AssertResultRankingsMiscUI";

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		AssertElementNotPresent(locator1 = "Pagination#PAGINATION_BAR");

		LexiconEntry.gotoAdd();

		Button.click(button = "Customize Results");

		Alert.viewRequiredField();

		Navigator.gotoBack();

		for (var query : list "Article 1,Article 2,Article 3,Article 4,Article 5,Article 6,Article 7") {
			SearchTuning.addResultRanking(searchQuery = "${query}");

			Button.clickSave();
		}

		Pagination.changePagination(itemsPerPage = "5");

		for (var query : list "Article 1,Article 2,Article 3,Article 4,Article 5") {
			AssertElementPresent(
				key_query = "${query}",
				locator1 = "SearchTuning#RESULT_RANKINGS_ENTRY");
		}

		for (var query : list "Article 6,Article 7") {
			AssertElementNotPresent(
				key_query = "${query}",
				locator1 = "SearchTuning#RESULT_RANKINGS_ENTRY");
		}

		Pagination.viewResults(results = "Showing 1 to 5 of 7 Entries");

		Click(locator1 = "Pagination#NEXT_LINK");

		for (var query : list "Article 6,Article 7") {
			AssertElementPresent(
				key_query = "${query}",
				locator1 = "SearchTuning#RESULT_RANKINGS_ENTRY");
		}

		for (var query : list "Article 1,Article 2,Article 3,Article 4,Article 5") {
			AssertElementNotPresent(
				key_query = "${query}",
				locator1 = "SearchTuning#RESULT_RANKINGS_ENTRY");
		}

		Pagination.viewResults(results = "Showing 6 to 7 of 7 Entries");

		Click(locator1 = "Pagination#PREVIOUS_LINK");

		Pagination.viewResults(results = "Showing 1 to 5 of 7 Entries");

		PortletEntry.deleteViaToolbar(rowEntries = "Article 1,Article 2,Article 3");

		for (var query : list "Article 1,Article 2,Article 3") {
			AssertElementNotPresent(
				key_query = "${query}",
				locator1 = "SearchTuning#RESULT_RANKINGS_ENTRY");
		}

		PortletEntry.selectAll();

		ClickNoError(locator1 = "Icon#DELETE");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No Custom Results Yet");
	}

	test AssertResultRankingsWithSynonyms {
		property test.name.skip.portal.instance = "SearchTuningES7#AssertResultRankingsWithSynonyms";

		JSONWebcontent.addWebContent(
			content = "Article Article Article Article",
			groupName = "Guest",
			title = "Article 1");

		JSONWebcontent.addWebContent(
			content = "Article Article Article",
			groupName = "Guest",
			title = "Article 2");

		JSONWebcontent.addWebContent(
			content = "Journal Journal",
			groupName = "Guest",
			title = "Journal 1");

		JSONWebcontent.addWebContent(
			content = "Journal",
			groupName = "Guest",
			title = "Journal 2");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Synonyms");

		SearchTuning.addSynonymSet(synonyms = "Article,Journal");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		SearchTuning.addResultRanking(searchQuery = "Article");

		SearchTuning.pinResult(assetTitle = "Journal 2");

		SearchTuning.hideResult(assetTitle = "Article 2");

		Button.clickSave();

		Navigator.openURL();

		SearchPortlets.searchEmbedded(searchTerm = "Article");

		SearchResultPortlet.viewSearchResultsSpecificOrder(resultsList = "Journal 2,Article 1,Journal 1");

		SearchResultPortlet.viewSearchResultNotPresent(
			searchAssetTitle = "Article 2",
			searchAssetType = "Web Content Article");

		SearchPortlets.searchEmbedded(searchTerm = "Journal");

		SearchResultPortlet.viewSearchResultsSpecificOrder(resultsList = "Article 1,Article 2,Journal 1,Journal 2");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Synonyms");

		SearchTuning.deleteSynonymSet(synonymSet = "Article, Journal");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "Article");

		AssertElementPresent(
			key_assetTitle = "Journal 2",
			locator1 = "SearchTuning#RESULT_RANKINGS_RESULT_PINNED");

		AssertElementNotPresent(
			key_assetTitle = "Journal 1",
			locator1 = "SearchTuning#RESULT_RANKINGS_RESULT");

		Navigator.openURL();

		SearchPortlets.searchEmbedded(searchTerm = "Article");

		SearchResultPortlet.viewSearchResultsSpecificOrder(resultsList = "Journal 2,Article 1");

		SearchResultPortlet.viewMultipleSearchResultsNotPresent(noResultsList = "Article 2/Web Content Article,Journal 1/Web Content Article");

		SearchPortlets.searchEmbedded(searchTerm = "Journal");

		SearchResultPortlet.viewSearchResultsSpecificOrder(resultsList = "Journal 1,Journal 2");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "Article");

		SearchTuning.unpinResult(assetTitle = "Journal 2");

		Button.clickSave();

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "Article");

		AssertElementNotPresent(
			key_assetTitle = "Journal 2",
			locator1 = "SearchTuning#RESULT_RANKINGS_RESULT");

		Navigator.openURL();

		SearchPortlets.searchEmbedded(searchTerm = "Article");

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "Article 1",
			searchAssetType = "Web Content Article");

		SearchResultPortlet.viewMultipleSearchResultsNotPresent(noResultsList = "Article 2/Web Content Article,Journal 1/Web Content Article,Journal 2/Web Content Article");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "Article");

		SearchTuning.addResult(
			assetTitle = "Journal 2",
			searchTerm = "Journal");

		Button.clickSave();

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Synonyms");

		SearchTuning.addSynonymSet(synonyms = "Article,Journal");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "Article");

		AssertElementPresent(
			key_assetTitle = "Journal 2",
			locator1 = "SearchTuning#RESULT_RANKINGS_RESULT_PINNED");

		AssertElementPresent(
			key_assetTitle = "Journal 1",
			locator1 = "SearchTuning#RESULT_RANKINGS_RESULT");

		SearchTuning.configureAliases(addAlias = "Journal");

		Button.clickSave();

		Navigator.openURL();

		SearchPortlets.searchEmbedded(searchTerm = "Article");

		SearchResultPortlet.viewSearchResultsSpecificOrder(resultsList = "Journal 2,Article 1,Journal 1");

		SearchResultPortlet.viewSearchResultNotPresent(
			searchAssetTitle = "Article 2",
			searchAssetType = "Web Content Article");

		SearchPortlets.searchEmbedded(searchTerm = "Journal");

		SearchResultPortlet.viewSearchResultsSpecificOrder(resultsList = "Journal 2,Article 1,Journal 1");

		SearchResultPortlet.viewSearchResultNotPresent(
			searchAssetTitle = "Article 2",
			searchAssetType = "Web Content Article");
	}

	test AssertSynonymBulkActions {
		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Synonyms");

		AssertElementNotPresent(locator1 = "Pagination#PAGINATION_BAR");

		for (var counter : list "1,2,3,4,5,6") {
			SearchTuning.addSynonymSet(synonyms = "phone${counter},mobile${counter}");
		}

		AssertElementNotPresent(locator1 = "Pagination#PAGINATION_BAR");

		PortletEntry.selectAll();

		for (var counter : list "1,2,3,4,5,6") {
			AssertChecked(
				key_rowEntry = "phone${counter}, mobile${counter}",
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_CHECKBOX");
		}

		PortletEntry.deselectAll();

		for (var counter : list "1,2,3,4,5,6") {
			AssertNotChecked(
				key_rowEntry = "phone${counter}, mobile${counter}",
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_CHECKBOX");
		}

		LexiconEntry.selectEntry(key_rowEntry = "phone2, mobile2");

		LexiconEntry.selectEntry(key_rowEntry = "phone5, mobile5");

		AssertTextEquals(
			locator1 = "ManagementBar#NAVBAR_TEXT",
			value1 = "2 of 6 Selected");

		ClickNoError(locator1 = "Icon#DELETE");

		Confirm();

		AssertElementNotPresent(
			key_rowEntry = "phone2, mobile2",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

		AssertElementNotPresent(
			key_rowEntry = "phone5, mobile5",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

		for (var counter : list "1,3,4,6") {
			AssertElementPresent(
				key_rowEntry = "phone${counter}, mobile${counter}",
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");
		}

		PortletEntry.selectAll();

		ClickNoError(locator1 = "Icon#DELETE");

		Confirm();

		for (var counter : list "1,2,3,4,5,6") {
			AssertElementNotPresent(
				key_rowEntry = "phone${counter}, mobile${counter}",
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");
		}

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no entries.");
	}

	test PinAndHideResultsWithAliases {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "SearchTuningES7#PinAndHideResultsWithAliases";

		JSONWebcontent.addWebContent(
			content = "WC WC",
			groupName = "Guest",
			title = "WC Title 1");

		JSONWebcontent.addWebContent(
			content = "apple WC",
			groupName = "Guest",
			title = "WC Title 2");

		JSONWebcontent.addWebContent(
			content = "",
			groupName = "Guest",
			title = "WC Title 3");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		SearchTuning.addResultRanking(searchQuery = "WC Title");

		SearchTuning.pinResult(assetTitle = "WC Title 3");

		SearchTuning.hideResult(assetTitle = "WC Title 2");

		SearchTuning.configureAliases(addAlias = "apple");

		SearchTuning.configureAliases(addAlias = "banana");

		Button.clickSave();

		Navigator.openURL();

		SearchPortlets.searchEmbedded(searchTerm = "WC Title");

		SearchResultPortlet.viewSearchResultsSpecificOrder(resultsList = "WC Title 3,WC Title 1");

		SearchResultPortlet.viewSearchResultNotPresent(
			searchAssetTitle = "WC Title 2",
			searchAssetType = "Web Content Article");

		SearchPortlets.searchEmbedded(searchTerm = "apple");

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "WC Title 3",
			searchAssetType = "Web Content Article");

		SearchResultPortlet.viewMultipleSearchResultsNotPresent(noResultsList = "WC Title 1/Web Content Article,WC Title 2/Web Content Article");

		SearchPortlets.searchEmbedded(searchTerm = "banana");

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "WC Title 3",
			searchAssetType = "Web Content Article");

		SearchResultPortlet.viewMultipleSearchResultsNotPresent(noResultsList = "WC Title 1/Web Content Article,WC Title 2/Web Content Article");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "WC Title");

		SearchTuning.configureAliases(deleteAlias = "apple");

		SearchTuning.configureAliases(deleteAlias = "banana");

		Button.clickSave();

		Navigator.openURL();

		SearchPortlets.searchEmbedded(searchTerm = "WC Title");

		SearchResultPortlet.viewSearchResultsSpecificOrder(resultsList = "WC Title 3,WC Title 1");

		SearchResultPortlet.viewSearchResultNotPresent(
			searchAssetTitle = "WC Title 2",
			searchAssetType = "Web Content Article");

		SearchPortlets.searchEmbedded(searchTerm = "apple");

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "WC Title 2",
			searchAssetType = "Web Content Article");

		SearchResultPortlet.viewMultipleSearchResultsNotPresent(noResultsList = "WC Title 1/Web Content Article,WC Title 3/Web Content Article");

		SearchPortlets.searchEmbedded(searchTerm = "banana");

		SearchResultPortlet.viewNoSearchResults(searchTerm = "banana");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "WC Title");

		SearchTuning.unpinResult(assetTitle = "WC Title 3");

		SearchTuning.unhideResult(assetTitle = "WC Title 2");

		Button.clickSave();

		Navigator.openURL();

		SearchPortlets.searchEmbedded(searchTerm = "WC Title");

		SearchResultPortlet.viewSearchResultsSpecificOrder(resultsList = "WC Title 1,WC Title 2,WC Title 3");
	}

	test ReorderAddedResultsFromOtherSites {
		property test.name.skip.portal.instance = "SearchTuningES7#ReorderAddedResultsFromOtherSites";

		JSONWebcontent.addWebContent(
			content = "WC Content",
			groupName = "Guest",
			title = "Liferay Article");

		JSONWebcontent.addWebContent(
			content = "WC Content",
			groupName = "Guest",
			title = "Liferay Journal");

		JSONGroup.addGroup(groupName = "New Site");

		JSONWebcontent.addWebContent(
			content = "WC Content",
			groupName = "New Site",
			title = "New Site Article");

		JSONWebcontent.addWebContent(
			content = "WC Content",
			groupName = "New Site",
			title = "New Site Journal");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		SearchTuning.addResultRanking(searchQuery = "Article");

		SearchTuning.pinResult(assetTitle = "New Site Article");

		SearchTuning.addResult(
			assetTitle = "Liferay Journal",
			searchTerm = "content");

		SearchTuning.addResult(
			assetTitle = "New Site Journal",
			searchTerm = "journal");

		DragAndDrop.javaScriptDragAndDropToObject(
			key_assetTitle = "New Site Journal",
			locator1 = "SearchTuning#RESULT_RANKINGS_RESULT_DRAG_HANDLE",
			locator2 = "//li[contains(@class,'results-ranking-item-pinned')][3]",
			value1 = "");

		Button.clickSave();

		Navigator.openURL();

		SearchPortlets.searchEmbedded(searchTerm = "Article");

		SearchResultPortlet.viewSearchResultsSpecificOrder(resultsList = "Liferay Journal,New Site Article,New Site Journal,Liferay Article");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Result Rankings");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "Article");

		for (var title : list "Liferay Journal,New Site Article,New Site Journal") {
			SearchTuning.unpinResult(assetTitle = "${title}");
		}

		Button.clickSave();

		Navigator.openURL();

		SearchPortlets.searchEmbedded(searchTerm = "Article");

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "Liferay Article",
			searchAssetType = "Web Content Article");

		SearchResultPortlet.viewMultipleSearchResultsNotPresent(noResultsList = "Liferay Journal/Web Content Article,New Site Article/Web Content Article,New Site Journal/Web Content Article");
	}

	test SearchWithSynonyms {
		property portal.acceptance = "true";

		for (var title : list "banana,red apple,citrus-orange,blue.berry!,\"xigua\"") {
			JSONWebcontent.addWebContent(
				content = "WC Content",
				groupName = "Guest",
				title = "${title}");
		}

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Synonyms");

		SearchTuning.addSynonymSet(synonyms = "banana,red apple,blue.berry!,&quot;xigua&quot;,fruit,sweet");

		AssertElementPresent(
			key_synonyms = "banana, red apple, blue.berry!, &quot;xigua&quot;, fruit, sweet",
			locator1 = "SearchTuning#SYNONYMS_ENTRY");

		Navigator.openURL();

		var searchTerms = "banana,red apple,blue.berry!,fruit,sweet";

		// Workaround for LPS-98622

		// var searchTerms = "banana,red apple,blue.berry!,&quot;xigua&quot;,fruit,sweet";

		for (var searchTerm : list "${searchTerms}") {
			SearchPortlets.searchEmbedded(searchTerm = "${searchTerm}");

			SearchResultPortlet.viewMultipleSearchResults(
				resultsList = "banana/Web Content Article,red apple/Web Content Article,blue.berry!/Web Content Article,&quot;xigua&quot;/Web Content Article",
				searchTerm = "${searchTerm}");
		}

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Synonyms");

		SearchTuning.editSynonymSet(
			addSynonyms = "pear",
			deleteSynonym = "banana",
			synonymSet = "banana, red apple, blue.berry!, &quot;xigua&quot;, fruit, sweet");

		AssertElementPresent(
			key_synonyms = "red apple, blue.berry!, &quot;xigua&quot;, fruit, sweet, pear",
			locator1 = "SearchTuning#SYNONYMS_ENTRY");

		Navigator.openURL();

		SearchPortlets.searchEmbedded(searchTerm = "banana");

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "banana",
			searchAssetType = "Web Content Article");

		SearchPortlets.searchEmbedded(searchTerm = "pear");

		SearchResultPortlet.viewSearchResultNotPresent(
			searchAssetTitle = "banana",
			searchAssetType = "Web Content Article");

		SearchResultPortlet.viewMultipleSearchResults(
			resultsList = "red apple/Web Content Article,blue.berry!/Web Content Article,&quot;xigua&quot;/Web Content Article",
			searchTerm = "pear");

		SearchPortlets.searchEmbedded(searchTerm = "red apple");

		SearchResultPortlet.viewSearchResultNotPresent(
			searchAssetTitle = "banana",
			searchAssetType = "Web Content Article");

		SearchResultPortlet.viewMultipleSearchResults(
			resultsList = "red apple/Web Content Article,blue.berry!/Web Content Article,&quot;xigua&quot;/Web Content Article",
			searchTerm = "red apple");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Synonyms");

		SearchTuning.addSynonymSet(synonyms = "red apple,citrus-orange");

		AssertElementPresent(
			key_synonyms = "red apple, citrus-orange",
			locator1 = "SearchTuning#SYNONYMS_ENTRY");

		Navigator.openURL();

		SearchPortlets.searchEmbedded(searchTerm = "red apple");

		SearchResultPortlet.viewMultipleSearchResults(
			resultsList = "red apple/Web Content Article,blue.berry!/Web Content Article,&quot;xigua&quot;/Web Content Article,citrus-orange/Web Content Article",
			searchTerm = "red apple");

		SearchPortlets.searchEmbedded(searchTerm = "citrus-orange");

		SearchResultPortlet.viewMultipleSearchResults(
			resultsList = "red apple/Web Content Article,citrus-orange/Web Content Article",
			searchTerm = "citrus-orange");

		ProductMenu.gotoPortlet(
			category = "Search Tuning",
			panel = "Control Panel",
			portlet = "Synonyms");

		SearchTuning.deleteSynonymSet(synonymSet = "red apple, blue.berry!, &quot;xigua&quot;, fruit, sweet, pear");

		Navigator.openURL();

		SearchPortlets.searchEmbedded(searchTerm = "fruit");

		SearchResultPortlet.viewNoSearchResults(searchTerm = "fruit");
	}

}