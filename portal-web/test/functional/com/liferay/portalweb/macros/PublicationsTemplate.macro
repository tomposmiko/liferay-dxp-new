definition {

	macro addPublicationTemplate {
		Click(locator1 = "Button#PLUS");

		if (isSet(templateName)) {
			Type(
				locator1 = "PublicationsTemplate#TEMPLATE_NAME",
				value1 = ${templateName});
		}

		if (isSet(templateDescription)) {
			Type(
				locator1 = "PublicationsTemplate#TEMPLATE_DESCRIPTION",
				value1 = ${templateDescription});
		}

		if (isSet(publicationName)) {
			PublicationsTemplate.expandPublicationInformation();

			Type(
				locator1 = "Publications#PUBLICATION_NAME",
				value1 = ${publicationName});

			if (isSet(publicationDescription)) {
				Type(
					locator1 = "Publications#PUBLICATION_DESCRIPTION",
					value1 = ${publicationDescription});
			}
		}

		if (${defaultSandboxTemplate} == "true") {
			FormFields.enableCheckbox(fieldName = "Default Sandbox Template");
		}

		if (${defaultTemplate} == "true") {
			FormFields.enableCheckbox(fieldName = "Default Template");
		}

		if (isSet(userInvite1)) {
			PublicationsTemplate.expandPublicationCollaborators();

			Click(locator1 = "PublicationsTemplate#INVITE_USERS_BUTTON");

			PublicationsTemplate.inviteUser(
				publicationRole = ${publicationRole1},
				titleRole = ${titleRole1},
				userInvite = ${userInvite1});

			if (isSet(userInvite2)) {
				PublicationsTemplate.inviteUser(
					publicationRole = ${publicationRole2},
					titleRole = ${titleRole2},
					userInvite = ${userInvite2});
			}

			Button.clickNoError(button = "Save");

			if (${userMissingPublicationsRole} == "true") {
				AssertConfirm(value1 = "You are inviting user ${userInvite} who does not have access to Publications. This user will be assigned the Publications User role to gain access to Publications.");
			}

			Alert.viewSuccessMessage();
		}

		Click.javaScriptClick(
			key_text = "Create",
			locator1 = "Button#ANY");

		Alert.viewSuccessMessage();
	}

	macro deletePublicationTemplate {
		Click(
			key_templateName = ${templateName},
			locator1 = "PublicationsTemplate#TEMPLATE_DROPDOWN_MENU");

		MenuItem.click(menuItem = "Delete");
	}

	macro editPublicationTemplate {
		Click(
			key_templateName = ${templateName},
			locator1 = "PublicationsTemplate#TEMPLATE_DROPDOWN_MENU");

		MenuItem.click(menuItem = "Edit");

		if (isSet(templateNameEdit)) {
			Type(
				locator1 = "PublicationsTemplate#TEMPLATE_NAME",
				value1 = ${templateNameEdit});
		}

		if (isSet(templateDescriptionEdit)) {
			Type(
				locator1 = "PublicationsTemplate#TEMPLATE_DESCRIPTION",
				value1 = ${templateDescriptionEdit});
		}

		PublicationsTemplate.expandPublicationInformation();

		if (isSet(publicationNameEdit)) {
			Type(
				locator1 = "Publications#PUBLICATION_NAME",
				value1 = ${publicationNameEdit});
		}

		if (isSet(publicationDescriptionEdit)) {
			Type(
				locator1 = "Publications#PUBLICATION_DESCRIPTION",
				value1 = ${publicationDescriptionEdit});
		}

		Button.clickSave();
	}

	macro expandPublicationCollaborators {
		if (IsElementPresent(locator1 = "PublicationsTemplate#PUBLICATION_COLLABORATORS_COLLAPSED")) {
			Click(locator1 = "PublicationsTemplate#PUBLICATION_COLLABORATORS_COLLAPSED");

			WaitForElementPresent(locator1 = "Publications#PUBLICATION_NAME");
		}
	}

	macro expandPublicationInformation {
		if (IsElementPresent(locator1 = "PublicationsTemplate#PUBLICATION_INFORMATION_COLLAPSED")) {
			Click(locator1 = "PublicationsTemplate#PUBLICATION_INFORMATION_COLLAPSED");

			WaitForElementPresent(locator1 = "PublicationsTemplate#INVITE_USERS_BUTTON");
		}
	}

	macro gotoPublicationTemplate {
		ApplicationsMenu.gotoPortlet(
			category = "Publications",
			panel = "Applications",
			portlet = "Publications");

		Click.waitForMenuToggleJSClick(locator1 = "Publications#PUBLICATIONS_ELLIPSIS");

		MenuItem.click(menuItem = "Templates");
	}

	macro inviteUser {
		Click(
			key_title = ${titleRole},
			locator1 = "Button#ANY_TITLE");

		Click(
			key_publicationRole = ${publicationRole},
			locator1 = "Publications#PUBLICATION_ROLE_BUTTON");

		Type(
			locator1 = "Publications#KEYWORD_TEXTFIELD",
			value1 = ${userInvite});

		Click(
			key_userInvite = ${userInvite},
			locator1 = "PublicationsTemplate#INVITE_USERS_DROPDOWN_ITEM");
	}

	macro viewPublicationTemplate {
		PublicationsTemplate.gotoPublicationTemplate();

		AssertTextEquals.assertPartialText(
			key_templateName = ${templateName},
			locator1 = "PublicationsTemplate#TEMPLATE_NAME_IN_PUBLICATION_TEMPLATE",
			value1 = ${templateName});
	}

	macro viewPublicationTemplateNotPresent {
		PublicationsTemplate.gotoPublicationTemplate();

		AssertElementNotPresent(
			key_templateName = ${templateName},
			locator1 = "PublicationsTemplate#TEMPLATE_NAME_IN_PUBLICATION_TEMPLATE");
	}

	macro viewTemplateInvitedUser {
		Click(
			key_templateName = ${templateName},
			locator1 = "PublicationsTemplate#TEMPLATE_DROPDOWN_MENU");

		MenuItem.click(menuItem = "Edit");

		PublicationsTemplate.expandPublicationCollaborators();

		Click(locator1 = "PublicationsTemplate#INVITE_USERS_BUTTON");

		for (var userAndRole : list ${usersAndRoles}) {
			var userInvite = StringUtil.extractFirst(${userAndRole}, "/");
			var templateRole = StringUtil.extractLast(${userAndRole}, "/");

			AssertElementPresent(
				key_publicationRole = ${templateRole},
				key_userInvite = ${userInvite},
				locator1 = "Publications#INVITED_USER");
		}
	}

}