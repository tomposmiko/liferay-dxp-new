definition {

	@summary = "Helper function to add fields '${templateFieldNames}' to the template"
	macro _addFields {
		if (isSet(templateFieldNames)) {
			for (var fieldName : list "${templateFieldNames}") {
				var key_fieldName = "${fieldName}";

				AssertClick(
					locator1 = "DDMEditTemplate#SCRIPT_FIELD",
					value1 = "${fieldName}");
			}
		}
	}

	@summary = "Helper function to add description '${description}' to the template"
	macro _inputDescription {
		if (isSet(templateDescription)) {
			PortletEntry.inputDescription(description = "${templateDescription}");
		}
	}

	@summary = "Helper function to link structure '${structureName}' to the template"
	macro _inputStructure {
		if (isSet(structureName)) {
			AssertClick(
				locator1 = "DDMEditTemplate#DETAILS_SELECT_STRUCTURE",
				value1 = "Select");

			SelectFrameTop();

			SelectFrame(locator1 = "IFrame#DIALOG");

			Click(locator1 = "Icon#BASIC_SEARCH");

			var key_ddlDataDefinitionName = "${structureName}";

			AssertClickNoError(
				locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK",
				value1 = "${structureName}");

			SelectFrameTop();
		}
	}

	@summary = "Helper function to add a script to a template via file"
	macro _inputTemplateFile {
		if (isSet(templateFile)) {
			UploadCommonFile(
				locator1 = "DDMEditTemplate#SCRIPT_FILE_FIELD",
				value1 = "${templateFile}");
		}
	}

	@summary = "Click on the add button to add a template named '${templateName}' for structure '${structureName}', input most basic fields and then save the template"
	macro addCP {
		LexiconEntry.gotoAdd();

		WebContentTemplates.inputTemplateAddScreenFields(
			imageSourceURL = "${imageSourceURL}",
			structureName = "${structureName}",
			templateDescription = "${templateDescription}",
			templateFieldNames = "${templateFieldNames}",
			templateFile = "${templateFile}",
			templateLanguage = "${templateLanguage}",
			templateName = "${templateName}",
			templateScript = "${templateScript}");

		DDLDataDefinition.save(changeListEnabled = "${changeListEnabled}");
	}

	@summary = "Attempt to add a blank template"
	macro addNullCP {
		LexiconEntry.gotoAdd();

		AssertElementNotPresent(locator1 = "TextInput#REQUIRED_ALERT");

		Button.clickSave();

		AssertElementPresent(locator1 = "TextInput#REQUIRED_ALERT");

		SelectFrame(value1 = "relative=top");
	}

	@summary = "Give the roles '${roles}' permissions to view the template '${templateName}'"
	macro addRoleViewPermissions {
		WebContentTemplates.gotoTemplateEllipsisItem(
			item = "Permissions",
			templateName = "${templateName}");

		SelectFrame(locator1 = "IFrame#DIALOG");

		PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
			addOrRemoveViewPermission = "Add",
			roleList = "${roles}");

		SelectFrameTop();
	}

	@summary = "Attempt to delete the template '${templateName}'"
	macro deleteCP {
		var key_ddlTemplateName = "${templateName}";

		Click(locator1 = "Icon#VERTICAL_ELLIPSIS_BUTTON");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		if ("${webContentHasReference}" == "true") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "The template cannot be deleted because it is required by one or more template links.");
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	@summary = "Click on '${templateName}' ellipsis > '${item}'"
	macro gotoTemplateEllipsisItem {
		LexiconCard.clickEllipsisItem(
			card = "${templateName}",
			item = "${item}");
	}

	@summary = "Type '${templateScript}' into the template ACE editor field"
	macro inputScript {
		if (isSet(templateScript)) {
			Type.typeAceEditor(
				locator1 = "DDMEditTemplate#SCRIPT_CONTENT_FIELD",
				value1 = "${templateScript}");
		}
	}

	@summary = "Input most basic fields inside a template"
	macro inputTemplateAddScreenFields {
		if (isSet(templateName)) {
			PortletEntry.inputName(name = "${templateName}");
		}

		if (isSet(structureName)) {
			WebContentTemplates._inputStructure(structureName = "${structureName}");
		}

		WebContentTemplates._inputDescription(templateDescription = "${templateDescription}");

		if (isSet(imageSourceURL)) {
			WebContent.addFeaturedImageFromURL(imageSourceURL = "${imageSourceURL}");
		}

		WebContentTemplates.inputScript(templateScript = "${templateScript}");

		if ("${templateLanguage}" == "xsl") {
			WebContentTemplates._inputTemplateFile(templateFile = "${templateFile}");
		}
		else {
			WebContentTemplates._addFields(templateFieldNames = "${templateFieldNames}");

			WebContentTemplates._inputTemplateFile(templateFile = "${templateFile}");
		}
	}

	@summary = "Add localization for the template name"
	macro localizeTemplateName {
		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		var key_ddlTemplateName = "${templateName}";

		WebContentNavigator.gotoViewTemplate(templateName = "${templateName}");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "${templateName}");

		PortletEntry.changeLocale(locale = "${locale}");

		Type.clickAtType(
			locator1 = "TextInput#NAME",
			value1 = "${localization1Name}");

		if (isSet(locale2)) {
			PortletEntry.changeLocale(locale = "${locale2}");

			Type.clickAtType(
				locator1 = "TextInput#NAME",
				value1 = "${localization2Name}");
		}
	}

	@summary = "Modify template"
	macro modifyTemplate {
		WebContentTemplates.gotoTemplateEllipsisItem(
			item = "Edit",
			templateName = "${templatePreviousName}");

		WebContentTemplates.inputTemplateAddScreenFields(
			imageSourceURL = "${imageSourceURL}",
			templateDescription = "${templateDescription}",
			templateFieldNames = "${templateFieldNames}",
			templateFile = "${templateFile}",
			templateLanguage = "${templateLanguage}",
			templateName = "${templateName}",
			templateScript = "${templateScript}");

		DDLDataDefinition.save(changeListEnabled = "${changeListEnabled}");
	}

	@summary = "Open Web Content Templates admin page for the site '${siteURLKey}'"
	macro openWebContentTemplatesAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_journal_web_portlet_JournalPortlet&_com_liferay_journal_web_portlet_JournalPortlet_mvcPath=%2Fview_ddm_templates.jsp");
	}

	@summary = "Teardown templates in the guest site"
	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "Web Content");

		Navigator.gotoNavItem(navItem = "Templates");

		Search.searchCP(searchTerm = "2");

		while (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			PortletEntry.selectAll();

			ClickNoError(locator1 = "Icon#DELETE");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}

		Search.searchCP(searchTerm = "Template");

		while (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			PortletEntry.selectAll();

			ClickNoError(locator1 = "Icon#DELETE");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}

		Portlet.viewEmptyMessage(message = "There are no results.");
	}

	@summary = "Assert that certain template attributes are present for the template '${templateName}'"
	macro viewCP {
		var key_ddlStructureName = "${structureName}";
		var key_ddlTemplateDescription = "${templateDescription}";
		var key_ddlTemplateName = "${templateName}";

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME",
			value1 = "${templateName}");

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_DESCRIPTION",
			value1 = "${templateDescription}");

		if (isSet(structureName)) {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_MODE",
				value1 = "${structureName}");
		}

		if ("${templateLanguage}" == "vm") {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_LANGUAGE",
				value1 = "Velocity");
		}
		else if ("${templateLanguage}" == "xsl") {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_LANGUAGE",
				value1 = "Extensible Stylesheet Language");
		}
		else {
			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_LANGUAGE",
				value1 = "FreeMarker");
		}

		if ("${userEditPermission}" != "noEditPermission") {
			WebContentNavigator.gotoViewTemplate(templateName = "${templateName}");

			AssertTextEquals(
				locator1 = "TextInput#NAME",
				value1 = "${templateName}");

			AssertTextEquals(
				locator1 = "TextArea#DESCRIPTION",
				value1 = "${templateDescription}");

			if (isSet(structureName)) {
				AssertTextEquals.assertValue(
					locator1 = "DDMEditTemplate#DETAILS_STRUCTURE",
					value1 = "${structureName}");
			}

			if ("${templateLanguage}" == "vm") {
				AssertTextEquals.assertPartialText(
					locator1 = "DDMEditTemplate#DETAILS_LANGUAGE",
					value1 = "Velocity (.vm)");
			}
			else if ("${templateLanguage}" == "xsl") {
				AssertTextEquals.assertPartialText(
					locator1 = "DDMEditTemplate#DETAILS_LANGUAGE",
					value1 = "Extensible Stylesheet Language (.xsl)");
			}
			else {
				AssertTextEquals.assertPartialText(
					locator1 = "DDMEditTemplate#DETAILS_LANGUAGE",
					value1 = "FreeMarker (.ftl)");
			}

			if (isSet(templateScript)) {
				AssertTextEquals.assertPartialTextAceEditor(
					locator1 = "DDMEditTemplate#SCRIPT_CONTENT_FIELD",
					value1 = "${templateScript}");
			}
		}
	}

	@summary = "Assert that the table entry for template '${templateName}' is present"
	macro viewTableEntryCP {
		var key_ddlTemplateName = "${templateName}";

		AssertTextEquals(
			locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_NAME",
			value1 = "${templateName}");

		if (isSet(templateSmallImage)) {
			AssertVisible(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_DESCRIPTION_SMALL_IMAGE",
				value1 = "${templateSmallImage}");
		}
		else {
			var key_ddlTemplateDescription = "${templateDescription}";

			AssertTextEquals(
				locator1 = "DDMSelectTemplate#TEMPLATE_TABLE_DESCRIPTION",
				value1 = "${templateDescription}");
		}
	}

}