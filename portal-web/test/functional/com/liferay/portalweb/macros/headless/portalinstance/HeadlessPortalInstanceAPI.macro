definition {

	@summary = "Get all current companies in a JSON format"
	macro _getPortalInstances {
		var portalURL = JSONCompany.getDefaultPortalURL();

		if ("${authenticationMethod}" == "By Screen Name") {
			var userName = "test";
		}
		else {
			var userName = "test@liferay.com";
		}

		var curl = '''
			${portalURL}/o/headless-portal-instances/v1.0/portal-instances \
			-u ${userName}:test \
			-H Content-Type: application/json
		''';

		var JSON = JSONCurlUtil.get("${curl}");

		return "${JSON}";
	}

	@summary = "Creates a new Portal Instance with domain = mailDomain, virtualHost = virtualHost, and portalInstanceId = webId"
	macro addPortalInstance {
		Variables.assertDefined(parameterList = "${domain},${virtualHost},${portalInstanceId}");

		var portalURL = JSONCompany.getDefaultPortalURL();

		var parameters = JSONUtil2._addJSONObject(
			JSON = "{}",
			key = "domain",
			value = ""${domain}"");

		var parameters = JSONUtil2._addJSONObject(
			JSON = "${parameters}",
			key = "virtualHost",
			value = ""${virtualHost}"");

		var parameters = JSONUtil2._addJSONObject(
			JSON = "${parameters}",
			key = "portalInstanceId",
			value = ""${portalInstanceId}"");

		if ("${authenticationMethod}" == "By Screen Name") {
			var userName = "test";
		}
		else {
			var userName = "test@liferay.com";
		}

		var curl = '''
			${portalURL}/o/headless-portal-instances/v1.0/portal-instances \
			-u ${userName}:test \
			-d ${parameters} \
			-H Content-Type: application/json
		''';

		JSONCurlUtil.post("${curl}");
	}

	@summary = "Creates a new Portal Instance with domain = mailDomain, virtualHost = virtualHost, portalInstanceId = webId, and siteInitializerKey = virtualInstanceInitializer"
	macro addPortalInstanceWithVirtualInstanceInitializer {
		Variables.assertDefined(parameterList = "${domain},${virtualHost},${portalInstanceId},${siteInitializerKey}");

		var portalURL = JSONCompany.getPortalURL();

		var parameters = JSONUtil2._addJSONObject(
			JSON = "{}",
			key = "domain",
			value = ""${domain}"");

		var parameters = JSONUtil2._addJSONObject(
			JSON = "${parameters}",
			key = "virtualHost",
			value = ""${virtualHost}"");

		var parameters = JSONUtil2._addJSONObject(
			JSON = "${parameters}",
			key = "portalInstanceId",
			value = ""${portalInstanceId}"");

		var parameters = JSONUtil2._addJSONObject(
			JSON = "${parameters}",
			key = "siteInitializerKey",
			value = ""${siteInitializerKey}"");

		if ("${authenticationMethod}" == "By Screen Name") {
			var userName = "test";
		}
		else {
			var userName = "test@liferay.com";
		}

		var curl = '''
			${portalURL}/o/headless-portal-instances/v1.0/portal-instances \
			-u ${userName}:test \
			-d ${parameters} \
			-H Content-Type: application/json
		''';

		JSONCurlUtil.post("${curl}");
	}

	@summary = "Clean up all Portal Instances that are not the default using Headless"
	macro cleanAllPortalInstances {
		var portalInstanceJSON = HeadlessPortalInstanceAPI._getPortalInstances(authenticationMethod = "${authenticationMethod}");

		var portalInstanceIds = JSONUtil.getWithJSONPath("${portalInstanceJSON}","$.items..portalInstanceId");

		for (var portalInstanceId : list "${portalInstanceIds}") {
			if ("${portalInstanceId}" != "liferay.com") {
				HeadlessPortalInstanceAPI.deletePortalInstance(
					authenticationMethod = "${authenticationMethod}",
					portalInstanceId = "${portalInstanceId}");
			}
		}
	}

	@summary = "Deletes a Portal Instance with portalInstanceId resembling webId"
	macro deletePortalInstance {
		Variables.assertDefined(parameterList = "${portalInstanceId}");

		var portalURL = JSONCompany.getDefaultPortalURL();

		var portalInstanceId = StringUtil.replace("${portalInstanceId}", " ", "%20");

		if ("${authenticationMethod}" == "By Screen Name") {
			var userName = "test";
		}
		else {
			var userName = "test@liferay.com";
		}

		var curl = '''
			${portalURL}/o/headless-portal-instances/v1.0/portal-instances/${portalInstanceId} \
			-u ${userName}:test \
			-H Content-Type: application/json
		''';

		JSONCurlUtil.delete("${curl}");
	}

}