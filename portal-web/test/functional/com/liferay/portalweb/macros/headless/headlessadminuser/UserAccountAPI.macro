definition {

	macro _curlUserAccount {
		var portalURL = JSONCompany.getPortalURL();

		if (isSet(userAccountId)) {
			var api = "user-accounts/${userAccountId}";
		}
		else {
			var api = "user-accounts";
		}

		var curl = '''
			${portalURL}/o/headless-admin-user/v1.0/${api} \
			-H accept: application/json \
			-H Content-Type: application/json \
			-u test@liferay.com:test \
		''';

		return "${curl}";
	}

	macro createUserAccount {
		Variables.assertDefined(parameterList = "${alternateName},${emailAddress},${familyName},${givenName}");

		var curl = UserAccountAPI._curlUserAccount();
		var body = '''"alternateName": "${alternateName}","emailAddress": "${emailAddress}","familyName": "${familyName}","givenName": "${givenName}"''';

		if (isSet(fieldName) && isSet(fieldValue)) {
			var body = StringUtil.add("${body},", "\"${fieldName}\":\"${fieldValue}\"", "");
		}

		var curl = StringUtil.add("${curl}", "-d {${body}}", " ");

		var response = JSONCurlUtil.post("${curl}");

		return "${response}";
	}

	macro deleteUserAccount {
		var curl = UserAccountAPI._curlUserAccount(userAccountId = "${staticUserAccountId}");

		JSONCurlUtil.delete("${curl}");
	}

	macro getUpdatedFieldOfUserAccountWithPatchRequest {
		Variables.assertDefined(parameterList = "${fieldName},${updatedFieldValue}");

		if (!(isSet(userAccountId))) {
			var userAccountId = "${staticUserAccountId}";
		}

		var curl = UserAccountAPI._curlUserAccount(userAccountId = "${userAccountId}");
		var body = '''-d {"${fieldName}":"${updatedFieldValue}"}''';

		var curl = StringUtil.add("${curl}", "${body}", "");

		var valueOfField = JSONCurlUtil.patch("${curl}", "$.${fieldName}");

		return "${valueOfField}";
	}

	macro getUpdatedFieldOfUserAccountWithPutRequest {
		Variables.assertDefined(parameterList = "${alternateName},${emailAddress},${familyName},${givenName},${fieldName},${updatedFieldValue}");

		if (!(isSet(userAccountId))) {
			var userAccountId = "${staticUserAccountId}";
		}

		var curl = UserAccountAPI._curlUserAccount(userAccountId = "${userAccountId}");
		var body = '''
			-d {
				"alternateName": "${alternateName}",
				"emailAddress": "${emailAddress}",
				"familyName": "${familyName}",
				"givenName": "${givenName}",
				"${fieldName}":"${updatedFieldValue}"
			}
		''';

		var curl = StringUtil.add("${curl}", "${body}", "");

		var valueOfField = JSONCurlUtil.put("${curl}", "$.${fieldName}");

		return "${valueOfField}";
	}

	macro setUpGlobalUserAccountId {
		var response = UserAccountAPI.createUserAccount(
			alternateName = "${alternateName}",
			emailAddress = "${emailAddress}",
			familyName = "${familyName}",
			fieldName = "${fieldName}",
			fieldValue = "${fieldValue}",
			givenName = "${givenName}");

		static var staticUserAccountId = JSONUtil.getWithJSONPath("${response}", "$.id");

		return "${response}";
	}

}