definition {

	macro _addOrganization {
		if (isSet(organizationName)) {
			var organizationNameEncoded = URLUtil.encodeUTF8("${organizationName}");
		}
		else {
			fail("Please set 'organizationName'.");
		}

		if (!(isSet(organizationSite))) {
			var organizationSite = "false";
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/organization/add-organization \
				-u test@liferay.com:test \
				-d parentOrganizationId=0 \
				-d name=${organizationNameEncoded} \
				-d type=organization \
				-d regionId=0 \
				-d countryId=0 \
				-d statusId=12017 \
				-d comments= \
				-d site=${organizationSite}
		''';

		echo("## Adding a 'Organization' with the following info using jsonws:");

		echo("## * Organization Name: ${organizationName}");

		echo("## * Organization Site: ${organizationSite}");

		var organizationId = JSONCurlUtil.post("${curl}", "$['organizationId']");
	}

	macro _assignUserToOrganization {
		if (!(isSet(organizationId))) {
			fail("Please set 'organizationId'.");
		}

		if (!(isSet(userId))) {
			fail("Please set 'userId'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/user/add-organization-users \
				-u test@liferay.com:test \
				-d organizationId=${organizationId} \
				-d userIds=${userId}
		''';

		var organizationName = JSONOrganization.getOrganizationName(organizationId = "${organizationId}");

		var userEmailAddress = JSONUser.getUserEmailAddress(userId = "${userId}");

		echo("## Assigning a 'User' to an 'Organization' with the following info using JSONWS:");

		echo("## * Organization Name: ${organizationName}");

		echo("## * User Email Address: ${userEmailAddress}");

		var temp = JSONCurlUtil.post("${curl}", "$");
	}

	macro _deleteOrganization {
		if (!(isSet(organizationId))) {
			fail("Please set 'organizationId'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/organization/delete-organization \
				-u test@liferay.com:test \
				-d organizationId=${organizationId}
		''';

		var organizationName = JSONOrganization.getOrganizationName(organizationId = "${organizationId}");

		echo("## Deleting an 'Organization' with the following info using JSONWS:");

		echo("## * Organization Name: ${organizationName}");

		var organizationId = JSONCurlUtil.post("${curl}", "$['organizationId']");
	}

	macro addOrganization {
		if (!(isSet(organizationName))) {
			fail("Please set 'organizationName'.");
		}

		JSONOrganization._addOrganization(
			organizationName = "${organizationName}",
			organizationSite = "${organizationSite}"
		);
	}

	macro assignUserToOrganization {
		if (!(isSet(organizationName))) {
			fail("Please set 'organizationName'.");
		}

		if (!(isSet(userEmailAddress))) {
			fail("Please set 'userEmailAddress'.");
		}

		var organizationId = JSONOrganization.getOrganizationIdByName(
			organizationName = "${organizationName}"
		);

		var userId = JSONUser.getUserIdByEmailAddress(userEmailAddress = "${userEmailAddress}");

		JSONOrganization._assignUserToOrganization(
			organizationId = "${organizationId}",
			userId = "${userId}"
		);
	}

	macro deleteOrganizationByName {
		if (!(isSet(organizationName))) {
			fail("Please set 'organizationName'.");
		}

		var organizationId = JSONOrganization.getOrganizationIdByName(
			organizationName = "${organizationName}"
		);

		JSONOrganization._deleteOrganization(organizationId = "${organizationId}");
	}

	macro getOrganizationIdByName {
		if (!(isSet(organizationName))) {
			fail("Please set 'organizationName'.");
		}

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/organization/get-organizations/company-id/${companyId}/parent-organization-id/0 \
				-u test@liferay.com:test
		''';
		var organizationId = JSONCurlUtil.get("${curl}", "$.[?(@['name'] == '${organizationName}')]['organizationId']");
		var organizationId = StringUtil.replace("${organizationId}", "[&quot;", "");
		var organizationId = StringUtil.replace("${organizationId}", "&quot;]", "");

		return "${organizationId}";
	}

	macro getOrganizationName {
		if (!(isSet(organizationId))) {
			fail("Please set 'organizationId'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/organization/get-organization/organization-id/${organizationId} \
				-u test@liferay.com:test
		''';
		var organizationName = JSONCurlUtil.get("${curl}", "$['name']");

		return "${organizationName}";
	}
}