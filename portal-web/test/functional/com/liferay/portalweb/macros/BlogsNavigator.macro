definition {
	macro gotoAddEntry {
		if (IsElementPresent(locator1 = "Blogs#ADD_BLOGS_ENTRY")) {
			Click(locator1 = "Blogs#ADD_BLOGS_ENTRY");
		}
		else {
			var key_text = "New Entry";

			AssertClick(locator1 = "Button#ANY", value1 = "New Entry");
		}

		AssertVisible(locator1 = "BlogsEntry#ENTRY_COVER_IMAGE_SELECT_FILE");
	}

	macro gotoBlogsEntriesPermissionsCP {
		LexiconEntry.gotoHeaderMenuItem(menuItem = "Entries Permissions");

		SelectFrame(locator1 = "IFrame#DIALOG");
	}

	macro gotoBlogsEntryPermissionsCP {
		LexiconList.clickEllipsisItem(
			item = "Permissions",
			listEntry = "${entryTitle}"
		);

		SelectFrame(locator1 = "IFrame#DIALOG");
	}

	macro gotoBlogsEntryPermissionsPG {
		Portlet.mouseOverPortletTitle(portletTitleName = "Blogs");

		Blogs.clickEllipsisItemPG(
			entryTitle = "${entryTitle}",
			item = "Permissions"
		);

		SelectFrame(locator1 = "IFrame#DIALOG");
	}

	macro gotoBrowseImage {
		Click(locator1 = "Button#BROWSE_IMAGE");
	}

	macro gotoConfigurationScopePG {
		Navigator.gotoNavItem(navItem = "Scope");
	}

	macro gotoConfigurationSetupPG {
		Portlet.gotoPortletOptions(portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#DIALOG");
	}

	macro gotoEditCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_entryTitle = "${entryTitle}";

		Blogs.viewEntryTitle(entryTitle = "${entryTitle}");

		Click(
			locator1 = "LexiconList#LIST_ENTRY_ELLIPSIS",
			key_listEntry = "${entryTitle}"
		);

		MenuItem.click(menuItem = "Edit");

		AssertElementPresent(locator1 = "Icon#BACK");

		AssertVisible(locator1 = "AlloyEditor#EDITOR", key_editor = "title");
	}

	macro gotoEditPG {
		var key_entryTitle = "${entryTitle}";

		AssertTextEquals(locator1 = "BlogsEntry#TITLE", value1 = "${entryTitle}");

		if (IsElementPresent(locator1 = "Button#EDIT_PENCIL")) {
			Click(locator1 = "Button#EDIT_PENCIL");
		}
		else {
			Portlet.mouseOverPortletTitle(portletTitleName = "Blogs");

			Blogs.clickEllipsisItemPG(
				entryTitle = "${entryTitle}",
				item = "Edit"
			);

		}

		AssertVisible(locator1 = "BlogsEntry#ENTRY_COVER_IMAGE_SELECT_FILE");
	}

	macro gotoEntryCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_entryTitle = "${entryTitle}";

		if (isSet(entryStatus)) {
			AssertTextEquals(
				locator1 = "Blogs#DESCRIPTIVE_VIEW_ENTRY_STATUS",
				value1 = "${entryStatus}"
			);
		}

		LexiconList.clickListEntryTitle(
			listEntry = "${entryTitle}"
		);
	}

	macro gotoEntryPG {
		var key_entryContent = "${entryContent}";
		var key_entryTitle = "${entryTitle}";

		AssertTextEquals(locator1 = "BlogsEntry#TITLE", value1 = "${entryTitle}");

		AssertTextEquals(locator1 = "BlogsEntry#CONTENT", value1 = "${entryContent}");

		AssertClick.assertTextClickAt(
			locator1 = "BlogsEntry#TITLE_LINK",
			value1 = "${entryTitle}"
		);
	}

	macro gotoEntryWithAbstractPG {
		var key_entryAbstractDescription = "${entryAbstractDescription}";
		var key_entryTitle = "${entryTitle}";

		AssertTextEquals(locator1 = "BlogsEntry#TITLE", value1 = "${entryTitle}");

		AssertTextEquals.assertPartialText(
			locator1 = "BlogsEntry#CONTENT_ABSTRACT",
			value1 = "${entryAbstractDescription}"
		);

		AssertClick(locator1 = "BlogsEntry#TITLE_LINK", value1 = "${entryTitle}");
	}

	macro gotoPG {
		var key_entryTitle = "${entryTitle}";

		if (isSet(blogEntryURL)) {
			AssertLocation(value1 = "${blogEntryURL}");
		}
		else {
			Click(locator1 = "BlogsEntry#TITLE_LINK", value1 = "${entryTitle}");
		}

		AssertTextEquals(locator1 = "BlogsEntry#TITLE", value1 = "${entryTitle}");
	}

	macro gotoSelectFile {
		AssertClick(
			locator1 = "BlogsEntry#ENTRY_COVER_IMAGE_SELECT_FILE",
			value1 = "Select File"
		);
	}

	macro moveToRecycleBinCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_entryTitle = "${entryTitle}";

		Blogs.viewEntryTitle(entryTitle = "${entryTitle}");

		Click(
			locator1 = "LexiconList#LIST_ENTRY_ELLIPSIS",
			key_listEntry = "${entryTitle}"
		);

		ClickNoError(
			locator1 = "MenuItem#DROPDOWN_MENU_ITEM",
			key_menuItem = "Move to Recycle Bin"
		);

		AssertConfirm(
			value1 = "Are you sure you want to move this to the Recycle Bin?"
		);

		AssertElementPresent(locator1 = "Message#SUCCESS");
	}
}