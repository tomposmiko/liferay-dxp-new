definition {

	macro addServiceAccessPolicy {
		LexiconEntry.gotoAdd();

		FormFields.editText(
			fieldName = "name",
			fieldValue = "${policyName}");

		FormFields.editText(
			fieldName = "title",
			fieldValue = "${title}");

		if (isSet(serviceClass)) {
			FormFields.editText(
				fieldName = "serviceClass",
				fieldValue = "${serviceClass}");
		}

		if (isSet(methodName)) {
			FormFields.editText(
				fieldName = "MethodName",
				fieldValue = "${methodName}");
		}

		if ("${enabled}" == "true") {
			FormFields.enableCheckbox(fieldName = "enabled");
		}

		PortletEntry.save();
	}

	macro editServiceAccessPolicy {
		ServiceAccessPolicy.gotoServiceAccessPolicy(policyName = "${policyName}");

		if (isSet(allowedServiceSignature)) {
			Click(
				locator1 = "ServiceAccessPolicy#SWITCH_TO_ADVANCED_MODE",
				value1 = "Switch to Advanced Mode");

			Type(
				locator1 = "ServiceAccessPolicy#ALLOWED_SERVICE_SIGNATURES_FIELD",
				value1 = "${allowedServiceSignature}");
		}

		if ("${enabled}" == "true") {
			FormFields.enableCheckbox(fieldName = "enabled");
		}
		else if ("${enabled}" == "false") {
			FormFields.disableCheckbox(fieldName = "enabled");
		}

		if (isSet(newName)) {
			FormFields.editText(
				fieldName = "name",
				fieldValue = "${newName}");
		}

		if (isSet(title)) {
			FormFields.editText(
				fieldName = "title",
				fieldValue = "${title}");
		}

		if (isSet(serviceClass)) {
			FormFields.editText(
				fieldName = "serviceClass",
				fieldValue = "${serviceClass}");
		}

		if (isSet(methodName)) {
			FormFields.editText(
				fieldName = "MethodName",
				fieldValue = "${methodName}");
		}

		PortletEntry.save();
	}

	macro gotoServiceAccessPolicy {
		AssertClick(
			key_policyName = "${policyName}",
			locator1 = "ServiceAccessPolicy#POLICY_NAME",
			value1 = "${policyName}");
	}

}