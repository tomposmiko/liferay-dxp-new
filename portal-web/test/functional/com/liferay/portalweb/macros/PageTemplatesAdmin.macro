definition {
	macro _gotoAddCollection {
		if (IsElementPresent(locator1 = "NavNested#NAV_NESTED_PLUS")) {
			Click(locator1 = "NavNested#NAV_NESTED_PLUS");
		}
		else {
			Button.click(button = "New");
		}
	}

	macro addCollection {
		PageTemplatesAdmin._gotoAddCollection();

		Collection.editCollection(
			collectionDescription = "${collectionDescription}",
			collectionName = "${collectionName}"
		);

		Button.clickSave();
	}

	macro addPageTemplate {
		if (isSet(pageTemplateType)) {
			var menuItem = "${pageTemplateType}";
		}
		else {
			var menuItem = "Content Page Template";
		}

		if ("${siteTemplate}" == "true") {
			LexiconEntry.gotoAdd();
		}
		else {
			LexiconEntry.gotoAddMenuItem(menuItem = "${menuItem}");
		}

		PortletEntry.inputName(name = "${pageTemplateName}");

		Button.clickSave();
	}

	macro configureWidgetPageTemplateLayout {
		Click.pauseClickAt(locator1 = "ControlMenu#EDIT_COG");

		Click(
			locator1 = "SitePagesEditPage#LAYOUT_CARD",
			key_columnName = "${columnName}"
		);

		PortletEntry.save();
	}

	macro deletePageTemplate {
		LexiconCard.delete(
			card = "${pageTemplateName}"
		);
	}

	macro gotoPageTemplateEllipsisItem {
		LexiconCard.clickEllipsisItem(
			card = "${pageTemplateName}",
			item = "${item}"
		);
	}

	macro openPageTemplatesAdmin {
		Navigator.openWithAppendToBaseURL(
			urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_layout_admin_web_portlet_GroupPagesPortlet&amp;_com_liferay_layout_admin_web_portlet_GroupPagesPortlet_tabs1=page-templates"
		);
	}

	macro renamePageTemplate {
		PageTemplatesAdmin.gotoPageTemplateEllipsisItem(
			item = "Rename",
			pageTemplateName = "${pageTemplateName}"
		);

		PortletEntry.inputName(name = "${newPageTemplateName}");

		Modal.save();
	}

	macro viewPageTemplateNotPresent {
		LexiconCard.viewCardNotPresent(
			card = "${pageTemplateName}"
		);
	}

	macro viewPageTemplatePresent {
		LexiconCard.viewCardPresent(
			card = "${pageTemplateName}"
		);
	}
}