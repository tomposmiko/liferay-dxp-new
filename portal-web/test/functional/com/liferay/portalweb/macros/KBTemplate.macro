definition {

	macro addCP {
		if (isSet(addByCenterButton)) {
			Button.click(button = "New");
		}
		else {
			Navigator.gotoNavItem(navItem = "Templates");

			LexiconEntry.gotoAdd();
		}

		PortletEntry.inputTitle(title = "${kbTemplateTitle}");

		CKEditor.addContent(content = "${kbTemplateContent}");

		PortletEntry.publish();
	}

	macro gotoCP {
		Navigator.gotoNavItem(navItem = "Templates");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "View",
			rowEntry = "${kbTemplateTitle}");
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Knowledge Base");

		LexiconEntry.gotoHeaderMenuItem(menuItem = "Templates");

		if (IsElementPresent(locator1 = "ContentRow#ENTRY_CONTENT_ROW_1")) {
			PortletEntry.selectAll();

			AssertClickNoError(
				locator1 = "Button#DELETE",
				value1 = "Delete");

			AssertConfirm(value1 = "Are you sure you want to delete the selected templates?");

			Alert.viewSuccessMessage();
		}
	}

	macro viewCP {
		KBTemplate.gotoCP(kbTemplateTitle = "${kbTemplateTitle}");

		AssertTextEquals(
			locator1 = "KnowledgeBaseTemplate#TEMPLATE_TITLE",
			value1 = "${kbTemplateTitle}");

		AssertTextEquals.assertPartialText(
			locator1 = "KnowledgeBaseTemplate#TEMPLATE_CONTENT",
			value1 = "${kbTemplateContent}");
	}

	macro viewCPViaAddTemplate {
		LexiconEntry.gotoAddMenuItem(menuItem = "${kbTemplateTitle}");

		AssertTextEquals(
			key_editor = "title",
			locator1 = "TextInput#TITLE",
			value1 = "${kbTemplateTitle}");

		SelectFrame.selectFrameNoLoading(locator1 = "CKEditor#BODY_FIELD_IFRAME");

		AssertTextEquals(
			key_editor = "content",
			locator1 = "CKEditor#BODY",
			value1 = "${kbTemplateContent}");
	}

	macro viewNoTemplates {
		AssertTextEquals.assertPartialText(
			locator1 = "Message#EMPTY_INFO_TAGLIB_TITLE",
			value1 = "There are no article templates.");

		AssertTextEquals.assertPartialText(
			locator1 = "Message#EMPTY_STATE_INFO",
			value1 = "There are no article templates.");
	}

	macro viewTemplatesThroughPageTreePanel {
		for (var kbTemplateTitle : list "${kbTemplatesList}") {
			AssertVisible(
				key_itemLinkName = "${kbTemplateTitle}",
				locator1 = "KnowledgeBase#PAGE_TREE_ITEM_LINK");
		}
	}

}