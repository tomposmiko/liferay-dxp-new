definition {
	macro addCP {
		var key_resourceName = "${resourceName}";

		AssertClick(
			locator1 = "CustomFields#RESOURCE_TABLE_RESOURCE_LINK",
			value1 = "${resourceName}"
		);

		LexiconEntry.gotoAdd();

		var key_customFieldType = "${customFieldType}";

		Click(locator1 = "CustomFields#CUSTOM_FIELD_TYPE");

		var customFieldKey = StringUtil.lowerCase("${customFieldName}");

		PortletEntry.inputName(name = "${customFieldKey}");

		if (isSet(startingValue)) {
			if ("${customFieldType}" == "Text Field") {
				PortletEntry.inputText(
					fieldLabel = "Starting Value",
					text = "${startingValue}"
				);
			}
		}

		if (("${customFieldType}" == "Dropdown") || ("${customFieldType}" == "Checkbox") || ("${customFieldType}" == "Radio")) {
			PortletEntry.inputTextArea(fieldId = "Value", text = "${customFieldType}");
		}

		if ("${localization}" == "true") {
			Check(locator1 = "Checkbox#ANY_CHECKBOX", checkboxName = "Make Field Localizable");
		}

		if ("${searchable}" == "true") {
			Check.checkToggleSwitch(
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH",
				key_toggleSwitchLabel = "Searchable"
			);
		}

		Button.clickSave();

		var key_customFieldName = "${customFieldName}";

		AssertTextEquals(
			locator1 = "CustomFieldsEditResource#CUSTOM_FIELDS_TABLE_NAME",
			value1 = "${customFieldName}"
		);
	}

	macro editCP {
		var key_resourceName = "${resourceName}";

		AssertClick(
			locator1 = "CustomFields#RESOURCE_TABLE_RESOURCE_LINK",
			value1 = "${resourceName}"
		);

		var key_customFieldName = "${customFieldName}";

		AssertVisible(locator1 = "CustomFieldsEditResource#CUSTOM_FIELDS_TABLE_NAME");

		Pause(locator1 = "3000");

		LexiconEntry.openEntryMenu(rowEntry = "${customFieldName}");

		MenuItem.click(menuItem = "Edit");

		if (isSet(startingValue)) {
			if ("${customFieldType}" == "Text Field") {
				PortletEntry.inputText(
					fieldLabel = "Starting Value",
					text = "${startingValue}"
				);
			}
		}

		PortletEntry.save();
	}

	macro tearDownCP {
		for (var resourceName : list "Blogs Entry,Calendar Event,Document,Documents Folder,Knowledge Base Article,Knowledge Base Folder,Message Boards Category,Message Boards Message,Organization,Page,Role,Site,User,User Group,Web Content Article,Web Content Folder,Wiki Page") {
			ProductMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "Custom Fields"
			);

			AssertClick(
				locator1 = "CustomFields#RESOURCE_TABLE_RESOURCE_LINK",
				value1 = "${resourceName}",
				key_resourceName = "${resourceName}"
			);

			if (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
				PortletEntry.selectAll();

				Click(locator1 = "Icon#DELETE");
			}

			AssertElementPresent(locator1 = "Message#EMPTY_INFO");
		}
	}
}