definition {

	macro addNewChangeList {
		Click(locator1 = "Button#PLUS");

		Type(
			locator1 = "ChangeList#CHANGE_LIST_NAME",
			value1 = "${changeListName}");

		Type(
			locator1 = "ChangeList#CHANGE_LIST_DESCRIPTION",
			value1 = "${changeListDescription}");

		Button.clickSave();
	}

	macro cancelChangeListCreation {
		Click(locator1 = "Button#PLUS");

		Button.clickCancel();

		AssertTextEquals(
			locator1 = "ChangeList#OVERVIEW_PAGE_TITLE",
			value1 = "Select Change List");
	}

	macro changeUserSettingsPopuConfirmation {
		Click(locator1 = "ChangeList#CHANGE_LIST_NAVIGATION_USER_SETTINGS");

		if ("${changeUserPopupConfirmation}" == "enable") {
			Check.checkToggleSwitch(locator1 = "ChangeList#CHANGE_LIST_USER_SETTINGS_POPUP_ENABLED");
		}
		else {
			Uncheck.uncheckToggleSwitch(locator1 = "ChangeList#CHANGE_LIST_USER_SETTINGS_POPUP_ENABLED");
		}

		Click(locator1 = "Button#SAVE");

		Alert.viewSuccessMessage();

		Click(locator1 = "ChangeList#CHANGE_LIST_NAVIGATION_GLOBAL_SETTINGS");
	}

	macro confirmSwitchingChangeList {
		AssertConfirm.assertPartialConfirmation(value1 = "Do you want to switch to ${changeListName} change list?");
	}

	macro deleteChangeListFromOverviewScreen {
		ClickNoError(locator1 = "Icon#DELETE");

		AssertConfirm(value1 = "Are you sure you want to delete '${changeListName}' change list?");

		AssertTextEquals(
			locator1 = "ChangeList#OVERVIEW_PAGE_TITLE",
			value1 = "Select Change List");

		Alert.viewSuccessMessageText(successMessage = "Production has been checked out. All your modifications will be added to the live site immediately.");
	}

	macro disableChangeList {
		Uncheck.uncheckToggleSwitch(locator1 = "ChangeListSettings#CHANGE_LISTS_TOGGLE_SWITCH");

		Click(locator1 = "Button#SAVE");

		Alert.viewSuccessMessage();
	}

	macro enableChangeList {
		Pause(locator1 = "2000");

		Check.checkToggleSwitch(locator1 = "ChangeListSettings#CHANGE_LISTS_TOGGLE_SWITCH");

		if (isSet(gotoOverview)) {
			Click(locator1 = "ChangeListSettings#SAVE_AND_GO_TO_OVERVIEW_BUTTON");
		}
		else {
			Click(locator1 = "Button#SAVE");
		}

		Alert.viewSuccessMessage();
	}

	macro gotoActivate {
		Click.waitForMenuToggleJSClick(
			key_rowEntry = "${rowEntry}",
			locator1 = "Icon#TABLE_VERTICAL_ELLIPSIS");

		MenuItem.clickNoError(menuItem = "Activate");
	}

	macro gotoDelete {
		Click.waitForMenuToggleJSClick(
			key_rowEntry = "${rowEntry}",
			locator1 = "Icon#TABLE_VERTICAL_ELLIPSIS");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete '${rowEntry}' change list?");
	}

	macro publishChangeList {
		Click(locator1 = "Button#PUBLISH");

		SelectFrame.selectFrameTop();

		Click(locator1 = "ChangeList#PUBLISH_BUTTON");

		Alert.viewSuccessMessage();

		AssertTextEquals(
			locator1 = "ChangeList#OVERVIEW_PAGE_TITLE",
			value1 = "History");

		Pause(locator1 = "5000");

		Refresh();

		var key_changeListName = "${changeListName}";

		AssertTextEquals(
			locator1 = "ChangeList#HISTORY_CHANGE_LIST_STATUS",
			value1 = "Published");
	}

	macro searchForChangeList {
		Search.searchCP(searchTerm = "${changeListName}");

		var key_changeListName = "${changeListName}";

		AssertTextPresent(
			locator1 = "ChangeList#CHANGE_LIST_TABLE_ENTRY_NAME",
			value1 = "${changeListName}");
	}

	macro selectChangeList {
		Click(locator1 = "ChangeList#SELECT_CHANGE_LIST_DROPDOWN");

		var key_changeListName = "${changeListName}";

		if ("${changeListName}" == "Select Change List") {
			Click(locator1 = "ChangeList#SELECT_CHANGE_LIST");

			AssertTextEquals(
				locator1 = "ChangeList#OVERVIEW_PAGE_TITLE",
				value1 = "Select Change List");
		}
		else {
			ClickNoError(
				key_changeListName = "${changeListName}",
				locator1 = "ChangeList#SELECT_CHANGE_LIST_VALUE");

			if ( "${changeListConfirmEnabled}" == "true") {
				ChangeList.confirmSwitchingChangeList(changeListName = "${changeListName}");
			}

			AssertTextEquals(
				locator1 = "ChangeList#OVERVIEW_PAGE_TITLE",
				value1 = "Overview");
		}
	}

	macro selectChangeListMenuItem {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "${menuItem}",
			rowEntry = "${rowEntry}");
	}

	macro viewAddedItemCounter {
		AssertTextEquals(
			locator1 = "ChangeList#CHANGE_LIST_ADDED",
			value1 = "${nbrOfAddedItems}");
	}

	macro viewChangeOnOverview {
		var key_contentTitle = "${contentTitle}";

		AssertTextEquals(
			locator1 = "ChangeList#LATEST_CHANGES_TITLE",
			value1 = "${contentTitle}");

		var key_contentType = "${contentType}";

		AssertTextEquals(
			locator1 = "ChangeList#LATEST_CHANGES_CONTENT_TYPE",
			value1 = "${contentType}");

		var key_contentChangeType = "${contentChangeType}";

		AssertTextEquals(
			locator1 = "ChangeList#LATEST_CHANGES_CHANGE_TYPE",
			value1 = "${contentChangeType}");
	}

	macro viewContentTypeOnOverview {
		var key_contentTitle = "${contentTitle}";

		AssertTextEquals(
			locator1 = "ChangeList#LATEST_CHANGES_CONTENT_TYPE",
			value1 = "${contentType}");
	}

	macro viewDeletedItemCounter {
		AssertTextEquals(
			locator1 = "ChangeList#CHANGE_LIST_DELETED",
			value1 = "${nbrOfDeletedItems}");
	}

	macro viewModifiedItemCounter {
		AssertTextEquals(
			locator1 = "ChangeList#CHANGE_LIST_MODIFIED",
			value1 = "${nbrOfModifiedItems}");
	}

	macro viewNoChangeListInSelectDropdown {
		Click(locator1 = "ChangeList#SELECT_CHANGE_LIST_DROPDOWN");

		AssertElementNotPresent(
			key_changeListName = "${changeListName}",
			locator1 = "ChangeList#SELECT_CHANGE_LIST_VALUE");
	}

	macro viewNoStagingMenuitemsNotAvailable {
		AssertElementNotPresent(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");

		ProductMenu.gotoPortlet(
			category = "Publishing",
			panel = "Site Administration",
			portlet = "Staging");

		Alert.viewWarningSpecific(warningMessage = "Change lists are enabled for the current virtual instance. Staging and change lists cannot be used together.");
	}

	macro viewPublishedChangeListDetails {
		AssertTextEquals(
			locator1 = "ChangeList#OVERVIEW_CHANGE_LIST_NAME",
			value1 = "${changeListName}");

		AssertTextEquals(
			locator1 = "ChangeList#OVERVIEW_DESCRIPTION",
			value1 = "${changeListDescription}");

		AssertTextPresent(
			locator1 = "ChangeList#OVERVIEW_PUBLISHED_BY",
			value1 = "${changeListPublishedBy}");
	}

	macro viewSpecificChangeListHistory {
		var key_contentTitle = "${contentTitle}";

		AssertTextEquals(
			locator1 = "ChangeList#VIEW_CHANGE_LIST_NAME_HISTORY",
			value1 = "${changeListName}");
	}

	macro viewWarningOnPortletExport {
		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "${portletName}");

		LAR._gotoExportImport();

		SelectFrame(locator1 = "IFrame#CONFIGURATION_DIALOG");

		Alert.viewWarningSpecific(warningMessage = "Change lists are enabled for the current virtual instance. Export/Import and change lists cannot be used together.");

		IFrame.closeFrame();
	}

	macro viewWarningOnPortletImport {
		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			panel = "Site Administration",
			portlet = "${portletName}");

		LAR._gotoExportImport();

		SelectFrame(locator1 = "IFrame#CONFIGURATION_DIALOG");

		NavItem.click(navItem = "Import");

		Alert.viewWarningSpecific(warningMessage = "Change lists are enabled for the current virtual instance. Export/Import and change lists cannot be used together.");

		IFrame.closeFrame();
	}

	macro viewWarningOnSiteExportImport {
		ProductMenu.gotoPortlet(
			category = "Publishing",
			panel = "Site Administration",
			portlet = "${portletName}");

		Alert.viewWarningSpecific(warningMessage = "Change lists are enabled for the current virtual instance. Export/Import and change lists cannot be used together.");
	}

	macro workOnProduction {
		if (IsTextEqual(locator1 = "ChangeList#OVERVIEW_PAGE_TITLE", value1 = "Overview")) {
			Click(locator1 = "ChangeList#SELECT_CHANGE_LIST_DROPDOWN");

			ClickNoError(
				key_changeListName = "Work on Production",
				locator1 = "ChangeList#SELECT_CHANGE_LIST_VALUE");

			if ("${changeListConfirmEnabled}" == "true") {
				ChangeList.confirmSwitchingChangeList(changeListName = "Work on Production");
			}

			AssertTextEquals(
				locator1 = "ChangeList#OVERVIEW_PAGE_TITLE",
				value1 = "Select Change List");
		}
		else {
			ClickNoError(
				key_changeListName = "Work on Production",
				locator1 = "ChangeList#CHANGE_LIST_NAME_CARD_VIEW");
			if ("${changeListConfirmEnabled}" == "true") {
				ChangeList.confirmSwitchingChangeList(changeListName = "Work on Production");
			}
		}

		Alert.viewSuccessMessageText(successMessage = "Production has been checked out. All your modifications will be added to the live site immediately.");
	}

}