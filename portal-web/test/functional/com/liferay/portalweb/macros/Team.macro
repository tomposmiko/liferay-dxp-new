definition {
	macro _editCP {
		PortletEntry.inputName(name = "${teamName}");

		PortletEntry.inputDescription(description = "${teamDescription}");
	}

	macro addCP {
		LexiconEntry.gotoAdd();

		Team._editCP(teamDescription = "${teamDescription}", teamName = "${teamName}");

		PortletEntry.save();
	}

	macro assignMembersCP {
		var key_teamName = "${teamName}";
		var key_userScreenName = "${userScreenName}";

		Search.searchCP(searchTerm = "${teamName}");

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(locator1 = "SiteTeams#TEAM_TABLE_NAME", value1 = "${teamName}");

		AssertClick(locator1 = "SiteTeams#TEAM_TABLE_NAME", value1 = "${teamName}");

		Navigator.gotoNavItem(navItem = "Users");

		LexiconEntry.gotoAdd();

		SelectFrame(locator1 = "IFrame#DIALOG");

		Search.searchCP(searchTerm = "${userScreenName}");

		if ("${userNameIsNotFound}" == "true") {
			AssertTextEquals(
				locator1 = "Message#EMPTY_INFO",
				value1 = "No users were found."
			);
		}
		else {
			LexiconEntry.changeDisplayStyle(displayStyle = "cards");

			var key_cardText = "${userScreenName}";

			AssertTextEquals(locator1 = "Card#SPECIFIC_TEXT", value1 = "${userScreenName}");

			Check(locator1 = "Card#SPECIFIC_CHECKBOX");

			Pause(locator1 = "5000");

			SelectFrameTop();

			AssertClick(locator1 = "Button#ADD", value1 = "Add");

			Alert.viewSuccessMessage();

			AssertElementPresent(locator1 = "Card#SPECIFIC_LARGE_TEXT");
		}

		SelectFrameTop();
	}

	macro assignUserGroups {
		var key_teamName = "${teamName}";

		Search.searchCP(searchTerm = "${teamName}");

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(locator1 = "SiteTeams#TEAM_TABLE_NAME", value1 = "${teamName}");

		AssertClick(locator1 = "SiteTeams#TEAM_TABLE_NAME", value1 = "${teamName}");

		Navigator.gotoNavItem(navItem = "User Groups");

		LexiconEntry.gotoAdd();

		SelectFrame(locator1 = "IFrame#DIALOG");

		var key_cardText = "${userGroupName}";

		Search.searchCP(searchTerm = "${userGroupName}");

		if ("${userGroupsNameIsNotAvailable}" == "true") {
			AssertTextNotPresent(
				locator1 = "SiteTeamsAssignMembers#USER_GROUPS_TABLE_NAME",
				value1 = "${userGroupName}"
			);
		}
		else {
			LexiconEntry.selectEntry(rowEntry = "${userGroupName}");

			SelectFrameTop();

			AssertClick(locator1 = "Button#ADD", value1 = "Add");

			Alert.viewSuccessMessage();
		}

		SelectFrameTop();
	}

	macro definePermissionPG {
		Check(
			locator1 = "//tr[contains(.,'${teamName}')]/td/input[contains(@id,'${permissionDefinitionKey}')]"
		);

		PortletEntry.save();

		AssertChecked(
			locator1 = "//tr[contains(.,'${teamName}')]/td/input[contains(@id,'${permissionDefinitionKey}')]"
		);
	}

	macro deleteCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		LexiconEntry.viewEntryName(rowEntry = "${teamName}");

		LexiconEntry.openEntryMenu(rowEntry = "${teamName}");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewSuccessMessage();
	}

	macro editCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		LexiconEntry.gotoEntryMenuItem(menuItem = "Edit", rowEntry = "${teamName}");

		Team._editCP(
			teamDescription = "${teamDescriptionEdit}",
			teamName = "${teamNameEdit}"
		);

		PortletEntry.save();
	}

	macro removeMemberCP {
		AssertClick(
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = "${teamName}",
			key_rowEntry = "${teamName}"
		);

		Navigator.gotoNavItem(navItem = "${membershipType}s");

		if ("${membershipType}" == "User") {
			Check(
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_CHECKBOX",
				key_rowEntry = "${userFirstName} ${userLastName}"
			);
		}
		else if ("${membershipType}" == "User Group") {
			Check(
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_CHECKBOX",
				key_rowEntry = "${userGroupName}"
			);
		}

		ClickNoError(locator1 = "Icon#TIMES");

		AssertConfirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewSuccessMessage();
	}

	macro removePermissionPG {
		Uncheck(
			locator1 = "//tr[contains(.,'${teamName}')]/td/input[contains(@id,'${permissionDefinitionKey}')]"
		);

		PortletEntry.save();

		AssertNotChecked(
			locator1 = "//tr[contains(.,'${teamName}')]/td/input[contains(@id,'${permissionDefinitionKey}')]"
		);
	}

	macro searchCP {
		Navigator.gotoNavItem(navItem = "${membershipType}s");

		Search.searchCP(searchTerm = "${searchTerm}");

		if ("${isEmpty}" == "true") {
			AssertElementPresent(locator1 = "Message#EMPTY_INFO");
		}
		else {
			var name = StringUtil.unquote("${searchTerm}");

			AssertTextEquals.assertPartialText(
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_1",
				value1 = "${name}"
			);
		}
	}

	macro tearDownCP {
		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		ProductMenu.gotoSite(site = "${siteName}");

		ProductMenu.gotoPortlet(
			category = "Members",
			panel = "Site Administration",
			portlet = "Site Teams"
		);

		if (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			PortletEntry.selectAll();

			ClickNoError(locator1 = "Icon#DELETE");

			AssertConfirm(
				value1 = "Are you sure you want to delete this? It will be deleted immediately."
			);

			Alert.viewSuccessMessage();
		}
	}

	macro viewCP {
		var key_teamName = "${teamName}";

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(locator1 = "SiteTeams#TEAM_TABLE_NAME", value1 = "${teamName}");

		AssertTextEquals(
			locator1 = "SiteTeams#TEAM_TABLE_DESCRIPTION",
			value1 = "${teamDescription}"
		);

		AssertElementPresent(locator1 = "SiteTeams#TEAM_TABLE_ACTIONS");
	}

	macro viewDefaultCP {
		var key_teamName = "${teamName}";

		PortletEntry.viewEmptyMessage(entity = "Site Teams");

		AssertTextNotPresent(
			locator1 = "SiteTeams#TEAM_TABLE_NAME",
			value1 = "${teamName}"
		);
	}

	macro viewMemberCP {
		AssertClick(
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = "${teamName}",
			key_rowEntry = "${teamName}"
		);

		Navigator.gotoNavItem(navItem = "${membershipType}s");

		if ("${membershipType}" == "User") {
			AssertTextEquals.assertPartialText(
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_1",
				value1 = "${userFirstName} ${userLastName}",
				key_rowEntry = "${userFirstName} ${userLastName}"
			);
		}
		else if ("${membershipType}" == "User Group") {
			AssertTextEquals.assertPartialText(
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_1",
				value1 = "${userGroupName}",
				key_rowEntry = "${userGroupName}"
			);
		}
	}

	macro viewNoMembersCP {
		AssertClick(
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = "${teamName}",
			key_rowEntry = "${teamName}"
		);

		for (var navItem : list "Users,User Groups") {
			Navigator.gotoNavItem(navItem = "${navItem}");

			var lowercaseNavItem = StringUtil.lowerCase("${navItem}");

			AssertTextEquals(
				locator1 = "Message#EMPTY_INFO",
				value1 = "No ${lowercaseNavItem} were found."
			);
		}
	}
}