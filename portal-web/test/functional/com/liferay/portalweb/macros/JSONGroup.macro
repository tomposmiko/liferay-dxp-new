definition {

	@summary = "Directly calls the JSON WS API to add a group"
	macro _addGroup {

		if (isSet(groupDescription)) {
			var groupDescriptionEncoded = URLUtil.encodeUTF8("${groupDescription}");

			var groupDescriptionMap = '''{"en_US":"${groupDescriptionEncoded}"}''';
		}
		else {
			var groupDescriptionMap = "{}";
		}

		if (isSet(groupName)) {
			var groupNameEncoded = URLUtil.encodeUTF8("${groupName}");

			var groupNameMap = '''{"en_US":"${groupNameEncoded}"}''';
		}
		else {
			fail("Please set 'groupName'.");
		}

		if (isSet(parentGroupName)) {
			var parentGroupId = JSONGroup._getSiteIdByGroupName(groupName = "${parentGroupName}");
		}
		else {
			var parentGroupId = "0";
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/group/add-group \
				-u test@liferay.com:test \
				-d parentGroupId=${parentGroupId} \
				-d liveGroupId=0 \
				-d nameMap=${groupNameMap} \
				-d descriptionMap=${groupDescriptionMap} \
				-d type=1 \
				-d manualMembership=true \
				-d membershipRestriction=0 \
				-d friendlyURL= \
				-d site=true \
				-d inheritContent=false \
				-d active=true
		''';

		echo("## Adding a 'Group' with the following info using JSONWS:");

		if (isSet(groupDescription)) {
			echo("## * Group Description: ${groupDescription}");
		}

		echo("## * Group Name: ${groupName}");

		var groupId = JSONCurlUtil.post("${curl}", "$['groupId']");
	}

	@summary = "Directly calls the JSON WS API to assign a user a role"
	macro _assignGroupRoleToUser {
		if (!(isSet(groupId))) {
			fail("Please set 'groupId'.");
		}

		if (!(isSet(roleId))) {
			fail("Please set 'roleId'.");
		}

		if (!(isSet(userId))) {
			fail("Please set 'userId'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/usergrouprole/add-user-group-roles \
				-u test@liferay.com:test \
				-d roleId=${roleId} \
				-d groupId=${groupId} \
				-d userIds=${userId}
		''';

		var groupName = JSONGroup._getGroupName(groupId = "${groupId}");

		var roleTitle = JSONRole.getRoleName(roleId = "${roleId}");

		var userEmailAddress = JSONUser.getUserEmailAddress(userId = "${userId}");

		echo("## Assign a 'Group Role' to a 'User' with the following info using JSONWS:");

		echo("## * Group Name: ${groupName}");

		echo("## * Role Name: ${roleTitle}");

		echo("## * User Email Address: ${userEmailAddress}");

		var temp = JSONCurlUtil.post("${curl}", "$");
	}

	@summary = "Directly calls the JSON WS API to add a user to a group"
	macro _assignUserToGroup {
		if (!(isSet(groupId))) {
			fail("Please set 'groupId'.");
		}

		if (!(isSet(userId))) {
			fail("Please set 'userId'.");
		}

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/user/add-group-users \
				-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d userIds=${userId}
		''';

		var groupName = JSONGroup._getGroupName(groupId = "${groupId}");

		var userEmailAddress = JSONUser.getUserEmailAddress(userId = "${userId}");

		echo("## Assigning a 'User' to an 'Group' with the following info using JSONWS:");

		echo("## * Group Name: ${groupName}");

		echo("## * User Email Address: ${userEmailAddress}");

		var temp = JSONCurlUtil.post("${curl}", "$");
	}

	@summary = "Directly calls the JSON WS API to delete a group"
	macro _deleteGroup {
		if (!(isSet(groupId))) {
			fail("Please set 'groupId'.");
		}

		var groupName = JSONGroup._getGroupName(groupId = "${groupId}");

		echo("## Deleting a 'Group' with the following info using JSONWS:");

		echo("## * Group Name: ${groupName}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/group/delete-group \
				-u test@liferay.com:test \
				-d groupId=${groupId}
		''';
		var temp = JSONCurlUtil.post("${curl}", "$");
	}

	@summary = "Get the groupId from any existing group by using groupKey"
	macro _getGroupIdByGroupKey {
		if (!(isSet(groupKey))) {
			fail("Please set 'groupKey'.");
		}

		if (!(isSet(site))) {
			var site = "true";
		}

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/group/get-groups/company-id/${companyId}/parent-group-id/0/site/${site} \
				-u test@liferay.com:test
		''';

		echo("${curl}', '$.[?(@[\'groupKey\'] == \'${groupKey}\')][\'groupId\']");

		var groupId = JSONCurlUtil.get("${curl}", "$.[?(@['groupKey'] == '${groupKey}')]['groupId']");
		var groupId = StringUtil.replace("${groupId}", "[&quot;", "");
		var groupId = StringUtil.replace("${groupId}", "&quot;]", "");

		if ("${groupId}" == "[]") {
			fail("FAIL. Cannot find group.");
		}
		else {
			echo("FOUND. Returning groupId: ${groupId}");
		}

		return "${groupId}";
	}

	@summary = "Get the groupId from any existing site by using groupName"
	macro _getGroupIdByName {
		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/group/get-groups/company-id/${companyId}/parent-group-id/0/site/true \
				-u test@liferay.com:test
		''';
		var groupId = JSONCurlUtil.get("${curl}", "$.[?(@['nameCurrentValue'] == '${groupName}')]['groupId']");
		var groupId = StringUtil.replace("${groupId}", "[&quot;", "");
		var groupId = StringUtil.replace("${groupId}", "&quot;]", "");

		if ("${groupId}" == "[]") {
			fail("FAIL. Cannot find group.");
		}
		else {
			echo("FOUND. Returning groupId: ${groupId}");
		}

		return "${groupId}";
	}

	@summary = "Get the groupName from any group by using groupId"
	macro _getGroupName {
		if (!(isSet(groupId))) {
			fail("Please set 'groupId'.");
		}

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/group/get-group/group-id/${groupId} \
				-u test@liferay.com:test
		''';
		var groupName = JSONCurlUtil.get("${curl}", "$['nameCurrentValue']");

		return "${groupName}";
	}

	@summary = "Get the groupId from any existing parent or child site by using groupName"
	macro _getSiteIdByGroupName {
		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/group/get-group \
				-u test@liferay.com:test \
				-d companyId=${companyId} \
				-d groupKey=${groupName}
		''';

		echo("## Searching for a 'Group Id' with the following info using JSONWS:");

		echo("## * Group Name: ${groupName}");

		var groupId = JSONCurlUtil.post("${curl}", "$.[?(@['groupKey'] == '${groupName}')]['groupId']");
		var groupId = StringUtil.replace("${groupId}", "[&quot;", "");
		var groupId = StringUtil.replace("${groupId}", "&quot;]", "");

		if ("${groupId}" == "[]") {
			fail("FAIL. Cannot find group.");
		}
		else {
			echo("FOUND. Returning groupId: ${groupId}");
		}

		return "${groupId}";
	}

	@summary = "Add a child site to a valid site."
	macro addChildSite {
		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		if (!(isSet(parentGroupName))) {
			fail("Please set 'parentGroupKey'.");
		}

		JSONGroup._addGroup(
			groupDescription = "${groupDescription}",
			groupName = "${groupName}",
			parentGroupName = "${parentGroupName}"
		);
	}

	@summary = "Add a basic site"
	macro addGroup {
		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		JSONGroup._addGroup(
			groupDescription = "${groupDescription}",
			groupName = "${groupName}"
		);
	}

	macro assignGroupRoleToUser {
		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		if (!(isSet(roleTitle))) {
			fail("Please set 'roleTitle'.");
		}

		if (!(isSet(userEmailAddress))) {
			fail("Please set 'userEmailAddress'.");
		}

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		var roleId = JSONRole.getRoleIdByName(roleTitle = "${roleTitle}");

		var userId = JSONUser.getUserIdByEmailAddress(userEmailAddress = "${userEmailAddress}");

		JSONGroup._assignGroupRoleToUser(
			groupId = "${groupId}",
			roleId = "${roleId}",
			userId = "${userId}"
		);
	}

	macro assignUserToGroup {
		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		if (!(isSet(userEmailAddress))) {
			fail("Please set 'userEmailAddress'.");
		}

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		var userId = JSONUser.getUserIdByEmailAddress(userEmailAddress = "${userEmailAddress}");

		JSONGroup._assignUserToGroup(groupId = "${groupId}", userId = "${userId}");
	}

	@summary = "Delete a valid group using its groupName"
	macro deleteGroupByName {
		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		JSONGroup._deleteGroup(groupId = "${groupId}");
	}
}