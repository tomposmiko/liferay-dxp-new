definition {

	macro _addOrganization {
		Variables.assertDefined(parameterList = ${organizationName});

		var organizationNameEncoded = URLUtil.encodeUTF8(${organizationName});

		if (!(isSet(organizationSite))) {
			var organizationSite = "false";
		}

		if (isSet(parentOrganizationId)) {
			var parentOrganizationId = ${parentOrganizationId};
		}
		else {
			var parentOrganizationId = 0;
		}

		if (isSet(organizationType)) {
			var organizationType = ${organizationType};
		}
		else {
			var organizationType = "organization";
		}

		if (isSet(organizationCountry)) {
			var countryId = JSONCountry.getCountryId(countryName = ${organizationCountry});
		}
		else {
			var countryId = 0;
		}

		if (isSet(organizationRegion)) {
			var regionId = JSONCountry.getRegionId(
				countryName = ${organizationCountry},
				regionName = ${organizationRegion});
		}
		else {
			var regionId = 0;
		}

		var portalURL = JSONCompany.getPortalURL();
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/organization/add-organization \
				-u ${userLoginInfo} \
				-d parentOrganizationId=${parentOrganizationId} \
				-d name=${organizationNameEncoded} \
				-d type=${organizationType} \
				-d regionId=${regionId} \
				-d countryId=${countryId} \
				-d statusListTypeId=12017 \
				-d comments= \
				-d site=${organizationSite}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post(${curl});
	}

	macro _addOrganizationToSite {
		Variables.assertDefined(parameterList = "${groupId},${organizationId}");

		var portalURL = JSONCompany.getPortalURL();
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/organization/add-group-organizations/group-id/${groupId}/organization-ids/${organizationId} \
				-u ${userLoginInfo}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post(${curl});
	}

	macro _assignPasswordPolicyToOrganization {
		Variables.assertDefined(parameterList = "${organizationId},${passwordPolicyId}");

		var portalURL = JSONCompany.getPortalURL();
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/organization/add-password-policy-organizations \
				-u ${userLoginInfo} \
				-d organizationIds=${organizationId} \
				-d passwordPolicyId=${passwordPolicyId}
		''';

		JSONCurlUtil.post(${curl});
	}

	macro _assignUserToOrganization {
		Variables.assertDefined(parameterList = "${organizationId},${userId}");

		var portalURL = JSONCompany.getPortalURL();
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/user/add-organization-users \
				-u ${userLoginInfo} \
				-d organizationId=${organizationId} \
				-d userIds=${userId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post(${curl});
	}

	macro _deleteOrganization {
		Variables.assertDefined(parameterList = ${organizationId});

		var portalURL = JSONCompany.getPortalURL();
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/organization/delete-organization \
				-u ${userLoginInfo} \
				-d organizationId=${organizationId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post(${curl});
	}

	@summary = "Get the number of childs organizations based on its parent organization id"
	macro _getChildOrganizationCount {
		Variables.assertDefined(parameterList = ${parentOrganizationId});

		var portalURL = JSONCompany.getPortalURL();
		var companyId = JSONCompany.getCompanyId();
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/organization/get-organizations-count \
				-u ${userLoginInfo} \
				-d companyId=${companyId} \
				-d parentOrganizationId=${parentOrganizationId}
		''';

		var numberOfChildren = JSONCurlUtil.post(${curl});

		var numberOfChildren = StringUtil.extractDigits(${numberOfChildren});

		return ${numberOfChildren};
	}

	@summary = "Get the childs organizations ids based on its parent organization id"
	macro _getOrganization {
		Variables.assertDefined(parameterList = ${parentOrganizationId});

		var portalURL = JSONCompany.getPortalURL();
		var companyId = JSONCompany.getCompanyId();
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/organization/get-organizations \
				-u ${userLoginInfo} \
				-d companyId=${companyId} \
				-d parentOrganizationId=${parentOrganizationId}
		''';

		var organizationIds = JSONCurlUtil.post(${curl}, "$..['organizationId']");

		return ${organizationIds};
	}

}