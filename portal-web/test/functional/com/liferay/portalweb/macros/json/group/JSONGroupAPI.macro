definition {

	@summary = "Directly calls the JSON WS API to add a group"
	macro _addGroup {
		Variables.assertDefined(parameterList = "${groupDescriptionMap},${groupNameMap},${manualMembership},${parentGroupId},${type}");

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/group/add-group \
				-u ${userLoginInfo} \
				-d parentGroupId=${parentGroupId} \
				-d liveGroupId=0 \
				-d nameMap=${groupNameMap} \
				-d descriptionMap=${groupDescriptionMap} \
				-d type=${type} \
				-d manualMembership=${manualMembership} \
				-d membershipRestriction=0 \
				-d friendlyURL= \
				-d site=true \
				-d inheritContent=false \
				-d active=true
		''';

		JSONCurlUtil.post(${curl});
	}

	@summary = "Directly calls the JSON WS API to assign a user a role"
	macro _assignGroupRoleToUser {
		Variables.assertDefined(parameterList = "${groupId},${roleId},${userId}");

		var portalURL = JSONCompany.getPortalURL();
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/usergrouprole/add-user-group-roles \
				-u ${userLoginInfo} \
				-d roleId=${roleId} \
				-d groupId=${groupId} \
				-d userIds=${userId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post(${curl});
	}

	@summary = "Directly calls the JSON WS API to assign a user group a site role"
	macro _assignGroupRoleToUserGroup {
		Variables.assertDefined(parameterList = "${groupId},${roleId},${userGroupId}");

		var portalURL = JSONCompany.getPortalURL();
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/usergroupgrouprole/add-user-group-group-roles \
				-u ${userLoginInfo} \
				-d roleId=${roleId} \
				-d groupId=${groupId} \
				-d userGroupIds=${userGroupId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post(${curl});
	}

	@summary = "Directly calls the JSON WS API to add a organization to a group"
	macro _assignOrganizationToGroup {
		Variables.assertDefined(parameterList = "${groupId},${organizationId}");

		var portalURL = JSONCompany.getPortalURL();
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/user/add-group-organizations \
				-u ${userLoginInfo} \
				-d groupId=${groupId} \
				-d organizationIds=${organizationId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post(${curl});
	}

	@summary = "Directly calls the JSON WS API to add a user to a group"
	macro _assignUserGroupToGroup {
		Variables.assertDefined(parameterList = "${groupId},${userGroupId}");

		var portalURL = JSONCompany.getPortalURL();
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/usergroup/add-group-user-groups/group-id/${groupId}/user-group-ids/${userGroupId} \
				-u ${userLoginInfo}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post(${curl});
	}

	@summary = "Directly calls the JSON WS API to add a user to a group"
	macro _assignUserToGroup {
		Variables.assertDefined(parameterList = "${groupId},${userId}");

		var portalURL = JSONCompany.getPortalURL();
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/user/add-group-users \
				-u ${userLoginInfo} \
				-d groupId=${groupId} \
				-d userIds=${userId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post(${curl});
	}

	@summary = "Directly calls the JSON WS API to delete a group"
	macro _deleteGroup {
		Variables.assertDefined(parameterList = ${groupId});

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/group/delete-group \
				-u ${userLoginInfo} \
				-d groupId=${groupId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post(${curl});
	}

	@summary = "Get the depotEntryGroupRelId from any existing depot that connects to a site by using groupName"
	macro _getDepotEntryGroupRelIdByGroupName {
		Variables.assertDefined(parameterList = ${groupName});

		var companyId = JSONCompany.getCompanyId();
		var portalURL = JSONCompany.getPortalURL();
		var groupId = JSONGroupAPI._getGroupIdByName(
			groupName = ${groupName},
			site = ${site});
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/depot.depotentrygrouprel/get-depot-entry-group-rels \
				-u ${userLoginInfo} \
				-d groupId=${groupId} \
				-d "start=-1" \
				-d "end=-1"
		''';

		var depotEntryGroupRelId = JSONCurlUtil.post(${curl}, "$.[?(@['groupId'] == '${groupId}')]['depotEntryGroupRelId']");

		if (${depotEntryGroupRelId} == "") {
			fail("FAIL. Cannot find group.");
		}

		return ${depotEntryGroupRelId};
	}

	@summary = "Get the depotId from any existing depot by using depotName"
	macro _getDepotIdByName {
		Variables.assertDefined(parameterList = ${depotName});

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		if (${noSelenium} == "true") {
			var companyId = JSONCompany.getCompanyIdNoSelenium(portalURL = ${portalURL});
		}
		else {
			var companyId = JSONCompany.getCompanyId();
		}

		if (!(isSet(defaultLocale))) {
			var defaultLocale = "en_US";
		}
		else {
			var defaultLocale = ${defaultLocale};
		}

		var locale = '''default-locale=\"${defaultLocale}\"''';
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/group/get-groups/company-id/${companyId}/parent-group-id/0/site/false \
				-u ${userLoginInfo}
		''';

		var depotId = JSONCurlUtil.get(${curl}, "$.[?(@['nameCurrentValue'] == '${depotName}' && @['name'] contains '${locale}')]['classPK']");

		if (${depotId} == "") {
			fail("FAIL. Cannot find group.");
		}

		return ${depotId};
	}

	@summary = "Get the grandParentGroupId from any existing site by using groupName"
	macro _getGrandParentGroupIdByName {
		Variables.assertDefined(parameterList = ${groupName});

		var companyId = JSONCompany.getCompanyId();
		var portalURL = JSONCompany.getPortalURL();

		if (!(isSet(site))) {
			var site = "true";
		}

		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/group/get-groups \
				-u ${userLoginInfo} \
				-d companyId=${companyId} \
				-d parentGroupId=0 \
				-d site=${site}
		''';

		var groupId = JSONCurlUtil.post(${curl}, "$.[?(@['nameCurrentValue'] == '${groupName}')]['groupId']");

		if (${groupId} == "") {
			fail("FAIL. Cannot find group.");
		}

		return ${groupId};
	}

	@summary = "Get the groupId from any existing group by using groupKey"
	macro _getGroupIdByGroupKey {
		Variables.assertDefined(parameterList = "${groupKey},${site}");

		var companyId = JSONCompany.getCompanyId();
		var parentGroupId = JSONGroupSetter.setParentGroupId(
			grandParentGroupName = ${grandParentGroupName},
			parentGroupName = ${parentGroupName});
		var portalURL = JSONCompany.getPortalURL();
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/group/get-groups/company-id/${companyId}/parent-group-id/${parentGroupId}/site/${site} \
				-u ${userLoginInfo}
		''';

		var groupId = JSONCurlUtil.get(${curl}, "$.[?(@['groupKey'] == '${groupKey}')]['groupId']");

		if (${groupId} == "") {
			fail("FAIL. Cannot find group.");
		}

		return ${groupId};
	}

	@summary = "Get the groupId from any existing site by using groupName"
	macro _getGroupIdByName {
		Variables.assertDefined(parameterList = "${groupName},${site}");

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		if (isSet(noSelenium)) {
			var companyId = JSONCompany.getCompanyIdNoSelenium(portalURL = ${portalURL});
		}
		else {
			var companyId = JSONCompany.getCompanyId(
				portalInstanceName = ${portalInstanceName},
				portalURL = ${portalURL});
		}

		var parentGroupId = JSONGroupSetter.setParentGroupId(
			grandParentGroupName = ${grandParentGroupName},
			parentGroupName = ${parentGroupName},
			portalInstanceName = ${portalInstanceName},
			portalURL = ${portalURL});
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/group/get-groups/company-id/${companyId}/parent-group-id/${parentGroupId}/site/${site} \
				-u ${userLoginInfo}
		''';

		var groupId = JSONCurlUtil.get(${curl}, "$.[?(@['nameCurrentValue'] == '${groupName}')]['groupId']");

		if (${groupId} == "") {
			fail("FAIL. Cannot find group.");
		}

		echo("## * GroupId: ${groupId}");

		return ${groupId};
	}

	@summary = "Get the groupId from any existing site by using groupName"
	macro _getGroupIdByNameNoError {
		Variables.assertDefined(parameterList = "${groupName},${site}");

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		var companyId = JSONCompany.getCompanyId(portalURL = ${portalURL});
		var parentGroupId = JSONGroupSetter.setParentGroupId(
			grandParentGroupName = ${grandParentGroupName},
			parentGroupName = ${parentGroupName});
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/group/get-groups/company-id/${companyId}/parent-group-id/${parentGroupId}/site/${site} \
				-u ${userLoginInfo}
		''';

		var groupId = JSONCurlUtil.get(${curl}, "$.[?(@['nameCurrentValue'] == '${groupName}')]['groupId']");

		return ${groupId};
	}

	@summary = "Get the groupId from any existing site by using groupName but not invoking selenium methods"
	macro _getGroupIdByNameNoSelenium {
		Variables.assertDefined(parameterList = "${groupName},${site}");

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		var companyId = JSONCompany.getCompanyIdNoSelenium(portalURL = ${portalURL});
		var parentGroupId = JSONGroupSetter.setParentGroupId(
			grandParentGroupName = ${grandParentGroupName},
			parentGroupName = ${parentGroupName});
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/group/get-groups/company-id/${companyId}/parent-group-id/${parentGroupId}/site/${site} \
				-u ${userLoginInfo}
		''';

		var groupId = JSONCurlUtil.get(${curl}, "$.[?(@['nameCurrentValue'] == '${groupName}')]['groupId']");

		if (${groupId} == "") {
			fail("FAIL. Cannot find group.");
		}

		echo("## * GroupId: ${groupId}");

		return ${groupId};
	}

	@summary = "Get the groupKey from any existing site by using groupName"
	macro _getGroupKeyByName {
		Variables.assertDefined(parameterList = "${groupName},${site}");

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		var companyId = JSONCompany.getCompanyId(portalURL = ${portalURL});
		var parentGroupId = JSONGroupSetter.setParentGroupId(
			grandParentGroupName = ${grandParentGroupName},
			parentGroupName = ${parentGroupName});
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/group/get-groups/company-id/${companyId}/parent-group-id/${parentGroupId}/site/${site} \
				-u ${userLoginInfo}
		''';

		var groupKey = JSONCurlUtil.get(${curl}, "$.[?(@['nameCurrentValue'] == '${groupName}')]['groupKey']");

		if (${groupKey} == "") {
			fail("FAIL. Cannot find group.");
		}

		echo("## * GroupKey: ${groupKey}");

		return ${groupKey};
	}

	@summary = "Get the groupName from any group by using groupId"
	macro _getGroupName {
		Variables.assertDefined(parameterList = ${groupId});

		var companyId = JSONCompany.getCompanyId();
		var portalURL = JSONCompany.getPortalURL();
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/group/get-group/group-id/${groupId} \
				-u ${userLoginInfo}
		''';

		var groupName = JSONCurlUtil.get(${curl}, "$['nameCurrentValue']");

		return ${groupNam};
	}

	@summary = "Get the nameCurrentValues from existing groups"
	macro _getNameCurrentValues {
		var companyId = JSONCompany.getCompanyId();
		var portalURL = JSONCompany.getPortalURL();

		if (!(isSet(site))) {
			var site = "true";
		}

		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/group/get-groups \
				-u ${userLoginInfo} \
				-d companyId=${companyId} \
				-d parentGroupId=0 \
				-d site=${site}
		''';

		var nameCurrentValues = JSONCurlUtil.post(${curl}, "$..nameCurrentValue");

		return ${nameCurrentValues};
	}

	@summary = "Get the parentGroupId from any existing site by using groupName"
	macro _getParentGroupIdByName {
		Variables.assertDefined(parameterList = ${groupName});

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		var companyId = JSONCompany.getCompanyId(
			portalInstanceName = ${portalInstanceName},
			portalURL = ${portalURL});
		var grandParentGroupId = JSONGroupSetter.setGrandParentGroupId(
			grandParentGroupName = ${grandParentGroupName},
			portalInstanceName = ${portalInstanceName},
			portalURL = ${portalURL});
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/group/get-groups/company-id/${companyId}/parent-group-id/${grandParentGroupId}/site/true \
				-u ${userLoginInfo}
		''';

		var groupId = JSONCurlUtil.get(${curl}, "$.[?(@['nameCurrentValue'] == '${groupName}')]['groupId']");

		if (${groupId} == "") {
			fail("FAIL. Cannot find group.");
		}

		return ${groupId};
	}

	@summary = "Get the groupId from any existing parent or child site by using groupName"
	macro _getSiteIdByGroupKey {
		Variables.assertDefined(parameterList = ${groupName});

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		var companyId = JSONCompany.getCompanyId(portalURL = ${portalURL});
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/group/get-group \
				-u ${userLoginInfo} \
				-d companyId=${companyId} \
				-d groupKey=${groupName}
		''';

		var groupId = JSONCurlUtil.post(${curl}, "$.[?(@['groupKey'] == '${groupName}')]['groupId']");

		if (${groupId} == "") {
			fail("FAIL. Cannot find group.");
		}

		return ${groupId};
	}

}