definition {

	macro addTeam {
		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		var groupId = JSONLayoutSetter.setGroupId(groupName = ${siteName});
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/team/add-team \
				-u ${userLoginInfo} \
				-d groupId=${groupId} \
				-d name=${teamName} \
				-d description=${teamDescription} \
		''';

		echo("## Adding a 'Team' with the following info using JSONWS:");

		echo("## * Name: ${teamName}");

		echo("## * Description: ${teamDescription}");

		var messageId = JSONCurlUtil.post(${curl});
	}

	macro addUserToTeam {
		Variables.assertDefined(parameterList = "${teamName},${userEmailAddress}");

		var portalURL = JSONCompany.getPortalURL();
		var userId = JSONRoleSetter.setUserId(userEmailAddress = ${userEmailAddress});
		var teamId = JSONTeam.getTeamId(
			siteName = ${siteName},
			teamName = ${teamName});
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/user/add-team-users \
				-u ${userLoginInfo} \
				-d teamId=${teamId} \
				-d userIds=[${userId}]
		''';

		JSONCurlUtil.post(${curl});
	}

	macro getTeamId {
		Variables.assertDefined(parameterList = ${teamName});

		var portalURL = JSONCompany.getPortalURL();
		var key_teamName = StringUtil.replace(${teamName}, " ", "%20");
		var groupId = JSONLayoutSetter.setGroupId(groupName = ${siteName});
		var userLoginInfo = JSONUtil2.formatJSONUser();

		var curl = '''
			${portalURL}/api/jsonws/team/get-team \
				-u ${userLoginInfo} \
				-d groupId=${groupId} \
				-d name=${key_teamName}
		''';

		var teamId = JSONCurlUtil.post(${curl}, "$.teamId");

		return ${teamId};
	}

}