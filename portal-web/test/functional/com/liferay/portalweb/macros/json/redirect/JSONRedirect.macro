definition {

	@summary = "This calls the JSON WS API to add a redirect entry"
	macro addRedirect {
		Variables.assertDefined(parameterList = "${sourceUrl},${destinationUrl}");

		if (!(isSet(site))) {
			var site = "true";
		}

		var groupId = JSONGroupAPI._getGroupIdByName(
			groupName = "${groupName}",
			site = "${site}");

		var portalUrl = JSONCompany.getPortalURL();

		var destinationUrl = StringUtil.replace("${destinationUrl}", ":", "%3A");

		if (!(isSet(expirationDate))) {
			var expirationDate = "2100-01-01";
		}

		if (!(isSet(permanent))) {
			var permanent = "false";
		}

		if (!(isSet(userEmailAddress))) {
			var userEmailAddress = "test@liferay.com";
		}

		if (!(isSet(userPassword))) {
			var userPassword = "test";
		}

		var curl = '''
			${portalUrl}/api/jsonws/redirect.redirectentry/add-redirect-entry \
			-u ${userEmailAddress}:${userPassword} \
			-d groupId=${groupId} \
			-d destinationURL=${destinationUrl} \
			-d expirationDate=${expirationDate} \
			-d permanent=${permanent} \
			-d sourceURL=${sourceUrl}
		''';

		echo("## Adding a 'Redirection' with the following info using JSONWS:");

		echo("## * SourceURL: ${sourceUrl}");

		echo("## * DestinationURL: ${destinationUrl}");

		var messageId = JSONCurlUtil.post("${curl}");
	}

	@summary = "This calls the JSON WS API to delete an entry"
	macro deleteEntry {
		var redirectEntryId = JSONRedirect.getRedirectEntryId(
			destinationUrl = "${destinationUrl}",
			groupName = "${groupName}");

		var portalUrl = JSONCompany.getPortalURL();

		if (!(isSet(userEmailAddress))) {
			var userEmailAddress = "test@liferay.com";
		}

		if (!(isSet(userPassword))) {
			var userPassword = "test";
		}

		var curl = '''
			${portalUrl}/api/jsonws/redirect.redirectentry/delete-redirect-entry \
				-u ${userEmailAddress}:${userPassword} \
				-d redirectEntryId=${redirectEntryId} \
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "This calls the JSON WS API to get the redirectEntryId"
	macro getRedirectEntryId {
		Variables.assertDefined(parameterList = "${destinationUrl},${groupName}");

		var portalUrl = JSONCompany.getPortalURL();

		var groupId = JSONGroupSetter.setGroupId(groupName = "${groupName}");

		var curl = '''
			${portalUrl}/api/jsonws/redirect.redirectentry/get-redirect-entries \
			-u test@liferay.com:test \
			-d groupId=${groupId} \
			-d "start=-1" \
			-d "end=-1" \
			-d -orderByComparator=
		''';
		var redirectEntryId = JSONCurlUtil.post("${curl}", "$.[?(@['destinationURL'] == '${destinationUrl}')]['redirectEntryId']");

		if ("${redirectEntryId}" == "") {
			fail("FAIL. Cannot find category.");
		}

		return "${redirectEntryId}";
	}

	@summary = "This calls the JSON WS API to update an entry"
	macro updateEntry {
		var redirectEntryId = JSONRedirect.getRedirectEntryId(
			destinationUrl = "${destinationUrl}",
			groupName = "${groupName}");

		var portalUrl = JSONCompany.getPortalURL();

		var destinationUrl = StringUtil.replace("${destinationUrl}", ":", "%3A");

		if (!(isSet(expirationDate))) {
			var expirationDate = "2100-01-01";
		}

		if (!(isSet(permanent))) {
			var permanent = "false";
		}

		if (!(isSet(userEmailAddress))) {
			var userEmailAddress = "test@liferay.com";
		}

		if (!(isSet(userPassword))) {
			var userPassword = "test";
		}

		var curl = '''
			${portalUrl}/api/jsonws/redirect.redirectentry/update-redirect-entry \
				-u ${userEmailAddress}:${userPassword} \
				-d redirectEntryId=${redirectEntryId} \
				-d destinationURL=${destinationUrl} \
				-d expirationDate=${expirationDate} \
				-d permanent=${permanent} \
				-d sourceURL=${sourceUrl}

		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

}