definition {

	@summary = "Directly calls the JSON WS API to add a tag"
	macro _addTag {
		Variables.assertDefined(parameterList = "${groupId},${tagName}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/assettag/add-tag \
				-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d name=${tagName}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	macro _getTagId {
		Variables.assertDefined(parameterList = "${groupId},${tagName}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/assettag/get-tags \
				-u test@liferay.com:test \
				-d groupIds=[${groupId}] \
				-d name='${tagName}' \
				-d 'start=-1' \
				-d 'end=-1' \
				-d -orderByComparator=
		''';

		var tagId = JSONCurlUtil.post("${curl}", "$..['tagId']");

		return "${tagId}";
	}

}