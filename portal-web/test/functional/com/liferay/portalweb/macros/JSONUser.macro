definition {

	macro _addUser {
		if (!(isSet(userEmailAddress))) {
			fail("Please set 'userEmailAddress'.");
		}

		if (isSet(userFirstName)) {
			var userFirstNameEncoded = URLUtil.encodeUTF8("${userFirstName}");
		}
		else {
			fail("Please set 'userFirstName'.");
		}

		if (isSet(userLastName)) {
			var userLastNameEncoded = URLUtil.encodeUTF8("${userLastName}");
		}
		else {
			fail("Please set 'userLastName'.");
		}

		if (!(isSet(userScreenName))) {
			fail("Please set 'userScreenName'.");
		}

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/user/add-user \
				-u test@liferay.com:test \
				-d companyId=${companyId} \
				-d autoPassword=true \
				-d password1=test \
				-d password2=test \
				-d autoScreenName=false \
				-d screenName=${userScreenName} \
				-d emailAddress=${userEmailAddress} \
				-d facebookId=0 \
				-d openId= \
				-d locale= \
				-d firstName=${userFirstNameEncoded} \
				-d middleName= \
				-d lastName=${userLastNameEncoded} \
				-d prefixId=0 \
				-d suffixId=0 \
				-d male=true \
				-d birthdayMonth=4 \
				-d birthdayDay=31 \
				-d birthdayYear=1986 \
				-d jobTitle= \
				-d groupIds= \
				-d organizationIds= \
				-d roleIds= \
				-d userGroupIds= \
				-d sendEmail=true
		''';

		echo("## Adding a 'User' with the following info using JSONWS:");

		echo("## * Company ID: ${companyId}");

		echo("## * Email Address: ${userEmailAddress}");

		echo("## * First Name: ${userFirstName}");

		echo("## * Last Name: ${userLastName}");

		echo("## * Screen Name: ${userScreenName}");

		var userId = JSONCurlUtil.post("${curl}", "$['userId']");
	}

	macro _agreeToTermsAndAnswerReminderQuery {
		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl1 = '''
			${portalURL}/api/jsonws/user/update-agreed-to-terms-of-use/user-id/${userId}/agreed-to-terms-of-use/true \
				-u test@liferay.com:test
		''';

		echo("## Agreeing to Terms of Use for user ${userId}");

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl1}");

		var curl2 = '''
			${portalURL}/api/jsonws/user/update-reminder-query/user-id/${userId}/question/what-is-your-father%27s-middle-name/answer/test \
				-u test@liferay.com:test
		''';

		echo("## Answering Password Reminder for ${userId}");

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl2}");
	}

	macro _deleteUser {
		if (!(isSet(userId))) {
			fail("Please set 'userId'.");
		}

		var userEmailAddress = JSONUser.getUserEmailAddress(userId = "${userId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/user/delete-user \
				-u test@liferay.com:test \
				-d userId=${userId}
		''';

		echo("## Deleting a 'User' with the following info using JSONWS:");

		echo("## * Email Address: ${userEmailAddress}");

		var temporary = JSONCurlUtil.post("${curl}", "");
	}

	macro _disablePasswordChangeRequired {
		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl1 = '''
			${portalURL}/api/jsonws/passwordpolicy/search/company-id/${companyId}/name/default/start/0/end/1/-obc \
				-u test@liferay.com:test
		''';

		var passwordPolicyId = JSONCurlUtil.post("${curl1}", "$.[?(@['name'] == 'Default Password Policy')]['passwordPolicyId']");
		var passwordPolicyId = StringUtil.replace("${passwordPolicyId}", "[&quot;", "");
		var passwordPolicyId = StringUtil.replace("${passwordPolicyId}", "&quot;]", "");

		echo("## Default Password Policy ID: ${passwordPolicyId}");

		var curl2 = '''
			${portalURL}/api/jsonws/passwordpolicy/update-password-policy \
				-u test@liferay.com:test \
				-d passwordPolicyId=${passwordPolicyId} \
				-d name='Default Password Policy' \
				-d description='Default Password Policy' \
				-d changeable=true \
				-d changeRequired=false \
				-d minAge=0 \
				-d checkSyntax=false \
				-d allowDictionaryWords=true \
				-d minAlphanumeric=0 \
				-d minLength=6 \
				-d minLowerCase=0 \
				-d minNumbers=1 \
				-d minSymbols=0 \
				-d minUpperCase=1 \
				-d regex="(?=.{4})(?:[a-zA-Z0-9]*)" \
				-d history=false \
				-d historyCount=6 \
				-d expireable=false \
				-d maxAge=8640000 \
				-d warningTime=86400 \
				-d graceLimit=0 \
				-d lockout=false \
				-d maxFailure=3 \
				-d lockoutDuration=0 \
				-d resetFailureCount=600 \
				-d resetTicketMaxAge=86400
		''';

		echo("## Disabling Change Required Password Policy");

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl2}");
	}

	macro addUser {
		if (!(isSet(userEmailAddress))) {
			fail("Please set 'userEmailAddress'.");
		}

		if (!(isSet(userFirstName))) {
			fail("Please set 'userFirstName'.");
		}

		if (!(isSet(userLastName))) {
			fail("Please set 'userLastName'.");
		}

		if (!(isSet(userScreenName))) {
			fail("Please set 'userScreenName'.");
		}

		JSONUser._addUser(
			userEmailAddress = "${userEmailAddress}",
			userFirstName = "${userFirstName}",
			userLastName = "${userLastName}",
			userScreenName = "${userScreenName}");
	}

	macro addUserToSite {
		if (!(isSet(groupName))) {
			fail("Please set 'groupName'.");
		}

		if (!(isSet(userEmailAddress))) {
			fail("Please set 'userEmailAddress'.");
		}

		var groupId = JSONGroup._getGroupIdByName(groupName = "${groupName}");

		var portalURL = JSONCompany.getPortalURL();

		var userId = JSONUser.getUserIdByEmailAddress(userEmailAddress = "${userEmailAddress}");

		var curl = '''
			${portalURL}/api/jsonws/user/add-group-users/group-id/${groupId}/user-ids/${userId} \
				-u test@liferay.com:test
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	macro deleteUserByEmailAddress {
		if (!(isSet(userEmailAddress))) {
			fail("Please set 'userEmailAddress'.");
		}

		var userId = JSONUser.getUserIdByEmailAddress(userEmailAddress = "${userEmailAddress}");

		JSONUser._deleteUser(userId = "${userId}");
	}

	macro deleteUserByUserId {
		if (!(isSet(userId))) {
			fail("Please set 'userId'.");
		}

		JSONUser._deleteUser(userId = "${userId}");
	}

	macro getUserEmailAddress {
		if (!(isSet(userId))) {
			fail("Please set 'userId'.");
		}

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/user/get-user-by-id/user-id/${userId} \
				-u test@liferay.com:test
		''';
		var userEmailAddress = JSONCurlUtil.get("${curl}", "$['emailAddress']");

		return "${userEmailAddress}";
	}

	macro getUserFirstName {
		if (!(isSet(userId))) {
			fail("Please set 'userId'.");
		}

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/user/get-user-by-id/user-id/${userId} \
				-u test@liferay.com:test
		''';
		var userFirstName = JSONCurlUtil.get("${curl}", "$['firstName']");

		return "${userFirstName}";
	}

	macro getUserIdByEmailAddress {
		if (!(isSet(userEmailAddress))) {
			fail("Please set 'userEmailAddress'.");
		}

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/user/get-user-by-email-address/company-id/${companyId}/email-address/${userEmailAddress} \
				-u test@liferay.com:test
		''';
		var userId = JSONCurlUtil.get("${curl}", "$['userId']");

		return "${userId}";
	}

	macro setFirstPassword {
		if (!(isSet(userEmailAddress))) {
			fail("Please set 'userEmailAddress'.");
		}

		if (!(isSet(newPassword))) {
			var newPassword = "test";
		}

		if (!(isSet(requireReset))) {
			var requireReset = "true";
		}

		var userId = JSONUser.getUserIdByEmailAddress(userEmailAddress = "${userEmailAddress}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/user/update-password \
				-u test@liferay.com:test \
				-d userId=${userId} \
				-d password1='${newPassword}' \
				-d password2='${newPassword}' \
				-d passwordReset=${requireReset}
		''';

		echo("## Updating password for ${userEmailAddress} to '${newPassword}'");

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");

		if ("${requireReset}" == "false") {
			JSONUser._disablePasswordChangeRequired();
		}

		if ("${agreeToTermsAndAnswerReminderQuery}" == "true") {
			JSONUser._agreeToTermsAndAnswerReminderQuery(userId = "${userId}");
		}
	}

	macro tearDownByFirstName {
		if (!(isSet(userFirstName))) {
			fail("Please set 'userFirstName'.");
		}

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/user/get-company-users-count/company-id/${companyId} \
				-u test@liferay.com:test
		''';
		var userCount = JSONCurlUtil.get("${curl}", "$");

		var curl = '''
			${portalURL}/api/jsonws/user/get-company-users/company-id/${companyId}/start/0/end/${userCount} \
				-u test@liferay.com:test
		''';
		var userIds = JSONCurlUtil.get("${curl}", "$..['userId']");
		var userIds = StringUtil.replace("${userIds}", "[", "");
		var userIds = StringUtil.replace("${userIds}", "]", "");
		var userIds = StringUtil.replace("${userIds}", "&quot;", "");

		for (var userId : list "${userIds}") {
			var currentUserFirstName = JSONUser.getUserFirstName(userId = "${userId}");

			if (contains("${currentUserFirstName}", "${userFirstName}")) {
				JSONUser.deleteUserByUserId(userId = "${userId}");
			}
		}
	}

}