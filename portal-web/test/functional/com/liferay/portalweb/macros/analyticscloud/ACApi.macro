definition {

	macro assertApiResponse {
		for (var expected : list ${expectedList}) {
			if (contains(${apiDataResponse}, ${expected})) {
				echo("${expected} - PASSED");
			}
			else {
				echo("${expected} - Not found in response");

				fail(${apiDataResponse});
			}
		}
	}

	macro assertGenericJSONPathResponse {
		var actual = JSONUtil.getWithJSONPath(${apiResponse}, "$..${apiPath}");

		TestUtils.assertEquals(
			actual = ${actual},
			expected = ${expected});
	}

	macro assertSearchKeywordNotPresent {
		if (!(isSet(keywordPosition))) {
			var keywordPosition = 0;
		}

		var actual = JSONUtil.getWithJSONPath(${apiResponse}, "$..search-keywords[${keywordPosition}].keywords");

		TestUtils.assertNotEquals(
			actual = ${actual},
			expected = ${expected});
	}

	macro assertSearchResponse {
		if (!(isSet(keywordPosition))) {
			var keywordPosition = 0;
		}

		var actual = JSONUtil.getWithJSONPath(${apiResponse}, "$..search-keywords[${keywordPosition}].keywords");

		TestUtils.assertEquals(
			actual = ${actual},
			expected = ${expected});
	}

	macro createDataReport {
		var analyticsCloudURL = PropsUtil.get("analytics.cloud.url");

		if (isSet(type)) {
			var curl = '''
				${analyticsCloudURL}/api/reports/${type} -H "Authorization: Bearer ${token}"
			''';

			if (isSet(filter)) {
				var curl = '''
					${analyticsCloudURL}/api/reports/${type}\?${filter}=${filterValue} -H "Authorization: Bearer ${token}"
				''';
			}
		}
		else {
			var curl = '''
				${analyticsCloudURL}/api/reports -H "Authorization: Bearer ${token}"
			''';
		}

		static var apiResponse = JSONCurlUtil.get(${curl});

		return ${apiResponse};
	}

	macro createIndividualOrAssetDataReport {
		var analyticsCloudURL = PropsUtil.get("analytics.cloud.url");

		if (isSet(type) && isSet(id)) {
			var curl = '''
				${analyticsCloudURL}/api/reports/${type}/${id} -H "Authorization: Bearer ${token}"
			''';

			static var apiIndividualOrAssetResponse = JSONCurlUtil.get(${curl});

			return ${apiIndividualOrAssetResponse};
		}
		else {
			fail("Enter both valids type and id");
		}
	}

	macro createNewExportSchedule {
		var analyticsCloudURL = PropsUtil.get("analytics.cloud.url");

		var curl = '''
			${analyticsCloudURL}/api/reports/export/${type}\?fromDate=${startDate}T${startTime}Z\&toDate=${endDate}T${endTime}Z
				-H "Authorization: Bearer ${token}"
		''';

		static var apiResponse = JSONCurlUtil.get(${curl}, "$");

		return ${apiResponse};
	}

	macro getAnalyticsConfiguration {
		ServerAdministration.openServerAdmin();

		ACApi.getSearchHeaderValues(header = "liferayAnalyticsFaroBackendURL");

		static var faroBackendURL = ${scriptValue};

		ACApi.getSearchHeaderValues(header = "liferayAnalyticsFaroBackendSecuritySignature");

		static var osbAsahFaroBackendSS = ${scriptValue};

		ACApi.getSearchHeaderValues(header = "liferayAnalyticsProjectId");

		static var osbAsahProjectId = ${scriptValue};
	}

	macro getSearchHeaderValues {
		var script = '''
			    com.liferay.analytics.settings.configuration.AnalyticsConfiguration analyticsConfiguration = (com.liferay.analytics.settings.configuration.AnalyticsConfiguration)com.liferay.portal.kernel.module.configuration.ConfigurationProviderUtil.getCompanyConfiguration(com.liferay.analytics.settings.configuration.AnalyticsConfiguration.class, com.liferay.portal.kernel.service.CompanyServiceUtil.getCompanies()[0].getCompanyId());
				out.println(analyticsConfiguration."${header}"());
		    ''';

		ServerAdministration.executeScript(
			language = "Groovy",
			script = ${script});

		static var scriptValue = selenium.getText("ServerAdministrationScript#OUTPUT_FIELD");
	}

	macro getSearchKeywords {

		// The pause is necessary because it takes some time for the keywords to appear during curl see LRAC-12141

		Pause(locator1 = 10000);

		if (isSet(parameters)) {
			var curl = '''
				${faroBackendURL}/api/1.0/pages/search-keywords?${parameters} \
					-H "OSB-Asah-Faro-Backend-Security-Signature: ${osbAsahFaroBackendSS}" \
					-H "OSB-Asah-Project-ID: ${osbAsahProjectId}"
			''';
		}
		else {
			var curl = '''
				${faroBackendURL}/api/1.0/pages/search-keywords \
					-H "OSB-Asah-Faro-Backend-Security-Signature: ${osbAsahFaroBackendSS}" \
					-H "OSB-Asah-Project-ID: ${osbAsahProjectId}"
			''';
		}

		static var apiResponse = JSONCurlUtil.get(${curl});

		return ${apiResponse};
	}

	macro getTokenValue {
		if (IsElementNotPresent(locator1 = "ACSettings#COPY_TOKEN_BUTTON")) {
			ACUtils.clickAnyButton(button = "Generate Token");
		}

		WaitForElementPresent(locator1 = "ACSettings#COPY_TOKEN_BUTTON");

		// Use getAttribute to get the token value from the page's HTML

		var token = selenium.getAttribute("//button[@aria-label='Click to Copy']@data-clipboard-text");

		return ${token};
	}

	macro searchApiResponse {
		if (!(isSet(resultPosition))) {
			var searchedValue = JSONUtil.getWithJSONPath(${apiDataResponse}, "$..${jsonQuery}");
		}
		else {
			var searchedValue = JSONUtil.getWithJSONPath(${apiDataResponse}, "$.results[${resultPosition}].${jsonQuery}");
		}

		return ${searchedValue};
	}

	macro sendEmailReport {
		var analyticsCloudURL = PropsUtil.get("analytics.cloud.url");

		var curl = '''
			${analyticsCloudURL}/o/faro/main/${groupId}/preferences/send_email_report
				-u 'test@liferay.com:test'
				-F 'channelId="${channelId}"'
				-F 'userId="${userID}"'
				-F 'frequency="${frequency}"'
		''';

		JSONCurlUtil.post(${curl});
	}

	macro viewApiResponseForError {

		// Checks if the API response message explains the reason for the error

		if (contains(${apiResponse}, ${message})) {
			echo("Correct error message - PASSED");
		}
		else {
			fail("Wrong error message - ${apiResponse}");
		}

		// Checks if the status value is ERROR

		if (contains(${apiResponse}, "status=ERROR")) {
			echo("Correct status - PASSED");
		}
		else {
			fail("Wrong status - ${apiResponse}");
		}
	}

	macro viewApiResponseForExportProcess {

		// Checks if the API response message is correct

		if (contains(${apiResponse}, ${message})) {
			echo("Correct message - PASSED");
		}
		else {
			fail("New export schedule was not created: Wrong message - ${apiResponse}");
		}

		// Checks if the fromDate value is the same as the one used to create the schedule

		if (contains(${apiResponse}, "fromDate=${startDate}T${startTime}Z")) {
			echo("Correct fromDate - PASSED");
		}
		else {
			fail("New export schedule was not created: Wrong fromDate - ${apiResponse}");
		}

		// Checks if the toDate value is the same as the one used to create the schedule

		if (contains(${apiResponse}, "toDate=${endDate}T${endTime}Z")) {
			echo("Correct toDate - PASSED");
		}
		else {
			fail("New export schedule was not created: Wrong toDate - ${apiResponse}");
		}

		// Checks if the type value is the same as the one used to create the schedule

		if (contains(${apiResponse}, "type=${upperCaseType}")) {
			echo("Correct type - PASSED");
		}
		else {
			fail("New export schedule was not created: Wrong type - ${apiResponse}");
		}

		// Checks if the status value is the same as the one used to create the schedule

		if (contains(${apiResponse}, "status=${status}")) {
			echo("Correct status - PASSED");
		}
		else {
			fail("New export schedule was not created: Wrong status - ${apiResponse}");
		}
	}

	macro viewApiResponseForRootEndpoint {
		for (var type : list ${typeList}) {
			if (contains(${apiResponse}, "http://osbasahbackend:8080/api/reports/${type}")) {
				echo("${type} - PASSED");
			}
			else {
				echo("${type} - Invalid type");

				fail(${apiResponse});
			}
		}
	}

}