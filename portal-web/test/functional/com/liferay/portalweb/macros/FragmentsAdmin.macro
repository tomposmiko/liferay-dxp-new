definition {
	macro _gotoAddCollection {
		if (IsElementPresent(locator1 = "NavNested#NAV_NESTED_PLUS")) {
			Click(locator1 = "NavNested#NAV_NESTED_PLUS");
		}
		else {
			Button.click(button = "New");

			MenuItem.click(menuItem = "Collection");
		}
	}

	macro addCollection {
		FragmentsAdmin._gotoAddCollection();

		Collection.editCollection(
			collectionDescription = "${collectionDescription}",
			collectionName = "${collectionName}"
		);

		Button.clickSave();
	}

	macro addSection {
		LexiconEntry.gotoAddMenuItem(menuItem = "Section");

		AssertTextEquals(locator1 = "Modal#HEADER", value1 = "Add Section");

		PortletEntry.inputName(name = "${fragmentName}");

		Button.clickSave();
	}

	macro deleteFragment {
		LexiconCard.delete(
			card = "${fragmentName}"
		);
	}

	@summary = "Type HTML into the fragment HTML editor using the auto complete feature."
	macro autocompleteFragmentHTML {

		var htmlSnippet = '''<lfr-''';

		Type.typeAceEditorNoError(
			locator1 = "FragmentEditor#HTML_EDITOR",
			value1 = "${htmlSnippet}"
		);

		Click(
			locator1 = "FragmentEditor#AUTOCOMPLETE_FIELD",
			autocompleteField = "${autocompleteField}"
		);

		AssertTextEquals.assertPartialText(
			locator1 = "FragmentEditor#HTML_EDITOR_CONTENT",
			value1 = "${fragmentHTML}"
		);
	}

	macro exportCollection {
		Navigator.gotoNavNested(navNested = "${collectionName}");

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Export");
	}

	macro gotoFragment {
		Click(
			locator1 = "LexiconCard#CARD_TITLE",
			key_card = "${fragmentName}"
		);
	}

	macro gotoFragmentEllipsisItem {
		LexiconCard.gotoEllipsisItem(
			card = "${fragmentName}",
			item = "${item}"
		);
	}

	macro gotoImportCollection {
		if (IsElementPresent(locator1 = "NavNested#NAV_NESTED_ELLIPSIS")) {
			Click(locator1 = "NavNested#NAV_NESTED_ELLIPSIS");
		}
		else {
			Button.click(button = "New");
		}

		Button.click(button = "Import");

		IFrame.selectConfigurationFrame();
	}

	macro importCollectionFromDependenciesFolder {
		FragmentsAdmin.gotoImportCollection();

		UploadCommonFile(locator1 = "TextInput#FILE", value1 = "${collectionZipFile}");

		Button.click(button = "Import");

		Alert.viewSuccessMessage();

		IFrame.closeFrame();
	}

	macro importCollectionFromTempFolder {
		FragmentsAdmin.gotoImportCollection();

		var collectionsZipFileName = TestCase.getDownloadedTempFileName(fileNamePattern = "collections-*.zip");

		UploadTempFile(
			locator1 = "TextInput#FILE",
			value1 = "${collectionsZipFileName}"
		);

		Button.click(button = "Import");

		Alert.viewSuccessMessage();

		IFrame.closeFrame();
	}

	macro openFragmentsAdmin {
		Navigator.openWithAppendToBaseURL(
			urlAppend = "group/${siteURLKey}/~/control_panel/manage/-/fragments"
		);
	}

	macro viewFragmentWorkflowStatus {
		LexiconCard.viewCardLabel(
			card = "${fragmentName}",
			cardLabel = "${workflowStatus}"
		);
	}
}