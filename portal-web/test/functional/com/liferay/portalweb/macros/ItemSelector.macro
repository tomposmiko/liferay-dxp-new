definition {

	macro addFromURL {
		var key_imageSourceURL = "${imageSourceURL}";
		var key_navTab = "${navTab}";

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		if (isSet(wikiAdmin)) {
			AssertElementNotPresent(locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB");
		}
		else {
			AssertClick(
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = "URL");
		}

		AssertElementPresent(locator1 = "ItemSelector#URL_LABEL");

		AssertElementNotPresent(locator1 = "ItemSelector#URL_IMAGE_PREVIEW");

		AssertVisible(
			key_text = "Add",
			locator1 = "Button#ANY_DISABLED");

		Type(
			locator1 = "TextInput#URL",
			value1 = "${imageSourceURL}");

		AssertTextEquals(
			locator1 = "TextInput#URL",
			value1 = "${imageSourceURL}");

		if (("${type}" == "Audio") || ("${type}" == "Video")) {
			AssertElementPresent.pauseAssertVisible(
				key_text = "Add",
				locator1 = "Button#ANY_ENABLED");

			Button.clickAdd();
		}
		else {
			AssertElementPresent.pauseAssertVisible(locator1 = "ItemSelector#URL_IMAGE_SOURCE");

			if (IsElementPresent(locator1 = "ItemSelector#URL_IMAGE_EXAMPLE")) {
				Click(locator1 = "ItemSelector#URL_IMAGE_EXAMPLE");
			}

			MouseOver(
				key_text = "Add",
				locator1 = "Button#ANY");

			Button.clickAdd();
		}

		SelectFrameTop(value1 = "relative=top");
	}

	macro changeWorkspaces {
		if (!(isSet(customImage))) {
			SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");
		}

		if (isSet(navTab)) {
			AssertClick(
				key_navTab = "${navTab}",
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = "${navTab}");
		}

		AssertClick(
			key_breadcrumbName = "Sites and Libraries",
			locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
			value1 = "Sites and Libraries");

		AssertClick(
			key_groupTab = "${workspacesType}",
			locator1 = "ItemSelector#NAVIGATION_GROUP_TAB",
			value1 = "${workspacesType}");
	}

	macro clearFilterResults {
		Click(locator1 = "Link#CLEAR");

		AssertElementNotPresent(locator1 = "Search#SEARCH_INFO");

		AssertVisible(
			key_rowEntry = "${currentSiteWebContentTitle}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

		AssertElementNotPresent(
			key_rowEntry = "${webContentTitle}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");
	}

	macro configureFilterViaSelectFile {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		AssertClick(
			key_navTab = "${navTab}",
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "${navTab}");

		ManagementBar.setFilterAndOrder(filterBy = "${filterBy}");
	}

	macro gotoItemSelectorViaAlloyEditor {
		AlloyEditor.clickContentField();

		Click(locator1 = "AlloyEditor#CONTENT_ADD_BUTTON");

		Click.clickNoMouseOver(locator1 = "AlloyEditor#CONTENT_ADD_MENUBAR_IMAGE");
	}

	macro gotoItemSelectorViaCKEditor {
		Click(locator1 = "CKEditor#TOOLBAR_IMAGE_BUTTON");

		SelectFrame(value1 = "relative=top");
	}

	macro gotoItemSelectorViaSelectButton {
		Click(locator1 = "FormFields#SELECT_UPLOAD_FIELD");
	}

	macro gotoPreviewViaSelectFile {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		AssertClick(
			key_navTab = "${navTab}",
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "${navTab}");

		Click(locator1 = "Icon#EYE_PREVIEW");
	}

	macro searchBlogImages {
		var key_imageFileName = "${imageFileName}";

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		var key_navTab = "Documents and Media";

		AssertClick(
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "Documents and Media");

		AssertElementNotPresent(locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		var key_navTab = "Blog Images";

		AssertClick(
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "Blog Images");

		Type(
			locator1 = "TextInput#SEARCH",
			value1 = "${imageFileName}");

		KeyPress(
			locator1 = "TextInput#SEARCH",
			value1 = "\RETURN");

		AssertTextEquals(
			locator1 = "Search#SEARCH_INFO",
			value1 = "1 Results for ${imageFileName}");

		AssertElementPresent(locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		SelectFrameTop(value1 = "relative=top");
	}

	macro searchDMImages {
		var key_imageFileName = "${imageFileName}";
		var key_uploadFileName = "${imageFileName}";

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		var key_navTab = "Blog Images";

		AssertClick(
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "Blog Images");

		LexiconCard.viewCardNotPresent(card = "${imageFileName}");

		var key_navTab = "Documents and Media";

		AssertClick(
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "Documents and Media");

		Type(
			locator1 = "TextInput#SEARCH",
			value1 = "${imageFileName}");

		KeyPress(
			locator1 = "TextInput#SEARCH",
			value1 = "\RETURN");

		AssertTextEquals(
			locator1 = "Search#SEARCH_INFO",
			value1 = "1 Results for ${imageFileName}");

		LexiconCard.viewCardPresent(card = "${imageFileName}");

		SelectFrameTop(value1 = "relative=top");
	}

	macro selectBlogImage {
		var key_imageFileName = "${imageFileName}";
		var key_navTab = "${navTab}";
		var key_uploadFileName = "${imageFileName}";

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		AssertClick(
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "Blog Images");

		Click(locator1 = "ItemSelector#SELECT_FILE_IMAGE_VIEW_BUTTON");

		AssertTextEquals(
			locator1 = "ItemSelector#FOOTER",
			value1 = "${imageFileName}");

		AssertClick(
			locator1 = "ItemSelector#ADD_BUTTON",
			value1 = "Add");
	}

	macro selectDMImage {
		var key_navTab = "${navTab}";
		var key_uploadFileName = "${imageFileName}";

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		if (IsElementPresent(locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB")) {
			AssertClick(
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = "${navTab}");
		}

		if (isSet(dmFolderName)) {
			var key_dmFolderName = "${dmFolderName}";

			AssertClick(
				locator1 = "DocumentsAndMedia#ICON_FOLDER_TITLE",
				value1 = "${dmFolderName}");
		}

		if (isSet(dmSubFolderName)) {
			var key_dmFolderName = "${dmSubFolderName}";

			AssertClick(
				locator1 = "DocumentsAndMedia#ICON_FOLDER_TITLE",
				value1 = "${dmSubFolderName}");
		}

		var key_imageFileName = StringUtil.replace("${imageFileName}", " ", "+");

		LexiconCard.clickCard(card = "${imageFileName}");
	}

	macro selectRepositoryImage {
		var key_imageFileName = "${imageFileName}";

		ItemSelector.changeWorkspaces(
			customImage = "${customImage}",
			navTab = "${navTab}",
			workspacesType = "Asset Library");

		LexiconCard.clickCard(card = "${depotName}");

		if (isSet(folderName)) {
			LexiconCard.clickCard(card = "${folderName}");
		}

		if (isSet(noViewPermission)) {
			AssertElementNotPresent(locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");
		}
		else {
			Click(locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");
		}
	}

	macro uploadFile {
		var key_navTab = "${navTab}";
		var key_uploadFileName = "${uploadFileName}";

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		if (IsElementPresent(locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB")) {
			AssertClick(
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = "${navTab}");
		}

		AssertElementPresent(locator1 = "ItemSelector#SELECT_FILE_UPLOAD_IMAGE");

		UploadCommonFile.uploadCommonFileHiddenNoMouseOver(
			locator1 = "TextInput#FILE",
			value1 = "${uploadFileName}");

		Pause(locator1 = "5000");

		AssertElementPresent.pauseAssertVisible(
			locator1 = "ItemSelector#ADD_BUTTON",
			value1 = "Add");

		AssertClick(
			locator1 = "ItemSelector#ADD_BUTTON",
			value1 = "Add");
	}

	macro viewBlogImageDetails {
		var key_imageFileName = "${imageFileName}";
		var key_uploadFileName = "${uploadFileName}";

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		AssertElementPresent(locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		Click(locator1 = "Icon#EYE_PREVIEW");

		AssertElementPresent.assertVisible(locator1 = "ItemSelector#VIEWER_INFO");

		Click(locator1 = "ItemSelector#VIEWER_INFO");

		AssertElementPresent(
			key_tab = "File Info",
			locator1 = "NavTab#TAB_LINK");

		AssertElementPresent(
			key_tab = "Version",
			locator1 = "NavTab#TAB_LINK");

		AssertTextEquals(
			locator1 = "ItemSelector#SIDEBAR_FILE_INFO_FORMAT",
			value1 = "${imageFormat}");

		AssertTextEquals(
			locator1 = "ItemSelector#SIDEBAR_FILE_INFO_SIZE",
			value1 = "${imageSize}");

		AssertTextEquals(
			locator1 = "ItemSelector#SIDEBAR_FILE_INFO_NAME",
			value1 = "${uploadFileName}");

		AssertTextEquals.assertPartialText(
			locator1 = "ItemSelector#SIDEBAR_FILE_INFO_MODIFIED",
			value1 = "${lastModifiedBy}");

		var key_tab = "Version";

		AssertClick(
			locator1 = "NavTab#TAB_LINK",
			value1 = "Version");

		AssertTextEquals(
			locator1 = "ItemSelector#SIDEBAR_VERSION_VERSION",
			value1 = "${versionNumber}");

		AssertTextEquals(
			locator1 = "ItemSelector#SIDEBAR_VERSION_STATUS",
			value1 = "${imageStatus}");

		SelectFrameTop(value1 = "relative=top");
	}

	macro viewCurrentSiteFilterResults {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		if (isSet(navTab)) {
			AssertClick(
				key_navTab = "${navTab}",
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = "${navTab}");
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		AssertVisible(
			key_imageFileName = "${imageFileName}",
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		AssertElementNotPresent(
			key_imageFileName = "${noImageFileName}",
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		ManagementBar.setFilterAndOrder(filterBy = "Current Site");

		AssertVisible(
			key_imageFileName = "${imageFileName}",
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		AssertElementNotPresent(
			key_imageFileName = "${noImageFileName}",
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");

		if (isSet(viewNoLocationInfo)) {
			AssertElementNotPresent(
				key_locationName = "${viewNoLocationInfo}",
				key_title = "${imageFileName}",
				locator1 = "ItemSelector#LOCATION_INFO_CARDS",
				value1 = "${viewNoLocationInfo}");
		}

		AssertElementNotPresent(locator1 = "Search#SEARCH_INFO");

		AssertVisible(
			key_imageFileName = "${imageFileName}",
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");
	}

	macro viewEverywhereFilterResults {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		if (isSet(navTab)) {
			AssertClick(
				key_navTab = "${navTab}",
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = "${navTab}");
		}

		ManagementBar.setFilterAndOrder(filterBy = "Everywhere");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		if (isSet(dmFolderName)) {
			Click(
				key_card = "${dmFolderName}",
				locator1 = "Card#CARD_TITLE");
		}

		AssertVisible(
			key_imageFileName = "${imageFileName}",
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");
	}

	macro viewFilteredResultsViaAssetLists {
		var key_assetType = "${assetType}";

		if (isSet(assetListName)) {
			Click(
				key_title = "${assetListName}",
				locator1 = "AssetLists#ASSET_LIST_ENTRY_TITLE");
		}

		if (IsElementPresent(locator1 = "APConfiguration#SOURCE_SCOPE_ASSET_TYPE_SELECT")) {
			AssetListsAdmin.configureItemTypeInManualCollection(
				itemSubtype = "${itemSubtype}",
				itemType = "${itemType}");
		}

		Click(locator1 = "AssetLists#ASSET_LIST_SELECT_BUTTON");

		Click(locator1 = "AssetLists#ASSET_TYPE_DROPDOWN_ITEM");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertTextEquals(
			key_assetTitle = "${assetTitle}",
			locator1 = "AssetLists#ASSET_TITLE_ROW",
			value1 = "${assetTitle}");

		ManagementBar.setFilterAndOrder(filterBy = "Current Asset Library");

		AssertTextEquals(
			key_assetTitle = "${assetTitle}",
			locator1 = "AssetLists#ASSET_TITLE_ROW",
			value1 = "${assetTitle}");

		ManagementBar.setFilterAndOrder(filterBy = "Everywhere");

		AssertTextEquals(
			key_assetTitle = "${assetTitle}",
			locator1 = "AssetLists#ASSET_TITLE_ROW",
			value1 = "${assetTitle}");

		Click(locator1 = "Link#CLEAR");

		AssertElementNotPresent(locator1 = "Search#SEARCH_INFO");

		AssertTextEquals(
			key_assetTitle = "${assetTitle}",
			locator1 = "AssetLists#ASSET_TITLE_ROW",
			value1 = "${assetTitle}");
	}

	macro viewLocationInfoViaBlogs {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		if (isSet(navTab)) {
			AssertClick(
				key_navTab = "${navTab}",
				locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
				value1 = "${navTab}");
		}

		ManagementBar.setFilterAndOrder(filterBy = "Everywhere");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		for (var title : list "${titleList}") {
			AssertTextEquals(
				key_locationName = "${locationName}",
				key_title = "${title}",
				locator1 = "ItemSelector#LOCATION_INFO_CARDS",
				value1 = "${locationName}");
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		for (var title : list "${titleList}") {
			AssertTextEquals(
				key_locationName = "${locationName}",
				key_title = "${title}",
				locator1 = "ItemSelector#LOCATION_INFO_LIST",
				value1 = "${locationName}");
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		for (var title : list "${titleList}") {
			AssertTextEquals(
				key_locationName = "${locationName}",
				key_title = "${title}",
				locator1 = "ItemSelector#LOCATION_INFO_TABLE",
				value1 = "${locationName}");
		}
	}

	macro viewLocationInfoViaWCD {
		Portlet.gotoPortletOptions(
			portletName = "Web Content Display",
			portletOption = "Configuration");

		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#MODAL_BODY");

		AssertClick(
			locator1 = "WCDConfiguration#SELECT_WEB_CONTENT_BUTTON",
			value1 = "Select");

		WaitForElementPresent(locator1 = "IFrame#MODAL_BODY");

		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#MODAL_BODY");

		WaitForPageLoad();

		ManagementBar.setFilterAndOrder(filterBy = "Everywhere");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		for (var title : list "${titleList}") {
			AssertTextEquals(
				key_locationName = "${locationName}",
				key_title = "${title}",
				locator1 = "ItemSelector#LOCATION_INFO_CARDS_2",
				value1 = "${locationName}");
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		for (var title : list "${titleList}") {
			AssertTextEquals(
				key_locationName = "${locationName}",
				key_title = "${title}",
				locator1 = "ItemSelector#LOCATION_INFO_LIST",
				value1 = "${locationName}");
		}

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		for (var title : list "${titleList}") {
			AssertTextEquals(
				key_locationName = "${locationName}",
				key_title = "${title}",
				locator1 = "ItemSelector#LOCATION_INFO_TABLE",
				value1 = "${locationName}");
		}
	}

	macro viewPreview {
		AssertElementPresent(
			key_footer = "${footer}",
			locator1 = "ItemSelector#VIEWER_FOOTER");

		AssertElementPresent(
			key_imageFileName = "${imageFileName}",
			locator1 = "ItemSelector#VIEWER_FOOTER_IMAGE");
	}

	macro viewWCCurrentSiteFilterResults {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		AssertVisible(
			key_rowEntry = "${webContentTitle}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

		AssertElementNotPresent(
			key_rowEntry = "${noWebContentTitle}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

		ManagementBar.setFilterAndOrder(filterBy = "Current Site");

		AssertVisible(
			key_rowEntry = "${webContentTitle}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

		AssertElementNotPresent(
			key_rowEntry = "${noWebContentTitle}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

		if (isSet(viewNoLocationInfo)) {
			AssertElementNotPresent(
				key_locationName = "${viewNoLocationInfo}",
				key_title = "${webContentTitle}",
				locator1 = "ItemSelector#LOCATION_INFO_CARDS_2",
				value1 = "${viewNoLocationInfo}");
		}

		AssertElementNotPresent(locator1 = "Search#SEARCH_INFO");

		AssertVisible(
			key_rowEntry = "${webContentTitle}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");
	}

	macro viewWCEverywhereFilterResults {
		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		ManagementBar.setFilterAndOrder(filterBy = "Everywhere");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		if (isSet(draft)) {
			AssertElementNotPresent(
				key_rowEntry = "${webContentTitle}",
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

			AssertTextEquals(
				locator1 = "Message#EMPTY_INFO",
				value1 = "No web content was found.");
		}
		else {
			for (var webContentTitle : list "${webContentTitle}") {
				AssertVisible(
					key_rowEntry = "${webContentTitle}",
					locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");
			}
		}

		if (isSet(viewFolderWC)) {
			Click(
				key_rowEntry = "${wcFolderName}",
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

			AssertVisible(
				key_breadcrumbName = "${siteName}",
				locator1 = "Breadcrumb#BREADCRUMB_ENTRY");

			Breadcrumb.viewActiveEntry(breadcrumbName = "${wcFolderName}");

			AssertVisible(
				key_rowEntry = "${folderWebContentTitle}",
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");
		}
	}

	macro viewWikiAttachment {
		var key_imageFileName = "${imageFileName}";

		ItemSelector.gotoItemSelectorViaCKEditor();

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		AssertElementPresent(
			key_imageFileName = "${imageFileName}",
			locator1 = "ItemSelector#SELECT_FILE_IMAGE_CARD");
	}

}