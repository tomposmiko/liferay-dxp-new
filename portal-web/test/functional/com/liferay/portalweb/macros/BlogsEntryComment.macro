definition {
	macro add {
		Type.typeEditor(locator1 = "Comments#COMMENT_FIELD", value1 = "${entryComment}");

		AssertClick(locator1 = "Button#REPLY", value1 = "Reply");

		Alert.viewSuccessMessage();
	}

	macro addPGAsGuest {
		BlogsNavigator.gotoEntryPG(
			entryContent = "${entryContent}",
			entryTitle = "${entryTitle}"
		);

		var entryComment = "${entryComment}";

		Click(locator1 = "Comments#COMMENT_FIELD");

		Type(locator1 = "Comments#COMMENT_FIELD", value1 = "${entryComment}");

		AssertElementPresent(locator1 = "UserBar#USER_SIGN_IN");

		AssertClick(locator1 = "Button#REPLY_AS", value1 = "Reply as...");

		SelectFrame(locator1 = "IFrame#DIALOG");

		AssertClick(locator1 = "Link#GUEST", value1 = "Guest");

		Type(locator1 = "TextInput#FIRST_NAME", value1 = "${userFirstName}");

		Type(locator1 = "TextInput#LAST_NAME", value1 = "${userLastName}");

		Type(locator1 = "TextInput#EMAIL_ADDRESS", value1 = "${userEmailAddress}");

		Button.clickSave();

		var uniqueSuccess = "Your comment has already been posted. Would you like to create an account with the provided information?";
		var key_uniqueSuccess = "${uniqueSuccess}";

		AssertTextEquals(
			locator1 = "Message#UNIQUE_SUCCESS",
			value1 = "${uniqueSuccess}"
		);

		AssertClick(locator1 = "Button#CANCEL", value1 = "Cancel");

		SelectFrame(value1 = "relative=top");

		Alert.viewSuccessMessage();
	}

	macro delete {
		var key_entryComment = "${entryComment}";

		MouseOver(
			locator1 = "Comments#COMMENT_BODY_SPECIFIC",
			value1 = "${entryComment}"
		);

		LexiconEntry.gotoEllipsisMenuItemNoError(menuItem = "Delete");

		AssertConfirm(
			value1 = "Are you sure you want to delete this? It will be deleted immediately."
		);

		Alert.viewSuccessMessage();

		AssertTextNotPresent(value1 = "${entryComment}");
	}

	macro edit {
		var key_entryComment = "${entryComment}";

		MouseOver(locator1 = "Comments#COMMENT_BODY_SPECIFIC");

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Edit");

		Click.clickAt(locator1 = "Comments#EDIT_COMMENT_FIELD");

		Type(locator1 = "Comments#EDIT_COMMENT_FIELD", value1 = "${entryCommentEdit}");

		AssertClick(locator1 = "Comments#PUBLISH_COMMENT_BUTTON", value1 = "Publish");

		var key_entryComment = "${entryCommentEdit}";

		Alert.viewSuccessMessage();

		AssertTextEquals(
			locator1 = "Comments#COMMENT_BODY_SPECIFIC",
			value1 = "${entryCommentEdit}"
		);
	}

	macro viewPG {
		var key_entryComment = "${entryComment}";

		Pause(locator1 = "1000");

		AssertTextEquals.assertPartialText(
			locator1 = "Comments#ENTRY_COUNT_PANEL",
			value1 = "${commentCount} Comment"
		);

		var key_userFullName = "${userFullName}";

		AssertTextEquals.assertPartialText(
			locator1 = "Comments#USER_NAME",
			value1 = "${userFullName}"
		);

		AssertTextEquals(
			locator1 = "Comments#COMMENT_BODY_SPECIFIC",
			value1 = "${entryComment}"
		);
	}

	macro viewSubscribedPG {
		AssertTextEquals(
			locator1 = "Comments#UNSUBSCRIBE_FROM_COMMENTS_LINK",
			value1 = "Unsubscribe from Comments"
		);
	}
}