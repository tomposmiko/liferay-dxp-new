definition {

	macro runScriptViaGogoShell {
		if (!(isSet(nodePort))) {
			var nodePort = 8080;
		}

		var leadingPortNumber = StringUtil.replace(${nodePort}, 080, "");

		var bundleIndex = ${leadingPortNumber} - 8;
		var liferayHome = PropsUtil.get("liferay.home.dir.name");

		if (${bundleIndex} != 0) {
			var liferayHome = "${liferayHome}-${bundleIndex}";
		}

		var scriptFilePath = TestCase.getDependenciesDirPath(fileName = ${scriptFile});

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "execute-groovy-script-from-gogo-shell -Dbundle.index=${bundleIndex} -Dscript.file.path=${scriptFilePath}");

		if (isSet(expectedOutput)) {
			var outputContent = FileUtil.read("${liferayHome}/gogo-output.txt");

			if (contains(${outputContent}, ${expectedOutput})) {
				echo("${expectedOutput} exists!");
			}
			else {
				fail("${expectedOutput} does not exist!");
			}
		}
	}

	macro runScriptViaUI {
		if (!(isSet(nodePort))) {
			var nodePort = 8080;
		}

		Navigator.openSpecificURL(url = "http://localhost:${nodePort}");

		if (IsElementPresent(locator1 = "UserBar#USER_SIGN_IN")) {
			User.firstLoginUI(
				password = "test",
				specificURL = "http://localhost:${nodePort}",
				userEmailAddress = "test@liferay.com");
		}

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Server Administration");

		var script = TestCase.getFileContent(fileName = ${scriptFile});

		ServerAdministration.executeScript(
			language = "Groovy",
			script = ${script});
	}

}